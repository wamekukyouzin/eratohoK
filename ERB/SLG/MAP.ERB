;マップ関係

;-------------------------------------------------
;全都市の描画色をリセットする関数
;-------------------------------------------------
@RESET_CITY_COLOR
VARSET CITY_COLOR, GETDEFCOLOR()

;-------------------------------------------------
;全都市の描画色を勢力の色にセットする関数
;-------------------------------------------------
@SET_CITY_COLOR_COUNTRY
FOR LOCAL:0, 0, MAX_CITY
	CITY_COLOR:(LOCAL:0) = COUNTRY_COLOR:(CITY_OWNER:(LOCAL:0))
NEXT

;-------------------------------------------------
;兵の存在する中継地点に色を付ける関数
;-------------------------------------------------
@SET_CITY_COLOR_UNIT
CALL TMP_CREATE_UNIT_MAP
FOR LOCAL:0, 0, MAX_CITY
	IF CITY_TYPE:(LOCAL:0) == 1
		IF IS_STAY_MYUNIT(LOCAL:0) == 1
			CITY_COLOR:(LOCAL:0) = COUNTRY_COLOR:(CFLAG:MASTER:所属)
		ELSEIF IS_STAY_UNIT(LOCAL:0) == 2
			CITY_COLOR:(LOCAL:0) = COUNTRY_COLOR:(GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_野盗)))
		ELSEIF IS_COUNTRY(TMP_UNIT_ONCITY_CNT:(LOCAL:0):0)
			CITY_COLOR:(LOCAL:0) = COUNTRY_COLOR:(TMP_UNIT_ONCITY_CNT:(LOCAL:0):0)
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------
;全都市の描画色を自勢力:青、敵勢力:赤にセットする関数
;-------------------------------------------------
@SET_CITY_COLOR_RELATION
FOR LOCAL:0, 0, MAX_CITY
	IF COUNTRY_BOSS:(CITY_OWNER:(LOCAL:0)) <= 0
		CITY_COLOR:(LOCAL:0) = GETDEFCOLOR()
	ELSEIF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:所属
		CITY_COLOR:(LOCAL:0) = COLOR("薄青")
	ELSEIF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:所属
		CITY_COLOR:(LOCAL:0) = COLOR("赤")
	ENDIF
NEXT

;-------------------------------------------------
;都市名をボタンとして描画する関数 ARG:0=都市番号
;-------------------------------------------------
@DRAWCITY_BUTTON(ARG:0)
SETCOLOR CITY_COLOR:(ARG:0)
FOR LOCAL:0,0,10
	SIF CITY_TYPE:(ARG:0) == 0
		FONTSTYLE IS_STAY_MYUNIT(ARG:0) * 2 + IS_STAY_ENEMY_UNIT(ARG:0) * 8
NEXT
PRINTBUTTON GET_CITYNAME_BUTTON(ARG:0), ARG:0 + 1000
RESETCOLOR
FONTSTYLE 0
;-------------------------------------------------
;戦略フェイズのメニューコマンドを表示する関数 ARG:0は行数
;-------------------------------------------------
@PRINT_SLG_MENU(ARG:0)
;観戦モードなら特殊メニュー
IF FLAG:観戦モード
	SELECTCASE ARG:0
		CASE 0
			CALL PRINTBUTTON_SHOP(0,  "[  勢力情報]", 7)
		CASE 1
			CALL PRINTBUTTON_SHOP(1,  "[  能力一覧]", 7)
		CASE 30
			CALL PRINTBUTTON_SHOP(99, "[  行動終了]", 7)
		CASE 33
			CALL PRINTBUTTON_SHOP(70, "[      設定]", 7)
		CASE 35
			CALL PRINTBUTTON_SHOP(80, "[    セーブ]", 7)
		CASE 37
			CALL PRINTBUTTON_SHOP(90, "[    ロード]", 7)
		CASEELSE
			PRINT 　　　　　　　
	ENDSELECT

;放浪中なら特殊メニュー
ELSEIF CFLAG:MASTER:所属 == 0
	SELECTCASE ARG:0
		CASE 0
			CALL PRINTBUTTON_SHOP(0,  "[  仕官する]", 7)
		CASE 1
			CALL PRINTBUTTON_SHOP(1,  "[    旗揚げ]", 7)
		CASE 4
			CALL PRINTBUTTON_SHOP(5,  "[  勢力情報]", 7)
		CASE 5
			CALL PRINTBUTTON_SHOP(6,  "[  能力一覧]", 7)
		CASE 8
			CALL PRINTBUTTON_SHOP(8,  "[      訓練]", 7)
		CASE 9
			CALL PRINTBUTTON_SHOP(9,  "[      謁見]", 7)
		CASE 30
			CALL PRINTBUTTON_SHOP(99, "[  行動終了]", 7)
		CASE 33
			CALL PRINTBUTTON_SHOP(70, "[      設定]", 7)
		CASE 35
			CALL PRINTBUTTON_SHOP(80, "[    セーブ]", 7)
		CASE 37
			CALL PRINTBUTTON_SHOP(90, "[    ロード]", 7)
		CASEELSE
			PRINT 　　　　　　　
	ENDSELECT

ELSE
	SELECTCASE ARG:0
		CASE 0
			CALL PRINTBUTTON_SHOP(0,  "[  部隊編成]", 7)
		CASE 1
			CALL PRINTBUTTON_SHOP(1,  "[  防衛解除]", 7)
		CASE 3
			CALL PRINTBUTTON_SHOP(10, "[      外交]", 7)
		CASE 4
			CALL PRINTBUTTON_SHOP(11, "[  勢力情報]", 7)
		CASE 5
			CALL PRINTBUTTON_SHOP(12,  "[  能力一覧]", 7)
		CASE 7
			CALL PRINTBUTTON_SHOP(20, "[  政策変更]", 7)
		CASE 8
			PRINT   現在:
			SELECTCASE COUNTRY_POLICY:(CFLAG:MASTER:所属)
				CASE 政策_経済
					SETCOLOR COLOR("パ緑")
					PRINT 経済   
				CASE 政策_徴兵
					SETCOLOR COLOR("CREAM")
					PRINT 徴兵   
				CASE 政策_防衛
					SETCOLOR COLOR("パ青")
					PRINT 防衛   
				CASEELSE
					SETCOLOR COLOR("明灰色")
					PRINT -------
			ENDSELECT
			RESETCOLOR
		CASE 9
			SELECTCASE COUNTRY_POLICY:(CFLAG:MASTER:所属)
				CASE 政策_経済
					PRINTFORM   経済成長x1.5
				CASE 政策_徴兵
					PRINTFORM   徴兵効率x1.2
				CASE 政策_防衛
					PRINTFORM   防衛倍率x1.5
				CASEELSE
					PRINTFORM   <AI専用政策>
			ENDSELECT
		CASE 10
			CALL PRINTBUTTON_SHOP(21, "[  都市管理]", 7)
		CASE 12
			CALL PRINTBUTTON_SHOP(30, "[  特別訓練]", 7)
		CASE 13
			IF SPECIAL_TRAIN_IS_BARGAIN()
				CALL COLORPRINT("  現在割引中！", COLOR("注意"))
			ELSE
				SIF MONEY < SPECIAL_TRAIN_PRICE
					SETCOLOR COLOR("選択不可")
				PRINTFORM 料金:{SPECIAL_TRAIN_PRICE, 9, RIGHT}
				RESETCOLOR
			ENDIF
		CASE 14
			CALL PRINTBUTTON_SHOP(31, "[ 徴兵/解雇]", 7)
		CASE 15
			CALL PRINTBUTTON_SHOP(32, "[  奴隷市場]", 7)
		CASE 16
			CALL PRINTBUTTON_SHOP(33, "[  領地探索]", 7)
		CASE 17
			CALL PRINTBUTTON_SHOP(34, "[  スカウト]", 7)
		CASE 18
			CALL PRINTBUTTON_SHOP(35, "[  国庫入金]", 7)
		CASE 20
			CALL PRINTBUTTON_SHOP(40, "[      独立]", 7)
		CASE 21
			CALL PRINTBUTTON_SHOP(41, @"\@ GET_COUNTRY_BOSS(CFLAG:MASTER:所属) == MASTER ? [    国譲り] # [クーデター]\@", 7)
		CASE 22
			CALL PRINTBUTTON_SHOP(42, "[  野に下る]", 7) 
		CASE 24
			CALL PRINTBUTTON_SHOP(50, "[オート設定]", 7)
		CASE 25
			PRINT   現在:
			IF CONFIG:302 == 0
				PRINT 手動   
			ELSE
				SETCOLOR COLOR("オレンジ")
				PRINT 自動   
				RESETCOLOR
			ENDIF
		CASE 30
			CALL PRINTBUTTON_SHOP(99, "[  行動終了]", 7)
		CASE 33
			CALL PRINTBUTTON_SHOP(70, "[      設定]", 7)
		CASE 35
			CALL PRINTBUTTON_SHOP(80, "[    セーブ]", 7)
		CASE 37
			CALL PRINTBUTTON_SHOP(90, "[    ロード]", 7)
		CASEELSE
			PRINT 　　　　　　　
	ENDSELECT
ENDIF
PRINT ┃

;-------------------------------------------------
;全都市数を返す関数
;-------------------------------------------------
@GET_CITY_NUM()
#FUNCTION
RETURNF CITY_NUM

;-------------------------------------------------
;都市名を取得する関数 ボタン用 ARG:0=都市番号
;-------------------------------------------------
@GET_CITYNAME_BUTTON(ARG:0)
#FUNCTIONS
SIF ARG:0 < 0 || ARG:0 >= MAX_CITY
	RETURNF "無名"
RETURNF CITY_NAME_SHORT:(ARG:0)

;-------------------------------------------------
;都市名を取得する関数 ARG:0=都市番号
;-------------------------------------------------
@GET_CITYNAME(ARG:0)
#FUNCTIONS
SIF ARG:0 < 0 || ARG:0 >= MAX_CITY
	RETURNF "無名"	
SIF CITY_TYPE:(ARG:0) == 1
	RETURNF "●"
RETURNF CITY_NAME:(ARG:0)

;-------------------------------------------------
;都市名から番号を取得する関数 REGISTER_ROOTやCITY_OWNERでの糖衣化用。ARGS:0=都市名
;-------------------------------------------------
@GET_CITYNUMBER(ARGS:0)
#FUNCTION
RETURNF FINDELEMENT(CITY_NAME, ARGS:0)

;-------------------------------------------------
;中継地点の名称を取得する関数 ARG:0=都市番号
;-------------------------------------------------
@GET_RELAYNAME(ARG:0)
#FUNCTIONS
SIF ARG:0 < 0 || ARG:0 >= MAX_CITY
	RETURNF "無名"	
RETURNF CITY_NAME:(ARG:0)

;-------------------------------------------------
;ルートの登録を行う関数
;ARG:0　登録元　ARG:1~10 登録先
;なお既に登録されていたら無視する
;-------------------------------------------------
@REGISTER_ROOT(ARG:0, ARG:1 = -99, ARG:2 = -99, ARG:3 = -99, ARG:4 = -99, ARG:5 = -99, ARG:6 = -99, ARG:7 = -99, ARG:8 = -99, ARG:9 = -99, ARG:10 = -99)
;接続先についてカウントアップを行う
FOR LOCAL:0, 1, 10
	;-99だった場合無視
	IF ARG:(LOCAL:0) == -99
		BREAK
	ENDIF
	;接続元のCITY_ROOTの0~10についてカウントアップを行う
	FOR LOCAL:1, 0, 10
		;フェイルセーフ。既に該当地域が登録されているならBREAK。
		IF CITY_ROOT:(ARG:0):(LOCAL:1) == ARG:(LOCAL:0)
			BREAK
		;ARG:0のCITY_ROOTのうち、LOCAL:1番目が空であれば登録する
		ELSEIF CITY_ROOT:(ARG:0):(LOCAL:1) == 0
			CITY_ROOT:(ARG:0):(LOCAL:1) = ARG:(LOCAL:0)
			;接続先にも接続元を登録しておく
			FOR LOCAL:3, 0, 10
				;フェイルセーフ。REGISTER_ROOTを通して操作する限りありえないとは思うが。
				IF CITY_ROOT:(ARG:(LOCAL:0)):(LOCAL:3) == ARG:0
					;DEBUGPRINTFORML @REGISTER_ROOT:{ARG:(LOCAL:0)}→{ARG:0}が登録済み
					BREAK
				ELSEIF CITY_ROOT:(ARG:(LOCAL:0)):(LOCAL:3) == 0
					CITY_ROOT:(ARG:(LOCAL:0)):(LOCAL:3) = ARG:0
					BREAK
				ENDIF
			NEXT
			BREAK
		ENDIF
	NEXT
NEXT

;----------------------------------------------------------------------------------------------
;以下ラッパ
;----------------------------------------------------------------------------------------------

;-------------------------------------------------
;都市タイプの設定
;-------------------------------------------------
@SET_CITY_TYPE
	CALLFORM SET_CITY_TYPE_%MAPID%

;-------------------------------------------------
;ルートの設定
;-------------------------------------------------
@SET_MAP_ROOT
	CALLFORM SET_MAP_ROOT_%MAPID%
	
;-------------------------------------------------
;マップの準備
;-------------------------------------------------
@MAPSETUP(ARG:0 = 0)
SIF ARG:0 == 0 && MAPREADY == 1
	RETURN
CALL SET_CITY_TYPE
CALLFORM SET_CITYNAME_%MAPID%
CALLFORM SET_SHORTCITYNAME_%MAPID%
CALLFORM SET_CITY_NUM_%MAPID%
CALL SET_MAP_ROOT
MAPREADY = 1

;-------------------------------------------------
;経済や防衛などの初期化
;-------------------------------------------------
@MAP_INIT
CALLFORM MAP_INIT_%MAPID%

;-------------------------------------------------
;マップの描画 ARG:0に1を指定すると軍事画面用のボタンを生成
;-------------------------------------------------
@DRAWMAP(ARG:0 = 0, ARG:1 = 0)
CALLFORM DRAWMAP_%MAPID%, ARG:0, ARG:1
