;-------------------------------------------------
;勢力の情報を表示する
;-------------------------------------------------
@SHOW_COUNTRY_INFO(勢力)
#DIM NOWREDRAW
#DIM FIRST_LINE
#DIM 勢力
#DIM 君主
#DIM 相手君主
#DIM 対象

SIF !IS_COUNTRY(勢力)
    RETURN

FIRST_LINE = LINECOUNT
CURRENTREDRAW
NOWREDRAW = RESULT
REDRAW 0


$SHOW_LOOP


君主 = GET_COUNTRY_BOSS(勢力)

CALL SINGLE_DRAWLINE

PRINTFORM 君主:
PRINTBUTTON @"[%NAME_FORMAL(君主)%]", 1000 + 君主
PRINTL 

CALL SINGLE_DRAWLINE
PRINTFORM 評価:
CALL TMP_PRINT_COUNTRY_STARS(勢力)
PRINTFORML   都市:{GET_OWN_CITY(勢力), 3} 経済:{GET_SUM_ECONOMY(勢力) / 100, 6} 国庫:{MONEY:勢力, 7} 兵力:{GET_SUM_SOLDIER(勢力), 7}
CALL SINGLE_DRAWLINE

PRINTFORM 士官:

FOR LOCAL, 0, CHARANUM
    SIF LOCAL == GET_COUNTRY_BOSS(勢力)
        CONTINUE
    SIF CFLAG:LOCAL:所属 != 勢力
        CONTINUE
    PRINTBUTTON @"[%SNAME(LOCAL)%]", 1000 + LOCAL
    PRINT  
NEXT

PRINTL
CALL SINGLE_DRAWLINE
PRINTFORML 各国との関係
PRINTFORM %"君主",MAX_CHARANAME_LENGTH / 2 + 4,LEFT%
PRINTFORM %"関係",8, RIGHT%
PRINTFORM %"隣接",6, RIGHT%
PRINTFORM %"印象",8, RIGHT%
PRINTFORM %"友好", 7, RIGHT%
PRINTFORM %"敵対", 7, RIGHT%
PRINTFORM %"囚えている", 26, RIGHT%
PRINTFORML %"囚われている", 26, RIGHT%

FOR LOCAL, 0, MAX_COUNTRY

    SIF !IS_COUNTRY(LOCAL) || LOCAL == 勢力
        CONTINUE

    SETCOLOR COUNTRY_COLOR:LOCAL
    PRINTBUTTON @"[%SNAME(GET_COUNTRY_BOSS(LOCAL:0)), MAX_CHARANAME_LENGTH / 2, LEFT%]", LOCAL
    RESETCOLOR

    CALL TMP_SETCOLOR_COUNTRY_RELATION(勢力, LOCAL:0)
    PRINTFORM %TMP_TOSTR_COUNTRY_RELATION(勢力, LOCAL:0, 1), 10, RIGHT%
    RESETCOLOR
    PRINTFORM %TMP_COUNTRY_IS_NEIBORING:(勢力):LOCAL == 1 ? "〇" # "×", 6, RIGHT%

    相手君主 = GET_COUNTRY_BOSS(LOCAL:0)
    PRINT      
    CALL TMP_PRINT_COUNTRY_RELATION2(相手君主, 君主)

;		PRINTFORM 
    PRINTFORM {REL_LIKE:相手君主:君主, 7, RIGHT}
    PRINTFORM {REL_HATE:相手君主:君主, 7, RIGHT}

		;囚えている捕虜の表示
    LOCALS:1 = 
    FOR LOCAL:1, 0, CHARANUM
        IF CFLAG:(LOCAL:1):所属 == LOCAL:0 && CFLAG:(LOCAL:1):捕虜先 == 勢力
            IF LOCALS:1 == ""
                LOCALS:1 = %SNAME(LOCAL:1)%
            ELSE
                IF STRLENS(@"%LOCALS:1%、%SNAME(LOCAL:1)%") >= 24
                    LOCALS:1 = %LOCALS:1%、他
                    BREAK
                ELSE
                    LOCALS:1 = %LOCALS:1%、%SNAME(LOCAL:1)%
                ENDIF
            ENDIF
        ENDIF
    NEXT
    SETCOLOR カラー_オレンジ
    PRINTFORM %LOCALS:1, 26, RIGHT%
    RESETCOLOR

		;囚えられている捕虜の表示
    LOCALS:1 = 
    FOR LOCAL:1, 0, CHARANUM
        IF CFLAG:(LOCAL:1):所属 == 勢力 && CFLAG:(LOCAL:1):捕虜先 == LOCAL:0
            IF LOCALS:1 == ""
                LOCALS:1 = %SNAME(LOCAL:1)%
            ELSE
                IF STRLENS(@"%LOCALS:1%、%SNAME(LOCAL:1)%") >= 24
                    LOCALS:1 = %LOCALS:1%、他
                    BREAK
                ELSE
                    LOCALS:1 = %LOCALS:1%、%SNAME(LOCAL:1)%
                ENDIF
            ENDIF
        ENDIF
    NEXT
    SETCOLOR カラー_シアン
    PRINTFORM %LOCALS:1, 26, RIGHT%
    RESETCOLOR
    PRINTL 
NEXT

CALL SINGLE_DRAWLINE()
PRINTBUTTON "[技術ツリー]", 99997
PRINTL
PRINTBUTTON "[関係一覧]", 99998
PRINTL 
PRINTBUTTON "[戻る]", 99999
PRINTL
CALL SINGLE_DRAWLINE()

$INPUT_LOOP

INPUT

IF RESULT == 99999
    REDRAW NOWREDRAW
    RETURN 1
ELSEIF RESULT == 99998
    CALL SINGLE_DRAWLINE
    CALL TMP_SHOW_COUNTRY_RELATION1
    WAIT
    CALL SINGLE_DRAWLINE
    CALL TMP_SHOW_COUNTRY_RELATION2
    CALL SINGLE_DRAWLINE
    WAIT
ELSEIF RESULT == 99997
    CALL SINGLE_DRAWLINE
    CALL SHOW_TECHNOLOGY_RESEARCH(勢力)
ELSEIF INRANGE(RESULT, 1000, 1000 + CHARANUM)
    対象 = RESULT - 1000
    IF CFLAG:対象:所属 == 勢力
        VARSET SHOP_CHARA_LIST, -1
        LOCAL:1 = 0
        FOR LOCAL, 0, CHARANUM
            IF CFLAG:LOCAL:所属 == 勢力
                SHOP_CHARA_LIST:(LOCAL:1) = LOCAL
                LOCAL:1 ++
            ENDIF
        NEXT
        CALL SHOW_INFO_WITH_UI(対象, 1)
    ENDIF

ELSEIF INRANGE(RESULT, 0, MAX_COUNTRY - 1) && IS_COUNTRY(RESULT)
    勢力 = RESULT
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP


