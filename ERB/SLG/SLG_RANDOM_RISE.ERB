@SELECT_CHARA_RANDOM_LOGIC_SLG_RANDOM_RISE(対象)
#DIM 対象
SIF 対象 == MASTER
	RETURN 0
	;放浪していないとだめ
SIF CFLAG:対象:特殊状態 != 特殊状態_放浪
	RETURN 0
	;特殊キャラはだめ（特殊キャラは放浪してなそうな気もするけど）
SIF IS_SP_CHARA(対象)
	RETURN 0
	;野心が皆無なやつもだめ
SIF ABL:対象:野心 <= 0
	RETURN 0
RETURN 1

@SELECT_CHARA_RANDOM_BIAS_SLG_RANDOM_RISE(対象)
#DIM 対象
RETURN ABL:対象:野心

;----------------------------------------------
;蜂起を起こすNPCの候補を選択し、確率ロール
;----------------------------------------------
@SLG_RANDOM_RISE
#DIM LCOUNT
#DIM 人数
#DIM 候補, MAX_CHARA_NUM
#DIM 候補値, MAX_CHARA_NUM
#DIM 野心無視フラグ
#DIM 結果
#DIM 判定値
#DIM 勢力番号
VARSET 人数
VARSET 候補
VARSET 候補値, __INT_MIN__

;10期以降
SIF DAY < SLG_PP:1
	RETURN 0

勢力番号 = GET_NEW_COUNTRY()

;新しく勢力作れないなら戻す
SIF 勢力番号 <= 0
	RETURN 0

;10%の確率で野心をフラットに扱う
野心無視フラグ = RAND:100 < 10

IF 野心無視フラグ
	CALL SELECT_CHARA_RANDOM("SLG_RANDOM_INDEPENDENCE")
ELSE
	CALL SELECT_CHARA_RANDOM("SLG_RANDOM_INDEPENDENCE", "SLG_RANDOM_INDEPENDENCE")
ENDIF

結果 = RESULT

判定値 = CALC_RISE_RATE(結果, 野心無視フラグ)
;確率ロールに失敗したら戻る

SIF 判定値 <= RAND:3000
	RETURN 0

;----確率ロール成功。勢力を作成。----

SELECTCASE RAND:7
	CASE 0, 1
		COUNTRY_AI_TYPE:勢力番号 = AI_通常
	CASE 2
		COUNTRY_AI_TYPE:勢力番号 = AI_外交
	CASE 3
		COUNTRY_AI_TYPE:勢力番号 = AI_好戦
	CASE 4
		COUNTRY_AI_TYPE:勢力番号 = AI_防衛
	CASE 5
		COUNTRY_AI_TYPE:勢力番号 = AI_内政
	CASE 6
		COUNTRY_AI_TYPE:勢力番号 = AI_蛮行
ENDSELECT

COUNTRY_COLOR:勢力番号 = CHARA_COUNTRY_COLOR(結果)

LOCAL:1 = 0
FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL)
		CONTINUE
	LOCAL:1 += GET_SUM_SOLDIER(LOCAL)
NEXT
LOCAL:1 /= GET_COUNTRY_NUM()

COUNTRY_SOLDIER:勢力番号 = MIN(LOCAL:1, 5000)
COUNTRY_BOSS:勢力番号 = GET_ID(結果)
INDEPENDENCE_COUNTER:勢力番号 = 10
DIPLOMACY_HATE:勢力番号 += 10

CALL SET_COUNTRY_AI_TYPE(勢力番号)

CALL RANDOM_RISE_SET_TECHNOLOGY(勢力番号)	

CALL CHANGE_COUNTRY(結果, 勢力番号, 1)
CFLAG:結果:特殊状態 = 0

CALL SP_COUNTRY_RISE(勢力番号)

VARSET 候補
VARSET 人数
;賛同者をつのる
FOR LOCAL, 1, CHARANUM
	SIF LOCAL == 結果
		CONTINUE
	;主人公は駄目
	SIF LOCAL == MASTER
		CONTINUE
	;主人公所属で陥落系素質ありなら賛同しない
	SIF CFLAG:LOCAL:所属 && CFLAG:MASTER:所属 == CFLAG:LOCAL:所属 && (IS_FALLEN(LOCAL) || TALENT:LOCAL:親友)
		CONTINUE
	;君主は駄目
	SIF GET_COUNTRY_BOSS(CFLAG:LOCAL:所属) == LOCAL
		CONTINUE
	;死んでたら駄目
	SIF CFLAG:LOCAL:特殊状態 == 特殊状態_死亡
		CONTINUE
	;捕虜なら駄目ですよねー
	SIF CFLAG:LOCAL:捕虜先
		CONTINUE
	;未登場でも駄目
	SIF !IS_APPEAR(LOCAL)
		CONTINUE
	;特殊キャラはだめ（特殊キャラは放浪してなそうな気もするけど）
	SIF IS_SP_CHARA(LOCAL)
		CONTINUE
	;特殊勢力に所属、かつその勢力に陥落している場合はダメ
	SIF IS_SP_COUNTRY(CFLAG:LOCAL:所属) && GETBIT(TALENT:LOCAL:特殊勢力陥落系, SP_COUNTRY_TO_CONST(CFLAG:LOCAL:所属))
		CONTINUE
	候補:人数 = LOCAL
	人数 ++
NEXT

IF 人数 == 0
	PRINTFORML ……が、だれも賛同しなかったようだ
	RETURN 1
ENDIF

CALL FISHER_YATES_SHAFFLE(人数)
LOCAL:1 = 1
FOR LOCAL, 0, 人数
	LOCAL:2 = 候補:(SHAFFLE_ARRAY:(LOCAL))
	LOCAL:3 = REL_LIKE:(LOCAL:2):結果
	IF CFLAG:(LOCAL:2):所属
		LOCAL:3 += REL_HATE:(LOCAL:2):(GET_COUNTRY_BOSS(CFLAG:(LOCAL:2):所属))
	ELSE
		LOCAL:3 += 500
	ENDIF
	IF LOCAL:3 > LOCAL:1 * 300
		PRINTFORMW %ANAME(LOCAL:2)%が賛同し、%ANAME(結果)%につきました
		CALL CHANGE_COUNTRY(LOCAL:2, 勢力番号, 1)
		LOCAL:1 ++
	ENDIF
NEXT

RETURN 1

;----------------------------------------------
;蜂起を起こす確率を算出
;----------------------------------------------
@CALC_RISE_RATE(対象, 野心無視フラグ = 0)
#FUNCTION
#DIM 対象
#DIM 判定値
#DIM 野心無視フラグ

;野心とDAYから判定値を算出
IF 野心無視フラグ
	判定値 = 160
ELSE
	判定値 = ABL:対象:野心 * 2
ENDIF

SELECTCASE ABL:対象:野心
	CASE IS >= ランク閾値:ランク_ＳＬＧ:ランク_A
		TIMES 判定値, 1.5
	CASE IS  >= ランク閾値:ランク_ＳＬＧ:ランク_B
		TIMES 判定値, 1.25
	CASE IS >= ランク閾値:ランク_ＳＬＧ:ランク_C
		TIMES 判定値, 1.1
ENDSELECT

;勢力数が少ないほど蜂起しやすい
判定値 += (MAX_COUNTRY - GET_COUNTRY_NUM()) * 2
;終盤は蜂起しにくくする（ただでさえ終わらない観戦モードがさらに終わらなくなるので）
判定値 -= LIMIT(DAY / 2 - 50, 0, 200)

SIF TALENT:対象:臆病
	判定値 -= 5
SIF TALENT:対象:反抗的
	判定値 += 15
SIF TALENT:対象:素直
	判定値 -= 8
SIF TALENT:対象:大人しい
	判定値 -= 5
SIF TALENT:対象:プライド高い
	判定値 += 20
SIF TALENT:対象:生意気
	判定値 += 10
SIF TALENT:対象:プライド低い
	判定値 -= 15
SIF TALENT:対象:自制心
	判定値 -= 5
SIF TALENT:対象:保守的
	判定値 -= 5
SIF TALENT:対象:好奇心
	判定値 += 5
SIF TALENT:対象:一線越えない
	判定値 -= 15
SIF TALENT:対象:抑圧
	判定値 -= 5
SIF TALENT:対象:解放
	判定値 += 15
SIF TALENT:対象:孤高
	判定値 += 20

;4能力が250越えてたらプラス補正
SIF ABL:対象:武闘 + ABL:対象:防衛 + ABL:対象:知略 + ABL:対象:政治 > 250
	判定値 += MAX(ABL:対象:武闘 + ABL:対象:防衛 + ABL:対象:知略 + ABL:対象:政治 - 200, 50)

RETURNF 判定値

;----------------------------------------------
;蜂起した勢力に技術ツリーを設定
;----------------------------------------------
@RANDOM_RISE_SET_TECHNOLOGY(勢力)
#DIM 勢力
#DIM 候補値, TECHNOLOGY_MAX_GENRE
#DIM ジャンル
#DIM 残り日数

残り日数 = DAY



WHILE 残り日数 > 0
	VARSET 候補値, 100

	FOR LOCAL, 0, TECHNOLOGY_MAX_GENRE
		候補値:LOCAL += AI_TECHNOLOGY_PREFERENCE:(COUNTRY_AI_TYPE:勢力):LOCAL
		候補値:LOCAL = MAX(候補値:LOCAL - TECHNOLOGY_STATUS:勢力:LOCAL * 5, 1)

	;そのジャンルもう取れないならダメ
		IF !IS_TECHNOLOGY_EXISTS(LOCAL, TECHNOLOGY_STATUS:勢力:LOCAL + 1)
			候補値:LOCAL = __INT_MIN__
		ELSE
			候補値:LOCAL = RAND:(候補値:LOCAL)
		ENDIF
	NEXT

	;マジでなんもとれるもん無かったらBREAK
	SIF MAXARRAY(候補値) == __INT_MIN__
		BREAK

	ジャンル = FINDELEMENT(候補値, MAXARRAY(候補値))

	残り日数 -= GET_RESEARCH_PERIOD(勢力, ジャンル, TECHNOLOGY_STATUS:勢力:ジャンル + 1)
	TECHNOLOGY_STATUS:勢力:ジャンル ++
WEND
