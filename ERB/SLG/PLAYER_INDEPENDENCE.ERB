;-------------------------------------------------
;独立の処理
;-------------------------------------------------
@PLAYER_INDEPENDENCE
;選択したキャラのリスト
#DIM SELECT_LIST, 3000
#DIM FIRST_LINE
#DIM 旧勢力
#DIM 新勢力
#DIM 君主
#DIM 都市
VARSET LOCAL, 0
VARSET SELECT_LIST, 0

旧勢力 = CFLAG:MASTER:所属
君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)

DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP
CALL RESET_CITY_COLOR
DRAWLINE
PRINTL 旗揚げする都市を選んで下さい
PRINTL 現在所属している勢力の都市のみ指定可能です
DRAWLINE
PRINTBUTTON " 0[キャンセル]", 0
PRINTL 

REDRAW 0

$INPUT_LOOP_CITY
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN
ELSEIF RESULT >= 1001 && RESULT <= 1001 + CITY_NUM
	都市 = RESULT - 1000
	;指定された都市が存在し、主人公勢力の支配下にある場合
	IF GET_RELAYNAME(都市) != "無名" && CITY_TYPE:(都市) == 0 && CITY_OWNER:(都市) == 旧勢力
		REDRAW 1
		PRINTFORMW %GET_RELAYNAME(都市)%で旗揚げします
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_CITY
	ENDIF
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP_CITY
ENDIF

DRAWLINE
PRINTFORML 関係性の深いキャラを新たな勢力に誘うことができます
PRINTFORML 新たな勢力の士官として加えるキャラを選択して下さい
DRAWLINE
CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(-1, "君主, 育児対象, 別勢力, 放浪, 死亡, 未登場, 動物, !IS_LOVER&&!IS_SLAVE", "")

新勢力 = GET_NEW_COUNTRY()

;部隊の解散
CALL CLEAR_ALL_UNIT(旧勢力, 1)

;初期設定
CITY_OWNER:都市 = 新勢力
COUNTRY_BOSS:新勢力 = GET_ID(MASTER)
COUNTRY_COLOR:新勢力 = COLOR("注意")

;兵力の移動
COUNTRY_SOLDIER:新勢力 += COUNTRY_SOLDIER:旧勢力 / GET_OWN_CITY(旧勢力) + 1
COUNTRY_SOLDIER:旧勢力 -= COUNTRY_SOLDIER:新勢力
COUNTRY_SOLDIER:新勢力 += CITY_SOLDIER:都市
CITY_SOLDIER:都市 = 0

CITY_SOLDIER:都市 = MIN(COUNTRY_SOLDIER:新勢力, 500)
COUNTRY_SOLDIER:新勢力 -= CITY_SOLDIER:都市

CALL CHANGE_COUNTRY(MASTER, 新勢力, 1)

;士官を移動
FOR LOCAL, 0, SELECTED_CHARA_NUM
	CALL CHANGE_COUNTRY(SELECTED_CHARA:LOCAL, 新勢力, 1)
NEXT


DRAWLINE
PRINTL 新しい勢力の色を選んで下さい
DRAWLINE
CALL CHANGE_COUNTRY_COLOR(新勢力, 1)

SETCOLOR COLOR("注意")
PRINTL 
PRINTFORMW %ANAME(MASTER)%は%GET_RELAYNAME(都市)%を占拠し独立しました！
PRINTL 
RESETCOLOR

;元の君主との関係を大幅に悪化させる
CALL CHANGE_RELATION_O_TO_C(旧勢力, 新勢力, -200, 500)

;-------------------------------------------------
;クーデターの処理
;-------------------------------------------------
@PLAYER_COUP
#DIM 勢力
#DIM 君主
#DIM 国譲りフラグ
PRINTFORML 現在の君主を捕らえて新たな君主を立てます
PRINTFORML もし現在の君主が強く陥落しているなら、平和裏に代替わりできるでしょう
PRINTFORML 新君主は%ANAME(MASTER)%自身がなるか、陥落している士官の中から選べます
PRINTFORML 本当にクーデターを実行しますか？

CALL ASK_YN()
SIF RESULT == 1
	RETURN

;全部隊の解散
CALL CLEAR_ALL_UNIT(CFLAG:MASTER:所属, 1)

勢力 = CFLAG:MASTER:所属
君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
国譲りフラグ = TALENT:君主:親愛 || TALENT:君主:隷属

;調教参加フラグをクリア
CVARSET CFLAG, 6, 0

DRAWLINE
PRINTFORML 新たな君主を決めて下さい キャンセルで%ANAME(MASTER)%が就任します
DRAWLINE
CALL SELECT_CHARA_LIST_ONLY_LOGIC_SLG("育児対象, 別勢力, 放浪, 死亡, 未登場, 動物, 君主, !IS_LOVER&&!IS_SLAVE", "")

SIF RESULT == -1
RESULT = MASTER

COUNTRY_BOSS:(CFLAG:MASTER:所属) = GET_ID(RESULT)

CALL COLORPRINT(@"%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%が新たな君主となりました！", COLOR("注意"), "W")

DRAWLINE
PRINTL 新しい勢力の色を選んで下さい
DRAWLINE
CALL CHANGE_COUNTRY_COLOR(CFLAG:MASTER:所属, 1)
DRAWLINE

PRINTL 
SETCOLOR COLOR("注意")
PRINTFORMW 混乱の影響で、我が勢力の兵力が減少しました
PRINTFORMW 軍を再編するため、我が軍の全部隊を解散させました
RESETCOLOR

FOR LOCAL:0, 0, MAX_CITY
	SIF CITY_OWNER:LOCAL == CFLAG:MASTER:所属
		TIMES CITY_SOLDIER:LOCAL, 0.8
NEXT

CALL CLEAR_ALL_UNIT(CFLAG:MASTER:所属, 1)
TIMES COUNTRY_SOLDIER:(CFLAG:MASTER:所属), 0.8

;旧君主が部隊に配属されている場合強制解除
CALL FORCE_FREE(君主)

IF 国譲りフラグ
	CALL COLORPRINT(@"%ANAME(MASTER)%の説得で、%ANAME(君主)%は代替わりに同意した……", COLOR("注意"), "W")
ELSEIF MIN(MAX(ABL:君主:武闘, ABL:君主:防衛, ABL:君主:知略, ABL:君主:政治), 80) < RAND:100
		CALL COLORPRINT(@"現君主である%ANAME(君主)%の身柄を確保した！", COLOR("注意"), "W")
		PRINTFORML 
		CALL SELECT_PRISONER_TREATMENT(勢力, 勢力, 君主)
;君主が逃亡
ELSE
	CALL COLORPRINT(@"現君主である%ANAME(君主)%の確保に失敗しました…", COLOR("注意"), "L")
	IF GET_NEW_COUNTRY() == -1 || ABL:君主:野心 < RAND:100
		$INDEPENDENCE_FAILED
		PRINTFORMW %ANAME(君主)%は放浪した模様です
		CALL CHANGE_COUNTRY(君主, 0, 1)
		CALL CHANGE_RELATION_O_TO_C(君主, CFLAG:MASTER:所属, -500, 1000)
	ELSE
		CALL NPC_INDEPENDENCE(君主)
		SIF RESULT == -1
			GOTO INDEPENDENCE_FAILED
	ENDIF
ENDIF


;-------------------------------------------------
;国譲り
;-------------------------------------------------
@PLAYER_GIVE_COUNTRY
#DIM 勢力
#DIM 君主
#DIM 国譲りフラグ
PRINTFORML 君主から退き、新たな君主を立てます
PRINTFORML 新君主は陥落している士官の中から選べます
PRINTFORML 本当に国譲りを実行しますか？

CALL ASK_YN()
SIF RESULT == 1
	RETURN

;全部隊の解散
CALL CLEAR_ALL_UNIT(CFLAG:MASTER:所属, 1)

勢力 = CFLAG:MASTER:所属
君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
国譲りフラグ = IS_LOVER(君主) || IS_SLAVE(君主)

;調教参加フラグをクリア
CVARSET CFLAG, 6, 0

DRAWLINE
PRINTFORML 新たな君主を決めて下さい
DRAWLINE
CALL SELECT_CHARA_LIST_ONLY_LOGIC_SLG("育児対象, 別勢力, 放浪, 死亡, 未登場, 動物, !IS_LOVER&&!IS_SLAVE", "")

SIF RESULT == -1
	RETURN

COUNTRY_BOSS:(CFLAG:MASTER:所属) = GET_ID(RESULT)

CALL COLORPRINT(@"%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%が新たな君主となりました！", COLOR("注意"), "W")
