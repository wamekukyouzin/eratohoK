;コンフィグから行う強制データ操作 勢力図の変更

;-------------------------------------------------
;各コマンドが選択可能かどうかを調べる
;-------------------------------------------------
@CONFIG_DATA_EDIT_SLG
#DIM MODE
#DIM T_COUNTRY
#DIM T_CITY
#DIM LIST_COUNTRY, MAX_COUNTRY
#DIM LIST_POINT
#DIM LIST_MAX
#DIM FIRST_LINE
;注意書き
PRINTL ※勢力図の変更を行うと、終了時に以下の変更がなされます
PRINTL 　◎支配都市をもたない勢力が消滅し、仕えていた士官が全て放浪状態になる
PRINTL 　◎全勢力の全ての部隊が解散され、兵数が最大数の1/2に変更される
PRINTL 　◎全都市の守将が解除され、防衛兵数が500になる
CALL ASK_YN("続行", "中止")
IF RESULT == 1
	RETURN
ENDIF

CALL SINGLE_DRAWLINE

MODE = 0
T_COUNTRY = 0
T_CITY = 0
LIST_POINT = 0

;国家リストの作成
LIST_COUNTRY:0 = 0
LIST_MAX = 1
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0)
		LIST_COUNTRY:(LIST_MAX) = LOCAL:0
		LIST_MAX ++
	ENDIF
NEXT

FIRST_LINE = LINECOUNT
$SHOW_LOOP

;コマンドの選択可否判定
CALL CHECK_SAVE_SLG

;マップとコマンドボタンを表示
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP(0)
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE

CALL PRINTBUTTON_EX(" 0[都市情報の表示]", 0, 1, MODE == 0)
PRINT     
CALL PRINTBUTTON_EX(" 1[支配都市の追加]", 1, 1, MODE == 1)
IF MODE != 1
	SETCOLOR カラー_選択不可
ENDIF
IF T_COUNTRY == 0
	PRINTFORM (対象勢力:無所属----
ELSE
	LOCALS:0 = %ANAME(GET_COUNTRY_BOSS(T_COUNTRY))%
	PRINTFORM (対象勢力:%LOCALS:0%
	FOR LOCAL:0, 0, 10 - STRLENS(LOCALS:0)
		PRINT -
	NEXT
ENDIF
CALL PRINTBUTTON_EX("2[変更]", 2, MODE == 1)
PRINT   
CALL PRINTBUTTON_EX("3[統一]", 3, MODE == 1 && T_COUNTRY != 0)
IF MODE != 1
	SETCOLOR カラー_選択不可
ENDIF
PRINTL )
RESETCOLOR

;都市の情報を表示
CALL SHOW_CITY_INFO_EDIT(T_CITY)

CALL PRINTBUTTON_EX("99[終了]", 99, 1)
PRINTL 

$INPUT_LOOP
INPUT

REDRAW 0

IF RESULT == 99
	;支配都市を持たない勢力を削除
	FOR LOCAL:0, 1, MAX_COUNTRY
		IF IS_COUNTRY(LOCAL:0) && GET_OWN_CITY(LOCAL:0) == 0 && COUNTRY_EVENT_ID:(LOCAL:0) != SP_COUNTRY_ID:(特殊勢力_野盗)
			FOR LOCAL:1, 0, CHARANUM
				IF CFLAG:(LOCAL:1):所属 == LOCAL:0
					CALL CHANGE_COUNTRY(LOCAL:1, 0)
				ENDIF
			NEXT
			CALL DESTROY_COUNTRY(LOCAL:0)
		;野盗勢力は都市数に応じて外交禁止
		ELSEIF COUNTRY_EVENT_ID:(LOCAL:0) == SP_COUNTRY_ID:(特殊勢力_野盗) && GET_OWN_CITY(LOCAL:0) == 0
			COUNTRY_IS_CLOSED:(LOCAL:0) = 1
		ELSEIF COUNTRY_EVENT_ID:(LOCAL:0) == SP_COUNTRY_ID:(特殊勢力_野盗) && GET_OWN_CITY(LOCAL:0) > 0
			COUNTRY_IS_CLOSED:(LOCAL:0) = 0
		ENDIF
	NEXT
	;全ての部隊を解散＆兵数を最大値の半分に調整
	FOR LOCAL:0, 1, MAX_COUNTRY
		IF IS_COUNTRY(LOCAL:0)
			CALL CLEAR_ALL_UNIT(LOCAL:0)
			COUNTRY_SOLDIER:(LOCAL:0) = GET_SUM_ECONOMY(LOCAL:0) / 20
		ENDIF
	NEXT
	;全都市の防衛解除
	FOR LOCAL:0, 1, MAX_CITY
		IF CITY_TYPE:(LOCAL:0) == 0 && GET_CITYNAME(LOCAL:0) != "無名"
			LOCAL:1 = CITY_OWNER:(LOCAL:0)
			COUNTRY_SOLDIER:(LOCAL:1) = MAX(COUNTRY_SOLDIER:(LOCAL:1) - 500, 0)
			CITY_SOLDIER:(LOCAL:0) = 500
			CITY_COMMANDER:(LOCAL:0) = 0
		ENDIF
	NEXT
	REDRAW 1
	RETURN
ELSEIF RESULT == 0 && MODE != 0
	MODE = 0
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 1 && MODE != 1
	MODE = 1
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 2
	LIST_POINT ++
	IF LIST_POINT >= LIST_MAX
		LIST_POINT = 0
	ENDIF
	T_COUNTRY = LIST_COUNTRY:LIST_POINT
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 3 && T_COUNTRY != 0
	FOR LOCAL:0, 1, MAX_CITY
		IF CITY_TYPE:(LOCAL:0) == 0 && GET_CITYNAME(LOCAL:0) != "無名"
			CITY_OWNER:(LOCAL:0) = T_COUNTRY
		ENDIF
	NEXT
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 10
	CALL EDIT_SET_ECONOMY(T_CITY)
	GOTO SHOW_LOOP
ELSEIF RESULT == 11
	CALL EDIT_SET_GUARD_RATE(T_CITY)
	GOTO SHOW_LOOP
ELSEIF RESULT >= 1000 && RESULT < 1000 + MAX_CITY
	LOCAL:2 = RESULT - 1000
	IF CITY_TYPE:(LOCAL:2) == 0 && GET_CITYNAME(LOCAL:2) != "無名"
		IF MODE == 0
			T_CITY = LOCAL:2
		ELSE
			CITY_OWNER:(LOCAL:2) = T_COUNTRY
		ENDIF
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;エディット用の都市情報表示関数
;-------------------------------------------------
@SHOW_CITY_INFO_EDIT(ARG:0)
IF ARG:0 == 0
	PRINTL 
	PRINTL 
	RETURN
ENDIF

LOCAL:6 = CITY_OWNER:(ARG:0)
LOCAL:7 = GET_COUNTRY_BOSS(LOCAL:6)
LOCALS:0 = ◆%GET_CITYNAME(ARG:0)%◆
PRINTFORM %LOCALS:0%%TOSTR_SPACE((10 - STRLENS(LOCALS:0)) / 2 * 2)%所属:
IF LOCAL:7 >= 0
	SETCOLOR COUNTRY_COLOR:(LOCAL:6)
	LOCALS:0 = %ANAME(LOCAL:7)%
	PRINTFORM %LOCALS:0%
	RESETCOLOR
ELSE
	LOCALS:0 = ----
	PRINTFORM %LOCALS:0%
ENDIF
PRINTL 
LOCAL:8 = CITY_ECONOMY:(ARG:0) * 3 / 10000 + 100
PRINTFORM           経済規模:{CITY_ECONOMY:(ARG:0) / 100, 4}/{CITY_ECONOMY_LIMIT:(ARG:0) / 100, 4}--
PRINTBUTTON "10[変更]", 10
PRINTFORM      防衛倍率:%DECIMAL_STRING(CITY_GUARD:(ARG:0), 2)%(都市)--
PRINTBUTTON "11[変更]", 11
PRINTL 

;-------------------------------------------------
;経済規模の設定
;-------------------------------------------------
@EDIT_SET_ECONOMY(ARG:0)
PRINTFORML 経済規模を設定して下さい(1～9999)  現在:{CITY_ECONOMY:(ARG:0) / 100}
INPUT
LOCAL:0 = LIMIT(RESULT, 1, 9999)
PRINTFORML 経済規模を{LOCAL:0}に設定しました

PRINTFORML 経済規模の基本限界を設定して下さい({LOCAL:0}～9999)  現在:{CITY_ECONOMY_LIMIT:(ARG:0) / 100}
INPUT
LOCAL:1 = LIMIT(RESULT, LOCAL:0, 9999)
PRINTFORMW 経済規模の基本限界を{LOCAL:1}に設定しました

CITY_ECONOMY:(ARG:0) = LOCAL:0 * 100
CITY_ECONOMY_LIMIT:(ARG:0) = LOCAL:1 * 100

;-------------------------------------------------
;防衛倍率の設定
;-------------------------------------------------
@EDIT_SET_GUARD_RATE(ARG:0)
PRINTFORML 防衛倍率を設定して下さい(実際の100倍の値を入力  10～999)  現在:{CITY_GUARD:(ARG:0)}
INPUT
CITY_GUARD:(ARG:0) = LIMIT(RESULT, 10, 999)
PRINTFORMW 防衛倍率を{CITY_GUARD:(ARG:0)}に設定しました
