[SKIPSTART]
MapLibrary
==========
概要
----
　eratohoKのマップ追加を楽にすることを目的に作ったもの。決まった書式で地図を書きそれを行ごとに関数に送り込むことで地図を実際に表示するものと同じようにERBに記述することができる。

使い方、使用例
--------------
MAP_SAMPLE.ERBを参照

外で使うために用意された関数群
------------------------------
+ @DRAWMAP_INIT(MENU_TYPE = 0, EMPTY_LINES = 0, AUTO_READ_CITYNAMES = 0)
+ @DRAWMAP_LINE_S(LINE, CITYNAMES:0 = "", CITYNAMES:1 = "", CITYNAMES:2 = "", CITYNAMES:3 = "", CITYNAMES:4 = "", CITYNAMES:5 = "", CITYNAMES:6 = "", CITYNAMES:7 = "", CITYNAMES:8 = "", CITYNAMES:9 = "")
+ @DRAWMAP_END()
	+ INITを読んでからLINE_Sを好きな回数読んでENDを呼ぶとマップが表示できる！
+ @REGISTER_ROUTE_S(TAR_CITYNAME, CITYNAMES:0 = "", CITYNAMES:1 = "", CITYNAMES:2 = "", CITYNAMES:3 = "", CITYNAMES:4 = "", CITYNAMES:5 = "", CITYNAMES:6 = "", CITYNAMES:7 = "", CITYNAMES:8 = "", CITYNAMES:9 = "")
+ @CITY_ECONOMY_S(ARGS:0, ARG:0)
+ @CITY_GUARD_S(ARGS:0, ARG:0)
	+ _Sがついてないものを都市名から自動でGET_CITYNUMBERを使って設定できるようにしたもの

予想される問題
--------------
+ 即席で作ったからこのファイルを理解するのが大変
+ AUTO_READ_CITYNAMESを利用するとCPUに問題がある環境だと少し負荷が出る可能性がある
+ 流し込まれる大量のDEBUGPRINT
[SKIPEND]


;このライブラリの設定部分を全部この関数に放り投げたい
;ARG
	;MENU_TYPE           行頭に表示するメニューを選択する
	;EMPTY_LINES         地図の前に空行を出力するとき空行の行数を設定する
	;AUTO_READ_CITYNAMES 都市名をCITY_SHORT_NAMEから自動的に読み出す
@DRAWMAP_INIT(MENU_TYPE = 0, EMPTY_LINES = 0, AUTO_READ_CITYNAMES = 0)
#DIM MENU_TYPE
#DIM EMPTY_LINES
#DIM AUTO_READ_CITYNAMES
#DIM LINE
IF MENU_TYPE != 0
	DRAWMAP_MENUBARTYPE = MENU_TYPE
	DRAWMAP_LINECOUNT = 0
ELSE
	DRAWMAP_MENUBARTYPE = 0
	DRAWMAP_LINECOUNT = -1
ENDIF

IF EMPTY_LINES > 0
	FOR DRAWMAP_LINECOUNT, 0, EMPTY_LINES
		CALL DRAWMAP_PRINT_MENU(DRAWMAP_MENUBARTYPE, DRAWMAP_LINECOUNT)
		PRINTL
	NEXT
ENDIF

IF AUTO_READ_CITYNAMES
	DRAWMAP_AUTO_READ_CITYNAMES = 1
ELSE
	DRAWMAP_AUTO_READ_CITYNAMES = 0
ENDIF

;マップ表示の後処理をする関数　セットで呼び出すこと
@DRAWMAP_END()
#DIM LINE
IF INRANGE(DRAWMAP_LINECOUNT, 0, MENU_HEIGHT - 1)
	FOR LINE, DRAWMAP_LINECOUNT, MENU_HEIGHT
		CALL DRAWMAP_PRINT_MENU(DRAWMAP_MENUBARTYPE, LINE)
		PRINTL
	NEXT
ENDIF
DRAWMAP_LINECOUNT = -1

@DRAWMAP_LINE(LINE, CITIES:0 = -1, CITIES:1 = -1, CITIES:2 = -1, CITIES:3 = -1, CITIES:4 = -1, CITIES:5 = -1, CITIES:6 = -1, CITIES:7 = -1, CITIES:8 = -1, CITIES:9 = -1)
#DIMS LINE
#DIM CITIES,10
#DIMS CITY
#DIM NEXT_CITY
#DIM CITIES_SIZE
#DIM READING_POSITION
#DIMS NOW_READING
#DIM LINE_LENGTH
#DIM CITY_NAME_READING
#DIM CITY_ID
LINE_LENGTH = STRLENSU(LINE)
NEXT_CITY = 0
CITY '= ""
CITY_NAME_READING = 0

;メニュー表示
IF DRAWMAP_MENUBARTYPE != 0
	CALL DRAWMAP_PRINT_MENU(DRAWMAP_MENUBARTYPE, DRAWMAP_LINECOUNT)
	DRAWMAP_LINECOUNT++
ENDIF

;DEBUGPRINTL
;DEBUGPRINTFORML start Line = %LINE%
;入力された文字列から1文字ずつ読み込む
FOR READING_POSITION, 0, LINE_LENGTH
	NOW_READING '= SUBSTRINGU(LINE, READING_POSITION, 1)
	;DEBUGPRINTFORM Read %NOW_READING% Is 

	;読み込んだ文字により中継地点の記号、都市もしくは地域名になる文字、それ以外の3パターンに分岐
	IF NOW_READING  == "◇" || NOW_READING == "●"
		;中継地点ならば即座に都市番号を注ぎ込む　もし他でエラーが出ていたら中継地点に漏れ出すので参考にしよう
		;todo:Cities:NextCityが-1である場合の例外を追加する
		CALL DRAWMAP_CITY_BUTTON(CITIES:NEXT_CITY)
		;DEBUGPRINTFORM Way Point Number = {CITIES:NEXT_CITY}
		NEXT_CITY++
	ELSEIF STRCOUNT(NOW_READING, @"\\w")
		;文字列だった場合は連続した文字列をため込み文字列が終了したらまとめて処理するフラグを立てる
		CITY += NOW_READING
		CITY_NAME_READING = 1
		;DEBUGPRINTFORM City or Area So Memory = %CITY%
	ELSE
		IF CITY_NAME_READING
			;文字列が終了した後の処理　都市名か地域名かを検査する
			;DEBUGPRINTFORM NEXT Piece Of City or Area And Last Is 
			IF DRAWMAP_AUTO_READ_CITYNAMES
				CITY_ID = FINDELEMENT(CITY_NAME_SHORT, CITY, 1, CITY_NUM + 1, 1)
				IF CITY_ID != -1
					;都市名だった場合はボタンを表示する　都市名かどうかは地図に実際に表示する文字列であるCITY_NAME_SHORTを使う　表記揺れは判別できないので注意
					CALL DRAWMAP_CITY_BUTTON(CITY_ID)
					;DEBUGPRINTFORM City Name = %CITY% Number = {CITY_ID}
					PRINTS NOW_READING
				ELSE
					;都市名じゃない場合は斜体で地域名を表示する　地域名の次に横棒以外の記号があると地図が崩れるので注意
					FONTSTYLE 2
					PRINTS CITY
					PRINTS NOW_READING
					FONTSTYLE 0
					;DEBUGPRINTFORM Area Name = %CITY% 
					;SIF CITIES:NEXT_CITY != -1
						;DEBUGPRINTFORM Is Not In CityShortNames
				ENDIF
			ELSE
				IF CITIES:NEXT_CITY != -1 && CITY == CITY_NAME_SHORT:(CITIES:NEXT_CITY)
					;都市名だった場合はボタンを表示する　都市名かどうかは地図に実際に表示する文字列であるCITY_NAME_SHORTを使う　表記揺れは判別できないので注意
					CALL DRAWMAP_CITY_BUTTON(CITIES:NEXT_CITY)
					;DEBUGPRINTFORM City Name = %CITY% Number = {CITIES:NEXT_CITY}
					NEXT_CITY++
					PRINTS NOW_READING
				ELSE
					;都市名じゃない場合は斜体で地域名を表示する　地域名の次に横棒以外の記号があると地図が崩れるので注意
					FONTSTYLE 2
					PRINTS CITY
					PRINTS NOW_READING
					FONTSTYLE 0
					;DEBUGPRINTFORM Area Name = %CITY% 
					;SIF CITIES:NEXT_CITY != -1
						;DEBUGPRINTFORM Is Not %CITY_NAME_SHORT:(CITIES:NEXT_CITY)%
					ENDIF
			ENDIF
			CITY_NAME_READING = 0
			CITY '= ""
		ELSE
			;DEBUGPRINTFORM Way or Else
			PRINTS NOW_READING
		ENDIF
	ENDIF
	;DEBUGPRINTL
NEXT
;DEBUGPRINTL Line Is End
;行の最後に文字列が存在する場合の後処理　面倒だからコピペで
IF CITY_NAME_READING
	;DEBUGPRINTFORM Last Is 
	IF CITIES:NEXT_CITY != -1 && CITY == CITY_NAME_SHORT:(CITIES:NEXT_CITY)
		CALL DRAWMAP_CITY_BUTTON(CITIES:NEXT_CITY)
		;DEBUGPRINTFORM City Name = %CITY% Number = {CITIES:NEXT_CITY}
		NEXT_CITY++
	ELSE
		FONTSTYLE 2
		PRINTS CITY
		FONTSTYLE 0
		;DEBUGPRINTFORM Area Name = %CITY% 
		;SIF CITIES:NEXT_CITY != -1
			;DEBUGPRINTFORM Is Not %CITY_NAME_SHORT:(CITIES:NEXT_CITY)%
	ENDIF
	CITY_NAME_READING = 0
	CITY '= ""
	;DEBUGPRINTL
ENDIF
PRINTL
;DEBUGPRINTL end
RETURN

@DRAWMAP_LINE_S(LINE, CITYNAMES:0 = "", CITYNAMES:1 = "", CITYNAMES:2 = "", CITYNAMES:3 = "", CITYNAMES:4 = "", CITYNAMES:5 = "", CITYNAMES:6 = "", CITYNAMES:7 = "", CITYNAMES:8 = "", CITYNAMES:9 = "")
#DIMS LINE
#DIM CITIES, 10
#DIMS CITYNAMES, 10
VARSET CITIES
CITIES:0 = GET_CITYNUMBER(CITYNAMES:0), GET_CITYNUMBER(CITYNAMES:1), GET_CITYNUMBER(CITYNAMES:2), GET_CITYNUMBER(CITYNAMES:3), GET_CITYNUMBER(CITYNAMES:4), GET_CITYNUMBER(CITYNAMES:5), GET_CITYNUMBER(CITYNAMES:6), GET_CITYNUMBER(CITYNAMES:7), GET_CITYNUMBER(CITYNAMES:8), GET_CITYNUMBER(CITYNAMES:9)
CALL DRAWMAP_LINE(LINE, CITIES:0 == -1 ? -1 # CITIES:0, CITIES:1 == -1 ? -1 # CITIES:1, CITIES:2 == -1 ? -1 # CITIES:2, CITIES:3 == -1 ? -1 # CITIES:3, CITIES:4 == -1 ? -1 # CITIES:4, CITIES:5 == -1 ? -1 # CITIES:5, CITIES:6 == -1 ? -1 # CITIES:6, CITIES:7 == -1 ? -1 # CITIES:7, CITIES:8 == -1 ? -1 # CITIES:8, CITIES:9 == -1 ? -1 # CITIES:9)


@REGISTER_ROUTE_S(TAR_CITYNAME, CITYNAMES:0 = "", CITYNAMES:1 = "", CITYNAMES:2 = "", CITYNAMES:3 = "", CITYNAMES:4 = "", CITYNAMES:5 = "", CITYNAMES:6 = "", CITYNAMES:7 = "", CITYNAMES:8 = "", CITYNAMES:9 = "")
#DIMS TAR_CITYNAME
#DIM CITIES, 10
#DIM TAR_CITY
#DIMS CITYNAMES, 10
VARSET CITIES
CITIES:0 = GET_CITYNUMBER(CITYNAMES:0), GET_CITYNUMBER(CITYNAMES:1), GET_CITYNUMBER(CITYNAMES:2), GET_CITYNUMBER(CITYNAMES:3), GET_CITYNUMBER(CITYNAMES:4), GET_CITYNUMBER(CITYNAMES:5), GET_CITYNUMBER(CITYNAMES:6), GET_CITYNUMBER(CITYNAMES:7), GET_CITYNUMBER(CITYNAMES:8), GET_CITYNUMBER(CITYNAMES:9)
TAR_CITY = GET_CITYNUMBER(TAR_CITYNAME)
CALL REGISTER_ROUTE(TAR_CITY, CITIES:0 == -1 ? -99 # CITIES:0, CITIES:1 == -1 ? -99 # CITIES:1, CITIES:2 == -1 ? -99 # CITIES:2, CITIES:3 == -1 ? -99 # CITIES:3, CITIES:4 == -1 ? -99 # CITIES:4, CITIES:5 == -1 ? -99 # CITIES:5, CITIES:6 == -1 ? -99 # CITIES:6, CITIES:7 == -1 ? -99 # CITIES:7, CITIES:8 == -1 ? -99 # CITIES:8, CITIES:9 == -1 ? -99 # CITIES:9)

@CITY_ECONOMY_S(ARGS:0, ARG:0)
SIF GET_CITYNUMBER(ARGS:0) == -1
	THROW @CITY_ECONOMY_Sに渡された都市名%ARGS:0%は存在しません
CITY_ECONOMY:GET_CITYNUMBER(ARGS:0) = ARG:0

@CITY_GUARD_S(ARGS:0, ARG:0)
SIF GET_CITYNUMBER(ARGS:0) == -1
	THROW @CITY_GUARD_Sに渡された都市名%ARGS:0%は存在しません
CITY_GUARD:GET_CITYNUMBER(ARGS:0) = ARG:0

@DRAWMAP_PRINT_MENU(MENU_TYPE, MENU_LINE)
#DIM MENU_TYPE
#DIM MENU_LINE
SELECTCASE MENU_TYPE
	CASE DRAWMAP_MENUBARTYPE_SLGMENU
		CALL COLUMN_LEFT_MENU_SHOW_SLG(MENU_LINE)
	CASE DRAWMAP_MENUBARTYPE_DEBUGMENU
		CALL PRINT_MAPDEBUG_MENU(MENU_LINE)
ENDSELECT

@DRAWMAP_CITY_BUTTON(CITY_ID)
#DIM CITY_ID
SELECTCASE DRAWMAP_MENUBARTYPE
	CASE DRAWMAP_MENUBARTYPE_DEBUGMENU
		CALL DRAW_CITY_BUTTON_MAPDEBUG(CITY_ID)
	CASEELSE
		CALL DRAWCITY_BUTTON(CITY_ID)
ENDSELECT
