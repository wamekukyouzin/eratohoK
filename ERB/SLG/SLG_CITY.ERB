;-------------------------------------------------
;都市ARG:0の情報を表示
;-------------------------------------------------
@SHOW_CITY_INFO(ARG:0, ARG:1 = 0)
#DIM SPACE
#DIM 行数
#DIM SHOW_UNITS
#DIM UNIT_PAIR,10,2
#DIM OWNER
#DIM CNT_ENEMY
#DIM CNT_UNIT
#DIM TARGET_NUM
SHOW_UNITS = 0

;領土割譲から呼ばれた場合の行番号あふれを退避しつつ開始ボタンを表示
IF ARG:1 == 0 && LINECOUNT - FIRST_COLUMN_RIGHT_LINE_SLG < LINES_SHOP_DRAW
	CALL COLUMN_LEFT_MENU_SHOW_SLG(LINECOUNT - FIRST_COLUMN_RIGHT_LINE_SLG)
	;SLGの右カラムでなければ区切り線と改行もオフにする
	CALL COLUMN_RIGHT_LINE
	CALL COLUMN_RIGHT_PRINTL_SLG
ENDIF


;指定された都市または中継地点が存在しない場合
IF GET_RELAYNAME(ARG:0) == "無名"
	PRINT 自勢力の都市、自勢力と隣接する都市の情報を見ることができます
	;以下空行
	IF ARG:1 == 0
		CALL COLUMN_RIGHT_CLEAR_SLG
	ELSE
		PRINTL
	ENDIF
	RETURN
ENDIF

;防衛情報が参照可能かどうかのフラグ(自勢力の都市、または自勢力と隣接する都市のみ可能。観戦モードなら全て可)
LOCAL:4 = CFLAG:MASTER:所属 >= 1 && (CITY_OWNER:(ARG:0) == CFLAG:MASTER:所属 || IS_NEIGHBORING_COUNTRY(ARG:0, CFLAG:MASTER:所属) >= 0)
LOCAL:4 = LOCAL:4 || FLAG:観戦モード

;都市上の部隊を10部隊まで取得
CNT_ENEMY = 0
CNT_UNIT  = 0
OWNER = CITY_OWNER:(ARG:0)
FOR LOCAL:0, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL:0)
		CONTINUE
	FOR LOCAL:1, 0, MAX_UNIT
		IF UNIT_SOLDIER:(LOCAL:0):(LOCAL:1) > 0 && UNIT_POSITION:(LOCAL:0):(LOCAL:1) == ARG:0
			IF CNT_UNIT < 10
				UNIT_PAIR:CNT_UNIT:0 = LOCAL:0
				UNIT_PAIR:CNT_UNIT:1 = LOCAL:1
			ENDIF
			CNT_UNIT++
			SIF OWNER != LOCAL:0
				CNT_ENEMY++
		ENDIF
	NEXT
NEXT

TARGET_NUM = -1
;都市を目的とする部隊を取得
FOR LOCAL, 0, MAX_UNIT
	IF UNIT_TARGET:(CFLAG:MASTER:所属):LOCAL == ARG:0
		TARGET_NUM = LOCAL
		BREAK
	ENDIF
NEXT

;中継地点
IF CITY_TYPE:(ARG:0) == 1
	LOCALS:0 = ◆中継地点(%GET_RELAYNAME(ARG:0)%)◆
	PRINTFORM %LOCALS:0%
	IF ARG:1 == 0
		CALL COLUMN_RIGHT_PRINTL_SLG
	ELSE
		PRINTL
	ENDIF
;都市
ELSE
	LOCAL:6 = CITY_OWNER:(ARG:0)
	LOCAL:7 = GET_COUNTRY_BOSS(LOCAL:6)
	LOCALS:0 = ◆%GET_CITYNAME(ARG:0)%◆
	PRINTFORM %LOCALS:0%
	SPACE = 40
	PRINTFORM %TOSTR_SPACE((SPACE - STRLENS(LOCALS:0)) / 2 * 2)%
	;CALL SPACER( MAX( 0, 23 - STRLENS(LOCALS:0) ) )
	IF LOCAL:7 >= 0
		PRINT 所属:
		SETCOLOR COUNTRY_COLOR:(LOCAL:6)
		PRINTFORM %SNAME(LOCAL:7)%
		PRINTFORM  
		SELECTCASE CHECK_COUNTRY_RELATION_F(CFLAG:MASTER:所属, LOCAL:6)
			CASE 0
				SETCOLOR カラー_警告
				PRINT 敵
			CASE 1
				SETCOLOR カラー_停戦
				PRINT 停
			CASE 2
				SETCOLOR カラー_連合
				PRINT 連
			CASE 3
				SETCOLOR カラー_同盟
				PRINT 同
			CASE 4
				SETCOLOR カラー_永久同盟
				PRINT 永
			CASE 5
				SETCOLOR カラー_シアン
				PRINT 自
		ENDSELECT
		PRINT  
		RESETCOLOR
		PRINTFORM 都市数:{GET_OWN_CITY(LOCAL:6)}
	ELSE
		PRINT 所属:
		SETCOLOR COUNTRY_COLOR:0
		LOCALS:0 = ----
		PRINTFORM %LOCALS:0%
		LOCALS:1 = 
		RESETCOLOR
	ENDIF
	IF ARG:1 == 0
		CALL COLUMN_RIGHT_PRINTL_SLG
	ELSE
		PRINTL
	ENDIF

	IF !LOCAL:4 || LOCAL:6 == -1
		PRINTFORM 攻:? 防:? 知:? 妖:?
	ELSE
		CALL GET_CITY_COMMANDER_ALL(ARG:0)
		CALL SET_BATTLE_MIRROR_VAL(0, RESULT:0, RESULT:1, RESULT:2)
		PRINTFORM 攻:
		CALL PRINT_ALPHABET_RANK(ランク_部隊, ATTACK_LEVEL(0, ARG:0))
		PRINTFORM  防:
		CALL PRINT_ALPHABET_RANK(ランク_部隊, DEFENSE_LEVEL(0, ARG:0))
		PRINTFORM  知:
		CALL PRINT_ALPHABET_RANK(ランク_部隊, INTELLIGENCE_LEVEL(0, ARG:0))
		PRINTFORM  妖:
		CALL PRINT_ALPHABET_RANK(ランク_部隊, MAGIC_LEVEL(0, ARG:0))
	ENDIF

	PRINTFORM %TOSTR_SPACE(1)%

	IF LOCAL:4
		LOCALS:0 = 兵数: %NUM_FORMAT(CITY_SOLDIER:(ARG:0)), 14, LEFT%
	ELSE
		LOCALS:0 = 兵数:????
	ENDIF
	SPACE = 20
	PRINTPLAINFORM %LOCALS:0%
	PRINTFORM %TOSTR_SPACE(SPACE - STRLENS(LOCALS:0))%
	IF LOCAL:4
		LOCALS:0 = %GET_CITY_COMMANDER_NAME(ARG:0)%
		IF LOCALS:0 == ""
			LOCALS:0 = ----
		ENDIF
		LOCALS:0 = 守将:%LOCALS:0%
	ELSE
		LOCALS:0 = 守将:????
	ENDIF
	PRINTPLAINFORM %LOCALS:0%

	IF ARG:1 == 0
		CALL COLUMN_RIGHT_PRINTL_SLG
	ELSE
		PRINTL
	ENDIF

	LOCAL:8 = CITY_ECONOMY:(ARG:0) / 5000 + 100
	LOCALS:0 = 経済:{CITY_ECONOMY:(ARG:0) / 100, 4}/{CITY_ECONOMY_LIMIT:(ARG:0) / 100, 4}
	SPACE = 20
	PRINTFORM %LOCALS:0%
	PRINTFORM %TOSTR_SPACE(SPACE - STRLENS(LOCALS:0))%
	PRINTFORM 防衛: %DECIMAL_STRING(CITY_GUARD:(ARG:0), 2)%(都市)
	PRINTFORM ×%DECIMAL_STRING(LOCAL:8, 2)%(経済)
	PRINT ×
	IF FLAG:観戦モード || LOCAL:6 == CFLAG:MASTER:所属
		SELECTCASE COUNTRY_POLICY:(CFLAG:MASTER:所属)
			CASE 政策_防衛
				PRINT 1.50
				LOCAL:9 = 15
			CASEELSE
				PRINT 1.00
				LOCAL:9 = 10
		ENDSELECT
		PRINTFORM (政策)
	ELSEIF LOCAL:7 < 0
		PRINTFORM 1.00(政策)
	ELSE
		PRINTFORM ????(政策)
	ENDIF
	PRINT ×
	IF FLAG:観戦モード || LOCAL:6 == CFLAG:MASTER:所属
		IF TECHNOLOGY_STATUS:(CFLAG:MASTER:所属):TECHNOLOGY_BATTLE_PASSIVE >= 6
			PRINT 1.30
			LOCAL:10 = 12
		ELSEIF TECHNOLOGY_STATUS:(CFLAG:MASTER:所属):TECHNOLOGY_BATTLE_PASSIVE >= 2
			PRINT 1.10
			LOCAL:10 = 11
		ELSE
			PRINT 1.00
			LOCAL:10 = 10
		ENDIF
		PRINTFORM (研究)＝%DECIMAL_STRING(CITY_GUARD:(ARG:0) * LOCAL:8 * LOCAL:9 * LOCAL:10 / 10000, 2)%
	ELSEIF LOCAL:7 < 0
		PRINTFORM 1.00(研究)＝%DECIMAL_STRING(CITY_GUARD:(ARG:0) * LOCAL:8 / 100, 2)%
	ELSE
		PRINTFORM ????(研究)＝????
	ENDIF

	IF ARG:1 == 0
		CALL COLUMN_RIGHT_PRINTL_SLG
	ELSE
		PRINTL
	ENDIF

	IF DAY >= SLG_PP:1
		IF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:0), CFLAG:MASTER:所属) <= 0 || CITY_OWNER:(ARG:0)==0
			IF IS_INVADABLE(ARG:0, CFLAG:MASTER:所属)
				PRINTBUTTON "[侵攻]", 2000+ARG:0
			ELSE
				SETCOLOR カラー_選択不可
				PRINT [侵攻]
				RESETCOLOR
				PRINT  
			ENDIF
		ELSEIF IS_PASSABLE(ARG:0, CFLAG:MASTER:所属)
			PRINTBUTTON "[移動]", 2000+ARG:0
			PRINT  
		ENDIF
	ENDIF
	LOCAL:2 = 0
	IF CFLAG:MASTER:所属 >= 1 && LOCAL:6 == CFLAG:MASTER:所属 && CONFIG:302 == 0
		IF CNT_ENEMY > 0
			SETCOLOR カラー_敵
			FONTSTYLE 4
			LOCAL:2 = 1
		ENDIF
		PRINTBUTTON "[兵数設定]" , ARG:0 + 200
		PRINT  
		PRINTBUTTON "[守将設定]" , ARG:0 + 400
		PRINT  
		PRINTBUTTON "[防衛解除]" , ARG:0 + 800
		PRINT  
		IF CITY_INVESTED:(ARG:0)
			PRINTPLAIN [投資済]
		ELSEIF NOW_INVEST == CALC_MAX_INVEST() && LOCAL:2 != 1
			SETCOLOR カラー_選択不可
			PRINTPLAINFORM [投資{NOW_INVEST}/{CALC_MAX_INVEST()}]
			RESETCOLOR
		ELSE
			PRINTBUTTON @"[投資{NOW_INVEST}/{CALC_MAX_INVEST()}]", ARG:0 + 1500
		ENDIF
		RESETCOLOR
		FONTREGULAR
		PRINT  
	ENDIF
	PRINTBUTTON "[色変更]", LOCAL:6 + 600
	PRINT  
	IF IS_COUNTRY(LOCAL:6)
		PRINTBUTTON "[勢力情報]", LOCAL:6 + 650
		PRINT  
		PRINTBUTTON "[技術ツリー]", LOCAL:6 + 750
		PRINT  
	ENDIF
	IF IS_COUNTRY(LOCAL:6) && !COUNTRY_IS_CLOSED:(LOCAL:6) && IS_COUNTRY(CFLAG:MASTER:所属) && !COUNTRY_IS_CLOSED:(CFLAG:MASTER:所属) && SLG_PP:0 <= DAY && LOCAL:6 != CFLAG:MASTER:所属
		PRINTBUTTON "[外交]", LOCAL:6 + 700
		PRINT 
	ENDIF
	IF TARGET_NUM >= 0
		PRINT  
		PRINTFORM 部隊進軍中
	ENDIF
	LOCAL:12 = 0
	FOR LOCAL:11,0,10
		SIF UNIT_POSITION:(CFLAG:MASTER:所属):(LOCAL:11) == ARG:0 && UNIT_SOLDIER:(CFLAG:MASTER:所属):(LOCAL:11) > 0
			LOCAL:12 ++
	NEXT
	IF LOCAL:12
		PRINT  
		SETCOLOR カラー_オレンジ
		PRINTFORM 部隊駐留中
		RESETCOLOR
	ENDIF

	;建造物関連
	IF ARG:1 == 0
		CALL COLUMN_RIGHT_PRINTL_SLG
	ELSE
		PRINTL
	ENDIF
	IF CITY_OWNER:(ARG:0) == CFLAG:MASTER:所属 && IS_COUNTRY(CFLAG:MASTER:所属)
		PRINTBUTTON "[建造物] ", ARG:0 + 3000
	ELSE
		SETCOLOR カラー_選択不可
		PRINTPLAIN [建造物] 
		RESETCOLOR
	ENDIF
	PRINTFORM %GET_DEVELOPMENT_NAME(CITY_DEVELOPMENT:(ARG:0))% 
	SIF CITY_DEVELOPMENT:(ARG:0)
		PRINTFORM %GET_DEVELOPMENT_EXPLANATION(CITY_DEVELOPMENT:(ARG:0))%

ENDIF

IF ARG:1 == 0
	CALL COLUMN_RIGHT_PRINTL_SLG
ELSE
	PRINTL
ENDIF
PRINTFORM ◆この場所に存在する部隊(5部隊まで表示)◆
IF ARG:1 == 0
	CALL COLUMN_RIGHT_PRINTL_SLG
ELSE
	PRINTL
ENDIF

FOR LOCAL, 0, MIN(CNT_UNIT,5)
	LOCAL:8 = UNIT_PAIR:(LOCAL:0):0
	LOCAL:1 = UNIT_PAIR:(LOCAL:0):1
	IF LOCAL:8 >= 0
		LOCAL:6 = GET_COUNTRY_BOSS(LOCAL:8)
		IF (!LOCAL:4 || LOCAL:6 == -1) && !IS_STAY_MYUNIT(ARG:0)
			CALL PRINT_UNIT_INFO(LOCAL:8, LOCAL:1, 1)
			SHOW_UNITS++
		ELSE
			CALL PRINT_UNIT_INFO(LOCAL:8, LOCAL:1)
			IF LOCAL:8 == CFLAG:MASTER:所属
				RESETCOLOR
				PRINTBUTTON "[編成]", 2500 + LOCAL:1
			ENDIF
			SHOW_UNITS++
		ENDIF
		IF ARG:1 == 0
			CALL COLUMN_RIGHT_PRINTL_SLG
		ELSE
			PRINTL
		ENDIF
	ENDIF
NEXT

IF !SHOW_UNITS
	CALL COLOR_PRINTL("部隊なし", カラー_選択不可)
	SHOW_UNITS ++
ENDIF

;以下空行
IF ARG:1 == 0
	CALL COLUMN_RIGHT_CLEAR_SLG
ELSE
	PRINTL
ENDIF

@SPACER(ARG:0)
PRINTFORM %TOSTR_SPACE(ARG:0)%
