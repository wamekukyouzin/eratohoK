; ARG:0に侵攻する
@INVADE(ARG:0)
#DIM 勢力
#DIM 部隊攻撃
#DIM 部隊知略
#DIM 部隊防御
#DIM 都市攻撃
#DIM 都市知略
#DIM 都市防御
VARSET 部隊攻撃
VARSET 部隊防御
VARSET 部隊知略
VARSET 都市攻撃
VARSET 都市防御
VARSET 都市知略
CLEARLINE 1
PRINTL 

SIF !IS_INVADABLE(ARG:0, CFLAG:MASTER:所属)
	RETURN

IF DAY < SLG_PP:1
	PRINTFORMW {SLG_PP:1}期までは侵攻を行うことができません
	RETURN 
ENDIF


勢力 = CITY_OWNER:(ARG:0)
IF IS_COUNTRY(CITY_OWNER:(ARG:0))
	;部隊能力を計算
	CALL TMP_CREATE_IS_FREE_MAP
	LOCALS:0 = 
	LOCAL:2 = 0
	CALL CHECK_BEST_COMMANDER(勢力)
	FOR LOCAL, 0, 3
		LOCAL:1 = BEST_COMMANDER:LOCAL
		IF LOCAL:1 >= 0
			SIF LOCAL:2 != 0
				LOCALS:0 = %LOCALS:0%・
			LOCALS:0 = %LOCALS:0%%ANAME(LOCAL:1)%
		ENDIF
		LOCAL:2 ++
	NEXT
	SIF LOCALS:0 != ""
		LOCALS:0 = <%LOCALS:0%>
	CALL SET_BATTLE_MIRROR_VAL(0, BEST_COMMANDER:0, BEST_COMMANDER:1, BEST_COMMANDER:2)
	部隊攻撃 = ATTACK_LEVEL(0, 0)
	部隊防御 = DEFENSE_LEVEL(0, 0)
	部隊知略 = INTELLIGENCE_LEVEL(0, 0)

	;都市能力を計算
	LOCALS:1 = 
	LOCAL:2 = 0
	CALL CHECK_BEST_COMMANDER(CITY_OWNER:(ARG:0))
	FOR LOCAL, 0, 2
		LOCAL:1 = BEST_COMMANDER:LOCAL
		IF LOCAL:1 >= 0
			SIF LOCAL:2 != 0
				LOCALS:1 = %LOCALS:1%・
			LOCALS:1 = %LOCALS:1%%ANAME(LOCAL:1)%
		ENDIF
		LOCAL:2 ++
	NEXT
	SIF LOCALS:1 != ""
		LOCALS:1 = <%LOCALS:1%>
	CALL SET_BATTLE_MIRROR_VAL(0, BEST_COMMANDER:0, BEST_COMMANDER:1)
	都市攻撃 = ATTACK_LEVEL(0, ARG:0)
	都市防御 = DEFENSE_LEVEL(0, ARG:0)
	都市知略 = INTELLIGENCE_LEVEL(0, ARG:0)
ENDIF

$INPUT_LOOP
CALL SINGLE_DRAWLINE
PRINTL ●侵攻する部隊を選択してください
IF IS_COUNTRY(CITY_OWNER:(ARG:0))
	PRINTFORM 現時点でありえる最強の部隊:%LOCALS:0, 32, LEFT% 
	PRINTFORM 攻:
	CALL PRINT_ALPHABET_RANK(ランク_部隊, 部隊攻撃)
	PRINTFORM   防:
	CALL PRINT_ALPHABET_RANK(ランク_部隊, 部隊防御)
	PRINTFORM   知
	CALL PRINT_ALPHABET_RANK(ランク_部隊, 部隊知略)
	PRINTL
	PRINTFORM 現時点でありえる最強の守将:%LOCALS:1, 32, LEFT% 
	PRINTFORM 攻:
	CALL PRINT_ALPHABET_RANK(ランク_部隊, 都市攻撃)
	PRINTFORM   防:
	CALL PRINT_ALPHABET_RANK(ランク_部隊, 都市防御)
	PRINTFORM   知
	CALL PRINT_ALPHABET_RANK(ランク_部隊, 都市知略)
	PRINTL
ENDIF
CALL SINGLE_DRAWLINE
CALL LISTUP_FORCE(CFLAG:MASTER:所属, "CHECK_FORCE_SELECTABLE_INVADE")
CALL SINGLE_DRAWLINE
PRINTBUTTON "20[新規部隊]", 20
PRINTL
PRINTBUTTON " 0[    戻る]", 0
INPUT
LOCAL:10 = RESULT
IF LOCAL:10 >=1 && LOCAL:10 <= MAX_UNIT
	IF CHECK_FORCE_SELECTABLE_INVADE_F(CFLAG:MASTER:所属, LOCAL:10 - 1)
		$EXECUTE_FORCE
		CALL FORCE_SELECT_DEPARTURE_POINT(ARG:0, LOCAL:10 - 1)
		;作成したばかりの部隊で、「やめる」で戻って来たなら所在がないので消しとく
		SIF RESULT == 0 && UNIT_POSITION:(CFLAG:MASTER:所属):(LOCAL:10 - 1) == 0
			CALL CLEAR_UNIT(CFLAG:MASTER:所属, LOCAL:10 - 1, 1)
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSEIF LOCAL:10 == 0
ELSEIF LOCAL:10 == 20
	LOCAL:1 = 0
	FOR LOCAL:0, 0, MAX_UNIT
		IF UNIT_SOLDIER:(CFLAG:MASTER:所属):(LOCAL:0) <= 0
			LOCAL:1 = 1
			CALL CREATE_UNIT(CFLAG:MASTER:所属, LOCAL:0, 1)
			IF RESULT
				LOCAL:10 = LOCAL:0 + 1
				GOTO EXECUTE_FORCE
			ENDIF
			BREAK
		ENDIF
	NEXT
	IF !LOCAL:1
		PRINTW これ以上部隊を編成することはできません
	ENDIF
	RESTART
ELSEIF 100 <= LOCAL:10 && LOCAL:10 < MAX_UNIT + 100
	CALL UNIT_SETTING(CFLAG:MASTER:所属, LOCAL:10 - 100, 1)
	IF RESULT == 1
		LOCAL:10 -= 99
		GOTO EXECUTE_FORCE
	ENDIF
	RESTART
ELSE
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
; ARG:0に移動する
;-------------------------------------------------
@MOVETO(ARG:0)
CLEARLINE 1
PRINTL 
IF DAY < SLG_PP:1
	PRINTFORMW {SLG_PP:1}期までは移動を行うことができません
	RETURN
ELSEIF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:0),   CFLAG:MASTER:所属) < 2
	RETURN
ENDIF

$INPUT_LOOP
CALL SINGLE_DRAWLINE
PRINTL ●移動する部隊を選択してください
CALL SINGLE_DRAWLINE
CALL LISTUP_FORCE(CFLAG:MASTER:所属, "CHECK_FORCE_SELECTABLE_MOVETO", ARG:0)
CALL SINGLE_DRAWLINE
PRINTBUTTON "20[新規部隊]", 20
PRINTL
PRINTBUTTON " 0[    戻る]", 0
INPUT
LOCAL:10 = RESULT
IF LOCAL:10 >=1 && LOCAL:10 <= MAX_UNIT
	IF CHECK_FORCE_SELECTABLE_MOVETO_F(CFLAG:MASTER:所属, LOCAL:10 - 1, ARG:0)
		IF CHECK_COUNTRY_RELATION_F(CFLAG:MASTER:所属, CITY_OWNER:(UNIT_POSITION:(CFLAG:MASTER:所属):(LOCAL:10 - 1))) >= 2
			$EXECUTE_MOVETO
			UNIT_POSITION:(CFLAG:MASTER:所属):(LOCAL:10 - 1) = ARG:0
			PRINTFORML 部隊{LOCAL:10}が%GET_RELAYNAME(ARG:0)%へ移動しました
			IF UNIT_TARGET:(CFLAG:MASTER:所属):(LOCAL:10 - 1) != 0
				UNIT_TARGET:(CFLAG:MASTER:所属):(LOCAL:10 - 1) = 0
				PRINTL 現在の移動目標は解除されます
			ENDIF
		ELSE
			UNIT_TARGET:(CFLAG:MASTER:所属):(LOCAL:10 - 1) = ARG:0
			PRINTFORML 部隊{LOCAL:10}の移動目標を%GET_RELAYNAME(ARG:0)%に設定します
		ENDIF

		WAIT
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSEIF LOCAL:10 == 20
	LOCAL:1 = 0
	FOR LOCAL:0, 0, MAX_UNIT
		IF UNIT_SOLDIER:(CFLAG:MASTER:所属):(LOCAL:0) <= 0
			LOCAL:1 = 1
			CALL CREATE_UNIT(CFLAG:MASTER:所属, LOCAL:0, 1)
			IF RESULT
				LOCAL:10 = LOCAL:0 + 1
				GOTO EXECUTE_MOVETO
			ENDIF
			BREAK
		ENDIF
	NEXT
	IF !LOCAL:1
		PRINTW これ以上部隊を編成することはできません
	ENDIF
	RESTART
ELSEIF LOCAL:10 == 0
ELSE
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
; ARG :0 の勢力の部隊を選択可能リストアップ
; ARGS:1 条件関数
; ARG :2 任意の引数
;  0=選択不可（表示無し）1=選択可 -1=選択不可（カラー_選択不可）-2=選択不可（カラー_警告）
;-------------------------------------------------
@LISTUP_FORCE(ARG:0, ARGS:1, ARG:2=0)
LOCAL:20 = GETCOLOR()
RESETCOLOR
FOR LOCAL:0, 0, MAX_UNIT
	RESETCOLOR
	RESULT = 0
	SIF UNIT_SOLDIER:(ARG:0):(LOCAL:0) <= 0
		CONTINUE
	TRYCALLFORM %ARGS:1%, ARG:0, LOCAL:0, ARG:2

	IF RESULT != 0
		SELECTCASE RESULT
			CASE -1
				SETCOLOR カラー_選択不可
			CASE -2
				SETCOLOR カラー_警告
		ENDSELECT

		LOCALS:0  = {LOCAL:0 + 1, 2}[  部隊{LOCAL:0 + 1, 2}]
		LOCALS:0 += " "
		IF RESULT > 0
			PRINTBUTTON LOCALS:0, LOCAL:0 +1
			PRINT 
			PRINTBUTTON "[編成]", LOCAL:0 + 100
		ELSE
			PRINTFORM %LOCALS:0%
			PRINT 
			PRINTPLAIN [編成]
		ENDIF
		CALL SHOW_UNIT_INFO(CFLAG:MASTER:所属, LOCAL:0, 24)
	ENDIF
NEXT
SETCOLOR LOCAL:20

;-------------------------------------------------
; ARG:0の勢力のARG:1番部隊が侵攻で選択可能かチェックする
; REUTNRN
;  0=選択不可（表示無し）1=選択可 -1=選択不可（カラー_選択不可）-2=選択不可（カラー_警告）
;-------------------------------------------------
@CHECK_FORCE_SELECTABLE_INVADE(ARG:0, ARG:1, ARG:2)
RESULT= CHECK_FORCE_SELECTABLE_INVADE_F(ARG:0, ARG:1, ARG:2)
RETURN RESULT


@CHECK_FORCE_SELECTABLE_INVADE_F(ARG:0, ARG:1, ARG:2)
#FUNCTION

SIF CHECK_COUNTRY_RELATION_F(ARG:0, CITY_OWNER:(UNIT_POSITION:(ARG:0):(ARG:1))) >= 2 && !IS_STAY_ENEMY_UNIT(UNIT_POSITION:(ARG:0):(ARG:1))
	RETURNF 1

RETURNF 0

;-------------------------------------------------
; ARG:0の勢力のARG:1番部隊がARG:2への移動で選択可能かチェックする
; REUTNRN
;  0=選択不可（表示無し）1=選択可 -1=選択不可（カラー_選択不可）-2=選択不可（カラー_警告）
;-------------------------------------------------
@CHECK_FORCE_SELECTABLE_MOVETO(ARG:0, ARG:1, ARG:2)
RESULT= CHECK_FORCE_SELECTABLE_MOVETO_F(ARG:0, ARG:1, ARG:2)
RETURN RESULT
@CHECK_FORCE_SELECTABLE_MOVETO_F(ARG:0, ARG:1, ARG:2)
#FUNCTION
IF UNIT_SOLDIER:(ARG:0):(ARG:1) > 0
	IF UNIT_POSITION:(ARG:0):(ARG:1) == ARG:2
		RETURNF -1
	;自分の位置に敵がいたらだめ
	ELSEIF IS_STAY_ENEMY_UNIT(UNIT_POSITION:(ARG:0):(ARG:1))
		RETURNF -1
	;関係2以上の国にいれば白
	ELSEIF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(UNIT_POSITION:(ARG:0):(ARG:1)), ARG:0) >= 2
		RETURNF 1
	;目的地が繋がっていて目的地が関係2以上の国か中継地点なら白
	ELSEIF IS_ROUTE(UNIT_POSITION:(ARG:0):(ARG:1), ARG:2) && (CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:2), ARG:0) >= 2 || CITY_TYPE:(ARG:2) == 1)
		RETURNF 1
	;そうじゃなかったら灰
	ELSE
		RETURNF -1
	ENDIF
	RETURNF 0
ENDIF


;-------------------------------------------------
; ARG:0へARG:1部隊が侵攻する

;-------------------------------------------------
@FORCE_SELECT_DEPARTURE_POINT(ARG:0, ARG:1)
#DIM POINTER
#DIM POINTER2
#DIM ARR,30
#DIM ARR2,30
#DIM FROM
POINTER = 0
POINTER2 = 0
FOR LOCAL:0, 0, MAX_CITY
	; 条件
	; LOCAL:0が中継地でない
	; LOCAL:0がARG:0と隣接
	; LOCAL:0が通行可能（LOCAL:0の支配国との関係が2以上）

	LOCAL:1 = CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:0),   CFLAG:MASTER:所属)
	LOCAL:2 = CHECK_COUNTRY_RELATION_F(CITY_OWNER:(LOCAL:0), CFLAG:MASTER:所属)
	IF CITY_TYPE:(LOCAL:0) == 0 && IS_ROUTE(ARG:0, LOCAL:0) && (LOCAL:1 <= 0 || CITY_OWNER:(ARG:0) == 0) && LOCAL:2 >= 2
		IF IS_ROUTE(ARG:0, LOCAL:0) == 1
			ARR:POINTER = LOCAL:0
			POINTER++
		ELSE
			ARR2:POINTER2 = LOCAL:0
			POINTER2++
		ENDIF
	ENDIF
NEXT

IF (POINTER + POINTER2) == 0
	PRINT このメッセージが表示されたら報告をお願いします
ELSEIF (POINTER + POINTER2) == 1
	IF POINTER == 1
		FROM = ARR:0
	ELSE
		FROM = ARR2:0
	ENDIF
ELSE
	CALL SINGLE_DRAWLINE
	PRINTL ●出発地点を選んでください
	CALL SINGLE_DRAWLINE
	IF POINTER
		PRINTL 1ターンで進軍可能
		PRINT   
		FOR LOCAL:0, 0, POINTER
			LOCAL:1 = ARR:(LOCAL:0)
			SIF LOCAL:1 == UNIT_POSITION:(CFLAG:MASTER:所属):(ARG:1)
				SETCOLOR カラー_オレンジ
			CALL GET_STAY_ENEMY_UNIT(LOCAL:1)
			SIF RESULT > 0
				SETCOLOR カラー_警告
			LOCALS:0 = [{LOCAL:0+1}] %GET_CITYNAME(ARR:(LOCAL:0))%
			PRINTBUTTON LOCALS:0, LOCAL:0 + 1
			RESETCOLOR
			PRINT   
		NEXT
		PRINTL 
		PRINTL 
	ENDIF

	IF POINTER2
		PRINTL 中継地を経由
		PRINT   
		FOR LOCAL:0, 0, POINTER2
			LOCAL:1 = ARR2:POINTER2
			SIF LOCAL:1 == UNIT_POSITION:(CFLAG:MASTER:所属):(ARG:1)
				SETCOLOR カラー_オレンジ
			LOCALS:0 = [{LOCAL:0+101}] %GET_CITYNAME(ARR2:(LOCAL:0))%
			PRINTBUTTON LOCALS:0, LOCAL:0 + 101
			RESETCOLOR
			PRINT   
		NEXT
		PRINTL 
		PRINTL 
	ENDIF
	PRINTBUTTON "[0] やめる", 0
	PRINTL 
	PRINTL 
	$INPUT_LOOP2
	INPUT
	LOCAL:11 = RESULT

	IF POINTER && INRANGE(LOCAL:11, 1, 0+POINTER)
		FROM = ARR:(LOCAL:11 - 1)
	ELSEIF POINTER2 && INRANGE(LOCAL:11, 101, 100+POINTER2)
		FROM = ARR2:(LOCAL:11 - 101)
	ELSEIF LOCAL:11 == 0
		RETURN 0
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP2
	ENDIF
ENDIF
UNIT_TARGET:(CFLAG:MASTER:所属):(ARG:1)   = ARG:0
IF FROM != UNIT_POSITION:(CFLAG:MASTER:所属):(ARG:1)
	UNIT_POSITION:(CFLAG:MASTER:所属):(ARG:1) = FROM
	PRINTFORML 部隊{ARG:1 + 1}を%GET_CITYNAME(FROM)%へ移動しました
	CALL SET_COMMANDER_COOLTIME(CFLAG:MASTER:所属, ARG:1)
ENDIF
PRINTFORML 部隊{ARG:1 + 1}の移動目標を%GET_RELAYNAME(ARG:0)%に設定します
IF IS_ROUTE(ARG:0, FROM) ==2
	SETCOLOR カラー_注意
	PRINTFORML 中継地点を経由して進軍します
	RESETCOLOR
ENDIF
WAIT
RETURN 1
