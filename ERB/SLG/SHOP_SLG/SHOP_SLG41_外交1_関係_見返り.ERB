;外交で相手から見返りを要求された場合の処理

;-------------------------------------------------
;ARG:0勢力が主人公勢力に要求を行った場合の処理
;ARG:1は要求の重さ 0=軽度 1=中度 2=重度
;要求を飲むと1、断ると0、要求できる内容がない場合は-1を返す
;-------------------------------------------------
@DIPLOMACY_REQUESTED(ARG:0, ARG:1)
;要求種別の最大数
#DIM CONST MAX_REQUEST_TYPE = 20

;各要求を行う確率
#DIM REQUEST_RATE, 20

VARSET REQUEST_RATE, 0

;自国の君主のキャラ番号を取得
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)

;相手勢力の君主のキャラ番号を取得
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;要求可能な士官の人数を確認
LOCAL:30 = 0
LOCAL:31 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && LOCAL:0 != MASTER && LOCAL:0 != LOCAL:4
		LOCAL:2 = MAX(ABL:(LOCAL:0):武闘, ABL:(LOCAL:0):防衛, ABL:(LOCAL:0):知略, ABL:(LOCAL:0):政治, ABL:(LOCAL:0):歌唱, ABL:(LOCAL:0):料理)
		IF LOCAL:2 >= 80
			LOCAL:30 ++
			LOCAL:31 ++
		ELSEIF LOCAL:2 >= 65
			LOCAL:30 ++
		ENDIF
	ENDIF
NEXT

;●各要求の確率を変数 REQUEST_RATE に設定
;0=兵を要求
;1=経済力を要求
;2=士官を要求
;10=性的行為を要求
;11=数期間の調教を要求

IF GET_SUM_SOLDIER(CFLAG:MASTER:所属) >= GET_SUM_ECONOMY(ARG:0) * 30 / 1000
	REQUEST_RATE:0 = 60
ENDIF
IF GET_SUM_ECONOMY(ARG:0) - GET_OWN_CITY(ARG:0) * 50 >= 300
	REQUEST_RATE:1 = 30
ENDIF
IF ABL:(LOCAL:5):欲望 >= 3 && ABL:(LOCAL:5):性知識 >= 3
	REQUEST_RATE:10 = 50 + ABL:(LOCAL:5):欲望 * 10
ENDIF

;要求強度:軽
IF ARG:1 == 0
	IF LOCAL:30
		REQUEST_RATE:2 = 15
	ENDIF

;要求強度:中
ELSEIF ARG:1 == 1
	IF LOCAL:31
		REQUEST_RATE:2 = 25
	ENDIF

;要求強度:重
ELSE
	IF LOCAL:31
		REQUEST_RATE:2 = 60
	ENDIF
	IF ABL:(LOCAL:5):欲望 >= 3 && ABL:(LOCAL:5):性知識 >= 3
		;REQUEST_RATE:11 = 50 + ABL:(LOCAL:5):欲望 * 10
	ENDIF
ENDIF

;要求内容を指定された確率に基づいて決定
LOCAL:1 = 0
FOR LOCAL:0, 0, MAX_REQUEST_TYPE
	LOCAL:1 += REQUEST_RATE:(LOCAL:0)
NEXT
;要求できる内容がない場合は-1を返す
IF LOCAL:1 <= 0
	RETURN -1
ENDIF
LOCAL:1 = RAND:(LOCAL:1)
LOCAL:10 = -1
FOR LOCAL:0, 0, MAX_REQUEST_TYPE
	LOCAL:1 -= REQUEST_RATE:(LOCAL:0)
	IF LOCAL:1 < 0
		LOCAL:10 = LOCAL:0
		BREAK
	ENDIF
NEXT

;設定：性的な要求は条件を満たせば常時
IF CONFIG:308 == 0
	IF ABL:(LOCAL:5):欲望 >= 3 && ABL:(LOCAL:5):性知識 >= 3
		IF DIPLOMACY_TRAINED_DAY == 0 && RAND:2
			LOCAL:10 = 11
		ELSE
			LOCAL:10 = 10
		ENDIF
	ENDIF
;設定：性的な要求は条件を満たせば常時＆欲望条件無視
ELSEIF CONFIG:308 == 2
	IF ABL:(LOCAL:5):性知識 >= 3
		IF DIPLOMACY_TRAINED_DAY == 0 && RAND:2
			LOCAL:10 = 11
		ELSE
			LOCAL:10 = 10
		ENDIF
	ENDIF
;設定：条件無視で100％性的な要求
ELSEIF CONFIG:308 == 3
	IF DIPLOMACY_TRAINED_DAY == 0 && RAND:2
		LOCAL:10 = 11
	ELSE
		LOCAL:10 = 10
	ENDIF
ENDIF

;要求の具体的な内容を設定
LOCAL:20 = 0
SELECTCASE LOCAL:10
	;兵を要求
	CASE 0
		CALL DIPLOMACTY_REQUESTED_SOLDIER(ARG:0, ARG:1)
		LOCAL:20 = RESULT

	;経済力を要求
	CASE 1
		CALL DIPLOMACTY_REQUESTED_ECONOMY(ARG:0, ARG:1)
		LOCAL:20 = RESULT

	;士官を要求
	CASE 2
		CALL DIPLOMACTY_REQUESTED_CHARA(ARG:0, ARG:1)
		LOCAL:20 = RESULT

	;性的行為を要求
	CASE 10
		CALL DIPLOMACY_REQUESTED_SEX(ARG:0, ARG:1)
		LOCAL:20 = RESULT

	;数期間の調教を要求
	CASE 11
		CALL DIPLOMACY_REQUESTED_TRAIN(ARG:0, ARG:1)
		LOCAL:20 = RESULT

	;エラーチェック
	CASEELSE
		;エラーメッセージを出力
		THROW 外交の要求種別が不正です(種別番号 LOCAL:10={LOCAL:10})
ENDSELECT

IF LOCAL:20
	PRINTFORMW %NAME:(LOCAL:4)%が要求を飲むと、%NAME:(LOCAL:5)%はようやくこちらの提案を承諾した…
	IF LOCAL:10 == 10 || LOCAL:10 == 11
		RETURN 2
	ENDIF
	RETURN 1
ENDIF

PRINTFORMW 交渉は成立しなかった…

RETURN 0

;-------------------------------------------------
;兵の要求の処理
;ARG:0は相手の勢力番号、ARG:1は要求の重さ。要求を飲むと1、断ると0を返す
;-------------------------------------------------
@DIPLOMACTY_REQUESTED_SOLDIER(ARG:0, ARG:1)
;相手勢力の君主のキャラ番号を取得
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

LOCAL:12 = GET_SUM_SOLDIER(CFLAG:MASTER:所属)
LOCAL:13 = GET_SUM_ARMY_DF(CFLAG:MASTER:所属)
IF ARG:1 == 0
	LOCAL:11 = MIN(GET_SUM_ECONOMY(CFLAG:MASTER:所属) * 3 / 1000, LOCAL:12 / 2)
ELSEIF ARG:1 == 1
	LOCAL:11 = MIN(GET_SUM_ECONOMY(CFLAG:MASTER:所属) * 8 / 1000, LOCAL:12 * 2 / 3)
ELSE
	LOCAL:11 = MIN(GET_SUM_ECONOMY(CFLAG:MASTER:所属) * 20 / 1000, LOCAL:12)
ENDIF
PRINTFORML %NAME:(LOCAL:5)%は、こちらの提案を飲む代わりに兵{LOCAL:11}を要求してきた
PRINTFORML 現在の総兵数:{LOCAL:12}(うち遊撃兵数:{COUNTRY_SOLDIER:(CFLAG:MASTER:所属)})
IF LOCAL:11 > COUNTRY_SOLDIER:(CFLAG:MASTER:所属)
	IF LOCAL:11 > COUNTRY_SOLDIER:(CFLAG:MASTER:所属) + LOCAL:13
		PRINTFORML ※要求兵数が現在の遊撃兵数より多いため、全部隊を解散し、防衛に着いている兵も渡す必要があります
	ELSE
		PRINTFORML ※要求兵数が現在の遊撃兵数より多いため、防衛に着いている兵も渡す必要があります
	ENDIF
ENDIF
WAIT
CALL ASK_YN("受け入れる", "断る")
IF RESULT != 0
	RETURN 0
ENDIF

IF LOCAL:11 > COUNTRY_SOLDIER:(CFLAG:MASTER:所属)
	IF LOCAL:11 > COUNTRY_SOLDIER:(CFLAG:MASTER:所属) + LOCAL:13
		CALL CLEAR_ALL_UNIT(CFLAG:MASTER:所属)
	ENDIF

	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:所属
			COUNTRY_SOLDIER:(CFLAG:MASTER:所属) += CITY_SOLDIER:(LOCAL:0)
			CITY_SOLDIER:(LOCAL:0) = 0
		ENDIF
	NEXT

	COUNTRY_SOLDIER:(CFLAG:MASTER:所属) = MAX(COUNTRY_SOLDIER:(CFLAG:MASTER:所属) - LOCAL:11, 0)

	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:所属
			LOCAL:15 = MIN(COUNTRY_SOLDIER:(CFLAG:MASTER:所属), 500)
			CITY_SOLDIER:(LOCAL:0) += LOCAL:15
			COUNTRY_SOLDIER:(CFLAG:MASTER:所属) -= LOCAL:15
		ENDIF
	NEXT

ELSE
	COUNTRY_SOLDIER:(CFLAG:MASTER:所属) = MAX(COUNTRY_SOLDIER:(CFLAG:MASTER:所属) - LOCAL:11, 0)
ENDIF

COUNTRY_SOLDIER:(ARG:0) += LOCAL:11

RETURN 1

;-------------------------------------------------
;経済力の要求の処理
;ARG:0は相手の勢力番号、ARG:1は要求の重さ。要求を飲むと1、断ると0を返す
;-------------------------------------------------
@DIPLOMACTY_REQUESTED_ECONOMY(ARG:0, ARG:1)
;相手勢力の君主のキャラ番号を取得
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

LOCAL:12 = GET_SUM_ECONOMY(CFLAG:MASTER:所属)
IF ARG:1 == 0
	LOCAL:11 = LOCAL:12 / 80 / 100 * 100
ELSEIF ARG:1 == 1
	LOCAL:11 = LOCAL:12 / 20 / 100 * 100
ELSE
	LOCAL:11 = LOCAL:12 / 7 / 100 * 100
ENDIF
PRINTFORML %NAME:(LOCAL:5)%は、こちらの提案を飲む代わりに経済力{LOCAL:11 / 100}を要求してきた
PRINTFORML 現在の経済力:{LOCAL:12 / 100}
WAIT
CALL ASK_YN("受け入れる", "断る")
IF RESULT != 0
	RETURN 0
ENDIF

;経済力の移動処理
CALL SHIFT_ECONOMY(CFLAG:MASTER:所属, ARG:0, LOCAL:11)

RETURN 1

;-------------------------------------------------
;士官の要求の処理
;ARG:0は相手の勢力番号、ARG:1は要求の重さ。要求を飲むと1、断ると0を返す
;-------------------------------------------------
@DIPLOMACTY_REQUESTED_CHARA(ARG:0, ARG:1)
;自国・相手勢力の君主のキャラ番号を取得
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;軽度・中度の場合、一定能力以上の士官から選択
IF ARG:1 == 0 || ARG:1 == 1
	PRINTFORMW %NAME:(LOCAL:5)%は、こちらの提案を飲む代わりに有能な士官を一人引き渡せと要求してきた
	IF ARG:1 == 0
		CALL DIPLOMACY_REQUESTED_CHARA_SELECT(ARG:0, 65)
	ELSE
		CALL DIPLOMACY_REQUESTED_CHARA_SELECT(ARG:0, 80)
	ENDIF

	IF RESULT
		RETURN 1
	ENDIF

;重度の場合、士官を指定
ELSE
	LOCAL:10 = -1
	LOCAL:11 = 0

	;1/4の確率で最も主人公への好感度が高いキャラを指定
	IF RAND:4 == 0
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && LOCAL:0 != MASTER && LOCAL:0 != LOCAL:4
				LOCAL:12 = CFLAG:(LOCAL:0):2
				IF LOCAL:12 >= LOCAL:11
					LOCAL:10 = LOCAL:0
					LOCAL:11 = LOCAL:12
				ENDIF
			ENDIF
		NEXT
	;3/4の確率で最も能力が高いキャラを指定
	ELSE
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && LOCAL:0 != MASTER && LOCAL:0 != LOCAL:4
				LOCAL:12 = ABL_SQRT(ABL:(LOCAL:0):武闘, -1) + ABL_SQRT(ABL:(LOCAL:0):防衛, -1) + ABL_SQRT(ABL:(LOCAL:0):知略, -1) + ABL_SQRT(ABL:(LOCAL:0):政治, -1)
				LOCAL:12 += ABL_SQRT(ABL:(LOCAL:0):歌唱, -1) + ABL_SQRT(ABL:(LOCAL:0):料理, -1)
				IF LOCAL:12 >= LOCAL:11
					LOCAL:10 = LOCAL:0
					LOCAL:11 = LOCAL:12
				ENDIF
			ENDIF
		NEXT
	ENDIF

	IF LOCAL:10 < 0
		PRINTW エラー:対象となる士官が存在しません @DIPLOMACTY_REQUESTED_CHARA 要求(重)
	ELSE
		PRINTFORMW %NAME:(LOCAL:5)%は、こちらの提案を飲む代わりに%NAME:(LOCAL:10)%を引き渡せと要求してきた
		CALL ASK_YN("受け入れる", "断る")
		IF RESULT == 0
			PRINTFORML %ANAME(LOCAL:10)%は%NAME:(LOCAL:5)%勢力の士官になりました…
			CALL CHANGE_COUNTRY(LOCAL:10, ARG:0, 1)
			RETURN 1
		ENDIF
	ENDIF
ENDIF

RETURN 0

;-------------------------------------------------
;ARG:0勢力に引き渡す士官を選択する関数 ARG:1は能力値の下限
;-------------------------------------------------
@DIPLOMACY_REQUESTED_CHARA_SELECT(ARG:0, ARG:1)
#DIM FIRST_LINE
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;対象となる士官の数を数える
LOCAL:6 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && LOCAL:0 != MASTER && LOCAL:0 != LOCAL:4 && !CFLAG:(LOCAL:0):捕虜先
		IF ABL:(LOCAL:0):武闘 >= ARG:1 || ABL:(LOCAL:0):防衛 >= ARG:1 || ABL:(LOCAL:0):知略 >= ARG:1 || ABL:(LOCAL:0):政治 >= ARG:1 || ABL:(LOCAL:0):歌唱 >= ARG:1 || ABL:(LOCAL:0):料理 >= ARG:1
			LOCAL:6 ++
		ENDIF
	ENDIF
NEXT

LOCAL:20 = 1

;1ページに表示する最大キャラ数の設定
LOCAL:7 = 44

;ページ数を計算
LOCAL:8 = (LOCAL:6 - 1) / LOCAL:7 + 1

$SHOW_LOOP1

CALL SINGLE_DRAWLINE
PRINTFORML ★★引き渡す士官を選択して下さい★★
PRINTFORML (最大能力が{ARG:1}以上の士官から選択)
CALL SINGLE_DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP2

LOCAL:10 = LOCAL:7 * (LOCAL:20 - 1)
LOCAL:11 = LOCAL:7 * LOCAL:20
LOCAL:12 = 0
LOCAL:13 = 0

FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && LOCAL:0 != MASTER && LOCAL:0 != LOCAL:4 && !CFLAG:(LOCAL:0):捕虜先
		IF ABL:(LOCAL:0):武闘 >= ARG:1 || ABL:(LOCAL:0):防衛 >= ARG:1 || ABL:(LOCAL:0):知略 >= ARG:1 || ABL:(LOCAL:0):政治 >= ARG:1 || ABL:(LOCAL:0):歌唱 >= ARG:1 || ABL:(LOCAL:0):料理 >= ARG:1
			IF LOCAL:13 >= LOCAL:10 && LOCAL:13 < LOCAL:11
				IF LOCAL:12 % 2 != 0
					PRINTPLAIN   
				ELSE
					IF LOCAL:12 >= 1
						PRINTL 
					ENDIF
					PRINTPLAIN   
				ENDIF
				CALL PRINT_PARTNER_DATA(LOCAL:0)
				LOCAL:12 ++
			ENDIF
			LOCAL:13 ++
		ENDIF
	ENDIF
NEXT
IF LOCAL:12 >= 1
	PRINTL 
ENDIF

IF LOCAL:8 >= 2
	CALL SINGLE_DRAWLINE
	PRINTPLAIN   
	IF LOCAL:20 >= 2
		PRINTBUTTON "  54[前のページ    ]", 54
	ELSE
		SETCOLOR COLOR("選択不可")
		PRINTPLAINFORM   54[前のページ    ]
		RESETCOLOR
	ENDIF

	PRINTPLAINFORM         page{LOCAL:20, 2, RIGHT}/{LOCAL:8, 2, LEFT}     
	IF LOCAL:20 < LOCAL:8
		PRINTBUTTON "  56[次のページ    ]", 56
	ELSE
		SETCOLOR COLOR("選択不可")
		PRINTPLAINFORM   56[次のページ    ]
		RESETCOLOR
	ENDIF
	PRINTL 
ENDIF

CALL SINGLE_DRAWLINE
PRINTPLAIN   
PRINTBUTTON "   0[断る          ]", 0

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 54 && LOCAL:20 >= 2
	LOCAL:20 --
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ELSEIF RESULT == 56 && LOCAL:20 < LOCAL:8
	LOCAL:20 ++
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP2
ENDIF

LOCAL:15 = NO_TO_CHARA(RESULT - 100)

IF LOCAL:15 >= 0 && CFLAG:(LOCAL:15):所属 == CFLAG:MASTER:所属 && LOCAL:15 != MASTER && LOCAL:15 != LOCAL:4 && !CFLAG:(LOCAL:15):捕虜先
	IF ABL:(LOCAL:15):武闘 >= ARG:1 || ABL:(LOCAL:15):防衛 >= ARG:1 || ABL:(LOCAL:15):知略 >= ARG:1 || ABL:(LOCAL:15):政治 >= ARG:1 || ABL:(LOCAL:15):歌唱 >= ARG:1 || ABL:(LOCAL:15):料理 >= ARG:1
		REDRAW 1

		;対象キャラの情報を表示
		CALL SINGLE_DRAWLINE
		CALL SHOW_INFO(LOCAL:15)
		CALL SINGLE_DRAWLINE
		PRINTFORML このキャラを移籍させますか？

		;はい／いいえ入力処理
		CALL ASK_YN()
		IF RESULT == 0
			PRINTFORML %ANAME(LOCAL:15)%は%NAME:(LOCAL:5)%勢力の士官になりました…
			CALL CHANGE_COUNTRY(LOCAL:15, ARG:0, 1)
			RETURN 1
		ENDIF
	ENDIF
	GOTO SHOW_LOOP1
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;性的行為の要求の処理
;ARG:0は相手の勢力番号、ARG:1は要求の重さ。要求を飲むと1、断ると0を返す
;-------------------------------------------------
@DIPLOMACY_REQUESTED_SEX(ARG:0, ARG:1)
;自国・相手勢力の君主のキャラ番号をそれぞれ取得
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;常に主人公が対象
IF CONFIG:307 == 1
	LOCAL:6 = MASTER
;常に君主が対象
ELSEIF CONFIG:307 == 2
	LOCAL:6 = LOCAL:4
;対象が状況によって変化
ELSE
	;対象キャラ判定値。正なら君主、負なら主人公が要求の対象になる
	LOCAL:10 = 0

	;●対象キャラの決定(君主 or 主人公)
	;君主に対するイメージが悪いほど君主を辱める方向に傾く
	IF REL_LIKE:(LOCAL:5):(LOCAL:4) >= 800
		LOCAL:10 -= 20
	ELSEIF REL_LIKE:(LOCAL:5):(LOCAL:4) >= 300
		LOCAL:10 -= 10
	ENDIF
	IF REL_HATE:(LOCAL:5):(LOCAL:4) >= 800
		LOCAL:10 += 30
	ELSEIF REL_HATE:(LOCAL:5):(LOCAL:4) >= 300
		LOCAL:10 += 15
	ENDIF

	;主人公との関係の深さに応じて主人公の奉仕を求める方に傾く
	IF TALENT:(LOCAL:5):親愛
		LOCAL:10 -= 40
	ELSEIF TALENT:(LOCAL:5):恋慕
		LOCAL:10 -= 20
	ELSEIF TALENT:(LOCAL:5):恋人
		LOCAL:10 -= 10
	ELSEIF TALENT:(LOCAL:5):親友
		LOCAL:10 += 20
	ELSEIF CFLAG:(LOCAL:5):2 >= 500
		LOCAL:10 -= 5
	ENDIF

	;性別による判定
	IF IS_MALE(LOCAL:5)
		SIF IS_FEMALE(MASTER)
			LOCAL:10 -= 10
		SIF IS_FEMALE(LOCAL:4)
			LOCAL:10 += 10
	ENDIF
	
	IF IS_FEMALE(LOCAL:5)
		SIF IS_MALE(MASTER)
			LOCAL:10 -= 10
		SIF IS_MALE(LOCAL:4)
			LOCAL:10 += 10
	ENDIF

	IF LOCAL:10 >= 0
		LOCAL:6 = LOCAL:4
	ELSE
		LOCAL:6 = MASTER
	ENDIF
ENDIF

;●内容の決定(キス、足舐め、フェラ・パイズリ・クンニ)
LOCAL:11 = 0
LOCAL:12 = 0

SIF TALENT:(LOCAL:5):素直
	LOCAL:12 += 10
SIF TALENT:(LOCAL:5):臆病
	LOCAL:12 += 10
SIF TALENT:(LOCAL:5):貞操観念
	LOCAL:12 += 10
SIF TALENT:(LOCAL:5):貞操無頓着
	LOCAL:12 -= 10
SIF TALENT:(LOCAL:5):生意気
	LOCAL:11 += 10
SIF TALENT:(LOCAL:5):プライド高い
	LOCAL:11 += 10

IF GETBIT(TALENT:(LOCAL:5):淫乱系, 素質_淫乱_サド)
	LOCAL:11 += 20
ELSEIF GETBIT(TALENT:(LOCAL:5):淫乱系, 素質_淫乱_マゾ)
	LOCAL:11 -= 20
ELSEIF ABL:(LOCAL:5):主導度Ｕ >= 500
	LOCAL:11 += 15
ELSEIF ABL:(LOCAL:5):主導度Ｕ >= 300
	LOCAL:11 += 10
ELSEIF ABL:(LOCAL:5):主導度Ｕ >= 100
	LOCAL:11 += 5
ELSEIF ABL:(LOCAL:5):主導度Ｕ > -100
	LOCAL:11 += 0
ELSEIF ABL:(LOCAL:5):主導度Ｕ > -300
	LOCAL:11 -= 5
ELSEIF ABL:(LOCAL:5):主導度Ｕ > -500
	LOCAL:11 -= 10
ELSE
	LOCAL:11 -= 15
ENDIF

IF ABL:(LOCAL:5):倒錯度 >= 800
	LOCAL:12 -= 15
ELSEIF ABL:(LOCAL:5):倒錯度 >= 500
	LOCAL:12 -= 10
ELSEIF ABL:(LOCAL:5):倒錯度 >= 300
	LOCAL:12 -= 5
ENDIF

IF ARG:1 == 1
	LOCAL:12 -= 10
ELSEIF ARG:1 == 2
	LOCAL:12 -= 20
ENDIF

IF LOCAL:12 >= 5 && LOCAL:6 == MASTER
	LOCAL:15 = 0
ELSEIF LOCAL:11 >= 15 && RAND:2
	LOCAL:15 = 1
ELSEIF LOCAL:12 <= -15 && RAND:2
	LOCAL:15 = 2
ELSEIF !RAND:3
	IF RAND:2 && (PENIS(MASTER) && VAGINA(LOCAL:5)) || (VAGINA(MASTER) && PENIS(LOCAL:5))
		LOCAL:15 = 30
	ELSE
		LOCAL:15 = 50
	ENDIF
ELSEIF PENIS(LOCAL:5)
	LOCAL:15 = 10
	IF (TALENT:(LOCAL:6):巨乳 || TALENT:(LOCAL:6):爆乳) && RAND:3 >= 1
		LOCAL:15 = 11
	ENDIF
ELSE
	LOCAL:15 = 20
ENDIF

SELECTCASE LOCAL:15
	CASE 0
		PRINTFORML %NAME:(LOCAL:5)%は%ANAME(LOCAL:6)%に対して、こちらの提案を飲む代わりにこの場でキスをしろと要求してきた
	CASE 1
		PRINTFORML %NAME:(LOCAL:5)%は%ANAME(LOCAL:6)%に対して、こちらの提案を飲む代わりにこの場で足を舐めろと要求してきた
	CASE 2
		PRINTFORML %NAME:(LOCAL:5)%は%ANAME(LOCAL:6)%に対して、こちらの提案を飲む代わりにこの場でオナニーしろと要求してきた
	CASE 10
		PRINTFORML %NAME:(LOCAL:5)%は%ANAME(LOCAL:6)%に対して、こちらの提案を飲む代わりにフェラで奉仕しろと要求してきた
	CASE 11
		PRINTFORML %NAME:(LOCAL:5)%は%ANAME(LOCAL:6)%に対して、こちらの提案を飲む代わりにパイズリで奉仕しろと要求してきた
	CASE 20
		PRINTFORML %NAME:(LOCAL:5)%は%ANAME(LOCAL:6)%に対して、こちらの提案を飲む代わりにクンニで奉仕しろと要求してきた
	CASE 30
		PRINTFORML %NAME:(LOCAL:5)%は%ANAME(LOCAL:6)%に対して、こちらの提案を飲む代わりに一晩%NAME:(LOCAL:5)%の慰み者になるように要求してきた
	CASE 50
		PRINTFORML %NAME:(LOCAL:5)%は%ANAME(LOCAL:6)%に対して、こちらの提案を飲む代わりに兵達の相手をしろと要求してきた
ENDSELECT

CALL ASK_YN("受け入れる", "断る")
IF RESULT == 1
	RETURN 0
ENDIF

IF LOCAL:6 != MASTER
	PRINTFORMW %ANAME(MASTER)%は%ANAME(LOCAL:6)%に対して、要求を飲むように説得した…
	PRINTL 
ENDIF

SELECTCASE LOCAL:15
	CASE 0
		IF LOCAL:11 >= 20
			PRINTFORML %ANAME(LOCAL:6)%が要求通り%ANAME(LOCAL:5)%に口付けると、%ANAME(LOCAL:5)%は%ANAME(LOCAL:6)%の頭の後ろに手を回し、
			PRINTFORMW 舌を入れて貪るように%ANAME(LOCAL:6)%の口内を舐め回してきた
			PRINTFORMW %ANAME(LOCAL:6)%は息苦しさに口を離そうとしたが、%ANAME(LOCAL:5)%に頭をしっかりと抱え込まれて逃げることが出来ない
			PRINTFORMW やがて%ANAME(LOCAL:5)%が満足するまで、%ANAME(LOCAL:6)%は延々と%ANAME(LOCAL:5)%に抱き締められてキスをされ続けた
		ELSE
			PRINTFORML %ANAME(LOCAL:6)%が要求通り%ANAME(LOCAL:5)%に口付けると、%ANAME(LOCAL:5)%は%ANAME(LOCAL:6)%の口内に舌を挿し入れ、
			PRINTFORMW 誘うように%ANAME(LOCAL:6)%の舌を絡め取った
			PRINTFORMW %ANAME(LOCAL:6)%は%ANAME(LOCAL:5)%に促されるまま、自らも舌を絡めて深い口付けを交わした
			PRINTFORMW しばらくして%ANAME(LOCAL:6)%が口を離すと、%ANAME(LOCAL:5)%は物足りないといった表情でさらなるキスをねだってきた
			PRINTFORMW やがて%ANAME(LOCAL:5)%が満足するまで、%ANAME(LOCAL:6)%は延々と%ANAME(LOCAL:5)%にキスを強要され続けた
		ENDIF
		IF TALENT:(LOCAL:4):恋慕 || TALENT:(LOCAL:4):恋人
			PRINTL 
			PRINTFORMW ふと背筋に寒気を感じて振り向くと、%ANAME(LOCAL:4)%が%ANAME(LOCAL:6)%のことを恨めしげに睨みつけていた
			PRINTFORMW 冷や汗を流す%ANAME(LOCAL:6)%をよそに、%ANAME(LOCAL:5)%はクスクスと楽しそうに微笑んでいた
			PALAM:(LOCAL:4):怒主 += 5000
		ENDIF
		PRINTL 

		PRINTFORML <%ANAME(LOCAL:6)%>
		CALL PRINT_ADD_EXP(LOCAL:6, "キス経験", 3)
		CALL TRAIN_AUTO_ABLUP(LOCAL:6)
		PRINTL 

		PRINTFORML <%ANAME(LOCAL:5)%>
		CALL PRINT_ADD_EXP(LOCAL:5, "キス経験", 3)
		CALL TRAIN_AUTO_ABLUP(LOCAL:5)
		PRINTL 

		PRINTFORMW ………………

	CASE 1
		PRINTFORMW %ANAME(LOCAL:5)%はその場で%ANAME(LOCAL:6)%を自分の前に跪かせると、靴を脱いでムレて臭い立つ足を%ANAME(LOCAL:6)%の鼻に押し当ててきた
		PRINTFORMW あまりの臭気に顔を背ける%ANAME(LOCAL:6)%に対し、%ANAME(LOCAL:5)%は舐めて奉仕するように命令した
		PRINTFORMW %ANAME(LOCAL:6)%は止むを得ず、口を開いて%ANAME(LOCAL:5)%の足を舐め始めた
		PRINTFORMW えづきそうになりながらも必死に奉仕する%ANAME(LOCAL:6)%の姿を見ながら、%ANAME(LOCAL:5)%は満足そうな笑みを浮かべた
		PRINTFORMW ………………

	CASE 2
		IF PENIS(LOCAL:6)
			PRINTFORMW %ANAME(LOCAL:5)%は、まず%ANAME(LOCAL:6)%に対して服を脱いで裸になるように命令した
			PRINTFORMW %ANAME(LOCAL:6)%は止むを得ず、%ANAME(LOCAL:5)%やその部下たちに見られながら服を脱ぎ裸体を晒した
			IF LOCAL:6 != MASTER && ABL:(LOCAL:6):欲望 < 3
				PRINTFORMW %ANAME(LOCAL:6)%は恥ずかしさに耐えながら、自身の肉棒を握り、ゆっくりと前後にシゴき始めた
				PRINTFORML %ANAME(LOCAL:5)%やその部下たちのクスクスという笑い声を聞きながら、
				PRINTFORMW %ANAME(LOCAL:6)%は目を伏せひたすら自らのペニスをシゴき続ける…
				PRINTFORMW やがて、絶頂に達した%ANAME(LOCAL:6)%のペニスから、大量の精液が吹き上がり床へと飛び散った
				PRINTFORMW 息を荒げながら、快感と屈辱に震える%ANAME(LOCAL:6)%を見て、%ANAME(LOCAL:5)%は満足そうな笑みを浮かべた
				PALAM:(LOCAL:6):怒外 += 5000
			ELSE
				IF ABL:(LOCAL:6):露出 >= 3
					PRINTFORMW %ANAME(LOCAL:6)%は恥ずかしさに興奮しながら、自身の肉棒を握り、激しく前後にシゴき始めた
				ELSE
					PRINTFORMW %ANAME(LOCAL:6)%は自身の肉棒を握ると、ゆっくりと前後にシゴき始めた
				ENDIF
				PRINTFORMW %ANAME(LOCAL:6)%がペニスを擦り上げるたび、周囲から%ANAME(LOCAL:5)%やその部下たちのクスクスという笑い声が響く…
				PRINTFORMW やがて、%ANAME(LOCAL:6)%が絶頂に達すると、ペニスから大量の精液が吹き上がり床へと飛び散った
				PRINTFORMW 息を荒げながら快感に震える%ANAME(LOCAL:6)%を見て、%ANAME(LOCAL:5)%は満足そうな笑みを浮かべた
			ENDIF
			PRINTL 
			CALL FUCK(LOCAL:6, "露出, 自慰, 射精, 性技, 欲望")
		ELSE
			PRINTFORMW %ANAME(LOCAL:5)%は、まず%ANAME(LOCAL:6)%に対して服を脱いで裸になるように命令した
			PRINTFORMW %ANAME(LOCAL:6)%は止むを得ず、%ANAME(LOCAL:5)%やその部下たちに見られながら服を脱ぎ裸体を晒した
			IF LOCAL:6 != MASTER && ABL:(LOCAL:6):欲望 < 3
				PRINTFORMW %ANAME(LOCAL:6)%は恥ずかしさに耐えながら、自身の秘部を指で弄り始めた
				PRINTFORMW %ANAME(LOCAL:5)%やその部下たちのクスクスという笑い声を聞かされながら、
				PRINTFORMW %ANAME(LOCAL:6)%は涙目になって必死に自分の体を慰め続ける…
				PRINTFORMW やがて、%ANAME(LOCAL:6)%は甘い声を漏らしながら、小さく震えて絶頂した
				PRINTFORMW 息を荒げながら、虚ろな目で快感の余韻に震える%ANAME(LOCAL:6)%を見て、%ANAME(LOCAL:5)%は満足そうな笑みを浮かべた
				PALAM:(LOCAL:6):怒外 += 5000
			ELSE
				IF ABL:(LOCAL:6):露出 >= 3
					PRINTFORMW %ANAME(LOCAL:6)%は恥ずかしさに興奮しながら、自身の秘部を指で弄り始めた
				ELSE
					PRINTFORMW %ANAME(LOCAL:6)%は恥ずかしさに耐えながら、自身の秘部を指で弄り始めた
				ENDIF
				PRINTFORMW %ANAME(LOCAL:5)%やその部下たちのクスクスという笑い声を聞くと、
				PRINTFORMW 羞恥のあまり%ANAME(LOCAL:6)%の体が自然と熱くなってしまう…
				PRINTFORMW やがて、%ANAME(LOCAL:6)%は甘い声を漏らしながら、衆目の前でビクビクと震えて絶頂した
				PRINTFORMW 息を荒げながら快感の余韻に震える%ANAME(LOCAL:6)%を見て、%ANAME(LOCAL:5)%は満足そうな笑みを浮かべた
			ENDIF
			CALL FUCK(LOCAL:6, "欲望, 自慰, Ｃ, Ｂ, 露出, 性技")
		ENDIF
		PRINTL 
		PRINTFORMW ………………

	CASE 10
		PRINTFORMW %ANAME(LOCAL:6)%は%ANAME(LOCAL:5)%の私室に連れ込まれた…
		PRINTFORMW ………………
		PRINTFORMW %ANAME(LOCAL:5)%は寝台の端に腰掛けると、%ANAME(LOCAL:6)%を自分の前に跪かせた
		PRINTFORMW %ANAME(LOCAL:5)%は服の中からイチモツを取り出すと、%ANAME(LOCAL:6)%に舐めて気持ち良くするように命令した
		IF LOCAL:6 != MASTER && ABL:(LOCAL:6):欲望 < 3
			PRINTFORMW %ANAME(LOCAL:6)%が戸惑っていると、%ANAME(LOCAL:5)%は%ANAME(LOCAL:6)%の頭を掴み、口の中に無理やり竿をねじ込んだ
			PRINTFORMW 苦しそうに呻く%ANAME(LOCAL:6)%を省みることなく、%ANAME(LOCAL:5)%は何度も喉奥までペニスを突き込んでいく
			PRINTFORMW やがて%ANAME(LOCAL:5)%がペニスを喉奥に突き入れたまま射精すると、%ANAME(LOCAL:6)%激しくむせ返り、蹲った
			PALAM:(LOCAL:6):怒外 += 5000
		ELSE
			PRINTFORMW %ANAME(LOCAL:6)%が%ANAME(LOCAL:5)%のペニスに舌を絡ませると、%ANAME(LOCAL:5)%は満足そうな表情を浮かべ%ANAME(LOCAL:6)%の頭を撫でた
			PRINTFORMW やがて感極まった%ANAME(LOCAL:5)%は%ANAME(LOCAL:6)%の頭を強引に掴み、竿を%ANAME(LOCAL:6)%の口の中に突き入れて射精した
			IF ABL:(LOCAL:6):精愛 >= 2
				PRINTFORMW 一滴もこぼさずに飲むようにと命令された%ANAME(LOCAL:6)%は、むせ返るような精液の臭いに恍惚としながら%ANAME(LOCAL:5)%の精を飲み下した
			ELSE
				PRINTFORMW 一滴もこぼさずに飲むようにと命令された%ANAME(LOCAL:6)%は、むせ返るような精液の臭いに耐えながら%ANAME(LOCAL:5)%の精を飲み込んだ
			ENDIF
		ENDIF
		PRINTL 
		CALL FUCK(LOCAL:5, "Ｃ, 欲望, 射精")
		CALL FUCK(LOCAL:6, "欲望, 奉仕, 性技, 精愛, 口淫", "キス喪失, 口内射精", 0, @"%ANAME(LOCAL:5)%のペニス", "", "", 喪失_外交)
		PRINTFORMW ………………

	CASE 11
		PRINTFORMW %ANAME(LOCAL:6)%は%ANAME(LOCAL:5)%の私室に連れ込まれた…
		PRINTFORMW ………………
		PRINTFORMW %ANAME(LOCAL:5)%は寝台の端に腰掛けると、%ANAME(LOCAL:6)%を自分の前に跪かせた
		PRINTFORMW %ANAME(LOCAL:5)%は服の中からイチモツを取り出すと、%ANAME(LOCAL:6)%に胸で気持ち良くするように命令した
		IF LOCAL:6 != MASTER && ABL:(LOCAL:6):欲望 < 3
			;胸が普通サイズ以上
			IF GET_BUSTSIZE_NUM(LOCAL:6) >= 0
				PRINTFORMW %ANAME(LOCAL:6)%が戸惑っていると、%ANAME(LOCAL:5)%は%ANAME(LOCAL:6)%を押し倒し、胸の間に無理やりペニスを突き入れた
				PRINTFORMW 強く胸を掴まれて痛みに呻く%ANAME(LOCAL:6)%だったが、%ANAME(LOCAL:5)%はお構いなしに%ANAME(LOCAL:6)%の胸でオナニーをし続ける
				PRINTFORMW やがて感極まった%ANAME(LOCAL:5)%の大量の白濁液が顔面へとぶちまけられ、%ANAME(LOCAL:6)%は嫌悪感と屈辱のあまり涙を浮かべた
			;貧乳以下
			ELSE
				PRINTFORMW %ANAME(LOCAL:6)%が戸惑っていると、%ANAME(LOCAL:5)%は%ANAME(LOCAL:6)%を押し倒し、胸に無理やりペニスを擦りつけ始めた
				PRINTFORMW 胸にグリグリとイチモツを押し付けられ痛みに呻く%ANAME(LOCAL:6)%だったが、%ANAME(LOCAL:5)%はお構いなしに%ANAME(LOCAL:6)%の胸でオナニーをし続ける
				PRINTFORMW やがて感極まった%ANAME(LOCAL:5)%の大量の白濁液が顔面へとぶちまけられ、%ANAME(LOCAL:6)%は嫌悪感と屈辱のあまり涙を浮かべた
			ENDIF
			PALAM:(LOCAL:6):怒外 += 5000
			PRINTL 
		ELSE
			;胸が普通サイズ以上
			IF GET_BUSTSIZE_NUM(LOCAL:6) >= 0
				PRINTFORML %ANAME(LOCAL:6)%が言われるまま胸の谷間で%ANAME(LOCAL:5)%のペニスを挟むと、
				PRINTFORMW %ANAME(LOCAL:5)%は意地の悪い笑みを浮かべながら%ANAME(LOCAL:6)%の乳首を摘み上げてきた
				PRINTFORMW %ANAME(LOCAL:6)%は逆らうことも出来ず、乳首への快感に耐えながら必死に胸を揺り動かして%ANAME(LOCAL:5)%のイチモツに奉仕し続けた
			;貧乳以下
			ELSE
				PRINTFORML %ANAME(LOCAL:6)%が言われるまま薄い胸を%ANAME(LOCAL:5)%のペニスに擦りつけると、
				PRINTFORMW %ANAME(LOCAL:5)%は意地の悪い笑みを浮かべながら%ANAME(LOCAL:6)%の乳首をクリクリと弄り回してきた
				PRINTFORMW %ANAME(LOCAL:6)%は逆らうことも出来ず、乳首への快感に耐えながら必死に胸を擦りつけて%ANAME(LOCAL:5)%のイチモツに奉仕し続けた
			ENDIF
			PRINTFORML やがて感極まった%ANAME(LOCAL:5)%に白濁を顔面へとぶちまけられながら、
			IF TALENT:(LOCAL:6):母乳体質
				PRINTFORMW %ANAME(LOCAL:6)%も乳首で達してしまい、大量の母乳を噴き出しながらビクビクと体を震わせた
			ELSE
				PRINTFORMW %ANAME(LOCAL:6)%も乳首で達してしまいビクビクと体を震わせた
			ENDIF
			PRINTL 
		ENDIF
		CALL FUCK(LOCAL:5, "Ｃ, 欲望, 射精")
		CALL FUCK(LOCAL:6, "欲望, 奉仕, 性技, 精愛, Ｂ")
		PRINTL 
		PRINTFORMW ………………

	CASE 20
		PRINTFORMW %ANAME(LOCAL:6)%は%ANAME(LOCAL:5)%の私室に連れ込まれた…
		PRINTFORMW ………………
		PRINTFORMW %ANAME(LOCAL:5)%は寝台の端に腰掛けると、%ANAME(LOCAL:6)%を自分の前に跪かせた
		PRINTFORMW %ANAME(LOCAL:5)%は股を開いて、%ANAME(LOCAL:6)%に舐めて気持ち良くするように命令した
		IF LOCAL:6 != MASTER && ABL:(LOCAL:6):欲望 < 3
			PRINTFORMW %ANAME(LOCAL:6)%が戸惑っていると、%ANAME(LOCAL:5)%は%ANAME(LOCAL:6)%の頭を掴み、強引にワレメに口を押し当てさせた
			PRINTFORMW まるで顔でオナニーするかのようにワレメを擦りつけられ、%ANAME(LOCAL:6)%は苦しそうに呻いた
			PALAM:(LOCAL:6):怒外 += 5000
		ELSE
			PRINTFORMW %ANAME(LOCAL:6)%が舌を出して奉仕を始めると、%ANAME(LOCAL:5)%は興奮した様子で%ANAME(LOCAL:6)%の頭を掴み、股間に強く押し当ててきた
			PRINTFORMW %ANAME(LOCAL:6)%は%ANAME(LOCAL:5)%の愛液にまみれながら、%ANAME(LOCAL:5)%が満足するまでひたすら奉仕を続けた
		ENDIF
		PRINTL 

		CALL FUCK(LOCAL:5, "Ｃ, 欲望")
		CALL FUCK(LOCAL:6, "欲望, 奉仕, 性技, 精愛, 口淫", "キス喪失", 0, @"%ANAME(LOCAL:5)%の秘貝", "", "", 喪失_外交)

		PRINTFORMW ………………
	CASE 30
		IF PENIS(LOCAL:5) && VAGINA(LOCAL:6)
			PRINTFORML %ANAME(LOCAL:5)%はさっそく、%ANAME(LOCAL:6)%の服を脱がせ、自らも服を脱いだ
			PRINTFORML %ANAME(LOCAL:5)%の股座にある逸物は、快楽への期待で早くもいきり勃っている……
			SELECTCASE RAND:7
				CASE 0
					PRINTFORML %ANAME(LOCAL:5)%は、%ANAME(LOCAL:6)%を四つん這いにさせ、後ろからその雌穴を貫いた
				CASE 1
					PRINTFORML %ANAME(LOCAL:5)%は、%ANAME(LOCAL:6)%を仰向けにさせ、覆い被さって上から何度も雌穴を突いた
				CASE 2
					PRINTFORML %ANAME(LOCAL:5)%は、%ANAME(LOCAL:6)%に壁に手をついて尻を向けさせ、後ろからその雌穴を貫いた
				CASE 3
					PRINTFORML %ANAME(LOCAL:5)%は、%ANAME(LOCAL:6)%と向かい合い、立ったまま雌穴を貫いた
				CASE 4
					PRINTFORML %ANAME(LOCAL:5)%は座り、%ANAME(LOCAL:6)%を向かい合った体勢で跨がらせ、下から何度も突き上げた
				CASE 5
					PRINTFORML %ANAME(LOCAL:5)%は座り、%ANAME(LOCAL:6)%を後ろ向きの体勢で跨がらせ、下から何度も突き上げた
				CASE 6
					PRINTFORML %ANAME(LOCAL:5)%は仰向けに寝ると、%ANAME(LOCAL:6)%を己の上に跨がらせ、自ら腰を振らせた
			ENDSELECT
			PRINTFORML やがて%ANAME(LOCAL:5)%は、%ANAME(LOCAL:6)%の膣内にたっぷりと射精した
			PRINTFORML ペニスを引き抜かれた淫裂からは、白いものがどろりとこぼれ落ちている……
			
			PRINTL 
			CALL FUCK(LOCAL:5, "Ｃ, 欲望, 性交, 射精, Ｖ挿入", "童貞喪失", 0, "", "", @"%ANAME(LOCAL:6)%の膣", 喪失_外交)
			CALL FUCK(LOCAL:6, "欲望, 奉仕, 性交, 精愛, Ｖ, 苦痛快楽, マゾ, Ｖ性交", "処女喪失, 膣内射精", GET_ID(LOCAL:5), "", ANAME(LOCAL:5), "", 喪失_外交)

			PRINTFORMW ………………
		ELSEIF PENIS(LOCAL:6) && VAGINA(LOCAL:5)
			PRINTFORML %ANAME(LOCAL:5)%はさっそく、%ANAME(LOCAL:6)%の服を脱がせ、自らも服を脱いだ
			PRINTFORML %ANAME(LOCAL:6)%の股座にある逸物は、快楽への期待で早くもいきり勃っている……
			PRINTFORML これは期待できそうだと、%ANAME(LOCAL:5)%は舌なめずりした
			SELECTCASE RAND:7
				CASE 0
					PRINTFORML %ANAME(LOCAL:5)%は自ら%ANAME(LOCAL:6)%に尻を向け、腰をくねらせて性交をねだった
				CASE 1
					PRINTFORML %ANAME(LOCAL:5)%は自ら仰向けに横たわり、股を開き秘裂を指で開いて性交をねだった
				CASE 2
					PRINTFORML %ANAME(LOCAL:5)%は自ら壁に手をついて%ANAME(LOCAL:6)%に尻を向け、後ろから雌穴を貫くようねだった
				CASE 3
					PRINTFORML %ANAME(LOCAL:5)%は%ANAME(LOCAL:6)%と向かい合い、立ったままの姿勢で犯すよう命じた
				CASE 4
					PRINTFORML %ANAME(LOCAL:5)%は%ANAME(LOCAL:6)%を座らせ、向かい合った体勢で跨がると、下から突き上げるようねだった
				CASE 5
					PRINTFORML %ANAME(LOCAL:5)%は%ANAME(LOCAL:6)%を座らせ、後ろ向きの体勢で跨がると、下から何度も突き上げるようねだった
				CASE 6
					PRINTFORML %ANAME(LOCAL:5)%は%ANAME(LOCAL:6)%を仰向けに寝かせ、その上に跨がると、亀頭に己の雌穴の入り口を擦りつける
			ENDSELECT
			PRINTFORML %ANAME(LOCAL:6)%は命じられるがまま、%ANAME(LOCAL:5)%に挿入した……
			PRINTFORML やがて%ANAME(LOCAL:6)%が限界を迎えると、%ANAME(LOCAL:5)%はそのまま膣内で射精させた……
			PRINTFORML ペニスを引き抜かれた淫裂からは、白い物がどろりとこぼれ落ちている……
			
			PRINTL 
			CALL FUCK(LOCAL:5, "欲望, 奉仕, 性交, 精愛, Ａ, 苦痛快楽, マゾ, Ａ性交", "Ａ処女喪失, 腸内射精", GET_ID(LOCAL:5), "", ANAME(LOCAL:5), "", 喪失_外交)
			CALL FUCK(LOCAL:6, "Ｃ, 欲望, 性交, 射精, Ａ挿入", "童貞喪失", 0, "", "", @"%ANAME(LOCAL:6)%のアナル", 喪失_外交)
			PRINTFORMW ………………
		ENDIF
	CASE 50
		IF IS_MALE(LOCAL:6)
			PRINTFORML %ANAME(LOCAL:5)%はさっそく、%ANAME(LOCAL:6)%の服を脱がせ、自らも服を脱いだ
			PRINTFORML %ANAME(LOCAL:5)%の秘所は、快楽への期待で早くも濡れ始めている……
			PRINTFORML 不意に、%ANAME(LOCAL:5)%はぱんぱんと手を叩いた
			PRINTFORML すると、艶やかな女兵士達がぞろぞろとなだれ込んできた
			PRINTFORML そのまま、%ANAME(LOCAL:6)%は、慰安として兵士達に提供されてしまった……
			CALL FUCK(LOCAL:5, "欲望, Ｃ, Ｂ, 自慰", "")
			CALL FUCK(LOCAL:6, "欲望, 奉仕, 性技, Ｃ, Ｂ, Ｍ, 射精, 苦痛快楽, 緊縛, マゾ, キス, 口淫, 輪姦, Ｖ挿入, Ａ挿入", "キス喪失, 童貞喪失", GET_SPERM_ID("兵士"), @"%ANAME(LOCAL:5)%の私兵の秘貝", "", @"%ANAME(LOCAL:5)%の私兵の膣", 喪失_外交)
			PRINTFORML やがてコトが終わると、そこには精根尽き果て陰茎から種を弱々しく零す%ANAME(LOCAL:6)%と、
			PRINTFORML %ANAME(LOCAL:6)%の痴態を肴に何度も自慰をし、ぐったりと疲れた%ANAME(LOCAL:5)%が残されていた
			PRINTL 
		ELSEIF VAGINA(LOCAL:6)
			PRINTFORML %ANAME(LOCAL:5)%はさっそく、%ANAME(LOCAL:6)%の服を脱がせ、自らも服を脱いだ
			PRINTFORML %ANAME(LOCAL:5)%の股座にある逸物は、快楽への期待で早くもいきり勃っている……
			PRINTFORML 不意に、%ANAME(LOCAL:5)%はぱんぱんと手を叩いた
			PRINTFORML すると、屈強な兵士達がぞろぞろとなだれ込んできた
			PRINTFORML そのまま、%ANAME(LOCAL:6)%は、慰安として兵士達に提供されてしまった……
			CALL FUCK(LOCAL:5, "欲望, Ｃ, Ｂ, 自慰", "")
			CALL FUCK(LOCAL:6, "欲望, 奉仕, 性技, Ｃ, Ｖ, Ａ, Ｂ, Ｍ, Ｖ拡張, Ａ拡張, 性交, 自慰, 精愛, 露出, 排泄, 射精, 苦痛快楽, 緊縛, マゾ, キス, 口淫, 輪姦, Ｖ性交, Ａ性交", "キス喪失, 処女喪失, Ａ処女喪失, 膣内射精, 腸内射精, 口内射精", GET_SPERM_ID("兵士"), @"%ANAME(LOCAL:5)%の私兵のペニス", @"%ANAME(LOCAL:5)%の私兵", "", 喪失_外交)
			PRINTFORML やがてコトが終わると、そこには全身あらゆる穴から種を零す%ANAME(LOCAL:6)%と、
			PRINTFORML %ANAME(LOCAL:6)%の痴態を肴に何度も自慰をし、ぐったりと疲れた%ANAME(LOCAL:5)%が残されていた
			PRINTL 
		ENDIF
ENDSELECT

RETURN 1

;-------------------------------------------------
;数期間の調教を要求
;ARG:0は相手の勢力番号、ARG:1は要求の重さ。要求を飲むと1、断ると0を返す
;-------------------------------------------------
@DIPLOMACY_REQUESTED_TRAIN(ARG:0, ARG:1)
#DIM TRAINING_DAY

;自国・相手勢力の君主のキャラ番号をそれぞれ取得
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;常に主人公が対象
IF CONFIG:307 == 1
	LOCAL:6 = MASTER
;常に君主が対象
ELSEIF CONFIG:307 == 2
	LOCAL:6 = LOCAL:4
;対象が状況によって変化
ELSE
	;対象キャラ判定値。正なら君主、負なら主人公が要求の対象になる
	LOCAL:10 = 0

	;●対象キャラの決定(君主 or 主人公)
	;君主に対するイメージが悪いほど君主を辱める方向に傾く
	IF REL_LIKE:(LOCAL:5):(LOCAL:4) >= 800
		LOCAL:10 -= 20
	ELSEIF REL_LIKE:(LOCAL:5):(LOCAL:4) >= 300
		LOCAL:10 -= 10
	ENDIF
	IF REL_HATE:(LOCAL:5):(LOCAL:4) >= 800
		LOCAL:10 += 30
	ELSEIF REL_HATE:(LOCAL:5):(LOCAL:4) >= 300
		LOCAL:10 += 15
	ENDIF

	;主人公との関係の深さに応じて主人公の奉仕を求める方に傾く
	IF TALENT:(LOCAL:5):親愛
		LOCAL:10 -= 40
	ELSEIF TALENT:(LOCAL:5):恋慕
		LOCAL:10 -= 20
	ELSEIF TALENT:(LOCAL:5):恋人
		LOCAL:10 -= 10
	ELSEIF TALENT:(LOCAL:5):親友
		LOCAL:10 += 20
	ELSEIF CFLAG:(LOCAL:5):2 >= 500
		LOCAL:10 -= 5
	ENDIF

	;性別による判定
	IF IS_MALE(LOCAL:5)
		SIF IS_FEMALE(MASTER)
			LOCAL:10 -= 10
		SIF IS_FEMALE(LOCAL:4)
			LOCAL:10 += 10
	ENDIF
	IF IS_FEMALE(LOCAL:5)
		SIF IS_MALE(MASTER)
			LOCAL:10 -= 10
		SIF IS_MALE(LOCAL:4)
			LOCAL:10 += 10
	ENDIF

	IF LOCAL:10 >= 0
		LOCAL:6 = LOCAL:4
	ELSE
		LOCAL:6 = MASTER
	ENDIF
ENDIF

;対象キャラは君主またはあなた
;内容は別の関数を使い、能力・素質に応じて分岐

;要求の重さと対象キャラへの判定値の大きさに応じて日数を算出　最大7日間
TRAINING_DAY = MIN(MAX(ABS(LOCAL:10)*(ARG:1), 1), 7)

PRINTFORML %NAME:(LOCAL:5)%は%ANAME(LOCAL:6)%に対して、こちらの提案を飲む代わりに{TRAINING_DAY}日間調教させろと要求してきた

CALL ASK_YN("受け入れる", "断る")
IF RESULT == 1
	RETURN 0
ENDIF

IF LOCAL:6 == MASTER
	;逆調教要求日数を設定
	DIPLOMACY_TRAINED_DAY = TRAINING_DAY
	DIPLOMACY_TRAINING_CHARA = LOCAL:5
	PRINTL 
ELSE
	PRINTFORMW %ANAME(MASTER)%は%ANAME(LOCAL:6)%に対して、要求を飲むように説得した…
	;逆調教要求日数を設定
	DIPLOMACY_TRAINED_DAY -= TRAINING_DAY
	DIPLOMACY_TRAINING_CHARA = LOCAL:5
	PRINTL 
ENDIF



RETURN 1
