@CITY_INVEST(都市)
#DIM 都市
#DIM 投資額
#DIM 効果
#DIM 成長前
#DIM 政治力

CALL SINGLE_DRAWLINE
PRINTFORML いくら投資しますか？(0でキャンセル)
PRINTFORML 現在の国庫:{MONEY:(CFLAG:MASTER:所属)}

$INPUT_LOOP

INPUT

IF RESULT <= 0
	RETURN 0
ELSEIF !INRANGE(RESULT, 0, MONEY:(CFLAG:MASTER:所属))
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

投資額 = RESULT
成長前 = CITY_ECONOMY:都市

MONEY:(CFLAG:MASTER:所属) -= 投資額
CALL TMP_CREATE_IS_FREE_MAP
政治力 = TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)

効果 = CALC_CITY_INVEST_EFFECT(都市, 投資額, 政治力)

CITY_ECONOMY:都市 += 効果
CITY_ECONOMY_LIMIT:都市 = MAX(CITY_ECONOMY_LIMIT:都市, CITY_ECONOMY:都市)

PRINTFORML %CITY_NAME:都市%に{投資額}の投資を行いました
PRINTFORMW 経済が{効果 / 100}成長しました

NOW_INVEST ++

RETURN 1

@CALC_MAX_INVEST()
#FUNCTION
RETURNF GET_OWN_CITY(CFLAG:MASTER:所属) / 5 + 1

@CALC_CITY_INVEST_EFFECT(都市, 投資額, 政治力)
#FUNCTION
#DIM 勢力
#DIM 都市
#DIM 効果
#DIM 政治力
#DIM 投資額
#DIM 余剰分

;計算
DUMPRAND
効果 = MAX(投資額 * SQRT(政治力) * RAND(80, 120) / 10000, 10)
INITRAND

SIF CITY_ECONOMY:都市 > 100000
	効果 /= 3

IF CITY_ECONOMY:都市 + 効果 > CITY_ECONOMY_LIMIT:都市
	余剰分 = CITY_ECONOMY:都市 + 効果 - CITY_ECONOMY_LIMIT:都市
	効果 = 効果 - 余剰分 + 余剰分 / 5
ENDIF

RETURNF 効果
