;-------------------------------------------
;戦闘計算で使用するミラー変数の用意
;-------------------------------------------
@SET_BATTLE_MIRROR_VAL(SIDE, CHARA:0 = -1, CHARA:1 = -1, CHARA:2 = -1)
#DIM SIDE
#DIM CHARA, 3
SIF !GROUPMATCH(SIDE, 0, 1)
	THROW SIDEには0か1を代入してください

FOR LOCAL, 0, VARSIZE("BATTLE_COMMANDER", 1)
	BATTLE_COMMANDER:SIDE:LOCAL = -1
	BATTLE_武闘:SIDE:LOCAL = 0
	BATTLE_防衛:SIDE:LOCAL = 0
	BATTLE_知略:SIDE:LOCAL = 0
	BATTLE_政治:SIDE:LOCAL = 0
	BATTLE_歌唱:SIDE:LOCAL = 0
	BATTLE_料理:SIDE:LOCAL = 0
	BATTLE_妖術:SIDE:LOCAL = 0
	BATTLE_武闘パワー:SIDE:LOCAL = 0
	BATTLE_防衛パワー:SIDE:LOCAL = 0
	BATTLE_知略パワー:SIDE:LOCAL = 0
	BATTLE_妖術パワー:SIDE:LOCAL = 0
	BATTLE_歌唱パワー:SIDE:LOCAL = 0
	BATTLE_料理パワー:SIDE:LOCAL = 0
	BATTLE_SKILL_RATE:SIDE:LOCAL = 0

NEXT
BATTLE_COMMANDER_NUM:SIDE = 0

FOR LOCAL:0, 0, VARSIZE("CHARA")
	SIF CHARA:LOCAL < 0 || CHARA:LOCAL > CHARANUM
		BREAK
	BATTLE_COMMANDER_NUM:SIDE ++
	BATTLE_COMMANDER:SIDE:LOCAL = CHARA:LOCAL
	BATTLE_武闘:SIDE:LOCAL = ABL:(CHARA:LOCAL):武闘
	BATTLE_防衛:SIDE:LOCAL = ABL:(CHARA:LOCAL):防衛
	BATTLE_知略:SIDE:LOCAL = ABL:(CHARA:LOCAL):知略
	BATTLE_政治:SIDE:LOCAL = ABL:(CHARA:LOCAL):政治
	BATTLE_歌唱:SIDE:LOCAL = ABL:(CHARA:LOCAL):歌唱
	BATTLE_料理:SIDE:LOCAL = ABL:(CHARA:LOCAL):料理
	BATTLE_妖術:SIDE:LOCAL = ABL:(CHARA:LOCAL):妖術
	BATTLE_武闘パワー:SIDE:LOCAL = ABL_POWER(BATTLE_武闘:SIDE:LOCAL, CHARA:LOCAL)
	BATTLE_防衛パワー:SIDE:LOCAL = ABL_POWER(BATTLE_防衛:SIDE:LOCAL, CHARA:LOCAL)
	BATTLE_知略パワー:SIDE:LOCAL = ABL_POWER(BATTLE_知略:SIDE:LOCAL, CHARA:LOCAL)
	SIF BATTLE_妖術:SIDE:LOCAL
		BATTLE_妖術パワー:SIDE:LOCAL = ABL_POWER(BATTLE_妖術:SIDE:LOCAL, CHARA:LOCAL)
	BATTLE_歌唱パワー:SIDE:LOCAL = ABL_POWER(BATTLE_歌唱:SIDE:LOCAL, CHARA:LOCAL)
	BATTLE_料理パワー:SIDE:LOCAL = ABL_POWER(BATTLE_料理:SIDE:LOCAL, CHARA:LOCAL)
	BATTLE_SKILL_RATE:SIDE:LOCAL = 100
NEXT

;-------------------------------------------------
;BATTLE:SIDE側のCOM_NO番が戦闘中に離脱した
;実際の部隊データからは外れないので注意(別途FORCE_FREE打て)
;-------------------------------------------------
@BATTLE_COMMANDER_WITHDRAWAL(SIDE, COM_IDX)
#DIM SIDE
#DIM COM_IDX

;最後尾を指定した場合はゼロフィル(シフトすると配列外参照で死にかねない)
IF COM_IDX == BATTLE_COMMANDER_NUM:SIDE - 1
	BATTLE_武闘:SIDE:COM_IDX = 0
	BATTLE_防衛:SIDE:COM_IDX = 0
	BATTLE_知略:SIDE:COM_IDX = 0
	BATTLE_政治:SIDE:COM_IDX = 0
	BATTLE_妖術:SIDE:COM_IDX = 0
	BATTLE_料理:SIDE:COM_IDX = 0
	BATTLE_歌唱:SIDE:COM_IDX = 0
	BATTLE_武闘パワー:SIDE:COM_IDX = 0
	BATTLE_防衛パワー:SIDE:COM_IDX = 0
	BATTLE_知略パワー:SIDE:COM_IDX = 0
	BATTLE_妖術パワー:SIDE:COM_IDX = 0
	BATTLE_歌唱パワー:SIDE:COM_IDX = 0
	BATTLE_料理パワー:SIDE:COM_IDX = 0
	BATTLE_SKILL_RATE:SIDE:COM_IDX = 0
	BATTLE_COMMANDER:SIDE:COM_IDX = -1
;最後尾以外を指定した場合はシフトして対応
;どうせCOMMANDER_NUM上限でループするやろしシフト元の値は放置でええやろ
ELSE
	FOR LOCAL, COM_IDX, BATTLE_COMMANDER_NUM:SIDE - 1
		BATTLE_武闘:SIDE:LOCAL = BATTLE_武闘:SIDE:(LOCAL + 1)
		BATTLE_防衛:SIDE:LOCAL = BATTLE_防衛:SIDE:(LOCAL + 1)
		BATTLE_知略:SIDE:LOCAL = BATTLE_知略:SIDE:(LOCAL + 1)
		BATTLE_政治:SIDE:LOCAL = BATTLE_政治:SIDE:(LOCAL + 1)
		BATTLE_妖術:SIDE:LOCAL = BATTLE_妖術:SIDE:(LOCAL + 1)
		BATTLE_料理:SIDE:LOCAL = BATTLE_料理:SIDE:(LOCAL + 1)
		BATTLE_歌唱:SIDE:LOCAL = BATTLE_歌唱:SIDE:(LOCAL + 1)
		BATTLE_武闘パワー:SIDE:LOCAL = BATTLE_武闘パワー:SIDE:(LOCAL + 1)
		BATTLE_防衛パワー:SIDE:LOCAL = BATTLE_防衛パワー:SIDE:(LOCAL + 1)
		BATTLE_知略パワー:SIDE:LOCAL = BATTLE_知略パワー:SIDE:(LOCAL + 1)
		BATTLE_妖術パワー:SIDE:LOCAL = BATTLE_妖術パワー:SIDE:(LOCAL + 1)
		BATTLE_歌唱パワー:SIDE:LOCAL = BATTLE_歌唱パワー:SIDE:(LOCAL + 1)
		BATTLE_料理パワー:SIDE:LOCAL = BATTLE_料理パワー:SIDE:(LOCAL + 1)
		BATTLE_SKILL_RATE:SIDE:LOCAL = BATTLE_SKILL_RATE:SIDE:(LOCAL + 1)
		BATTLE_COMMANDER:SIDE:LOCAL = BATTLE_COMMANDER:SIDE:(LOCAL + 1)
	NEXT
ENDIF

BATTLE_COMMANDER_NUM:SIDE -= 1

;---------------------------------
;暗殺の効果用
;---------------------------------
@BATTLE_KNOCKOUT(対象側, 対象番号)
#DIM 対象側
#DIM 対象番号
BATTLE_武闘:対象側:対象番号 = 0
BATTLE_防衛:対象側:対象番号 = 0
BATTLE_知略:対象側:対象番号 = 0
BATTLE_政治:対象側:対象番号 = 0
BATTLE_妖術:対象側:対象番号 = 0
BATTLE_料理:対象側:対象番号 = 0
BATTLE_歌唱:対象側:対象番号 = 0
BATTLE_武闘パワー:対象側:対象番号 = 0
BATTLE_防衛パワー:対象側:対象番号 = 0
BATTLE_知略パワー:対象側:対象番号 = 0
BATTLE_妖術パワー:対象側:対象番号 = 0
BATTLE_歌唱パワー:対象側:対象番号 = 0
BATTLE_料理パワー:対象側:対象番号 = 0
BATTLE_SKILL_RATE:対象側:対象番号 = 0
BATTLE_SKILL_SEALED:対象側:対象番号 = 1



;-------------------------------------------------
;BATTLE:SIDE側の攻撃能力を決定する。
;CITY_NOが1以上で、対応するCITY_TYPEが0なら、防衛部隊とみなす。
;-------------------------------------------------
@ATTACK_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM ATTACK
ATTACK = 0

;だれもいない場合、能力値0の武将がひとりいるものとみなす。
IF BATTLE_COMMANDER_NUM:SIDE == 0
	ATTACK = ABL_POWER(0, -1)
	GOTO BASE_CALCED
ENDIF

;誰かいる場合、対応能力と妖術から算出する。
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	SIF CURRENT_CHARA < 0
		CONTINUE
	ATTACK += BATTLE_武闘パワー:SIDE:LOCAL
	ATTACK += BATTLE_妖術パワー:SIDE:LOCAL / 3
NEXT

$BASE_CALCED

ATTACK = ATTACK * SUPPORT_LEVEL(SIDE, CITY_NO) / 1000

;防衛部隊用の計算式
IF CITY_NO && CITY_TYPE:CITY_NO == 0
	RETURNF ATTACK / 2 * MAX(CITY_GUARD_RATE(CITY_NO) - 100, 100) / 100
ENDIF

RETURNF MAX(ATTACK / 3, 1)

;-------------------------------------------------
;BATTLE:SIDE側の防御能力を決定する。
;CITY_NOが1以上で、対応するCITY_TYPEが0なら、防衛部隊とみなす。
;-------------------------------------------------
@DEFENSE_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM DEFENSE
DEFENSE = 0

;だれもいない場合、能力値0の武将がひとりいるものとみなす。
IF BATTLE_COMMANDER_NUM:SIDE == 0
	DEFENSE = ABL_POWER(0, -1)
	GOTO BASE_CALCED
ENDIF

;誰かいる場合、対応能力と妖術から算出する。
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	SIF CURRENT_CHARA < 0
		CONTINUE
	DEFENSE += BATTLE_防衛パワー:SIDE:LOCAL
	DEFENSE += BATTLE_妖術パワー:SIDE:LOCAL / 3
NEXT

$BASE_CALCED

DEFENSE = DEFENSE * SUPPORT_LEVEL(SIDE, CITY_NO) / 1000

;防衛部隊用の計算式
;無所属都市は防衛倍率を勘案しない
IF CITY_NO && CITY_TYPE:CITY_NO == 0
	IF IS_COUNTRY(CITY_OWNER:CITY_NO)
		DEFENSE = DEFENSE * CITY_GUARD_RATE(CITY_NO) / 100
	ENDIF
	RETURNF DEFENSE
ENDIF
RETURNF MAX(DEFENSE / 3, 1)


;-------------------------------------------------
;BATTLE:SIDE側の知略能力を決定する。
;CITY_NOが1以上で、対応するCITY_TYPEが0なら、防衛部隊とみなす。
;-------------------------------------------------
@INTELLIGENCE_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM INTELLIGENCE
INTELLIGENCE = 0

;だれもいない場合、能力値0の武将がひとりいるものとみなす。
IF BATTLE_COMMANDER_NUM:SIDE == 0
	INTELLIGENCE = ABL_POWER(0, -1)
	GOTO BASE_CALCED
ENDIF

FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	SIF CURRENT_CHARA < 0
		CONTINUE
	INTELLIGENCE += BATTLE_知略パワー:SIDE:LOCAL
NEXT

$BASE_CALCED

INTELLIGENCE = INTELLIGENCE * SUPPORT_LEVEL(SIDE, CITY_NO) / 1000

RETURNF MAX(INTELLIGENCE / 3, 1)


;-------------------------------------------------
;BATTLE:SIDE側の妖術能力を決定する。
;CITY_NOが1以上で、対応するCITY_TYPEが0なら、防衛部隊とみなす。
;-------------------------------------------------
@MAGIC_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM MAGIC
MAGIC = 0
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	;おそらく無い
	SIF CURRENT_CHARA < 0
		CONTINUE
	MAGIC += BATTLE_妖術パワー:SIDE:LOCAL
NEXT
RETURNF MAGIC

;-------------------------------------------------
;BATTLE:SIDE側の妖術能力を決定する。
;CITY_NOが1以上で、対応するCITY_TYPEが0なら、防衛部隊とみなす。
;-------------------------------------------------
@SUPPORT_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM SONG_SUPPORT
#DIM FOOD_SUPPORT
#DIM SUPPORT
SUPPORT = 0
SONG_SUPPORT = 0
FOOD_SUPPORT = 0
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	;おそらく無い
	SIF CURRENT_CHARA < 0
		CONTINUE
	SONG_SUPPORT += BATTLE_歌唱パワー:SIDE:LOCAL
	FOOD_SUPPORT += BATTLE_料理パワー:SIDE:LOCAL
NEXT
;能力値100が3人いた場合に対する割合
;1000分率で返す必要があるので返却値は1000をかけておく
SONG_SUPPORT = SONG_SUPPORT * 1000 / (ABL_POWER(100, -1) * 3)
FOOD_SUPPORT = FOOD_SUPPORT * 1000 / (ABL_POWER(100, -1) * 3)
SUPPORT = 1000 + SONG_SUPPORT + FOOD_SUPPORT
RETURNF SUPPORT

;-------------------------------------------------
;連合討伐対象を撃破したときの処理
;-------------------------------------------------
@SLG_COMBAT_BEAT_TREATY_TARGET(勢力, 討伐対象)
#DIM 勢力
#DIM 討伐対象
#DIM 連合番号
#DIM 報奨金
#DIM ループ勢力
連合番号 = FINDELEMENT(TREATY_U_TARGET, 討伐対象)
FOR LOCAL, 0, MAX_TREATY_COUNTRY
	SIF TREATY_U_COUNTRY:連合番号:LOCAL == 勢力
		BREAK
NEXT

SIF LOCAL == MAX_TREATY_COUNTRY
	RETURN

報奨金 = RAND(3000, 5000)

PRINTFORML 連合討伐対象である%ANAME(GET_COUNTRY_BOSS(討伐対象))%を撃破したことで、連合参加国からの%ANAME(GET_COUNTRY_BOSS(勢力))%の評価が向上した
CALL ICPRINT(@"また、報奨金として金<{報奨金}>が与えられた", "L", カラー_注意)
FOR LOCAL, 0, MAX_TREATY_COUNTRY
	ループ勢力 = TREATY_U_COUNTRY:連合番号:LOCAL
	SIF IS_COUNTRY(ループ勢力) && ループ勢力 != 勢力
		CALL CHANGE_RELATION_C_TO_C(ループ勢力, 勢力, RAND(10, 30), RAND(10, 30) * -1)
NEXT
MONEY:勢力 += 報奨金


;-------------------------------------------------
;ARG:0勢力のARG:1部隊がARG:2の情報をPRINTする関数
;ARG:3が真なら??を表示
;-------------------------------------------------
@PRINT_UNIT_INFO_COMBAT(ARG:0, ARG:1, ARG:2, ARG:3 = 0, ARG:4 = 1)
IF ARG:3
	CALL COLOR_PRINT(@"?? %"????軍", 10, LEFT% ★:%"??", 4, LEFT% 兵:%"????", 6, LEFT%攻:%"????", 5, LEFT%防:%"????", 5, LEFT%知:%"????", 5, LEFT%", カラー_選択不可)
	RETURN
ENDIF
LOCAL:1 = GET_COUNTRY_BOSS(ARG:0)
LOCALS:1 = %SNAME(LOCAL:1)%
LOCALS:2 = %GET_COMMANDER_NAME_COMBAT(ARG:2, " ")%
;あんまり長いなら半角に
SIF STRLENS(LOCALS:2) >= 25
	LOCALS:2 = %TOHALF(LOCALS:2)%
SETCOLOR COUNTRY_COLOR:(ARG:0)
;部隊能力の用意
PRINTFORM %LOCALS:1, MAX_CHARANAME_LENGTH / 2, LEFT%軍:
IF ARG:4
	SELECTCASE CHECK_COUNTRY_RELATION_F(CFLAG:MASTER:所属, ARG:0)
		CASE 0
			IF IS_COUNTRY(CFLAG:MASTER:所属)
				SETCOLOR カラー_警告
				PRINT 敵
			ELSE
				PRINT   
			ENDIF
		CASE 1
			SETCOLOR カラー_停戦
			PRINT 停
		CASE 2
			SETCOLOR カラー_連合
			PRINT 連
		CASE 3
			SETCOLOR カラー_同盟
			PRINT 同
		CASE 4
			SETCOLOR カラー_永久同盟
			PRINT 永
		CASE 5
			SETCOLOR カラー_シアン
			PRINT 自
	ENDSELECT
	SETCOLOR COUNTRY_COLOR:(ARG:0)
	PRINT  
ENDIF
PRINTFORM  %LOCALS:2, 25, LEFT%
PRINTFORM ★:{SUM_STARS_COMBAT(ARG:2), 4, LEFT}
PRINTFORM 兵:{BATTLE_SOL:(ARG:2), 6, LEFT}
PRINTFORM 攻:
CALL PRINT_ALPHABET_RANK(ランク_部隊, BATTLE_ATK:(ARG:2))
SETCOLOR COUNTRY_COLOR:(ARG:0)
PRINTFORM   防:
CALL PRINT_ALPHABET_RANK(ランク_部隊, BATTLE_DEF:(ARG:2))
SETCOLOR COUNTRY_COLOR:(ARG:0)
PRINTFORM   知:
CALL PRINT_ALPHABET_RANK(ランク_部隊, BATTLE_INT:(ARG:2))
SETCOLOR COUNTRY_COLOR:(ARG:0)
PRINTFORM   妖:
CALL PRINT_ALPHABET_RANK(ランク_部隊, BATTLE_MAG:(ARG:2))
SETCOLOR COUNTRY_COLOR:(ARG:0)
PRINTFORM   疲:
PRINTFORM {UNIT_TIRED_COUNT:(ARG:0):(ARG:1), 2, LEFT}
RESETCOLOR

;-------------------------------------------------
;ARG:0精力のARG:1部隊と、ARG:2精力のARG:3部隊の戦力差を表示する関数 ARG:3が負ならARG:2を都市番号とみなす
;-------------------------------------------------
@PRINT_COMBAT_INFO(ARG:0, ARG:1, ARG:2, ARG:3)
#DIM 勢力色, 2
#DIM 兵数, 2
#DIM 攻撃力, 2
#DIM 防御力, 2
#DIM 知略, 2
勢力色:0 = COUNTRY_COLOR:(ARG:0)
勢力色:1 = ARG:3 >= 0 ? COUNTRY_COLOR:(ARG:2) # COUNTRY_COLOR:(CITY_OWNER:(ARG:2))

PRINTFORM 兵数:
CALL PRINT_VERSUSBAR(BATTLE_SOL:0, BATTLE_SOL:1, 20, 勢力色:0, 勢力色:1)
PRINTL
PRINTFORM 攻撃:
CALL PRINT_VERSUSBAR(BATTLE_ATK:0, BATTLE_ATK:1, 20, 勢力色:0, 勢力色:1)
PRINTL
PRINTFORM 防御:
CALL PRINT_VERSUSBAR(BATTLE_DEF:0, BATTLE_DEF:1, 20, 勢力色:0, 勢力色:1)
PRINTL
PRINTFORM 知略:
CALL PRINT_VERSUSBAR(BATTLE_INT:0, BATTLE_INT:1, 20, 勢力色:0, 勢力色:1)
PRINTL


;-------------------------------------------------
;都市ARG:0の情報をPRINTする関数
;-------------------------------------------------
@PRINT_CITY_INFO_COMBAT(ARG:0, ARG:1, ARG:2 = 1)
LOCAL:1 = CITY_OWNER:(ARG:0)
LOCAL:2 = GET_COUNTRY_BOSS(LOCAL:1)
LOCALS:1 = %(LOCAL:2 != -1 ? SNAME(LOCAL:2) # "無所属")%
LOCALS:2 = %GET_COMMANDER_NAME_COMBAT(ARG:1, " ")%
;あんまり長いなら半角に
SIF STRLENS(LOCALS:2) >= 25
	LOCALS:2 = %TOHALF(LOCALS:2)%
SETCOLOR COUNTRY_COLOR:(LOCAL:1)
PRINTFORM %LOCALS:1, MAX_CHARANAME_LENGTH / 2, LEFT%軍:
IF ARG:2
	SELECTCASE CHECK_COUNTRY_RELATION_F(CFLAG:MASTER:所属, LOCAL:1)
		CASE 0
			IF IS_COUNTRY(CFLAG:MASTER:所属)
				SETCOLOR カラー_警告
				PRINT 敵
			ELSE
				PRINT   
			ENDIF
		CASE 1
			SETCOLOR カラー_停戦
			PRINT 停
		CASE 2
			SETCOLOR カラー_連合
			PRINT 連
		CASE 3
			SETCOLOR カラー_同盟
			PRINT 同
		CASE 4
			SETCOLOR カラー_永久同盟
			PRINT 永
		CASE 5
			SETCOLOR カラー_シアン
			PRINT 自
	ENDSELECT
	SETCOLOR COUNTRY_COLOR:(LOCAL:1)
ENDIF
PRINTFORM  %LOCALS:2, 25, LEFT%
PRINTFORM ★:{SUM_STARS_COMBAT(ARG:1), 4, LEFT}
PRINTFORM 兵:{BATTLE_SOL:(ARG:1), 6, LEFT}
PRINTFORM 攻:
CALL PRINT_ALPHABET_RANK(ランク_部隊, BATTLE_ATK:(ARG:1))
SETCOLOR COUNTRY_COLOR:(LOCAL:1)
PRINTFORM   防:
CALL PRINT_ALPHABET_RANK(ランク_部隊, BATTLE_DEF:(ARG:1))
SETCOLOR COUNTRY_COLOR:(LOCAL:1)
PRINTFORM   知:
CALL PRINT_ALPHABET_RANK(ランク_部隊, BATTLE_INT:(ARG:1))
SETCOLOR COUNTRY_COLOR:(LOCAL:1)
PRINTFORM   妖:
CALL PRINT_ALPHABET_RANK(ランク_部隊, BATTLE_MAG:(ARG:1))
RESETCOLOR

;-------------------------------------------------
;ARG:0側のARG:1部隊の星の数を返す関数
;-------------------------------------------------
@SUM_STARS_COMBAT(ARG:0)
#FUNCTION
LOCAL:2 = 0
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:(ARG:0)
	LOCAL:1 = BATTLE_COMMANDER:(ARG:0):LOCAL
	SIF LOCAL:1 >= 0
		LOCAL:2 += TMP_CHARA_STARS:(LOCAL:1)
NEXT
RETURNF LOCAL:2


;-------------------------------------------------
;ARG:0側部隊の将の名前を返す関数 ARGS:2=複数の将がいる場合に間を区切る文字
;-------------------------------------------------
@GET_COMMANDER_NAME_COMBAT(ARG:0, ARGS:2 = "・")
#FUNCTIONS
LOCALS:0 = 
LOCAL:2 = 0
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:(ARG:0)
	LOCAL:1 = BATTLE_COMMANDER:(ARG:0):LOCAL
	IF LOCAL:1 >= 0
		SIF BATTLE_SKILL_SEALED:(ARG:0):LOCAL && ALLSAMES(0, BATTLE_武闘パワー:(ARG:0):(LOCAL), BATTLE_知略パワー:(ARG:0):(LOCAL), BATTLE_防衛パワー:(ARG:0):(LOCAL))
			CONTINUE
		SIF LOCAL:2 >= 1
			LOCALS:0 += @"%ARGS:2%"
		LOCALS:0 += @"%SNAME(LOCAL:1)%"
		IF CFLAG:(LOCAL:1):行動不能状態 == 行動不能_臨月
			LOCALS:0 += "(臨)"
		ELSEIF TALENT:(LOCAL:1):妊娠
			LOCALS:0 += "(妊)"
		ELSEIF CFLAG:(LOCAL:1):行動不能状態 == 行動不能_育児
			LOCALS:0 += "(育)"
		ENDIF
	ENDIF
	LOCAL:2 ++
NEXT
RETURNF LOCALS:0
