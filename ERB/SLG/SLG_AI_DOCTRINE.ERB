;-------------------------------------------------
;●国家政策の決定
;政策メモ
;AIは専用の国策を取得できるようになる
;-------------------------------------------------
@SLG_AI_SETDOCTRINE(勢力)
#DIM 勢力
#DIM 勢力タイプ
#DIM 隣接勢力
#DIM 敵と隣接
#DIM 経済
#DIM 経済限界
#DIM 経済比率
#DIM 兵力
#DIM 兵力限界
#DIM 隣接兵力
#DIM 隣接兵力限界
#DIM 隣接経済
#DIM 隣接経済限界
#DIM 隣接兵力比
#DIM 隣接経済比
#DIM スコア, 3

VARSET スコア
VARSET 隣接経済
VARSET 隣接経済限界
VARSET 隣接兵力
VARSET 隣接兵力限界
VARSET 敵と隣接
勢力タイプ = COUNTRY_AI_TYPE:勢力
経済 = TMP_SUM_ECONOMY:勢力
経済限界 = TMP_SUM_ECONOMY_LIMIT:勢力
兵力 = TMP_SUM_SOLDIER:勢力
兵力限界 = 経済 * 10

SIF DAY < SLG_PP:1
	GOTO CANCEL

;隣接する敵対勢力のうち、最大の経済と最大の兵力を算出する
FOR 隣接勢力, 1, MAX_COUNTRY
	IF IS_COUNTRY(隣接勢力) && TMP_COUNTRY_IS_NEIBORING:勢力:隣接勢力 && TMP_COUNTRY_RELATION:勢力:隣接勢力 == 0
		敵と隣接 = 1
		隣接経済 = MAX(TMP_SUM_ECONOMY:隣接勢力, 隣接経済)
		隣接兵力 = MAX(TMP_SUM_SOLDIER:隣接勢力, 隣接兵力)
	ENDIF
NEXT

$CANCEL

;敵と隣接している
IF 敵と隣接 && DAY >= SLG_PP:1
	隣接経済比 = 経済 * 100 / MAX(隣接経済, 1)
	隣接兵力比 = 兵力 * 100 / MAX(隣接兵力, 1)
	;隣接勢力との経済の比ないし兵力の比のうち、小さい（劣っている）ほうで考える
	SELECTCASE MIN(隣接経済比, 隣接兵力比)
		;自身より強力
		CASE 0 TO 74
			スコア:政策_経済 = 隣接経済比 * 2 / 3
			スコア:政策_徴兵 = 隣接兵力比
			スコア:政策_防衛 = 隣接兵力比 * 2
			IF TMP_OWN_CITY:勢力 < 3
				スコア:政策_防衛 *= 2
			ENDIF
		;同程度
		CASE 75 TO 124
			スコア:政策_経済 = 隣接経済比
			スコア:政策_徴兵 = 隣接兵力比
			スコア:政策_防衛 = 隣接兵力比
		;自軍より貧弱
		;防衛を軽視し、経済と兵力に重きをおく
		CASEELSE
			スコア:政策_経済 = 隣接経済比
			スコア:政策_徴兵 = 隣接兵力比
	ENDSELECT
	SIF TMP_OWN_CITY:勢力 < 3
		スコア:政策_防衛 *= 2
;敵と隣接していない
;防衛のことは考えない
ELSE
	スコア:政策_経済 = 10000 - (経済 * 10000 / 経済限界)
	スコア:政策_徴兵 = 10000 - (兵力 * 10000 / 兵力限界)
ENDIF

スコア:政策_経済 = スコア:政策_経済 * AI_POLICY_PRIORITY:勢力タイプ:政策_経済 / 100
スコア:政策_徴兵 = スコア:政策_徴兵 * AI_POLICY_PRIORITY:勢力タイプ:政策_防衛 / 100
スコア:政策_防衛 = スコア:政策_防衛 * AI_POLICY_PRIORITY:勢力タイプ:政策_徴兵 / 100

COUNTRY_POLICY:勢力 = FINDELEMENT(スコア, MAXARRAY(スコア))
