;-------------------------------------------------
;シナリオ1
;作品対抗幻想郷大戦（暫定）
;-------------------------------------------------
@SCENARIO_NAME_1
RESULTS = 幻想郷大戦
RETURN

@SCENARIO_INTRO_1
PRINTFORML
PRINTFORMW 幻想郷は分裂した！
PRINTFORMW 各勢力が様々な思惑を絡め、争いあう！
PRINTFORMW だが目に見える勢力だけに囚われるな、水面下の動きにも気を払え！
PRINTFORMW 君はこの戦いを生き残り、幻想郷の覇者となれるのか！
PRINTL

;ランダムキャラは選択に委ねる
FLAG:OPランダムキャラ使用 = 0


@SCENARIO_PLACEMENT_1

CALL SCENARIO_PLACEMENT_DEFAULT

;慧音、神綺、純狐にゲタをはかせる
COUNTRY_SOLDIER:15 += 2000
COUNTRY_SOLDIER:21 += 3000
COUNTRY_SOLDIER:23 += 2000


;-------------------------------------------------
;定例イベント
;ターンエンド時に呼び出される
;SCVAR:0 封獣ぬえ離反済みフラグ
;-------------------------------------------------
@SCENARIO_EVENT_1

CALL SCENARIO_EVENT_1_INDEPENDENCE

@SCENARIO_EVENT_1_INDEPENDENCE
#DIM 正邪
#DIM ぬえ

正邪 = NAME_TO_CHARA("正邪")
ぬえ = NAME_TO_CHARA("ぬえ")

SIF DAY < 10
	RETURN

IF GETBIT(SCVAR:0, 0) && GETBIT(SCVAR:0, 1)
	RETURN
ELSE
	LOCAL:1 = IFRAND(@"{正邪}", !GETBIT(SCVAR:0, 0), @"{ぬえ}", !GETBIT(SCVAR:0, 1))
ENDIF

SIF !CAN_RANDOM_INDEPENDENCE(LOCAL:1)
	RETURN

SIF CALC_INDEPENDENCE_RATE(LOCAL:1) < RAND:5000
	RETURN

CALL NPC_INDEPENDENCE(LOCAL:1)

SELECTCASE LOCAL:1
	CASE NAME_TO_CHARA("正邪")
		SETBIT SCVAR:0 , 0
	CASE NAME_TO_CHARA("ぬえ")
		SETBIT SCVAR:0 , 1
ENDSELECT
