;-------------------------------------------------
;シナリオ19
;幻想狂騒曲
;テーマは「完全シャッフル」
;勢力メンバーはおろか、リーダー及びその本拠地すらプレイごとにランダムで決めます
;
;-------------------------------------------------
@SCENARIO_NAME_19
RESULTS = 幻想狂騒曲
RETURN

@SCENARIO_INTRO_19
PRINTFORMW ※君主・メンバー・領地がランダムに選ばれるシナリオです
PRINTFORML

;ランダムキャラは選択に委ねる
FLAG:OPランダムキャラ使用 = 0


@SCENARIO_MAPSELECT_19
PRINTFORML
PRINTFORML どのマップでプレイしますか？
CALL ASK_MULTI("デフォルト", "日本", "ヨーロッパ")
SELECTCASE RESULT
	CASE 0
		MAPID '= "DEFAULT"
	CASE 1
		MAPID '= "JAPAN"
	CASE 2
		MAPID '= "EU"
ENDSELECT


@SCENARIO_PLACEMENT_19
#DIM 勢力数
#DIM 士官数, MAX_COUNTRY
#DIM 振り分け

COUNTRY_BOSS:0 = 0
COUNTRY_COLOR:0 = GETDEFCOLOR()


CALL ASK_YN("旧作キャラも君主になりうる", "旧作キャラは君主にならない")
SCVAR:0 = RESULT

CALL ASK_MULTI("勢力数ふつう", "勢力数おおめ", "勢力数ひかえめ")
SELECTCASE RESULT
	CASE 0
		勢力数 = 12 + RAND:5 + RAND:5 + RAND:5
	CASE 1
		勢力数 = MIN(18 + RAND:7 + RAND:7 + RAND:7, MAX_COUNTRY - 2)
	CASE 2
		勢力数 = 6 + RAND:3 + RAND:3 + RAND:3
ENDSELECT

PRINTFORML 君主に「したくない」キャラを選択
CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(50, "NONE", "NONE")

CALL FISHER_YATES_SHAFFLE(CHARANUM)
VARSET 振り分け

;君主の設定
FOR LOCAL, 0, CHARANUM
	LOCAL:1 = SHAFFLE_ARRAY:LOCAL
	SIF !IS_TOHO_CHARA(LOCAL:1)
		CONTINUE
	SIF FINDELEMENT(SELECTED_CHARA, LOCAL:1) != -1
		CONTINUE
	SIF LOCAL:1 == NAME_TO_CHARA("あなた")
		CONTINUE
	SIF SCVAR:0 && TALENT:(LOCAL:1):キャラ区分
		CONTINUE
	COUNTRY_BOSS:(振り分け + 1) = GET_ID(LOCAL:1)
	CALL CHANGE_COUNTRY(LOCAL:1, 振り分け + 1, 1)
	振り分け ++
	SIF 振り分け == 勢力数
		BREAK
NEXT

;勢力の作成
FOR LOCAL, 1, 勢力数 + 1
	COUNTRY_COLOR:LOCAL = CHARA_COUNTRY_COLOR(GET_COUNTRY_BOSS(LOCAL))
	CALL SP_COUNTRY_RISE(LOCAL)
NEXT

CALL TMP_CREATE_OWN_CITY_MAP()
CALL FISHER_YATES_SHAFFLE(CHARANUM)

;士官の振り分け
VARSET 士官数
VARSET 振り分け
LOCAL:3 = 1
FOR LOCAL, 0, CHARANUM
	LOCAL:1 = SHAFFLE_ARRAY:(LOCAL)
	SIF LOCAL:1 == GET_COUNTRY_BOSS(CFLAG:(LOCAL:1):所属)
		CONTINUE
	SIF LOCAL:1 == NAME_TO_CHARA("あなた")
		CONTINUE

	;士官を振り分けていく
	;都市の数が少ないほど、士官の数は多くなる
	;振り分けた時点で振り分け位置を記録してループを脱出,その次から次回ループを開始
	FOR LOCAL:2, LOCAL:3, 勢力数 + 1
		SIF LIMIT(7 - TMP_OWN_CITY:(LOCAL:2), 1, 6) - 士官数:(LOCAL:2) <= 0
			CONTINUE
		CALL CHANGE_COUNTRY(LOCAL:1, LOCAL:2, 1)
		士官数:(LOCAL:2) ++
		LOCAL:3 = LOCAL:2 + 1
		SIF LOCAL:3 == 勢力数 + 1
			LOCAL:3 = 1
		BREAK
	NEXT
NEXT

;指定されなかったキャラは放浪 「あなた」は除外（MASTER入れ替えたときに放浪するのを防ぐ）。
FOR LOCAL:0, 1, CHARANUM
	SIF CFLAG:(LOCAL:0):1 == 0  && CSTR:(LOCAL:0):99 != "あなた"
		CFLAG:(LOCAL:0):特殊状態 = 特殊状態_放浪
NEXT

;無所属都市の兵数は経済の7.5%
FOR LOCAL:0, 0, MAX_CITY
	SIF CITY_OWNER:(LOCAL:0) == 0
		CITY_SOLDIER:(LOCAL:0) = CITY_ECONOMY:(LOCAL:0) * 75 / 1000
NEXT

FOR LOCAL:0, 1, 勢力数 + 1
	SELECTCASE TMP_OWN_CITY:LOCAL
		CASE 1, 2
			MONEY:LOCAL = 5000
			COUNTRY_SOLDIER:LOCAL = RAND(10000, 15000)
		CASE 3, 4
			MONEY:LOCAL = 2000
			COUNTRY_SOLDIER:LOCAL = RAND(3000, 7000)
		CASE 5, 6
			MONEY:LOCAL = 100
			COUNTRY_SOLDIER:LOCAL = RAND(100, 2000)
		CASEELSE
	ENDSELECT
NEXT


;■関係設定
CALL SHARED_SETTING_RELATION

;勢力間の感情値
FOR LOCAL:0, 0, 勢力数 + 1
	FOR LOCAL:1, LOCAL:0 + 1, 勢力数 + 1
		CALL CHANGE_RELATION_C_TO_C(LOCAL:0, LOCAL:1, 500 + POWER(-1, RAND:2 + 1) * (RAND:101 + RAND:101), 500 + POWER(-1, RAND:2 + 1) * (RAND:101 + RAND:101))
		CALL CHANGE_RELATION_C_TO_C(LOCAL:1, LOCAL:0, 500 + POWER(-1, RAND:2 + 1) * (RAND:101 + RAND:101), 500 + POWER(-1, RAND:2 + 1) * (RAND:101 + RAND:101))
	NEXT
NEXT

;個人間の感情値
CALL FISHER_YATES_SHAFFLE(CHARANUM)
FOR LOCAL:1, 0, CHARANUM - 1
	CALL CHANGE_RELATION_O_TO_O(SHAFFLE_ARRAY:(LOCAL:1), SHAFFLE_ARRAY:(LOCAL:1 + 1), RAND:400 - 200, RAND:400 - 200)
	CALL CHANGE_RELATION_O_TO_O(SHAFFLE_ARRAY:(LOCAL:1 + 1), SHAFFLE_ARRAY:(LOCAL:1), RAND:400 - 200, RAND:400 - 200)
NEXT



;-------------------------------------------------
;定例イベント
;ターンエンド時に呼び出される
;-------------------------------------------------
@SCENARIO_EVENT_19
#DIM UNIFY

