;-------------------------------------------------
;シナリオ20
;英雄集結
;-------------------------------------------------
@SCENARIO_NAME_20
RESULTS = 英雄集結
RETURN

@SCENARIO_INTRO_20
PRINTFORML
PRINTFORML 今、幻想郷でどっかの誰かが何人か立ち上がった！
PRINTFORML 目指すは幻想郷統一。仲間は０。
PRINTFORML 仲間を集めて幻想郷を統一するのは果たしてどこの誰か……。
PRINTFORML どうなる幻想郷！
PRINTFORML
PRINTFORML ※君主と初期領地(一箇所)がランダムに選ばれるシナリオです
PRINTFORMW
PRINTL

;ランダムキャラは選択に委ねる
FLAG:OPランダムキャラ使用 = 0

@SCENARIO_PLACEMENT_20
#DIM S20_choose_country_num
#DIM S20_choose_country, 20, 2
#DIM CONST S20_start_area = 0
#DIM CONST S20_start_monarch = 1
VARSET LOCAL
LOCAL:40 = 0
PRINTFORML 君主になるキャラを主要なキャラのみに絞りますか？
PRINTFORML (旧作を除く東方キャラに絞られます)
CALL ASK_YN
SIF RESULT == 0
	LOCAL:40 = 1


;勢力設定
COUNTRY_BOSS:0 = 0
COUNTRY_COLOR:0 = GETDEFCOLOR()

;作成する国の数
S20_choose_country_num = 5 + RAND:11

VARSET S20_choose_country, -1

FOR LOCAL,0,S20_choose_country_num

	;国の君主を選択
	LOCAL:32 = -1
	FOR LOCAL:1,0,MIN_NO_SP_CHARA
		LOCAL:32 = RAND:CHARANUM
		IF LOCAL:32 >= 0 && CSTR:(LOCAL:32):99 != "あなた"
			IF LOCAL:40 ;ハイを選んでいた場合、真っ白でない固有カラー持ち以外は弾く
				IF CHARA_DIPRO_COLOR_F(CSTR:(LOCAL:32):99)== 0xffffff
					CONTINUE
				ENDIF
			ENDIF
			LOCAL:33 = 0
			FOR LOCAL:2,0,20
				IF LOCAL:32 == S20_choose_country:(LOCAL:2):S20_start_monarch
					LOCAL:33 = 1
				ENDIF
			NEXT
			IF !LOCAL:33
				S20_choose_country:(LOCAL):S20_start_monarch = LOCAL:32
				BREAK
			ENDIF
		ENDIF
	NEXT

	;国の開始位置を選択
	LOCAL:32 = -1
	FOR LOCAL:1,0,MIN_NO_SP_CHARA
		LOCAL:32 = RAND:99 + 1
		IF LOCAL:32 > 0
			LOCAL:33 = 0
			FOR LOCAL:2,0,20
				IF LOCAL:32 == S20_choose_country:(LOCAL:2):S20_start_area
					LOCAL:33 = 1
				ENDIF
			NEXT
			IF !LOCAL:33
				S20_choose_country:(LOCAL):S20_start_area = LOCAL:32
				BREAK
			ENDIF
		ENDIF
	NEXT
NEXT

LOCAL:33 = 0
FOR LOCAL,0,S20_choose_country_num
	IF S20_choose_country:(LOCAL):S20_start_monarch >= 0 && S20_choose_country:(LOCAL):S20_start_area > 0
		LOCAL:33++
	ENDIF
NEXT
IF LOCAL:33 <= 1
	PRINTFORMW 国の作成に失敗しました
	RETURN
ENDIF

;選択した国の作成
FOR LOCAL,0,S20_choose_country_num
	SIF S20_choose_country:(LOCAL):S20_start_monarch < 0 || S20_choose_country:(LOCAL):S20_start_area <= 0
		CONTINUE
	COUNTRY_BOSS:(LOCAL+1) = GET_ID(S20_choose_country:(LOCAL):S20_start_monarch)
	COUNTRY_COLOR:(LOCAL+1) = RAND:(256*256*256)
	
	CITY_OWNER:(S20_choose_country:(LOCAL):S20_start_area) = (LOCAL+1)
	CFLAG:(S20_choose_country:(LOCAL):S20_start_monarch):1 = (LOCAL+1)
NEXT

;指定されなかったキャラは放浪 「あなた」は除外（MASTER入れ替えたときに放浪するのを防ぐ）。
FOR LOCAL:0, 1, CHARANUM
	SIF CFLAG:(LOCAL:0):1 == 0  && CSTR:(LOCAL:0):99 != "あなた"
		CFLAG:(LOCAL:0):特殊状態 = 1
NEXT


;■経済規模の設定
CALL SHARED_SETTING_ECONOMY

;■防衛倍率の設定
CALL SHARED_SETTING_GUARD

;兵力の初期配置
CALL INIT_ARMY

;無所属都市の兵数は経済の7.5%
FOR LOCAL:0, 0, MAX_CITY
	SIF CITY_OWNER:(LOCAL:0) == 0
		CITY_SOLDIER:(LOCAL:0) = CITY_ECONOMY:(LOCAL:0) * 75 / 1000
NEXT

;■関係設定
CALL SHARED_SETTING_RELATION

;都市次第では序盤詰むのでこづかい
FOR LOCAL:0, 0, MAX_COUNTRY
	SIF IS_COUNTRY(LOCAL)
		MONEY:LOCAL += 10000
NEXT

