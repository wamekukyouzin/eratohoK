;-------------------------------------------------
;シナリオ20
;英雄集結
;-------------------------------------------------
@SCENARIO_NAME_20
RESULTS = 英雄集結
RETURN

@SCENARIO_INTRO_20
PRINTFORML
PRINTFORML 今、幻想郷でどっかの誰かが何人か立ち上がった！
PRINTFORML 目指すは幻想郷統一。仲間は０。
PRINTFORML 仲間を集めて幻想郷を統一するのは果たしてどこの誰か……。
PRINTFORML どうなる幻想郷！
PRINTFORML
PRINTFORML ※君主と初期領地(一箇所)がランダムに選ばれるシナリオです
PRINTFORMW
PRINTL

;ランダムキャラは選択に委ねる
FLAG:OPランダムキャラ使用 = 0

@SCENARIO_MAPSELECT_19
PRINTFORML
PRINTFORML どのマップでプレイしますか？
CALL ASK_MULTI("デフォルト", "日本", "ヨーロッパ")
SELECTCASE RESULT
	CASE 0
		MAPID '= "DEFAULT"
	CASE 1
		MAPID '= "JAPAN"
	CASE 2
		MAPID '= "EU"
ENDSELECT

@SCENARIO_PLACEMENT_20
#DIM 勢力数
#DIM 振り分け
COUNTRY_BOSS:0 = 0
COUNTRY_COLOR:0 = GETDEFCOLOR()


CALL ASK_YN("旧作キャラも君主になりうる", "旧作キャラは君主にならない")
SCVAR:0 = RESULT

CALL ASK_MULTI("勢力数ふつう", "勢力数おおめ", "勢力数ひかえめ")
SELECTCASE RESULT
	CASE 0
		勢力数 = 12 + RAND:5 + RAND:5 + RAND:5
	CASE 1
		勢力数 = MIN(18 + RAND:7 + RAND:7 + RAND:7, MAX_COUNTRY - 2)
	CASE 2
		勢力数 = 6 + RAND:3 + RAND:3 + RAND:3
ENDSELECT

CALL FISHER_YATES_SHAFFLE(CHARANUM)
VARSET 振り分け

;君主の設定
FOR LOCAL, 0, CHARANUM
	LOCAL:1 = SHAFFLE_ARRAY:LOCAL
	SIF !IS_FIXED_CHARA(LOCAL:1)
		CONTINUE
	SIF LOCAL:1 == NAME_TO_CHARA("あなた")
		CONTINUE
	SIF SCVAR:0 && TALENT:(LOCAL:1):キャラ区分
		CONTINUE
	COUNTRY_BOSS:(振り分け + 1) = GET_ID(LOCAL:1)
	CALL CHANGE_COUNTRY(LOCAL:1, 振り分け + 1, 1)
	振り分け ++
	SIF 振り分け == 勢力数
		BREAK
NEXT


CALL FISHER_YATES_SHAFFLE(GET_CITY_NUM() + 1)
;初期都市の設定
FOR LOCAL, 1, 勢力数 + 1
	FOR LOCAL:1, 0, GET_CITY_NUM() + 1
		LOCAL:2 = SHAFFLE_ARRAY:(LOCAL:1)
		SIF LOCAL:2 == 0 || CITY_OWNER:(LOCAL:2) != 0
			CONTINUE
		CITY_OWNER:(LOCAL:2) = LOCAL
		BREAK
	NEXT
NEXT

;勢力色の設定
FOR LOCAL, 1, 勢力数 + 1
	COUNTRY_COLOR:LOCAL = RAND:16 * (16 * 256 * 256) + RAND:16 * (16 * 256) + RAND:16 * 16
	;黒すぎたらどれか一系統を明るくして、多少なりとも分かりやすくする
	IF (COUNTRY_COLOR:(LOCAL:0) >> 16 & 0xFF) <= 0x50 && (COUNTRY_COLOR:(LOCAL:0) >> 8 & 0xFF) <= 0x50 && (COUNTRY_COLOR:(LOCAL:0) & 0xFF) <= 0x50
		SELECTCASE RAND:3
			CASE 0
				LOCAL:2 = RAND(0x30, 0x60)
			CASE 1
				LOCAL:2 = RAND(0x30, 0x60) << 8
			CASE 2
				LOCAL:2 = RAND(0x30, 0x60) << 16
		ENDSELECT
		COUNTRY_COLOR:(LOCAL:0) += LOCAL:2
	;白すぎたらどれか一系統を暗めに
	ELSEIF (COUNTRY_COLOR:(LOCAL:0) >> 16 & 0xFF) >= 0xC0 && (COUNTRY_COLOR:(LOCAL:0) >> 8 & 0xFF) >= 0xC0 && (COUNTRY_COLOR:(LOCAL:0) & 0xFF) >= 0xC0
		SELECTCASE RAND:3
			CASE 0
				LOCAL:2 = RAND(0x30, 0x50)
			CASE 1
				LOCAL:2 = RAND:0x50 << 8
			CASE 2
				LOCAL:2 = RAND(0x30, 0x50) << 16
		ENDSELECT
		COUNTRY_COLOR:(LOCAL:0) -= LOCAL:2
	ENDIF
NEXT

FOR LOCAL, 1, 勢力数 + 1
	COUNTRY_SOLDIER:LOCAL = 8000
NEXT

;指定されなかったキャラは放浪 「あなた」は除外（MASTER入れ替えたときに放浪するのを防ぐ）。
FOR LOCAL:0, 1, CHARANUM
	SIF CFLAG:(LOCAL:0):1 == 0  && CSTR:(LOCAL:0):99 != "あなた"
		CFLAG:(LOCAL:0):特殊状態 = 特殊状態_放浪
NEXT



FOR LOCAL, 1, GET_CITY_NUM() + 1
	SIF !IS_COUNTRY(CITY_OWNER:LOCAL)
		CONTINUE
	SIF CITY_ECONOMY_LIMIT:LOCAL <= 100000
		CITY_ECONOMY_LIMIT:LOCAL = 100000
	CITY_ECONOMY:LOCAL = MIN(CITY_ECONOMY_LIMIT:LOCAL, 100000)
NEXT


FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(CITY_OWNER:LOCAL)
		CONTINUE
	COUNTRY_SOLDIER:LOCAL = 8000
	MONEY:LOCAL = 5000
NEXT

;無所属都市の兵数は経済の7.5%
FOR LOCAL:0, 0, MAX_CITY
	SIF CITY_OWNER:(LOCAL:0) == 0
		CITY_SOLDIER:(LOCAL:0) = CITY_ECONOMY:(LOCAL:0) * 75 / 1000
NEXT

;■関係設定
CALL SHARED_SETTING_RELATION

;都市次第では序盤詰むのでこづかい
FOR LOCAL:0, 0, MAX_COUNTRY
	SIF IS_COUNTRY(LOCAL)
		MONEY:LOCAL += 10000
NEXT

