;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K57_AFTER_COMPETE_RATE(対象)
#DIM 対象
RETURN 60

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K57_AFTER_COMPETE_DECISION(対象)
#DIM 対象

;マスターにペニスがないと出ない
SIF !HAS_PENIS(MASTER)
	RETURN 0

;レイプしていること
SIF KDVAR:対象:キスメ_飛んで井の中 != 2
	RETURN 0

;終わらせたら出ない
SIF KDVAR:対象:キスメ_いやらしき井戸の怪
	RETURN 0

SIF IS_LOVER(対象)
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 0, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K57_AFTER_COMPETE_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_エロ


;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K57_AFTER_COMPETE(対象)
#DIM 対象
#DIM 判定値

PRINTFORML 深夜……
PRINTFORMW 領内を見回り中、突如としてつるべが降ってきた！
PRINTFORMW ……が、華麗に回避した！
PRINTFORMW 「……ちッ」
PRINTFORML 何かと思ったらまた%ANAME(対象)%だ
PRINTFORML 犯された恨みか、すごい形相でこっちを睨んでくる
PRINTFORML ……その割に、不意打ちを避けられた時点で諦めてしまっているようだが……
PRINTFORML 
CALL ASK_MULTI("犯す", "兵士に提供", "帰す")
SELECTCASE RESULT
	CASE 2
		PRINTFORML そういう気分でもない
		PRINTFORML 何もせずに帰してやることにした
		PRINTFORMW 「諦めないから……」
		PRINTFORML  %ANAME(対象)%は何事か呟きながらも、大人しく帰っていった……
	CASE 1
		PRINTFORMW 「う、こ、このぉっ……！」
		PRINTFORML 応援を呼び、%ANAME(対象)%を兵士に提供することにした
		PRINTFORML せいぜい慰安のために役立ってもらおう……
		PRINTFORML
		CALL FUCK_GANGBANG(対象, GET_SPERM_ID("兵士"), @"兵士の\@ RAND:2 ? ペニス # 唇\@", "兵士")
		PRINTFORML
		PRINTFORMW 「ふあ……あくぅっ、はぅう……♥」
		PRINTFORMW 気を失い痙攣するばかりになった%ANAME(対象)%を放置して、拠点へ戻った……
	CASE 0
		PRINTFORMW 「ッ……」
		PRINTFORML %ANAME(対象)%の服を剥ぎ取る。諦めたのか、抵抗しなかった
		PRINTFORML 好都合だ。舌なめずりして、%ANAME(対象)%にむしゃぶりついた……
		PRINTFORML
		CALL FUCK_RAPE(対象, GET_ID(MASTER), @"%ANAME(MASTER)%のペニス", @"%ANAME(MASTER)%")
		CALL FUCK(MASTER, "Ｃ, 性交, 射精, 奉仕", "キス喪失, 童貞喪失", 0, @"%ANAME(対象)%の\@ RAND:2 ? 秘貝 # 唇\@", "", @"%ANAME(対象)%の膣")
		PRINTFORML 
		PRINTFORMW 「ふあ……あくぅっ、はぅう……♥」
		PRINTFORMW 「あはっ、ああぅっ……♥」
		PRINTFORMW ひとしきり%ANAME(対象)%で楽しんだ%ANAME(MASTER)%は、%PRONOUN(対象)%からモノを引き抜き一息ついた
		PRINTFORML %ANAME(対象)%は全身を痙攣させながら、快楽に震えている
		PRINTFORML 今なら何を言っても聞きそうだが……？
		CALL ASK_MULTI_JUDGE("奴隷になることを誓わせる", GET_COUNTRY_BOSS(CFLAG:対象:所属) != 対象, "また来るよう言い聞かせる", 1)
		SELECTCASE RESULT
			CASE 1
				PRINTFORMW 「はひ……わかりましたぁっ……♥」
				PRINTFORMW また犯してやるから必ず来い
				PRINTFORMW 厳しい口調で命じてやると、%ANAME(対象)%は精液まみれの顔でこくこくと頷いた……
				CFLAG:対象:従属度 += 300
				CFLAG:対象:依存度 += 100
			CASE 0
				PRINTFORMW 「はひ、誓います……私は%ANAME(MASTER)%様の奴隷です……♥」
				PRINTFORMW 「どうか可愛がってくださいませ、ご主人様ぁ……んむぅ」
				PRINTFORMW ペニスを差し出し、コレに誓いのキスをしろと命じると、%ANAME(対象)%は精液まみれの顔を蕩かしてしゃぶりつく
				PRINTFORMW その姿に、当初みなぎらせていた殺気はまるで残っていなかった……
				CALL COLOR_PRINTW(@"%ANAME(対象)%を服従させました", カラー_注意)
				CALL COLOR_PRINTW(@"%ANAME(対象)%が奴隷になりました", カラー_注意)
				IF RAND:3
					CALL SET_PIERCE_RANKED(対象, 0, ピアス_ラビア, ピアス_ペニス, ピアス_クリトリス, ピアス_乳首, ピアス_へそ, ピアス_耳, ピアス_舌, ピアス_鼻)
					SIF RESULT != -1
						CALL COLOR_PRINTW(@"%ANAME(対象)%の%GET_PIERCE_NAME(RESULT)%に、服従の証のピアスが取り付けられた……", カラー_ピンク)
				ELSE
					CALL COLOR_PRINTW(@"服従の証として、%ANAME(対象)%の陰毛はハート型に整えられた……", カラー_ピンク)
					TALENT:対象:陰毛目標値 = 陰毛_ハート型
					TALENT:対象:陰毛現在値 = 陰毛_ハート型
				ENDIF
				CALL CHANGE_COUNTRY(対象, CFLAG:MASTER:所属, 1)
				TALENT:対象:服従 = 1
				TALENT:対象:烙印 = 1
				TALENT:対象:合意 = 1
				TALENT:対象:チョロイン = 1
				TALENT:対象:プライド低い = 1
				TALENT:対象:Ｍ気質 = 1
				TALENT:対象:保守的 = 0
				TALENT:対象:濡れにくい = 0
				TALENT:対象:習得遅い = 0
				TALENT:対象:汚臭敏感 = 0
				CFLAG:対象:従属度 = MAX(CFLAG:対象:従属度, 1500)
				CFLAG:対象:依存度 = MAX(CFLAG:対象:依存度, 500)
				SETBIT TALENT:対象:淫乱系, 素質_淫乱_マゾ
				KDVAR:対象:キスメ_いやらしき井戸の怪 = 1
		ENDSELECT
ENDSELECT
RETURN 1
