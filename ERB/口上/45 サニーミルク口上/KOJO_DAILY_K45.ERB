;------------------------------------
;そのキャラがデイリーイベントをもつか判断する関数
;KOJO_EXISTとは別にこの関数も必要になるので注意
;------------------------------------
@KOJO_DAILY_EXIST_K45()


;------------------------------------
;そのキャラのデイリー本体を呼び出すハブとなる関数
;------------------------------------
@KOJO_DAILY_K45()
#DIM 発生フラグ
発生フラグ = 0

;デイリーイベント
DAILY_NAME = いたずらに命をかけて
CALL KOJO_DAILY_K45_TRICK
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

;;デイリーイベント
;DAILY_NAME = デイリーイベントの名前
;CALL KOJO_DAILY_K45_NAMAE_OF_DAILY_EVENT
;SIF RESULT != -1
;	発生フラグ = 1
;SIF DAILY_CANCEL
;	RETURN 1

RETURN 発生フラグ


;------------------------------------
;デイリーイベント本体
;創意工夫
;発生判定に失敗した場合（イベントが発生しない場合）はマイナス１を返すこと
;-----------------------------------
@KOJO_DAILY_K45_TRICK
#DIM サニー
#DIM スター
#DIM ルナ
#DIM 判定値
サニー = NAME_TO_CHARA("サニーミルク")
ルナ = NAME_TO_CHARA("ルナチャイルド")
スター = NAME_TO_CHARA("スターサファイア")

SIF !(CHECK_KOJO_DAILY_HAPPEN(サニー, 60, 0, 0) && CHECK_KOJO_DAILY_HAPPEN(スター, 1000, 0, 0) && CHECK_KOJO_DAILY_HAPPEN(ルナ, 1000, 0, 0))
	RETURN -1

SIF GROUPMATCH(MASTER, スター, ルナ, NAME_TO_CHARA("大妖精"), NAME_TO_CHARA("チルノ"), NAME_TO_CHARA("クラウンピース"))
	RETURN -1

SIF KDVAR:サニー:サニー_いたずらに命をかけて == -1
	RETURN -1

SIF !ALLSAMES(CFLAG:サニー:所属, CFLAG:スター:所属, CFLAG:ルナ:所属)
	RETURN -1

;上で同一勢力判定してるのでこれでいい
SIF GROUPMATCH(GET_COUNTRY_BOSS(CFLAG:サニー:所属), サニー,　スター, ルナ)
	RETURN -1

判定値 = LIMIT(ABL:MASTER:知略 - (ABL:サニー:知略 + ABL:ルナ:知略 + ABL:スター:知略), 20, 100)

判定値 += KDVAR:サニー:サニー_いたずらに命をかけて * 8

CALL KOJO_DAILY_START()
IF KDVAR:サニー:サニー_いたずらに命をかけて == 0 || 判定値 < RAND:100
	CALL COLORPRINT(@"あれっ？？？？", COLOR("警告"), "W")
	PRINTFORML いつもの道を歩いていたはずなのに、気がついたら全く知らないところに出ていた
	PRINTFORML ついでにいうと、振り返った道にも見覚えがない
	PRINTFORMW なんと、迷子になってしまったようだ……
	PRINTFORMW 結局、拠点に戻るころには日がとっぷり暮れてしまった
	PRINTFORMW 「いぇーい、大成功！」
	PRINTFORMW 「まさか気づかないなんてねー」
	PRINTFORMW 「まぁ私たちに気づけるほうが珍しいんだけどね」
	CALL ADD_COOLTIME(MASTER, 1)
	KDVAR:サニー:サニー_いたずらに命をかけて　++
	RETURN
ENDIF

PRINTFORML いつもの道を歩いていたはずなのに、気がついたら全く知らないところに出ていた……
PRINTFORML それは妖精のしわざだと、%ANAME(MASTER)%は見破った！
PRINTFORMW いたずらをしていた妖精三匹を捕らえた！
PRINTFORMW 「あわわわバレちゃった！　ちょっとルナ、ちゃんと気配消したの！？」
PRINTFORMW 「サニーこそ！　　ちゃんと光曲げてたんでしょうね！？」
PRINTFORMW 「……」
PRINTFORMW 「スター！　何逃げようとしてんのよ！」
PRINTFORML ……会話からするに、どうやらそれぞれ光と音と気配に関連した能力をもつらしい
PRINTFORMW 妖精だからいたずらくらいにしか使えないが、もう少し知恵のある輩であれば、いくらでも悪用できるだろう
PRINTFORMW 危険だ。どうしたものか……
PRINTFORML
CALL ASK_YN("まあ、逃がしてやろう", "牢に放り込んでおく")

IF RESULT == 0
	PRINTFORMW 「えっ、いいの？　やった！」
	PRINTFORMW 「ありがと！　この恩は……まぁ忘れるかもしんないけど！」
	PRINTFORMW 三匹はそろってどこかへ飛んでいった……
	CFLAG:サニー:好感度 += 100
	CFLAG:スター:好感度 += 100
	CFLAG:ルナ:好感度 += 100
	IF IS_COUNTRY(CFLAG:サニー:所属)
		CALL COLORPRINT(@"三匹が所属する、%ANAME(GET_COUNTRY_BOSS(CFLAG:サニー:所属))%との関係が改善されました", COLOR("注意"), "W")
		CALL CHANGE_RELATION_C_TO_C(CFLAG:サニー:所属, CFLAG:MASTER:所属, 300, -300)
	ENDIF
ELSE
	PRINTFORMW 「えっ、ろ、牢屋は勘弁！　やだー！」
	PRINTFORMW 三匹まとめて牢屋に放り込んでおいた……
	CALL CAPTURE(サニー, CFLAG:MASTER:所属)
	CALL CAPTURE(スター, CFLAG:MASTER:所属)
	CALL CAPTURE(ルナ, CFLAG:MASTER:所属)
ENDIF
CFLAG:サニー:面識 = 1
CFLAG:ルナ:面識 = 1
CFLAG:スター:面識 = 1
KDVAR:サニー:サニー_いたずらに命をかけて = -1
RETURN
