;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K69_MAGIC_ITEM_RATE(対象)
#DIM 対象
RETURN 70

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K69_MAGIC_ITEM_DECISION(対象)
#DIM 対象
#DIM 香霖堂

香霖堂 = GET_CITYNUMBER("香霖堂")

;マップに「香霖堂」がないと駄目
SIF 香霖堂 == -1
	RETURN 0

;好感度が500以上
SIF MAX(CFLAG:対象:好感度) < 500
	RETURN 0

SIF KDVAR:対象:霖之助_マジックアイテム作成 == -1
	RETURN 0

;お金を2500以上持ってないと駄目
SIF MONEY <= 2500
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, -1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K69_MAGIC_ITEM_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K69_MAGIC_ITEM(対象)
#DIM 対象
#DIM 代金
#DIM 代金2
#DIM 香霖堂

香霖堂 = GET_CITYNUMBER("香霖堂")
代金 = MIN(MONEY / 5, RAND(500, 1000))
代金2 = MIN(MONEY / 5, RAND(1000, 2500))

SIF !CFLAG:対象:面識
	CFLAG:対象:面識 = 1

IF KDVAR:対象:霖之助_マジックアイテム作成 == 0
	PRINTFORMW 「やあ、%ANAME(MASTER)%。調子はどうだい？」
	PRINTFORML 廊下を歩いていると、%ANAME(対象)%が話しかけてきた
	PRINTFORMW 普段はずっと読書をしているような物静かな男が珍しい。何の用だろうか？
	PRINTFORMW 「いや、なに。確かに僕は荒事が苦手だが、それを君や皆に任せっきりというのがちょっと心苦しくなってね」
	PRINTFORMW 「だから自分の得意分野で役に立てることはないか、と考えたわけさ」
	PRINTFORMW 「僕はこう見えてマジックアイテムを作ることも出来るんだ。戦いの役には立つと思う」
	CALL ICPRINT("「それで、もし良かったら君専用のマジックアイテムを作ってみないか？　無論、代金は頂くが」", "W", カラー_注意)
	;香霖堂を占領している場合
	IF CITY_OWNER:香霖堂 == CFLAG:MASTER:所属
		PRINTFORMW 「とはいえ戦乱のせいか、碌な材料が集まらないからすごい効果を期待してもらっても困るが……」
		PRINTFORMW 「幸い、僕の工房…というか店が無事だから、精度の良いものは作れると思うよ」
		CALL ICPRINT(@"「ざっと見積もって…金<{代金2}>くらいでどうだろう？」", "W", カラー_注意)
		PRINTFORML ……なるほど、そういうことか
		PRINTFORMW すごい効果は無い、とのことだが、やはり自分専用のマジックアイテムというのは心が躍る響きだ
	;占領していない場合
	ELSE
		PRINTFORMW 「とは言っても戦乱のせいか、材料が碌な物じゃないから大した効果は期待できないが……」
		PRINTFORMW 「その分安くしておくよ。そうだなあ…」
		CALL ICPRINT(@"「諸々含めて金<{代金}>くらいで作ってあげるよ、どうだい？」", "W", カラー_注意)
		PRINTFORMW 「……僕の工房、というか店が有ったらもっとちゃんとしたのを作れるんだが…」
		PRINTFORML ……なるほど、そういうことか
		PRINTFORMW 大した物ではないとのことだが、自分専用のマジックアイテムというのは心が躍る響きだ
	ENDIF
ELSE
	PRINTFORMW 「やあ、%ANAME(MASTER)%。調子はどうだい？」
	PRINTFORML 廊下を歩いていると、また%ANAME(対象)%が話しかけてきた
	PRINTFORMW 「前に作ったアイテムはどうだったかな？　お気に召したようならまた作ろうか？」
	PRINTFORMW 「無論、代金は頂くけど…そうだなぁ」
	IF CITY_OWNER:香霖堂 == CFLAG:MASTER:所属
		CALL ICPRINT(@"「諸々含めて、金<{代金2}>くらいでどうだろう？」", "W", カラー_注意)
	ELSE
		CALL ICPRINT(@"「諸々含めて、金<{代金}>くらいでどうだろう？」", "W", カラー_注意)
	ENDIF
ENDIF
PRINTFORMW さて、どうしよう……
PRINTFORML
CALL ASK_YN("作ってもらう", "必要ない")
IF RESULT == 0
	PRINTFORMW せっかくの機会だ。大金というわけでもないし、頼んでみよう
	PRINTFORMW 「引き受けたよ。それでは出来上がりを楽しみにしていてくれ」
	PRINTFORML ・
	PRINTFORML ・
	PRINTFORML ―　数日後　―
	PRINTFORMW 
	PRINTFORMW 「お待たせ。ご注文の品が用意できたよ」
	PRINTFORML 
	SELECTCASE RAND:6
		CASE 0
			PRINTFORML 「これは武器の柄の部分に巻き付けることで効果が現れるアイテムだ。人妖問わず、その体には魔力が備わっており、一部の魔法使い等がそれを有効に活用しているが、これはその誰しもに流れる魔力を活用して武器の体感重量を軽減してくれるアイテムなんだ。それほど劇的に重量が変わるわけではないが、これの凄いところは、軽くなるのはあくまで『体感重量』であって本来の重量が軽くなったわけではない。つまり、敵に与える衝撃はそのままに自分だけが軽く扱えるようになるという所なんだ！　実戦において『速さ』は何よりも重要な点だ。このアイテムを使えば攻撃力を損なうことなく今まで以上のスピードで武器を振るうことが出来るようになるということさ！　具体的な原理は……」
			PRINTFORMW ……　香霖堂店主の説明（薀蓄）は長く続いた　……
			;香霖堂を占領している場合
			IF CITY_OWNER:香霖堂 == CFLAG:MASTER:所属
				CALL COLOR_PRINTW(@"マジックアイテムの効果により、%ANAME(MASTER)%の武闘が2上がった！", カラー_注意)
				ABL:MASTER:武闘 += 2
			;占領していない場合
			ELSE
				CALL COLOR_PRINTW(@"マジックアイテムの効果により、%ANAME(MASTER)%の武闘が1上がった", カラー_注意)
				ABL:MASTER:武闘 += 1
			ENDIF
		CASE 1
			PRINTFORML 「これは武器の表面に塗り重ねるだけでその部分の強度が上がる魔法の塗料さ。攻撃を防ぐための手段には盾を用いるのが一般的だが、乱戦になり易い戦場では片手を防御に専念することを徹底するのは中々難しいことだろう。何しろシールドバッシュなんていう盾で攻撃する技術もあるぐらいだしね。だがもしも、剣などといった武器自体が盾並みの強度を持てば、攻防一体となり生存性が増すのではないかと考えたんだ。この塗料は、内部に魔女が良く使う術式を非常に小さい粒度まで落とし込んで混ぜ込んでいる。これによってただこの塗料を塗るだけで、魔女が防御に用いる障壁魔法的な効果を付与させることに成功したんだ！　まあ、生きてる存在には毒になるから無機物にしか塗れないんだけど……」
			PRINTFORMW ……　香霖堂店主の説明（薀蓄）は長く続いた　……
			IF CITY_OWNER:香霖堂 == CFLAG:MASTER:所属
				CALL COLOR_PRINTW(@"マジックアイテムの効果により、%ANAME(MASTER)%の防衛が2上がった！", カラー_注意)
				ABL:MASTER:防衛 += 2
			ELSE
				CALL COLOR_PRINTW(@"マジックアイテムの効果により、%ANAME(MASTER)%の防衛が1上がった", カラー_注意)
				ABL:MASTER:防衛 += 1
			ENDIF
		CASE 2
			PRINTFORML 「これは耳に取り付けることで、常に冷静かつリラックスした状態になれるアイテムだ。人妖問わず、慌てていると普段は何でもないようなことが途端に失敗続きになったりするものだ。かといって実際に戦場に立って戦う者たちに常に冷静でいろ、なんてことは中々言えるものじゃないし出来ることでもない。そのために作成したのがこの魔法のイヤーカフスだ。これを着けることにより、耳を通る血流を落ち着かせ、さらに精神を沈静させる効果のある香りを発することにより、常に落ち着いた気分で事に望めるようになるんだ。一瞬の判断ミスが命取りになり得る戦場では、冷静な判断を下せることがもっとも重要な自衛策になるからね。このアイテムの具体的な原理というと……」
			PRINTFORMW ……　香霖堂店主の説明（薀蓄）は長く続いた　……
			IF CITY_OWNER:香霖堂 == CFLAG:MASTER:所属
				CALL COLOR_PRINTW(@"マジックアイテムの効果により、%ANAME(MASTER)%の知略が2上がった！", カラー_注意)
				ABL:MASTER:知略 += 2
			ELSE
				CALL COLOR_PRINTW(@"マジックアイテムの効果により、%ANAME(MASTER)%の知略が1上がった", カラー_注意)
				ABL:MASTER:知略 += 1
			ENDIF
		CASE 3
			PRINTFORML 「これは掛けている間だけ、式神並の処理能力を持つことが出来る魔法の眼鏡だ。式神とは外の世界で言うところのコンピュータだ。戦時の政は平時と違い、色々とやることが多いがその内容自体は極端に複雑になるわけではない。そこでこのアイテムの出番さ。これはレンズ部分に極小の術式を透明な塗料で刻み込むことで、書類に書かれている計算式や重要な項目を即座にピックアップして最適解を表示し、導き出してくれるという代物さ。どんな戦も政無くして出来る物ではない。故に政をより効率よく的確にこなす事こそが、結果的に戦にも有効な形になって現れてくるというわけさ。何せ兵を雇うのも賄うのも全て民の力、ひいてはそれを導く政の力になるのだからね。ああ、コレの具体的な原理は……」
			PRINTFORMW ……　香霖堂店主の説明（薀蓄）は長く続いた　……
			IF CITY_OWNER:香霖堂 == CFLAG:MASTER:所属
				CALL COLOR_PRINTW(@"マジックアイテムの効果により、%ANAME(MASTER)%の政治が2上がった！", カラー_注意)
				ABL:MASTER:政治 += 2
			ELSE
				CALL COLOR_PRINTW(@"マジックアイテムの効果により、%ANAME(MASTER)%の政治が1上がった", カラー_注意)
				ABL:MASTER:政治 += 1
			ENDIF
		CASE 4
			PRINTFORML 「これは首に掛けておくと、非常に声が通りやすくなるという魔法のチョーカーさ。古今東西、声というのは非常に重要なファクターだ。平時は歌手なんて職業があるくらいだし、戦場に立てば大声での情報伝達は欠かせない物がある。何より兵士たちにとって、自分たちを指揮して導いてくれる将の声というのは全体の士気にも関わることだ。そこでコレの出番さ！　このチョーカーをつけると、声質が微妙に変わるんだ。『1/ｆゆらぎ』というのを聞いたことはあるかい？　このチョーカーは装着者の喉を通る空気に干渉して、『1/ｆゆらぎ』の性質を持つ声に変えることが出来るんだ。それだけでなく、声の拡大や収束、指向性を持たせて特定の場所にだけ聞こえる声を出すことも出来るんだ！　特にこの部分が力を入れた所でね……」
			PRINTFORMW ……　香霖堂店主の説明（薀蓄）は長く続いた　……
			IF CITY_OWNER:香霖堂 == CFLAG:MASTER:所属
				CALL COLOR_PRINTW(@"マジックアイテムの効果により、%ANAME(MASTER)%の歌唱が2上がった！", カラー_注意)
				ABL:MASTER:歌唱 += 2
			ELSE
				CALL COLOR_PRINTW(@"マジックアイテムの効果により、%ANAME(MASTER)%の歌唱が1上がった", カラー_注意)
				ABL:MASTER:歌唱 += 1
			ENDIF
		CASE 5
			PRINTFORML 「これは包丁だ。だがただの包丁じゃあない。魔法の万能包丁さ。これは握る人の意思によって包丁表面の斥力、引力、摩擦係数なんかを自在に変化させることが出来るんだ。どういう事かって？　よく包丁で切ってたら食材が包丁の側面に引っ付いて邪魔だ！　なんてことは無いかい？　もしくはあえて引っ付けておきたい時とか。これは、そういうことを全て握る人の意思によってリアルタイムで変化させることが出来る包丁なんだ！　念のために聞くが、食の重要性はことさら説明する必要は無いよね？　古今東西、美味しいご飯というのは平時も戦時も関係なく求められてきた。特に戦地で命がけで戦うのにまずい飯しか食えないなんてひどい話だ。美味いご飯というのは人間にとっては生きる活力に、妖怪にとっては日々の娯楽に欠かせない生活の要素だし……」
			PRINTFORMW ……　香霖堂店主の説明（薀蓄）は長く続いた　……
			IF CITY_OWNER:香霖堂 == CFLAG:MASTER:所属
				CALL COLOR_PRINTW(@"マジックアイテムの効果により、%ANAME(MASTER)%の料理が2上がった！", カラー_注意)
				ABL:MASTER:料理 += 2
			ELSE
				CALL COLOR_PRINTW(@"マジックアイテムの効果により、%ANAME(MASTER)%の料理が1上がった", カラー_注意)
				ABL:MASTER:料理 += 1
			ENDIF
	ENDSELECT
	;香霖堂を占領している場合
	IF CITY_OWNER:香霖堂 == CFLAG:MASTER:所属
		CALL COLOR_PRINTW(@"金{代金2}を支払いました", カラー_警告)
		MONEY -= 代金2
		CFLAG:対象:好感度 += 100
	;占領していない場合
	ELSE
		CALL COLOR_PRINTW(@"金{代金}を支払いました", カラー_警告)
		MONEY -= 代金
		CFLAG:対象:好感度 += 50
	ENDIF
	PRINTFORML 
	KDVAR:対象:霖之助_マジックアイテム作成 ++
	IF KDVAR:対象:霖之助_マジックアイテム作成 >= 4
		PRINTFORMW 「ああ、そうだ。マジックアイテムの作成なんだが、もう材料が無くなってしまってね。これが最後の品になる」
		PRINTFORML なんと！　残念だ……
		CALL COLOR_PRINTW("もうマジックアイテムは作れなくなりました……", カラー_警告)
		KDVAR:対象:霖之助_マジックアイテム作成 = -1
	ENDIF
ELSE
	PRINTFORMW 「ふむ、そうかい？　まぁ必要ないならいいんだけどね」
	PRINTFORMW 「それじゃあ、また。必要になったら言っておくれ」
	PRINTFORMW %ANAME(対象)%は立ち去っていった……
ENDIF

RETURN 1
