;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K52_FUCK_YOU_RATE(対象)
#DIM 対象
RETURN 300


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K52_FUCK_YOU_DECISION(対象)
#DIM 対象
#DIM 紫

紫 = NAME_TO_CHARA("紫")

SIF KDVAR:対象:天子_犬猿の仲
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, -1) && CHECK_KOJO_DAILY_HAPPEN(紫, 1, 0, -1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K52_FUCK_YOU_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K52_FUCK_YOU(対象)
#DIM 対象
#DIM 紫

紫 = NAME_TO_CHARA("紫")

CFLAG:対象:面識 = 1
CFLAG:紫:面識 = 1

KDVAR:対象:天子_犬猿の仲 = 1

PRINTFORML 「%ANAME(MASTER)%ーーーーーーッ！」
PRINTFORMW 何事かと思いきや、%ANAME(対象)%がすごい剣幕で駆け寄ってきた
PRINTFORMW その横には%ANAME(紫)%もいる……
PRINTFORMW 「なんでコイツがここに居るのよ！？　このタチの悪い、うさんくさい、えーと、信じらんないやつが！」
PRINTFORMW 「それはこっちの台詞ですわ。なんで貴方のような不良がここに？」
PRINTFORMW ……ずいぶんと仲が悪いらしい
PRINTFORML 今は仲間同士なのだから、あまり反目してほしくないのだが……
CALL ASK_MULTI(@"%ANAME(対象)%の肩をもつ", @"%ANAME(紫)%の肩をもつ", "なあなあで済ませる", "どちらにもつかない")

SELECTCASE RESULT
	CASE 0
		PRINTFORML 「あら、その不良の肩をもつのね」
		PRINTFORMW 「少し見損なったわよ？」
		PRINTFORML 「へへーん、言ってなさいよ隙間妖怪！」
		PRINTFORMW 「さすが%ANAME(MASTER)%、分かってくれると思ってたわ！」
		PRINTFORMW %ANAME(紫)%の機嫌をちょっと損ねてしまったようだ……
		CFLAG:紫:好感度 -= 300
		CFLAG:対象:好感度 += 300
	CASE 1
		PRINTFORML 「は！？　なーんでアンタがそいつの肩もつわけ！？」
		PRINTFORMW 「信じらんない！　見損なったわよ！」
		PRINTFORML 「見損なうもなにも、自分の素行を考えてごらんなさいな」
		PRINTFORMW 「そりゃあ%ANAME(MASTER)%だってフォローできないわよ、ねえ？」
		PRINTFORMW %ANAME(対象)%の機嫌をちょっと損ねてしまったようだ……
		CFLAG:紫:好感度 += 300
		CFLAG:対象:好感度 -= 300
	CASE 2
		PRINTFORMW 「……%ANAME(MASTER)%、そういうの一番嫌われるって知ってる？」
		PRINTFORMW 「この不良と気が合うのは、いささか業腹なのですけど、ねぇ」
		PRINTFORMW 両方の機嫌を損ねてしまったようだ……
		CFLAG:紫:好感度 -= 300
		CFLAG:対象:好感度 -= 300
	CASE 3
		PRINTFORML こっちを巻き込まないでほしい
		PRINTFORMW 放っておいた……
ENDSELECT
		
RETURN 1
