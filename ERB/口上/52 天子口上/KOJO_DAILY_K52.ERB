@KOJO_DAILY_EXIST_K52()

@KOJO_DAILY_K52()
#DIM 発生フラグ
発生フラグ = 0

;罰をあなたに
DAILY_NAME = 罰をあなたに
CALL KOJO_DAILY_K52_PUNISHMENT_FOR_YOU
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

;罰をわたしに
DAILY_NAME = 罰をわたしに
CALL KOJO_DAILY_K52_PUNISHMENT_FOR_ME
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

;特別だからね
DAILY_NAME = 特別だからね
CALL KOJO_DAILY_K52_SPECIAL_TREATMENT
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1


RETURN 発生フラグ


;------------------------------------
;罰をあなたに
;-----------------------------------
@KOJO_DAILY_K52_PUNISHMENT_FOR_YOU
#DIM 天子

天子 = NAME_TO_CHARA("天子")

;天子が同一勢力で捕虜でなく面識があって12.5%、IS_SLAVEでもIS_LOVERでもなく、MASTERが男で2ターン以上のクールタイムに入っている
SIF !(CHECK_KOJO_DAILY_HAPPEN(天子, 125, 1, 0, 1) && !IS_SLAVE(天子) && !IS_LOVER(天子) && IS_MALE(MASTER) && COOLTIME:MASTER:0 >= 2)
	RETURN -1

;紫と衣玖は駄目（女なので、上の条件で弾かれるはずだけど）
SIF GROUPMATCH(MASTER, NAME_TO_CHARA("紫"), NAME_TO_CHARA("衣玖"))
	RETURN -1

CALL KOJO_DAILY_START()

PRINTFORML 負傷した%ANAME(MASTER)%がベッドで横になっていると、%ANAME(天子)%が訪ねてきた……
PRINTFORMW 「%ANAME(MASTER)%、ケガしたんだって？　ちょっと見せてみなさい」
PRINTFORMW 「……なによ、大したことないじゃない。こんなの、私だったら余裕で耐えられるわ。修行が足りてないんじゃない？」
PRINTFORMW 「これはお仕置きが必要ね！　ふふ、この私が直々に相手してあげるんだから、感謝しなさいよね！」
PRINTFORMW 何を言っているのかと訝る%ANAME(MASTER)%に、%ANAME(天子)%は覆い被さってきた……
CFLAG:天子:好感度 += 300
CFLAG:天子:依存度 += 300
ABL:天子:主導度Ｕ += 100
;全員の調教参加フラグをクリア
CVARSET CFLAG, 6, 0
CFLAG:天子:調教参加フラグ = 1
CFLAG:天子:調教中帰らない = 1
CFLAG:天子:強制友好化 = 1
BASE:天子:体力 = MAXBASE:天子:体力
BASE:天子:気力 = MAXBASE:天子:気力
;強制逆調教フラグをオン
FLAG:ターンエンド調教 = 2
;それ以降のデイリーイベント発生を潰す
DAILY_CANCEL = 1



;------------------------------------
;罰をわたしに
;-----------------------------------
@KOJO_DAILY_K52_PUNISHMENT_FOR_ME
#DIM 天子

天子 = NAME_TO_CHARA("天子")

;天子が同一勢力で捕虜でなく面識があって12.5%、IS_SLAVEで2ターン以上のクールタイムに入っており、MASTERが男
SIF !(CHECK_KOJO_DAILY_HAPPEN(天子, 125, 1, 0, 1) && IS_SLAVE(天子) && COOLTIME:天子:0 >= 2 && IS_MALE(MASTER))
	RETURN -1

;紫と衣玖は駄目（女なので、上の条件で弾かれるはずだけど）
SIF GROUPMATCH(MASTER, NAME_TO_CHARA("紫"), NAME_TO_CHARA("衣玖"))
	RETURN -1

CALL KOJO_DAILY_START()

PRINTFORML 負傷した%ANAME(天子)%を見舞いにいくと、%PRONOUN(天子)%は申し訳なさそうな表情を見せた……
PRINTFORMW 「%ANAME(MASTER)%、ごめんなさい、やらかしちゃったわ……」
PRINTFORMW 「普段だったら別にどうってことないんだけど、不注意だったわ」
PRINTFORMW 「……ね、私、失敗しちゃったから……"お仕置き"、してくれない……？」
PRINTFORMW 何を言っているのかと訝る%ANAME(MASTER)%だが、%ANAME(天子)%は本気のようだ
PRINTFORML さて、どうするか……
PRINTFORML
CALL ASK_YN("お仕置きする", "やめておく")
IF RESULT == 1
	PRINTFORMW 「……そう」
	PRINTFORMW 「優しいご主人様ね。それはそれで嬉しいことだわ」
	PRINTFORMW 「……ちょっと物足りないけど」
	PRINTFORML %ANAME(天子)%はそれきり、お仕置きのことについては言わなかった……
	RETURN 1
ENDIF
PRINTFORML %ANAME(天子)%がそう望むなら、そうしてやろう……
PRINTFORMW %ANAME(MASTER)%は、恍惚の表情を浮かべる%ANAME(天子)%に覆い被さった……
CFLAG:天子:従属度 += 300
CFLAG:天子:依存度 += 300
ABL:天子:主導度Ｕ -= 100
;全員の調教参加フラグをクリア
CVARSET CFLAG, 6, 0
CFLAG:天子:調教参加フラグ = 1
CFLAG:天子:調教中帰らない = 1
CFLAG:天子:強制友好化 = 1
BASE:天子:体力 = MAXBASE:天子:体力
BASE:天子:気力 = MAXBASE:天子:気力
;強制逆調教フラグをオン
FLAG:ターンエンド調教 = 1
;それ以降のデイリーイベント発生を潰す
DAILY_CANCEL = 1

;------------------------------------
;特別だからね
;-----------------------------------
@KOJO_DAILY_K52_SPECIAL_TREATMENT
#DIM 天子

天子 = NAME_TO_CHARA("天子")

;天子が同一勢力で捕虜でなく面識があって25%、IS_LOVERで、MASTERが男で2ターン以上のクールタイムに入っている
SIF !(CHECK_KOJO_DAILY_HAPPEN(天子, 125, 1, 0, 1) && IS_LOVER(天子) && IS_MALE(MASTER) && COOLTIME:MASTER:0 >= 2)
	RETURN -1

;紫と衣玖は駄目（女なので、上の条件で弾かれるはずだけど）
SIF GROUPMATCH(MASTER, NAME_TO_CHARA("紫"), NAME_TO_CHARA("衣玖"))
	RETURN -1

CALL KOJO_DAILY_START()

PRINTFORML 負傷した%ANAME(MASTER)%がベッドで横になっていると、%ANAME(天子)%が訪ねてきた……
PRINTFORMW 「%ANAME(MASTER)%、ケガしたんだって？　ちょっと見せてみなさい」
PRINTFORMW 「……なによ、大したことないじゃない。こんなの、私だったら余裕で耐えられるわ。修行が足りてないんじゃない？」
PRINTFORMW 「もう、しょうがないんだから……ほら、これ、お見舞い」
PRINTFORMW そう言って、%ANAME(天子)%は桃を差し出してきた
PRINTFORMW 「天界で取れる桃よ。不老長寿とか金剛不壊の象徴」
PRINTFORMW 「私からもらったって、そこらで言いふらさないでよ？　これ、天人以外が食べちゃダメって規則があるんだから」
PRINTFORMW 「これでも食べて、とっとと元気出しなさい。……あんたがいないと張り合いがないのよ」
PRINTFORMW 言うだけ言って、%ANAME(天子)%はさっさと立ち去っていった……
PRINTFORML

CALL COLORPRINT(@"桃の力で、%ANAME(MASTER)%はクールタイムから回復した！", COLOR("注意"), "W")
COOLTIME:MASTER:0 = 0
CFLAG:天子:好感度 += 200
