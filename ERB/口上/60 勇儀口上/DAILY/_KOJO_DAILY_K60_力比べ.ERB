;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K60_COMPETE_RATE(対象)
#DIM 対象
RETURN (60 <= ABL:MASTER:武闘) * (20 + (ABL:MASTER:武闘 - 60) * 5)


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K60_COMPETE_DECISION(対象)
#DIM 対象

;おわってたら出ない
SIF KDVAR:対象:勇儀_力比べ == -1
	RETURN 0

;勇儀が放浪してないとだめ
SIF CFLAG:対象:特殊状態 != 特殊状態_放浪
	RETURN 0

;マスターが男でないと出ない
SIF !IS_MALE(MASTER)
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 0, 0, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K60_COMPETE_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K60_COMPETE(対象)
#DIM 対象
#DIM 判定値

PRINTFORMW 「%ANAME(MASTER)%、だっけ？　ってのはいるかい！？」
PRINTFORMW %ANAME(MASTER)%が仕事に忙殺されているさなか、急に誰かが入ってきた
PRINTFORML しっかりとした体つきの、金髪の女性だ。額から生えた立派な角と、零れそうな乳がそれぞれ目を惹く
PRINTFORMW いかにも自分が%ANAME(MASTER)%だと応えると、女性はニカッと笑ってみせた
PRINTFORMW 「なるほどアンタが！　なかなかいい男じゃないか、会いに来た甲斐があったよ」
PRINTFORM 「私は%ANAME(対象)%、鬼の四天王、
CALL COLOR_PRINT(@"%NAME_FORMAL(対象)%", カラー_注意)
PRINTFORMW さ。ひとつよろしく」
PRINTFORML 鬼。なるほど、それは立派な角も生えているはずだ
PRINTFORMW しかしその鬼が、一体何の用だというのだろう
PRINTFORMW 「いやなに、私は強い奴が好きでね、たまにこうして出歩いては、手合わせ願ってるわけさ」
PRINTFORML ……つまり、武者修行というわけか
PRINTFORMW 悪いが今は本当に仕事が忙しい、そう言って断ろうとするが、%ANAME(対象)%は食い下がった
PRINTFORMW 「そう言わずにさ、一つ頼むよ。実際、アンタにとっても悪い話じゃないからさ」
PRINTFORM 「もしアンタが勝てば、そうだね……
CALL COLOR_PRINT(@"アンタの頼みを一つ聞いてやろう", カラー_注意)
PRINTFORMW 」
PRINTFORMW 「嘘じゃないよ？　鬼は嘘が嫌いなんだ。信用してくれていい」
PRINTFORMW 「そうだ、ハンデもつけてあげるよ。私は片手に杯をもって、酒を一滴もこぼさないよう立ち回る」
CALL ICPRINT(@"「その代わり……私が勝ったら、<有り金すべて>いただこうか」", "W", カラー_注意)
PRINTFORMW 何を言っているんだと思ったが、どうやら%ANAME(対象)%は本気であるようだった
PRINTFORML ……鬼といえば、昔話でおなじみの、力自慢の妖怪だ。ハンデをつけても、勝つのは容易ではあるまい
PRINTFORML しかし――もし勝てば、頼みを何でも一つ。
PRINTFORMW ……%ANAME(対象)%の顔つき、身体つきは、女性として実に魅力的なものだ。有り金全てを賭けてでも、受けて立つ価値はある……
PRINTL
CALL ASK_YN("のぞむところだ！", "いや今忙しいつってんだろ")
IF RESULT == 1
	PRINTFORMW いやいや、仕事があるのだ。この大量の書類を見ろ。手合わせどころではない
	PRINTFORMW 「むっ……。そうかい、そりゃ残念だ」
	PRINTFORMW 「あんたは中々腕が立つと聞いてたから、是非やりあってみたかったんだけど」
	PRINTFORMW 「ま、そういうことならしょうがない。邪魔したね」
	PRINTFORMW %ANAME(対象)%は立ち去っていった……
ELSE
	PRINTFORMW 「そうこなくっちゃぁ！　さぁ、さっそくやろうじゃないか！」
	PRINTFORMW %ANAME(対象)%に腕を引かれるようにして、中庭に出た……
	PRINTFORML ・
	PRINTFORML ・
	PRINTFORML ・
	;対象と武闘が等しいと50%。以降1勝るごとに5％。
	判定値 = (ABL:MASTER:武闘 + 10 - ABL:対象:武闘) * 5
	IF RAND:100 <= 判定値
		PRINTFORMW 「ははは、負けだ！　私の！」
		PRINTFORML どうにかなった……ぎりぎりだったが
		PRINTFORMW 鬼の拳は予想以上だった。ぎりぎりのところでかわしながら、酒一滴をこぼさせて勝つのがやっとだった
		PRINTFORMW 「それでもいいさ、アンタの勝ちってことに変わりはない」
		PRINTFORMW 「さて……まさか正直、負けるとは思ってなかったけど。頼みを一つ、なんでも聞いてやろう」
		PRINTL
		CALL ASK_MULTI("部下になって", "カキタレになって", "投獄する", "力が欲しい", "金くれ")
		SELECTCASE RESULT
			CASE 0
				PRINTFORMW 「あぁ、そういうことならこっちも大歓迎だよ」
				PRINTFORMW 「私に勝てる奴だなんて、久々だからね。さぁて、それじゃあ一つ、やってやろうじゃないか！」
				CALL COLOR_PRINTW(@"%ANAME(対象)%が部下になりました", カラー_注意)
				CALL CHANGE_COUNTRY(対象, CFLAG:MASTER:所属, 1)
				CFLAG:対象:好感度 += 500
			CASE 1
				PRINTFORMW 「えっ、……あ、ああ、頼みってそういう？」
				PRINTFORMW 「うーん、英雄色を好むとか言ったりするけど……いや、まあ、約束だし、受け入れるけどさ」
				PRINTFORMW 「その……何というか、お手柔らかに頼むよ？」
				CALL COLOR_PRINTW(@"%ANAME(対象)%が部下になりました", カラー_注意)
				CALL COLOR_PRINTW(@"%ANAME(対象)%の合意を得ました", カラー_ピンク)
				CALL CHANGE_COUNTRY(対象, CFLAG:MASTER:所属, 1)
				TALENT:対象:合意 = 1
				TALENT:対象:チョロイン = 1
			CASE 2
				PRINTFORMW これだけの実力者をのさばらせるのは危険だ
				PRINTFORMW %ANAME(対象)%を投獄することにした
				PRINTFORMW 「あん？　まぁ、是非もなし」
				PRINTFORMW 「せいぜいまともな待遇を期待してるよ」
				CALL COLOR_PRINTW(@"%ANAME(対象)%を投獄しました", カラー_注意)
				CALL CAPTURE(対象, CFLAG:MASTER:所属)
			CASE 3
				PRINTFORMW 「ほう、私に勝っておいて、まだ力を欲するときた！　はは、アンタみたいな武辺者は大好きだよ」
				PRINTFORMW 「じゃあそうだな、鬼流の喧嘩の仕方を伝授してあげるよ」
				CALL COLOR_PRINTW(@"%ANAME(対象)%に喧嘩の仕方を教わりました", カラー_注意)
				CALL COLOR_PRINTW(@"%ANAME(MASTER)%の武闘が3上昇しました", カラー_注意)
				CALL LEARN_SKILL_GENERAL_BY_NAME(対象, "総力戦＋＋")
				SIF RESULT
					CALL COLOR_PRINTW(@"%ANAME(MASTER)%は＜総力戦＋＋＞を得ました", カラー_注意)
				CFLAG:対象:好感度 += 300
				ABL:MASTER:武闘 += 3
				PRINTFORMW 「じゃ、私はこれで。楽しかったよ！」
				PRINTFORMW %ANAME(対象)%は立ち去っていった……
			CASE 4
				PRINTFORMW 「金か。堅実でいい望みじゃないか」
				PRINTFORMW 「鬼退治して財宝を得るってのは、昔話の定番だしね」
				PRINTFORMW 「わかった！　ほら、あるだけ持って行きなよ」
				CALL COLOR_PRINTW(@"金100000を得ました", カラー_注意)
				MONEY += 100000
				PRINTFORMW 「じゃ、私はこれで。楽しかったよ！」
				PRINTFORMW %ANAME(対象)%は立ち去っていった……
		ENDSELECT
	ELSE
		PRINTFORMW 「ははは、勝ちだな！　私の！」
		PRINTFORML ……やはり、鬼には勝てなかった
		PRINTFORMW 杯の酒をこぼさないようにするというのは相当なハンデだと思うが、それでもなお駄目だとは……
		PRINTFORMW 「さてと、悪いけど約束は約束だ」
		PRINTFORMW 「有り金全部、もらっていくよ」
		PRINTFORMW 「昔なら命をもらってたとこだからさ、ま、拾いもんだと思いなよ」
		CALL COLOR_PRINTW(@"所持金を全て奪われました", カラー_警告)
		PRINTFORMW 「じゃ、私はこれで。楽しかったよ！」
		PRINTFORMW %ANAME(対象)%は立ち去っていった……
		MONEY = 0
	ENDIF
ENDIF

CALL COLOR_PRINTW(@"%ANAME(対象)%の面識を得ました", カラー_注意)
CFLAG:対象:面識 = 1
KDVAR:対象:勇儀_力比べ = 1

RETURN 1
