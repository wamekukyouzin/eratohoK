;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K139_MOON_STYLE_TRAINING_RATE(対象)
#DIM 対象
RETURN 50


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K139_MOON_STYLE_TRAINING_DECISION(対象)
#DIM 対象

SIF GROUPMATCH(MASTER, NAME_TO_CHARA("依姫"), NAME_TO_CHARA("豊姫"), NAME_TO_CHARA("サグメ"), NAME_TO_CHARA("永琳"), NAME_TO_CHARA("輝夜"))
	RETURN 0

SIF KDVAR:対象:月の門番_月面式訓練
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K139_MOON_STYLE_TRAINING_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他


;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K139_MOON_STYLE_TRAINING(対象)
#DIM 対象

PRINTFORMW 「おお、%ANAME(MASTER)%。ちょっと訓練に付き合ってくれないか？」
PRINTFORMW %ANAME(対象)%に呼び止められた
PRINTFORML 「おっと、気軽に頷いてくれるなよ？」
PRINTFORML 「なんたって月の姉妹直伝の訓練だからな、無茶苦茶キツい」
PRINTFORMW 「正直、いっぺんやると数日は身体が動かん」
PRINTFORML 「身になるのは確かだがな。どうする？」
PRINTFORML なんだか%ANAME(対象)%は挑戦的だ
PRINTFORML そこまで言われると乗らないといけない気がしてきたが……
PRINTFORML 
CALL ASK_YN("やってみる", "やめておく")

IF RESULT == 1
	PRINTFORMW 「ふむ。それが賢明だよ」
	PRINTFORMW 「ではな」
	PRINTFORMW %ANAME(対象)%は立ち去った……
	KDVAR:対象:月の門番_月面式訓練 = 1
	RETURN 1
ENDIF

PRINTFORMW 「そう言うだろうと思っていたよ」
PRINTFORMW 「よし、善は急げだ。訓練場へいくぞ」
PRINTFORMW %ANAME(対象)%と訓練をした……
PRINTFORMW %PRONOUN(対象)%の言葉通り、恐ろしくキツかった……
CALL PRINT_ADD_EXP(MASTER, EXPNAME:GET_EXP(GETNUM(ABL, "武闘")), RAND(10, 15), 1)
CALL PRINT_ADD_EXP(MASTER, EXPNAME:GET_EXP(GETNUM(ABL, "防衛")), RAND(10, 15), 1)
CALL TRAIN_AUTO_ABLUP(MASTER)
CALL ADD_COOLTIME(MASTER, 3, 1)
WAIT

KDVAR:対象:月の門番_月面式訓練 = 1

RETURN 1
