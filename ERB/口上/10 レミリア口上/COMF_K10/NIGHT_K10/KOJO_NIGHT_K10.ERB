;─────────────────────────────────────── 
;■閨の攻め受け分岐
;　KOJO_K10_前.ERBとKOJO_K10_後.ERBで閨と判断されると呼び出される
;　閨は実行対象や主導権だけでは攻め受け関係が判断しにくいので
;　ここを通してまとめて分岐させる
;─────────────────────────────────────── 
@KOJO_K10_NIGHT(レミリア_対象, 前後)
#DIM レミリア
#DIM レミリア_対象
#DIMS 前後
#DIMS レミリア口上攻受カテゴリ

IF !レミリア_対象
	レミリア_対象 = MASTER
ENDIF

;リセット
レミリア = NAME_TO_CHARA("レミリア")
レミリア口上攻受カテゴリ '= ""
レミリア_レミリアが受け = 0

;─────────────────────────────────────── 
;●詳細カテゴリ決定
;─────────────────────────────────────── 
;レミリアが実行
IF IS_MPLY(レミリア)
	;土下座・おねだり（主導権に関わらず実行が受け）
	IF GROUPMATCH(SELECTCOM, 110, 111)
		レミリア口上攻受カテゴリ '= "レミリア受け"
	;100自慰 101顔面騎乗 102Ａ顔面騎乗 （主導権がなければ受け）
	ELSEIF GROUPMATCH(SELECTCOM, 100, 101, 102) && !IS_INITIATIVE(レミリア)
		レミリア口上攻受カテゴリ '= "レミリア受け"
	;主導権なし排泄者　130放尿する 131尿を掛ける 132口内放尿 150放屁　151顔騎放屁（主導権がなければ受け）
	ELSEIF GROUPMATCH(SELECTCOM, 130, 131, 132, 150, 151) && !IS_INITIATIVE(レミリア)
		レミリア口上攻受カテゴリ '= "レミリア受け"
	;それ以外は実行が攻め
	ELSE
		レミリア口上攻受カテゴリ '= "レミリア攻め"
	ENDIF
;レミリアがレミリア_対象
ELSEIF IS_MTAR(レミリア)
	;土下座・おねだり（主導権に関わらず対象が攻め）
	IF GROUPMATCH(SELECTCOM, 110, 111)
		レミリア口上攻受カテゴリ '= "レミリア攻め"
	;100自慰 101顔面騎乗 102Ａ顔面騎乗（主導権があれば攻め）
	ELSEIF GROUPMATCH(SELECTCOM, 100, 101, 102) && IS_INITIATIVE(レミリア)
		レミリア口上攻受カテゴリ '= "レミリア攻め"
	;主導権あり排泄指示者　130放尿する 131尿を掛ける 132口内放尿 150放屁　151顔騎放屁（主導権があれば攻め）
	ELSEIF GROUPMATCH(SELECTCOM, 130, 131, 132, 150, 151) && IS_INITIATIVE(レミリア)
		レミリア口上攻受カテゴリ '= "レミリア攻め"
	;それ以外は対象が受け
	ELSE
		レミリア口上攻受カテゴリ '= "レミリア受け"
	ENDIF
ENDIF

;─────────────────────────────────────── 
;●詳細カテゴリによる分岐
;─────────────────────────────────────── 
IF レミリア口上攻受カテゴリ == "レミリア受け"
	;文末決定関数で使用する
	レミリア_レミリアが受け = 1
	;前文か後文か
	IF 前後 == "前"
		CALL KOJO_K10_NIGHT_BEFORE_BOTTOM(レミリア_対象)
	ELSEIF 前後 == "後"
		CALL KOJO_K10_NIGHT_AFTER_BOTTOM(レミリア_対象)
	ENDIF
ELSEIF レミリア口上攻受カテゴリ == "レミリア攻め"
	;前文か後文か
	IF 前後 == "前"
		CALL KOJO_K10_NIGHT_BEFORE_TOP(レミリア_対象)
	ELSEIF 前後 == "後"
		CALL KOJO_K10_NIGHT_AFTER_TOP(レミリア_対象)
	ENDIF
ENDIF

;─────────────────────────────────────── 
;●終了
;─────────────────────────────────────── 
レミリア_レミリアが受け = 0
RESETCOLOR
RETURN RESULT

