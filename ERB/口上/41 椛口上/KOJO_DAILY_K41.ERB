;------------------------------------
;そのキャラがデイリーイベントをもつか判断する関数
;KOJO_EXISTとは別にこの関数も必要になるので注意
;------------------------------------
@KOJO_DAILY_EXIST_K41()


;------------------------------------
;そのキャラのデイリー本体を呼び出すハブとなる関数
;------------------------------------
@KOJO_DAILY_K41()
#DIM 発生フラグ
発生フラグ = 0

;デイリーイベント
DAILY_NAME = 大将棋
CALL KOJO_DAILY_K41_DAISYOUGI
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

;;デイリーイベント
;DAILY_NAME = デイリーイベントの名前
;CALL KOJO_DAILY_K41_NAMAE_OF_DAILY_EVENT
;SIF RESULT != -1
;	発生フラグ = 1
;SIF DAILY_CANCEL
;	RETURN 1

RETURN 発生フラグ


;------------------------------------
;大将棋
;-----------------------------------
@KOJO_DAILY_K41_DAISYOUGI
#DIM 対象

対象 = NAME_TO_CHARA("椛")

;椛が同勢力、捕虜でない、面識あり
SIF !CHECK_KOJO_DAILY_HAPPEN(対象, 40, 1, 0, 1)
	RETURN -1

;はたてとにとりは駄目
SIF GROUPMATCH(MASTER, NAME_TO_CHARA("はたて"), NAME_TO_CHARA("にとり"))
	RETURN -1

;3回勝ったら発動しない
SIF KDVAR:対象:椛_大将棋_勝利回数 > 3
	RETURN -1


CALL KOJO_DAILY_START()

IF KDVAR:対象:椛_大将棋 == 0
	KDVAR:対象:椛_大将棋 = 1
	PRINTFORML %ANAME(対象)%を見かけた
	PRINTFORMW 地面に置いた大きな木の板を睨んでいるが……？
	PRINTFORMW 「ああ、%ANAME(MASTER)%さん」
	PRINTFORMW 「これですか？　大将棋ですよ。通常の将棋より駒も盤も複雑な将棋です。天狗や河童の間で人気なんです」
	PRINTFORMW 「そうだ、折角ですから一局いかがですか、私と？」
	PRINTFORML ……目の前の木の板は、将棋盤とは到底思えないようなサイズをしている
	PRINTFORML やってみようか……
ELSEIF KDVAR:対象:椛_大将棋_勝利回数 == 2
	PRINTFORML %ANAME(対象)%を見かけた
	PRINTFORMW 将棋盤を抱えているが……
	PRINTFORMW 「%ANAME(MASTER)%さん。あなたにはずいぶん辛酸を舐めさせられました
	PRINTFORMW 「ですが、私も今度という今度は負けませんよ」
	PRINTFORMW 「たっぷり勉強して、天狗の名手たちと何局も戦ってきました。これで負けたら、何だってしてあげてもいいですよ」
	PRINTFORMW 「さあ、勝負です！」
ELSEIF KDVAR:対象:椛_大将棋 == 1 && !KDVAR:対象:椛_大将棋_前回勝利
	PRINTFORML %ANAME(対象)%を見かけた
	PRINTFORML また将棋をやっているようだが……
	PRINTFORMW 「ああ、%ANAME(MASTER)%さん」
	PRINTFORMW 「どうですか？　大将棋」
ELSE
	PRINTFORML %ANAME(対象)%を見かけた
	PRINTFORML また将棋をやっているようだが……
	PRINTFORMW 「ああ、%ANAME(MASTER)%さん」
	PRINTFORMW 「どうですか？　大将棋」
	PRINTFORMW 「私も勉強してきましたから、今度こそは負けませんよ」
ENDIF
PRINTL
CALL ASK_YN("やる", "やめておく")
IF RESULT == 1
	PRINTFORMW 「そうですか？　それは残念」
	PRINTFORMW 「では、またの機会にでも」
	KDVAR:対象:椛_大将棋_前回勝利 = 0
	RETURN
ENDIF

PRINTFORMW 「やってくれますか。では、お手柔らかに」
SIF KDVAR:対象:椛_大将棋_前回勝利
	PRINTFORMW 「前のようにいくと思ったら大間違いですよ？」

FOR LOCAL, 0, 5
	PRINTFORML ・
NEXT

IF MAX(ABL:MASTER:知略 - ABL:対象:知略, 15) <= RAND:100
	KDVAR:対象:椛_大将棋_前回勝利 = 0
	PRINTFORML ……詰みだ
	PRINTFORMW %ANAME(MASTER)%も善戦はしたが、あと一手及ばなかった……
	PRINTFORML 「ふふふ、私の勝ちですね」
	PRINTFORMW 「楽しかったです。付き合ってくださって、ありがとうございました」
	CALL COLORPRINT(@"頭を使ったため、%ANAME(対象)%の知略が少しだけ鍛えられました", COLOR("注意"), "W")
	CALL PRINT_ADD_EXP(対象, "知略経験値", 5, 1)
	CALL TRAIN_AUTO_ABLUP(対象)
	RETURN
ENDIF

KDVAR:対象:椛_大将棋_前回勝利 = 1
KDVAR:対象:椛_大将棋_勝利回数 ++
PRINTFORML ……王手だ
PRINTFORMW 手強かったが、どうにか%ANAME(対象)%を詰ませることができた……
PRINTFORMW 「……むむ、参りました」
PRINTFORMW 「私も、もっと勉強しないといけませんね……」
PRINTFORMW 「付き合ってくださって、ありがとうございました」
CALL COLORPRINT(@"頭を使ったため、%ANAME(MASTER)%の知略が少しだけ鍛えられました", COLOR("注意"), "W")
CALL PRINT_ADD_EXP(MASTER, "知略経験値", 5, 1)
CALL TRAIN_AUTO_ABLUP(MASTER)
IF KDVAR:対象:椛_大将棋_勝利回数 == 3
	PRINTFORMW ……「何だってしてあげてもいい」と彼女は言っていた
	PRINTFORMW その約束を守ってもらおうか……？
	CALL ASK_MULTI_JUDGE("武将として頑張ってもらう", 1, "お金を要求する", 1, "身体を要求する", IS_MALE(MASTER))
	SELECTCASE RESULT
		CASE 0
			PRINTFORMW 「修行を積め？　そんなことでいいのなら、いくらでも」
			PRINTFORMW 「お役に立てるところを、見せてさしあげますよ」
			CALL COLORPRINT(@"%ANAME(対象)%は修行をつみました", COLOR("注意"), "W")
			CALL COLORPRINT(@"%ANAME(対象)%の武闘、知略、政治が5ずつ上がりました", COLOR("注意"), "W")
			ABL:対象:武闘 += 5
			ABL:対象:知略 += 5
			ABL:対象:政治 += 5
		CASE 1
			PRINTFORMW 「お金……ですか」
			PRINTFORMW 「うーん、私もそんなに持ってないんですが……これくらいで勘弁していただけますか？」
			PRINTFORMW 「はあ、今月節約しなくちゃ……」
			CALL COLORPRINT(@"%ANAME(対象)%から金10000をまきあげました", COLOR("注意"), "W")
			MONEY += 10000
		CASE 2
			IF TALENT:対象:合意
				PRINTFORMW 「身体……って」
				PRINTFORMW 「そんなの、今さら断ったりしませんよ」
				PRINTFORMW 「そういうコトがしたいなら最初から言えばいいのに……」
			ELSE
				PRINTFORMW 「からっ……、え！　えっ！？」
				PRINTFORMW 「その、それって、アレですか、男女が閨を共にするという……その、アレですよね」
				PRINTFORMW 「えーっ、えーっ……？」
				PRINTFORMW 「……う、うう、分かりました。一度言った手前、ひっくり返したりはできませんから……」
				PRINTFORMW 「でもその……お手柔らかにお願いしますね？」
			ENDIF
			PRINTFORMW 早速、%ANAME(対象)%を寝室に連れ込んだ……
			CALL COLORPRINT(@"%ANAME(対象)%と楽しい時間を過ごしました", COLOR("注意"), "W")
			SIF !TALENT:対象:合意
				CALL COLORPRINT(@"%ANAME(対象)%の合意を得ました", COLOR("注意"), "W")
			CALL FUCK(対象, "欲望, 奉仕, 性技, Ｃ, Ｖ, Ｂ, Ｍ, 性交, 精愛, 射精, キス, 口淫, Ｖ性交", "キス喪失, 処女喪失, 膣内射精, 口内射精", GET_ID(MASTER), @"%ANAME(MASTER)%の唇", ANAME(MASTER), "", 喪失_和姦)
			CFLAG:対象:好感度 += 500
			CFLAG:対象:従属度 += 500
			CFLAG:対象:依存度 += 100
			TALENT:対象:チョロイン = 1
			TALENT:対象:合意 = 1
	ENDSELECT
ENDIF
