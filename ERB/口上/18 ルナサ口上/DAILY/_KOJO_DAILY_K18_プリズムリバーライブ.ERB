;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K18_PRISMRIVER_LIVE_RATE(対象)
#DIM 対象
RETURN 40


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K18_PRISMRIVER_LIVE_DECISION(対象)
#DIM 対象
#DIM メルラン
#DIM リリカ
リリカ = NAME_TO_CHARA("リリカ")
メルラン = NAME_TO_CHARA("メルラン")

;三人の所属が同一
SIF !ALLSAMES(CFLAG:対象:所属, CFLAG:メルラン:所属, CFLAG:リリカ:所属)
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象 -1, 0, -1) && CHECK_KOJO_DAILY_HAPPEN(リリカ, -1, 0, -1) && CHECK_KOJO_DAILY_HAPPEN(メルラン, -1, 0, -1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K18_PRISMRIVER_LIVE_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;特定の条件を満たすキャラをランダムに選択する場合に利用
;他の関数は必須だが、これだけはなくてもよい　というかパフォーマンスへ影響するので不要なら作ってはならない
;対象が存在せずデイリーを開始できない場合は0を返すことでデイリーの発生をキャンセルする
;---------------------
@KOJO_DAILY_K18_PRISMRIVER_LIVE_SETTARGET(対象)
#DIM 対象
CALL DAILY_EVENT_RAND_CITYSELECT(1)
DAILY_TARGET:0 = RESULT
SIF DAILY_TARGET:0 == -1
	RETURN 0
RETURN 1


;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K18_PRISMRIVER_LIVE(対象)
#DIM 対象
#DIM メルラン
#DIM リリカ
#DIM 対象都市

リリカ = NAME_TO_CHARA("リリカ")
メルラン = NAME_TO_CHARA("メルラン")

対象都市 = DAILY_TARGET:0

;対象都市が見つからなければ（まぁないと思うけど）駄目
SIF 対象都市 == -1
	RETURN 0

PRINTFORMW プリズムリバー幻樂団が%CITY_NAME:対象都市%でライブを行ったようだ……
PRINTFORMW 大人気の彼女たちのライブは、なかなかの経済効果を生み出した！
IF 対象都市 > 0
	LOCAL:1 = (ABL:対象:歌唱 + ABL:メルラン:歌唱 + ABL:リリカ:歌唱) * 100
	LOCAL:1 = RAND(LOCAL:1 / 2, LOCAL:1 + 5000)
	SETCOLOR カラー_オレンジ
	;経済が成長しきってたら限界を伸ばす
	IF CITY_ECONOMY:対象都市 == CITY_ECONOMY_LIMIT:対象都市
		CITY_ECONOMY_LIMIT:対象都市 += LOCAL:1
		PRINTFORML %CITY_NAME:対象都市%の経済規模の上限が{LOCAL:1 / 100}上昇し、{CITY_ECONOMY_LIMIT:対象都市 / 100}になった！
	;成長しきってなければ限界まで伸ばす
	ELSE
		SIF CITY_ECONOMY:対象都市 + LOCAL:1 >= CITY_ECONOMY_LIMIT:対象都市
			LOCAL:1 = MIN(LOCAL:1, CITY_ECONOMY_LIMIT:対象都市 - CITY_ECONOMY)
		CITY_ECONOMY:対象都市 += LOCAL:1
		CITY_ECONOMY_LIMIT:対象都市 = MAX(CITY_ECONOMY:対象都市, CITY_ECONOMY_LIMIT:対象都市)
		PRINTFORML %CITY_NAME:対象都市%の経済規模が{LOCAL:1 / 100}上昇し、{CITY_ECONOMY:対象都市 / 100}になった！
	ENDIF
	RESETCOLOR
ENDIF

RETURN 1
