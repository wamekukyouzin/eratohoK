;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K16_WHAT_NUMBER_SHALL_WE_PLAY_RATE(対象)
#DIM 対象
RETURN 40


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K16_WHAT_NUMBER_SHALL_WE_PLAY_DECISION(対象)
#DIM 対象
#DIM メルラン
#DIM ルナサ

ルナサ = NAME_TO_CHARA("ルナサ")
メルラン = NAME_TO_CHARA("メルラン")

SIF KDVAR:対象:リリカ_セトリ
	RETURN 0


RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1) && CHECK_KOJO_DAILY_HAPPEN(ルナサ, 1, 0, -1) && CHECK_KOJO_DAILY_HAPPEN(メルラン, 1, 0, -1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K16_WHAT_NUMBER_SHALL_WE_PLAY_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K16_WHAT_NUMBER_SHALL_WE_PLAY(対象)
#DIM 対象
#DIM メルラン
#DIM リリカ
KDVAR:対象:リリカ_セトリ = 1

PRINTFORMW 「ウーン……決まらない」
PRINTFORML %ANAME(対象)%がなにか悩んでいるようだが……
PRINTFORML 
CALL ASK_YN("何をしてるんだ？", "放っておく")

IF RESULT == 1
	PRINTFORML そういうこともあるだろう
	PRINTFORML 放っておいた
	RETURN 1
ENDIF

PRINTFORML 「ああ、%ANAME(MASTER)%」
PRINTFORML 「いや、次のライブのセトリを決めてるんだけどさ？」
PRINTFORMW 「ルナ姉もメル姉も極端なのしか出してこないから結局いっつも私が決めてんだけど」
PRINTFORMW 「最近マンネリ気味だし、参ってるんだよね」
PRINTFORML なるほど手元の紙には、曲名がずらずらと並べられているようだが……
PRINTFORML 
CALL ASK_MULTI("明るい曲はどうだ？", "暗めの曲はどうだ？", "テンションあがる奴はどうだ？")

SELECTCASE RESULT
	CASE 0
		PRINTFORML 「うーん？　メル姉みたいなこと言うんだね」
		PRINTFORML 「まぁ、それで言ってみるよ。ありがと」
		PRINTFORMW %ANAME(対象)%はメモに何かしら書き込んだ……
	CASE 1
		PRINTFORML 「うーん？　ルナ姉みたいなこと言うんだね」
		PRINTFORML 「まぁ、それで言ってみるよ。ありがと」
		PRINTFORMW %ANAME(対象)%はメモに何かしら書き込んだ……
	CASE 2
		PRINTFORML 「テンション上がるって、そんなフンワリした……いや待てよ」
		PRINTFORMW 「鳥獣伎楽とコラボなんていいかもね、うん……うん」
		PRINTFORMW 「うん、イケそうかも！　ありがとね！」
		PRINTFORMW なんだかよく分からないが、助けになったようだ……
		CALL COLOR_PRINTW(@"%ANAME(対象)%の歌唱が2上がった", カラー_注意)
		ABL:対象:歌唱 += 2
ENDSELECT

RETURN 1
