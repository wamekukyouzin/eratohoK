;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K6_TAIKYOKUKEN_RATE(対象)
#DIM 対象
RETURN 35


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K6_TAIKYOKUKEN_DECISION(対象)
#DIM 対象
RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K6_TAIKYOKUKEN_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K6_TAIKYOKUKEN(対象)
#DIM 対象
#DIM 経験値

IF CSTR:MASTER:99 == "レミリア"
	LOCALS:0 = お嬢様
ELSEIF CSTR:MASTER:99 == "パチュリー"
	LOCALS:0 = %ANAME(MASTER)%様
ELSE
	LOCALS:0 = %ANAME(MASTER)%さん
ENDIF



IF KDVAR:対象:美鈴_太極拳 == 0
	PRINTFORML %ANAME(MASTER)%が散歩をしていると、%ANAME(対象)%が庭先で怪しげな動きをしているのを見つけた
	PRINTFORMW 一体何事かと思っていると、向こうもこちらに気づいたようで、こちらに声をかけてきた
	PRINTFORMW 「%LOCALS:0%。どうしたんですか？　こっちをじっと見て」
	PRINTFORMW 「これですか？　これは太極拳といって、古くから伝わる拳法から編み出された、健康体操みたいなものです」
	PRINTFORMW 「%LOCALS:0%も一緒にどうですか？　気分がスッキリすると思いますよ」
	PRINTFORML なるほど、最近運動不足気味だし、それもいいかもしれない……
ELSE
	PRINTFORML %ANAME(MASTER)%が散歩をしていると、%ANAME(対象)%が太極拳をしているのを見つけた
	PRINTFORML 「あっ、%LOCALS:0%。どうですか？　太極拳」
ENDIF
PRINTFORML
CALL ASK_YN("やってみる", "今はいいや")
IF RESULT == 0
	SELECTCASE RAND:100
		CASE IS < 40
			経験値 = GET_EXP(GETNUM(ABL, "武闘"))
		CASE IS < 70
			経験値 = GET_EXP(GETNUM(ABL, "防衛"))
		CASE IS < 90
			経験値 = GET_EXP(GETNUM(ABL, "知略"))
		CASEELSE
			経験値 = GET_EXP(GETNUM(ABL, "政治"))
	ENDSELECT
	PRINTFORML %ANAME(対象)%と太極拳を楽しんだ！
	CALL PRINT_ADD_EXP(MASTER, EXPNAME:経験値, RAND:5 + 1, 1)
	PRINTFORMW 「ふう、まぁ、こんなものでしょう」
	PRINTFORMW 「私はたまにここで太極拳してますから、また気が向いたらどうぞ！」
	PRINTFORMW 身体もスッキリしたし、仕事に励むとしよう……
ELSE
	PRINTFORMW 「あら、残念ですね、じゃあまた今度」
	PRINTFORMW 残念だが今は忙しい。対象の誘いを断った……
ENDIF

KDVAR:対象:美鈴_太極拳 = 1

RETURN 1
