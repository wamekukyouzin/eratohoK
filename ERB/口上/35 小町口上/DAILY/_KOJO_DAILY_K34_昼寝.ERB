;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K35_SABORI_RATE(対象)
#DIM 対象
RETURN 50 + (KDVAR:対象:小町_昼寝 > 0) * 350


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K35_SABORI_DECISION(対象)
#DIM 対象
#DIM 四季映姫
四季映姫 = NAME_TO_CHARA("四季映姫")

;MASTERが四季映姫だとおかしなことになるんで駄目
SIF MASTER == NAME_TO_CHARA("四季映姫")
	RETURN 0

;終わってたら発生しない
SIF KDVAR:対象:小町_昼寝 == -1
	RETURN 0

;サボり魔が直ってたら駄目
SIF !TALENT:対象:サボり魔
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1) && CHECK_KOJO_DAILY_HAPPEN(四季映姫, 1, 0, -1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K35_SABORI_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他


;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K35_SABORI(対象)
#DIM 対象
#DIM 四季映姫
四季映姫 = NAME_TO_CHARA("四季映姫")

SELECTCASE KDVAR:対象:小町_昼寝
	CASE 0
		PRINTFORMW 「おっ、%ANAME(MASTER)%じゃないか」
		PRINTFORML ある日、裏庭を散歩していると、%ANAME(対象)%を見かけた
		PRINTFORMW この時間帯は彼女は警邏の当番だったはずだが……？
		PRINTFORMW 「いやほら、今日はぽかぽかとしてて気持ちがいいだろう？」
		PRINTFORMW 「こんな日に働くなんて間違ってるよ。ここは休息して、英気を養うべきだ」
		PRINTFORML ……要はサボって昼寝をしようということらしい
		PRINTFORMW どうしたものかと思っていると、%ANAME(対象)%は手招きしてきた
		PRINTFORMW 「%ANAME(MASTER)%もさ。働き過ぎはよくないよ？　あたいが正しい休息の仕方ってのをレクチャーしたげるよ」
		PRINTFORMW 「いや、共犯者を得ようとかそんなこと考えてないから、マジで」
		PRINTFORMW そう言う%ANAME(対象)%は、地面に布を敷いて、昼寝する気まんまんでいる
		PRINTFORML さて、どうしようか……
		PRINTFORML
		CALL ASK_MULTI("誘いに乗る", "やめておく", "四季映姫に報告だ")
		SELECTCASE RESULT
			CASE 0
				PRINTFORMW 「おっ、話が分かるねぇ」
				PRINTFORMW 「ほら、おいでよ、一緒に昼寝しようじゃないか」
				PRINTFORML %ANAME(対象)%と一緒に昼寝した……
				CALL COLORPRINT(@"%ANAME(MASTER)%が＜サボり魔＞になりました", カラー_警告, "W")
				CALL COLORPRINT(@"サボって腕が鈍った%ANAME(MASTER)%と%ANAME(対象)%の武闘が3下がりました", カラー_警告, "W")
				TALENT:MASTER:サボり魔 = 1
				ABL:MASTER:武闘 = MIN_DECREASE(ABL:MASTER:武闘, 3, 1)
				ABL:対象:武闘 = MIN_DECREASE(ABL:対象:武闘, 3, 1)
				CFLAG:対象:好感度 += 50
				KDVAR:対象:小町_昼寝 = 1
			CASE 1
				PRINTFORMW 「む……そうかい？」
				PRINTFORMW 「ならしょうがないね。ここはあたいの秘密スポットだから、誰にも言わないどいとくれよ？」
				PRINTFORMW 言うだけ言って、%ANAME(対象)%はグースカ寝始めた……
				KDVAR:対象:小町_昼寝 = -1
			CASE 2
				PRINTFORMW 「ちょ……それは駄目だって……！」
				PRINTFORMW 「何が駄目だというんです？」
				PRINTFORMW 「おあぁ四季様！！？　いつからいらしたんです！！！！？」
				PRINTFORMW 「ほんの一部始終ですよ。『おっ、%ANAME(MASTER)%じゃないか』のくだりから」
				PRINTFORMW 「全部じゃないですか！　うわぁすみませんお説教は勘弁して下さい！」
				PRINTFORMW 「だまらっしゃい！　今日という今日は改心してもらいますからね！」
				PRINTFORMW その後、%ANAME(対象)%は%ANAME(四季映姫)%にこってりとしぼられたようだ……
				CALL COLORPRINT(@"%ANAME(対象)%が＜サボり魔＞ではなくなりました", カラー_注意, "W")
				TALENT:対象:サボり魔 = 0
				KDVAR:対象:小町_昼寝 = -1
		ENDSELECT
	CASEELSE
		PRINTFORMW 「おっ、%ANAME(MASTER)%」
		PRINTFORMW 「また昼寝するかい？」
		PRINTFORML
		CALL ASK_YN("誘いに乗る", "やめておく")
		SELECTCASE RESULT
			CASE 0
				IF KDVAR:対象:小町_昼寝 * 10 < RAND:100
					PRINTFORMW 「そう来なくっちゃね」
					PRINTFORMW 「ささ、おいでよ、一緒に寝ようじゃないか♪」
					PRINTFORMW 対象と昼寝して英気を養った……
					CALL COLORPRINT(@"サボって腕が鈍った%ANAME(MASTER)%と%ANAME(対象)%の武闘が3下がりました", カラー_警告, "W")
					TALENT:MASTER:サボり魔 = 1
					ABL:MASTER:武闘 = MIN_DECREASE(ABL:MASTER:武闘, 3, 1)
					ABL:対象:武闘 = MIN_DECREASE(ABL:対象:武闘, 3, 1)
					CFLAG:対象:好感度 += 50
					KDVAR:対象:小町_昼寝 ++
				ELSE
					PRINTFORMW 「なぁにが『誘いに乗る』ですか。二人揃って嘆かわしい」
					PRINTFORMW ……ふと、後ろから声をかけられた
					PRINTFORMW おそるおそる振り返る……%ANAME(四季映姫)%だ！
					PRINTFORMW 「げぇっ　四季様！　いつからいらしたんです！？」
					PRINTFORMW 「だまらっしゃい！　二人揃ってサボるとは言語道断です！　そのたるんだ精神を徹底的にたたき直してあげますからね！」
					PRINTFORMW 二人揃って、%ANAME(四季映姫)%にたっぷりとしぼられた……
					LOCAL = 3 * KDVAR:対象:小町_昼寝 + KDVAR:対象:小町_昼寝
					CALL COLORPRINT(@"%ANAME(四季映姫)%のお説教で、二人の武闘が{LOCAL}上昇しました", カラー_注意, "W")
					CALL COLORPRINT(@"二人は＜サボり魔＞ではなくなりました", カラー_注意, "W")
					TALENT:MASTER:サボり魔 = 0
					TALENT:対象:サボり魔 = 0
					ABL:MASTER:武闘 += LOCAL
					ABL:対象:武闘 += LOCAL
					CFLAG:対象:好感度 += 50
					KDVAR:対象:小町_昼寝 = -1
				ENDIF
			CASE 1
				PRINTFORMW 「おや、そうかい？」
				PRINTFORMW 「それなら仕方ない。あたい一人で寝るとするかね」
				PRINTFORMW 言うなり、%ANAME(対象)%はグースカと眠り始めた……
				KDVAR:対象:小町_昼寝 = -1
		ENDSELECT
ENDSELECT

RETURN 1
