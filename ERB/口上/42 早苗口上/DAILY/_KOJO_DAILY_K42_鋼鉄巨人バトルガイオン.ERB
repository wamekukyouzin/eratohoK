;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K42_OTAKU_RATE(対象)
#DIM 対象
RETURN 40 + (KDVAR:対象:早苗_鋼鉄巨人 > 0) * 310


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K42_OTAKU_DECISION(対象)
#DIM 対象
#DIM 神奈子
神奈子 = NAME_TO_CHARA("神奈子")

;主人公はかなすわ以外
SIF MASTER == NAME_TO_CHARA("神奈子") || MASTER == NAME_TO_CHARA("諏訪子")
	RETURN 0

;終わってたら発生しない
SIF KDVAR:対象:早苗_鋼鉄巨人 == -1
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1) && CHECK_KOJO_DAILY_HAPPEN(神奈子, 1, 0, -1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K42_OTAKU_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他


;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K42_OTAKU(対象)
#DIM 対象
#DIM 神奈子
神奈子 = NAME_TO_CHARA("神奈子")

SELECTCASE KDVAR:対象:早苗_鋼鉄巨人
	CASE 0
		PRINTFORMW 「鋼のから～だ、唸らせて～、今こそとどろく、ガイオンパン～チ～♪」
		PRINTFORMW 「弱きを踏みにじる、悪い奴を～♪　熱い拳で打ち砕け～～イエ～～♪」
		PRINTFORMW どこからか調子っぱずれな歌が聞こえてくる……夜雀かと思いきや、%ANAME(対象)%だ！
		PRINTFORMW 「あれ、%ANAME(MASTER)%さんじゃないですか。どうしたんです？」
		PRINTFORMW どうしたもこうしたも、謎の歌に引き寄せられただけのことだが……
		PRINTFORML 今の歌について、聞いてみようか？
		PRINTFORML
		CALL ASK_YN("気になるし、聞いてみよう", "絶対に面倒なことになる気がするぞ……")
		IF RESULT == 1
			PRINTFORMW 「？　じゃあ、私はこれで」
			PRINTFORMW %ANAME(対象)%は立ち去っていった……
			KDVAR:対象:早苗_鋼鉄巨人 = -1
		ELSE
			PRINTFORMW 「ンンンッ！！！！！！！！！　興味を持ちましたね！！！！！！！！！！？」
			PRINTFORMW %ANAME(対象)%は目を血走らせ、こちらににじり寄ってきた。なんだか嫌な予感がする……
			PRINTFORMW 「今のは1992年から1995年にかけて放映された四角山プロダクションのロボットアニメ『鋼鉄巨人バトルガイオン』、の！　第三クールの主題歌『燃える魂バトルガイオン』の二番のサビなんですよ！！！！！」
			PRINTFORMW 「ちなみに主題歌を歌ってるのはアニメソング界の大王とも呼ばれる山本三郎さんです！！！！　いいですよね山本ニキ！！！！！！」
			PRINTFORMW 「スタッフもキャストも当時脂の乗りに乗ったベテランたちを起用し！！！！　『後にも先にもこれ以上のアニメはない』と評論家たちを唸らせるほどの脚本！！！！！！！　日本のロボットアニメ界の金字塔とでも言うべき名作なんです！！！！！　ご存じないですか！！！！！？」
			PRINTFORML ご存じないですか、と聞いてきたわりに、%ANAME(対象)%はこちらの返答を待たなかった
			PRINTFORMW いそいそと自分の部屋へ駆け戻ると、風呂敷包みを持って戻ってきた
			PRINTFORMW 「はいこれ、どうぞ！」
			PRINTFORMW 「流石に幻想郷にDVDなんてないですからね、コミック版バトルガイオン全72巻を貸してあげます！」
			PRINTFORMW 「読んだら感想聞かせてくださいね！」
			PRINTFORMW そう言って、%ANAME(対象)%は立ち去っていった……
			PRINTFORMW ……押しつけられたコミックはものすごく重かった……
			KDVAR:対象:早苗_鋼鉄巨人 = 1
		ENDIF
	CASE 1
		PRINTFORMW そういえば、%ANAME(対象)%からコミックを押しつけられていたんだったか……
		PRINTFORML そのままにしておくのも悪いし、読んでおこうか？
		CALL ASK_YN("まぁ、一巻だけ……", "興味ない")
		IF RESULT == 1
			PRINTFORMW 今は忙しいし、まぁいいか……
			PRINTFORMW 後回しにしておくことにした
		ELSE
			PRINTFORMW 折角借りたのだし、触りだけでも読んでみるとしよう……
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORMW これは確かに、中々面白そうだ。もう一巻読もう……
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORMW これは確かに、中々面白い。もう一巻読もう……
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORMW なるほどこれは面白い。もう一巻読もう……
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORMW いやこれはヤバい。もう一巻読もう……
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORMW もう一巻読もう……もう一巻読もう……もう一巻読もう……もう一巻読もう……もう一巻読もう……もう一巻読もう……もう一巻読もう……もう一巻読もう……
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORMW 気がついたら、日が暮れ、夜が明けていた……
			PRINTFORMW 当然一睡もしていないが、心はやけに充実していた。戦え、バトルガイオン！
			CALL COLORPRINT(@"夜更かしの反動で、%ANAME(MASTER)%の武闘が3下がりました", カラー_警告, "W")
			CALL ADD_COOLTIME(MASTER, 2)
			ABL:MASTER:武闘 = MAX(ABL:MASTER:武闘 - 3, 1)
			KDVAR:対象:早苗_鋼鉄巨人 ++
		ENDIF
	CASE IS < 5
		PRINTFORMW 「あっ、%ANAME(MASTER)%さん！　バトルガイオンの話しましょう！」
		PRINTFORMW %ANAME(対象)%に誘われ、鋼鉄巨人バトルガイオンについての話を一晩中繰り広げた……
		PRINTFORML
		PRINTDATAW
			DATA 「やっぱり斉藤伍長が北極海に飛び込むシーンはいいですねぇ……」
			DATA 「私柳田隊長が好きなんですよー、渋いおじさまのキャラっていいですよね！」
			DATA 「いい……」「わかる」
			DATA 「13巻の138ページ目4コマ目のバトルガイオンの右肩パーツ、ベタ入れるの忘れちゃってますよねぇ……」
			DATA 「この敵の女幹部ファオランが大っ嫌いなんです。こういうお高くとまった人は本当、もう！」
			DATA 「ううっ……43巻のこのシーンは涙なしには語れませんよ……」
			DATA 「いい……」「それな」
			DATA 「この健太くんの成長っぷり……これが5巻で初登場したときは敵の攻撃に逃げ惑ってた子だとは思えません……大きくなったなぁ……」
			DATA 「全編通して榊原は本当に暗躍してくれますね……悪役ながらあっぱれですよ！」
			DATA 「ガイオンの食事シーンっておいしそうですよねぇ……お腹空いてきました、ラーメン食べます？」
			DATA 「このシーン！　このシーンは是非アニメで見て欲しい……！　なんで幻想郷はブルーレイないの！？」
			DATA 「今だ繰り出せ～～～～♪　ガイオンソーーーーーーーード♪」
		ENDDATA
		PRINTFORML
		CALL COLORPRINT(@"%ANAME(対象)%からの好感度が上がりました", カラー_注意, "W")
		CALL COLORPRINT(@"夜更かしの反動で、%ANAME(MASTER)%と%ANAME(対象)%の武闘が3下がりました", カラー_警告, "W")
		CALL ADD_COOLTIME(MASTER, 2)
		ABL:MASTER:武闘 = MAX(ABL:MASTER:武闘 - 3, 1)
		CALL ADD_COOLTIME(対象, 2)
		ABL:対象:武闘 = MAX(ABL:対象:武闘 - 3, 1)
		CFLAG:対象:好感度 += 100
		KDVAR:対象:早苗_鋼鉄巨人 ++
	CASE 5
		PRINTFORMW 「いい加減にせんかァーーーーーーーーッ！」
		PRINTFORML 今日もバトルガイオン話に花を咲かせようと、%ANAME(対象)%の部屋にあがった途端のことだ
		PRINTFORMW 突如として、%ANAME(神奈子)%が乱入してきた！
		PRINTFORMW 「若い二人が夜な夜な部屋で全くもう！　そういうのは\@ TALENT:対象:正妻 || TALENT:対象:妾 ? ほどほどに # 結婚してからに\@しなさいと言ったろう%ANAME(対象)%！」
		PRINTFORMW 「えっ……あの、あっ！？　あ、いや、神奈子様、これはそういうのではなくって！」
		PRINTFORMW 「言い訳無用！　%ANAME(MASTER)%も%ANAME(MASTER)%だ、うちの%ANAME(対象)%を誑かして、タダじゃおかないよ！」
		PRINTFORML なんだか嫌な予感がする……予感はすぐに的中した！
		PRINTFORMW 弾幕を繰り出した%ANAME(神奈子)%を、二人で必死に止めた……
		PRINTFORML ・
		PRINTFORML ・
		PRINTFORML ・
		PRINTFORMW 「なんと、アニメの話！？」
		PRINTFORMW 「いや、それは悪いことをしたね……私はてっきり、こう、二人が\@ TALENT:対象:正妻 || TALENT:対象:妾 ? ただれた性生活を送っているのかと # いけない関係にあるのかと \@」
		PRINTFORML 確かに、夜な夜な部屋で二人でいたのは、勘違いされても仕方ないかもしれない
		PRINTFORMW とはいえ、バトルガイオンの話ができるのは%ANAME(対象)%しかいないし、不可抗力である
		PRINTFORMW 「まぁそれはそれとして！　二人とも！　最近それにかかりっきりで、腕が鈍ってるだろう！」
		PRINTFORMW 「明日から私がびしばししごいてやるからね！　覚悟しな！」
		PRINTFORMW ……なんだか、えらいことになった気がする
		PRINTFORMW %ANAME(対象)%の方を見ると、観念するしかない、という顔をしていた……
		CALL COLORPRINT(@"%ANAME(神奈子)%にしごかれ、%ANAME(MASTER)%と%ANAME(対象)%の武闘が15上がりました", カラー_注意, "W")
		;（差し引きすると全然上がって）ないです
		ABL:MASTER:武闘 += 15
		ABL:対象:武闘 += 15
		KDVAR:対象:早苗_鋼鉄巨人 = -1
ENDSELECT

RETURN 1
