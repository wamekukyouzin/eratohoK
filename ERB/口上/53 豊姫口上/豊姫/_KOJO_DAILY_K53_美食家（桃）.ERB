;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K53_GOURMET_RATE(対象)
#DIM 対象
RETURN 50


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K53_GOURMET_DECISION(対象)
#DIM 対象
#DIM 天子
天子 = NAME_TO_CHARA("天子")

;一度しか発生しないイベントならこんな感じ
SIF KDVAR:対象:豊姫_美食家
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1) && CHECK_KOJO_DAILY_HAPPEN(天子, 1, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K53_GOURMET_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_エロ

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K53_GOURMET(対象)
#DIM 対象
#DIM 天子
天子 = NAME_TO_CHARA("天子")

;一度しか発生しないイベントならこんな感じ
SIF KDVAR:対象:豊姫_美食家
	RETURN 0

KDVAR:対象:豊姫_美食家 = 1


PRINTFORMW %ANAME(対象)%と%ANAME(天子)%が何か話しているようだ……
PRINTFORML 珍しい取り合わせに思えるが……
CALL ASK_YN("声をかける", "放っておく")

IF RESULT == 1
	PRINTFORML そういうこともあるだろう
	PRINTFORMW 放っておくことにした……
	RETURN 1
ENDIF

PRINTFORML 「あら、%ANAME(MASTER)%」
PRINTFORML 「いえね、地上にも桃があるっていうから、ちょっと気になって」
PRINTFORMW 「こちらの%ANAME(天子)%さんが持ってるっていうから、譲ってもらってたの」
PRINTFORMW 見てみれば、%PRONOUN(対象)%の片手には食べかけの桃が握られている……
PRINTFORMW 「なんでも、この天界の桃っていうのには特別な力があるそうね」
PRINTFORMW 「それかどうかしらないけど、なんだか力が湧いてきた気がするわ」
CALL COLORPRINT(@"%ANAME(対象)%の体力が少し増えた", カラー_注意, "W")
MAXBASE:対象:体力 += 100

RETURN 1
