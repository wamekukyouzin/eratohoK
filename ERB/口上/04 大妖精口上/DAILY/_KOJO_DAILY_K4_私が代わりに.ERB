;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K4_INSTEAD_OF_HER_RATE(対象)
#DIM 対象
RETURN 1000


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K4_INSTEAD_OF_HER_DECISION(対象)
#DIM 対象
#DIM チルノ
チルノ = NAME_TO_CHARA("チルノ")

SIF CHECK_KOJO_DAILY_HAPPEN(対象, -1, 0) || CHECK_KOJO_DAILY_HAPPEN(チルノ, -1, 1)
	RETURN 0
SIF CFLAG:対象:所属 != CFLAG:チルノ:所属
	RETURN 0

SIF CFLAG:チルノ:所属 == CFLAG:チルノ:捕虜先
	RETURN 0

RETURN !KDVAR:対象:大妖精_私が代わりに

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K4_INSTEAD_OF_HER_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K4_INSTEAD_OF_HER(対象)
#DIM 対象
#DIM チルノ
チルノ = NAME_TO_CHARA("チルノ")

PRINTFORMW 「ち……%ANAME(チルノ)%ちゃんが……！」
PRINTFORML %ANAME(対象)%は狼狽していた。%ANAME(チルノ)%が捕らえられたからだ
PRINTFORML %PRONOUN(対象)%にとって、%ANAME(チルノ)%は親友、あるいはそれ以上の存在だ
PRINTFORMW もし何事かあれば……気が気でなくなるのも当然のことだった
PRINTFORMW 「何とかしなくちゃ、わ、私が……私が」
PRINTFORML 自分が。
PRINTFORML 根が臆病である%ANAME(対象)%にとって、自分が戦うというのは相当に決断力の必要なことだ
PRINTFORML だが、%ANAME(チルノ)%の危機なのだ。その程度の勇気、ふり絞れないはずがなかった
PRINTFORMW 「待っててね%ANAME(チルノ)%ちゃん……すぐに助けるから！」
CALL COLORPRINT(@"%ANAME(対象)%が覚悟を決めました", カラー_注意, "W")
CALL COLORPRINT(@"%ANAME(対象)%の能力が上昇しました", カラー_注意, "W")

ABL:対象:武闘 = MAX(ABL:対象:武闘 + 5, 60)
ABL:対象:防衛 = MAX(ABL:対象:防衛 + 5, 60)
ABL:対象:知略 = MAX(ABL:対象:知略 + 5, 60)

CALL SKILL_LEARN_BY_NAME(対象, スキル_ジャンル_BASE, NO:対象, "すごい応援")
SIF RESULT
	CALL COLORPRINT(@"%ANAME(対象)%がスキル＜すごい応援＞を習得しました", カラー_注意, "W")

KDVAR:対象:大妖精_私が代わりに = 1

RETURN 1
