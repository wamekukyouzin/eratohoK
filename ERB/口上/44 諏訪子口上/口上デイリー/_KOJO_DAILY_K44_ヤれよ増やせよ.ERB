;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K44_FUCK_HER_RATE(対象)
#DIM 対象
RETURN 50


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K44_FUCK_HER_DECISION(対象)
#DIM 対象
#DIM 早苗

早苗 = NAME_TO_CHARA("早苗")

SIF KDVAR:対象:諏訪子_ヤれよ増やせよ
	RETURN 0

SIF TALENT:早苗:合意
	RETURN 0

SIF ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:防衛 + ABL:MASTER:政治 < 280 || ABL:MASTER:性技 < 4
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0) && CHECK_KOJO_DAILY_HAPPEN(早苗, 1, 0)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K44_FUCK_HER_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_エロ

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K44_FUCK_HER(対象)
#DIM 対象
#DIM 早苗

CFLAG:対象:面識 = 1

早苗 = NAME_TO_CHARA("早苗")

PRINTFORMW 「あっ、%ANAME(MASTER)%、ちょうどいいところに」
PRINTFORML 拠点を見回っていると、%ANAME(対象)%に声をかけられた
PRINTFORMW なにか用があるようだが……
PRINTFORMW 「いやなに、話というか、こっちの一方的な通達なんだけどさ」
PRINTFORMW 「あんたは"合格"だ。うちの%ANAME(早苗)%を好きにしていいよ」
PRINTFORML ……話がさっぱり見えない
PRINTFORMW %ANAME(対象)%は笑いながら話を続ける
PRINTFORML 「いやさ、今は守矢神社も、%ANAME(早苗)%がいるから安泰だけど」
PRINTFORMW 「いつまでも現役でいられるわけもなし、次代の神官を用意しておかなきゃなわけ」
PRINTFORMW 「だから%ANAME(早苗)%にはどんどん産んでもらわないとなんだけど……凡人の種じゃ、凡人しか生まれない」
PRINTFORML 「その点あんたは合格だ。能もあるし、閨事も得意のようじゃないか」
PRINTFORMW 「あんたの種と%ANAME(早苗)%の卵なら、いい子も産まれるだろう」
PRINTFORMW 「%ANAME(早苗)%には話を通してるから、どんどんヤッてどんどん増やしなよ」
PRINTFORMW つまり、%ANAME(早苗)%を抱いて孕ませろということか
PRINTFORML なんだかとんでもないことを言い始めたが……

CALL ASK_MULTI("わかった", @"%ANAME(早苗)%の意思は？", @"%ANAME(対象)%のことも抱きたい")

SELECTCASE RESULT
	CASE 0
		PRINTFORMW 「ん、それでよろしい」
		PRINTFORMW %ANAME(対象)%は立ち去った……
	CASE 1
		PRINTFORMW 「ん？　まあ受け入れちゃーいるよね、それも祝の仕事だってさ」
		PRINTFORMW 「心から受け入れてるか？　……そこはアンタがどうにかすればいいんじゃない？」
		PRINTFORMW 「ま、そういうことだから、あとはよろしく」
		PRINTFORMW %ANAME(対象)%は立ち去った……
	CASE 2
		PRINTFORML 「は？　あんた私みたいなちんちくりん捕まえて……あーいや、待てよ」
		PRINTFORML 「まあ確かに、私の子の方が血は濃くなるか……一理ある？」
		PRINTFORML 「それに私もご無沙汰だし……子孫も交えて爛れた生活なんてのもありかな……」
		PRINTFORMW 「ふむ、いいよ。私もあんたに抱かれるとしよう」
		PRINTFORMW 「いつでも準備しとくから、その気になったら呼んでよ」
		PRINTFORMW 冗談のつもりだったのだが、%PRONOUN(対象)%にとっては案外良い案だったようだ
		PRINTFORMW %ANAME(対象)%は立ち去った……
		CALL COLOR_PRINTW(@"%ANAME(対象)%の合意を得ました", カラー_注意)
		TALENT:対象:合意 = 1
		TALENT:対象:チョロイン = 1
		CFLAG:対象:好感度 += 500
		CFLAG:対象:従属度 += 500
		CFLAG:対象:依存度 += 500
ENDSELECT

CALL COLOR_PRINTW(@"%ANAME(早苗)%の合意を得ました", カラー_注意)

CFLAG:早苗:好感度 += 500
CFLAG:早苗:従属度 += 500
CFLAG:早苗:依存度 += 500

TALENT:早苗:合意 = 1
TALENT:早苗:チョロイン = 1

KDVAR:対象:諏訪子_ヤれよ増やせよ = 1



RETURN 1
