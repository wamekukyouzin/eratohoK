@KOJO_DAILY_EXIST_K1()

@KOJO_DAILY_K1()
#DIM 発生フラグ
発生フラグ = 0

;すけべ巫女
DAILY_NAME = すけべ巫女
CALL KOJO_DAILY_K1_NAUGHTY_MAIDEN
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

;素敵な賽銭箱
DAILY_NAME = 素敵な賽銭箱
CALL KOJO_DAILY_K1_LOVELY_SAISENBAKO
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1
RETURN 発生フラグ


;------------------------------------
;すけべ巫女
;-----------------------------------
@KOJO_DAILY_K1_NAUGHTY_MAIDEN
#DIM 霊夢

霊夢 = NAME_TO_CHARA("霊夢")

;MASTERが男で、霊夢と既成事実LV3まで到達しており、霊夢が恋慕か従属系を取得しており、同一勢力で捕虜でなく4%
SIF !(CHECK_KOJO_DAILY_HAPPEN(霊夢, 40, 1, 0) && IS_MALE(MASTER) && MARK:霊夢:既成事実 == 3 && (IS_LOVER(霊夢) || IS_SLAVE(霊夢)))
	RETURN -1

IF IS_SLAVE(霊夢)
	LOCALS:0 = ご主人様
ELSEIF MASTER == NAME_TO_CHARA("霖之助")
	LOCALS:0 = %ANAME(MASTER)%さん
ELSE
	LOCALS:0 = %ANAME(MASTER)%
ENDIF

CALL KOJO_DAILY_START()

PRINTFORML 深夜。見回りのため廊下を歩いていると、%ANAME(霊夢)%が自分の部屋から手招きしているのを見つけた
PRINTFORML いったいなんの用だろう……
PRINTFORML
CALL ASK_YN("近づいてみる", "放っておく")
IF RESULT == 1
	PRINTFORMW 別にいいか、今忙しいし……
	PRINTFORMW %ANAME(霊夢)%には悪いが、無視して先に進むことにした
	RETURN 0
ELSE
	IF KDVAR:霊夢:霊夢_すけべ巫女 == 0
		PRINTFORMW 「%LOCALS:0%。実はちょっと、お願いがあるの」
		PRINTFORMW あの%ANAME(霊夢)%が、しおらしくも「お願い」するとは。いったいどうしたというのだろう
		PRINTFORML そういえば、%ANAME(霊夢)%はどこか本調子ではないようだった。顔が赤いし、呼吸も乱れている
		PRINTFORMW 風邪でもひいてるんじゃないのか、温かくして寝た方がいいというと、%ANAME(霊夢)%は首を横に振った
		;かつて調教されたパターン
		IF IS_FALLEN_TO_SP_COUNTRY(霊夢) && !IS_SP_COUNTRY(CFLAG:霊夢:所属)
			PRINTFORMW 「我慢しなきゃって、思うんだけど……でも、どうしようもないの」
			PRINTFORMW 「夜になると、"あの人たち"にされたことを思い出して、それで、身体が疼いて」
			PRINTFORML "あの人たち"。かつて%ANAME(霊夢)%を捕虜とし、陵辱した連中だろう
			PRINTFORMW そいつらのことを考えると、はらわたが煮える……
			PRINTFORMW 「駄目だって分かってるんだけど、でもどうしようもないの」
			PRINTFORMW 「シたくて、たまらなくて、慰めても、慰めても、全然足りないの」
			PRINTFORMW 「他に頼める人なんていないし、なにより%LOCALS:0%以外なんて、私自身がいや」
			PRINTFORMW 「だから、ね？　お願い、めちゃくちゃにしていいから、なんとかしてよぉ……」
		;単純にすけべ
		ELSEIF ABL:霊夢:欲望 >= 5
			PRINTFORMW 「誰のせいで、こんなことになったと思ってるのよ」
			PRINTFORMW 「私、どんどんやらしくなってて……触ってもらわないと、おかしくなっちゃいそうで」
			PRINTFORMW 「気持ちよくなりたいのっ、抑えられないから、自分で慰めてるのに、でも全然足りなくって」
			PRINTFORMW 「もう、頭の中ぐちゃぐちゃで、……こんな風にしたの、%LOCALS:0%でしょ、責任とってよぉ……！」
		;神降ろしの影響
		ELSE
			PRINTFORMW 「昼間に、久々に神降ろしをしたの。修行しろって紫がうるさいし」
			PRINTFORML 「でもね、アレって、巫女は、降ろした神から影響を受けるの。どんな影響かは、神の性質次第だけど」
			PRINTFORMW 「ところで、神様って多産でしょ？　つまり、すけべなの。今回はそういう影響、もろに受けたみたいで……その」
			PRINTFORMW すけべな神の影響を受けた……%ANAME(霊夢)%の息が荒いことと合わせ、言わんとするところはなんとなく分かった
			PRINTFORMW つまるところ、神降ろしの影響で、発情している……ということになるのだろう
			PRINTFORMW 「お昼から、ずっと一人でシてるのに、ぜんっぜん足りなくて、っていうかどんどんひどくなってて、おかしくなりそうで」
			PRINTFORMW 「他の人になんて頼めないし、%LOCALS:0%にしてもらえたら、なんとかなりそうだから、だからその……お願い」
		ENDIF
		PRINTFORMW そう言って抱きついてくる%ANAME(霊夢)%の身体は熱く火照っており、男を迎え入れる用意をすっかり整えているようだった
		PRINTFORML ……もう夜も遅いし、明日も早い。とっとと寝るべきなのは間違いないが……
	ELSEIF KDVAR:霊夢:霊夢_すけべ巫女 == 1
		PRINTFORML 部屋に入る。%ANAME(霊夢)%の顔は熱く上気していた。呼吸も荒いようだ
		PRINTFORMW 以前のように、色々と抑えられなくなったのだろう……
		PRINTFORMW 「ごめんね……我慢しなくちゃって、分かってはいるんだけど」
		PRINTFORML 「でも我慢なんて、無理。……シて？」
	ELSE
		PRINTFORML 部屋に入る。%ANAME(霊夢)%の顔は熱く上気していた。呼吸も荒いようだ
		PRINTFORMW 以前のように、色々と抑えられなくなったのだろう……
		PRINTFORMW 「今度は、にがさないんだから……」
		PRINTFORMW %ANAME(霊夢)%はそれだけ言って、こちらに抱きついてきた
		PRINTFORML だいぶまいってしまっているようだが……
	ENDIF
	PRINTFORML
	CALL ASK_YN("抱きしめる", "忙しいから……")
	IF RESULT == 1
		PRINTFORMW 「%LOCALS:0%の意地悪ぅっ……！」
		PRINTFORMW 恨み言を背に浴びながら、部屋を後にした……
		CFLAG:霊夢:好感度 = MAX(CFLAG:霊夢:好感度 - 500, 0)
		CFLAG:霊夢:従属度 = MAX(CFLAG:霊夢:従属度 - 500, 0)
		KDVAR:霊夢:霊夢_すけべ巫女 = 2
		RETURN 0
	ELSE
		PRINTFORMW 据え膳食わぬは、だ。霊夢の身体を抱きしめ、優しく押し倒した……
		CFLAG:霊夢:好感度 += 300
		;全員の調教参加フラグをクリア
		CVARSET CFLAG, 6, 0
		CFLAG:霊夢:調教参加フラグ = 1
		CFLAG:霊夢:調教中帰らない = 1
		CFLAG:霊夢:強制友好化 = 1
		BASE:霊夢:体力 = MAXBASE:霊夢:体力
		BASE:霊夢:気力 = MAXBASE:霊夢:気力
		;強制逆調教フラグをオン
		FLAG:ターンエンド調教 = 1
		;それ以降のデイリーイベント発生を潰す
		DAILY_CANCEL = 1
		;フラグ
		KDVAR:霊夢:霊夢_すけべ巫女 = 1
	ENDIF
ENDIF


;------------------------------------
;素敵な賽銭箱
;-----------------------------------
@KOJO_DAILY_K1_LOVELY_SAISENBAKO
#DIM 霊夢
#DIM 賽銭
#DIM 御利益種類
#DIM 御利益量

霊夢 = NAME_TO_CHARA("霊夢")
;霊夢と同勢力、捕虜でなく60%
SIF !CHECK_KOJO_DAILY_HAPPEN(霊夢, 60, 1, 0, -1)
	RETURN -1

;面識がなければ付与
SIF !CFLAG:霊夢:面識
	CFLAG:霊夢:面識 = 1
	
IF MASTER == NAME_TO_CHARA("霖之助")
	LOCALS = %ANAME(MASTER)%さん
ELSE
	LOCALS = %ANAME(MASTER)%
ENDIF

SELECTCASE RAND:100
	CASE IS < 40
		賽銭 = 10
	CASE IS < 60
		賽銭 = 100
	CASE IS < 80
		賽銭 = 500
	CASE IS < 90
		賽銭 = 1000
	CASEELSE
		賽銭 = 10000
ENDSELECT

CALL KOJO_DAILY_START()
IF KDVAR:霊夢:霊夢_素敵な賽銭箱 == 0
	PRINTFORMW 「ん」
	PRINTFORML 仕事中、急に%ANAME(霊夢)%が現れたかと思ったら、小さな木箱を差し出してきた
	PRINTFORMW 募金箱か何かのように見えるが……
	PRINTFORMW 「楽園の素敵な賽銭箱、出張版よ。お金を入れたら御利益があるかもね」
	PRINTFORM 「というわけで、お賽銭をいれてね。そうね、金
	CALL COLORPRINT(@"{賽銭}", カラー_注意)
	PRINTFORMW くらいがいいかもね」
	PRINTFORMW ……こんなに押しつけがましい賽銭箱というのも初めてだ
	PRINTFORML というか、賽銭というのは金額まで指定してくるものなのだろうか……
ELSEIF KDVAR:霊夢:霊夢_素敵な賽銭箱 == 1
	PRINTFORML 「また来たわよ」
	PRINTFORML 仕事中、%ANAME(霊夢)%がやって来て、賽銭箱を差し出してきた
	PRINTFORM 「というわけで、お賽銭をいれてね。そうね、金
	CALL COLORPRINT(@"{賽銭}", カラー_注意)
	PRINTFORML くらいがいいかもね」
	PRINTFORML 相変わらず額は指定らしいが……
ELSE
	PRINTFORML 「今度はお金、ちょうだいね？」
	PRINTFORML 仕事中、%ANAME(霊夢)%がやって来て、賽銭箱を差し出してきた
	PRINTFORM 「というわけで、お賽銭をいれてね。そうね、金
	CALL COLORPRINT(@"{賽銭}", カラー_注意)
	PRINTFORML くらいがいいかもね」
	PRINTFORML 相変わらず額は指定らしいが……
ENDIF
PRINTFORML
CALL ASK_MULTI_JUDGE("いっちょ入れてみる", MONEY >= 賽銭, "やめておく", 1)
IF RESULT == 1
	PRINTFORMW 「なによう、ケチ！」
	PRINTFORMW %ANAME(霊夢)%にむくれられた……
	KDVAR:霊夢:霊夢_素敵な賽銭箱 = 2
ELSE
	PRINTFORMW 「うふふ、ありがと♪」
	PRINTFORMW %ANAME(霊夢)%は恵比寿顔で立ち去っていった……
	CFLAG:霊夢:好感度 += MIN(賽銭 / 10, 500)
	MONEY -= 賽銭
	IF RAND:1000 < LIMIT(賽銭, 200, 800)
		SELECTCASE RAND:100
			CASE IS < 40
				御利益種類 = GETNUM(EXP, "武闘経験値")
			CASE IS < 80
				御利益種類 = GETNUM(EXP, "知略経験値")
			CASEELSE
				御利益種類 = GETNUM(EXP, "政治経験値")
		ENDSELECT
		御利益量 = LIMIT(賽銭 / 50, 5, 50)
		IF RAND:2
			CALL COLORPRINT("ちょっとした御利益があったようだ……", カラー_注意, "W")
			CALL PRINT_ADD_EXP(MASTER, EXPNAME:御利益種類, 御利益量, 1)
			CALL TRAIN_AUTO_ABLUP(MASTER)
		ELSE
			CALL COLORPRINT(@"ちょっとした御利益があったようだ……%ANAME(霊夢)%に", カラー_注意, "W")
			CALL PRINT_ADD_EXP(霊夢, EXPNAME:御利益種類, 御利益量, 1)
			CALL TRAIN_AUTO_ABLUP(霊夢)
		ENDIF
	ENDIF
	KDVAR:霊夢:霊夢_素敵な賽銭箱 = 1
ENDIF
