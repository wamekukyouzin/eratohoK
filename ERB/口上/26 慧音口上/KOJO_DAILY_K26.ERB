;------------------------------------
;そのキャラがデイリーイベントをもつか判断する関数
;KOJO_EXISTとは別にこの関数も必要になるので注意
;------------------------------------
@KOJO_DAILY_EXIST_K26()


;------------------------------------
;そのキャラのデイリー本体を呼び出すハブとなる関数
;------------------------------------
@KOJO_DAILY_K26()
#DIM 発生フラグ
発生フラグ = 0

;デイリーイベント
DAILY_NAME = うたたね
CALL KOJO_DAILY_K26_ASLEEP_1()
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

;デイリーイベント
DAILY_NAME = うたたね
CALL KOJO_DAILY_K26_ASLEEP_2()
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

RETURN 発生フラグ


;------------------------------------
;うたたね（してるのを見つける方）
;-----------------------------------
@KOJO_DAILY_K26_ASLEEP_1
#DIM 慧音

慧音 = NAME_TO_CHARA("慧音")

SIF !CHECK_KOJO_DAILY_HAPPEN(慧音, 40, 1, 0, 1)
	RETURN -1

SIF CFLAG:慧音:好感度 < 500
	RETURN -1

SIF KDVAR:慧音:慧音_うたたね発見
	RETURN -1

CALL KOJO_DAILY_START()

PRINTFORMW 用があって%ANAME(慧音)%の部屋を訪ねたが、反応がない
PRINTFORMW この時間の%PRONOUN(慧音)%は自室で仕事をしているはずだが……
PRINTL
CALL ASK_YN("のぞいてみる", "外出しているのだろう")

IF RESULT == 1
	PRINTFORMW たまたま外出しているのだろう
	PRINTFORMW %ANAME(MASTER)%はその場を後にした……
	KDVAR:慧音:慧音_うたたね発見 = 1
	RETURN
ENDIF

PRINTFORMW 怪訝に思ってのぞいてみると、%ANAME(慧音)%が机に突っ伏しているのが見えた
PRINTFORMW どうやら仕事中にうたた寝してしまったようだ……
CALL ASK_MULTI("起こす", "毛布を掛けておく", "立ち去る")

SELECTCASE RESULT
	CASE 0
		PRINTFORMW %ANAME(慧音)%を優しく揺り起こした
		PRINTFORMW 「んぅ……ん……？　わっ、あ、ああ、%ANAME(MASTER)%か……」
		PRINTFORMW 「私は……居眠りしてしまっていたのか。恥ずかしいところを見られてしまったな」
		PRINTFORMW 「少し眠って、頭も冴えてきたようだ。残りの仕事を終えてしまうよ」
		PRINTFORMW %PRONOUN(慧音)%ははにかみながら仕事に戻った……
		CFLAG:慧音:好感度 += 50
	CASE 1
		PRINTFORMW 疲れているのだろう。起こすのも悪い
		PRINTFORMW 毛布だけかけて、その場を立ち去った……
		CFLAG:慧音:好感度 += 100
	CASE 2
		PRINTFORMW 疲れているのだろう。起こすのも悪い
		PRINTFORMW そっと扉を閉め、その場を立ち去った……
ENDSELECT

KDVAR:慧音:慧音_うたたね発見 = 1



;------------------------------------
;うたたね（してるのを見つけられる方）
;-----------------------------------
@KOJO_DAILY_K26_ASLEEP_2
#DIM 慧音

慧音 = NAME_TO_CHARA("慧音")

SIF !CHECK_KOJO_DAILY_HAPPEN(慧音, 40, 1, 0, 1)
	RETURN -1

SIF CFLAG:慧音:好感度 < 500
	RETURN -1

SIF KDVAR:慧音:慧音_うたたね発見される
	RETURN -1

CALL KOJO_DAILY_START()

PRINTFORMW なんだか頭のあたりに柔らかい感触を感じる……
PRINTFORMW 目を開くと、%ANAME(慧音)%がこちらを見下ろしていた
PRINTFORMW 「おはよう、ねぼすけさん？」
PRINTFORMW 「君がうたた寝とは疲れているんだな、働き過ぎじゃないか？」
PRINTFORML どうやら居眠りしてしまったようだ
PRINTFORML それで、%ANAME(慧音)%が膝枕をしてくれていたらしい
PRINTFORMW 起き上がろうとすると、手で制された
PRINTFORMW 「こらこら。疲れてるくせに、無理しようとするんじゃない」
PRINTFORMW 「もう少しこのままでいてやるから、ゆっくり寝てなさい、ほら」
PRINTFORMW そこまで言われて、無理に起き上がるわけにもいかないだろう
PRINTFORMW しばらくの間、%ANAME(慧音)%の脚を貸してもらった……

CFLAG:慧音:好感度 += 200
KDVAR:慧音:慧音_うたたね発見される = 1

