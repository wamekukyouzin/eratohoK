;---------------------
;対応するデイリーのDISABLEを返す。設定しない場合、イベントは発生しない。
;---------------------
@KOJO_DAILY_K75_DERIVATION_FROM_THE_DEEP_DISABLE(対象)
#DIM 対象
RETURN KOJO_DAILY_GET_DISABLE_CONFIG(対象, "FROM_THE_DEEP")


;---------------------
;発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K75_DERIVATION_FROM_THE_DEEP_DECISION(対象)
#DIM 対象

;開始イベント前、終了イベント後ならだめ
SIF GROUPMATCH(KDVAR:対象:水蜜_仄暗い水の底から, 0, 2)
	RETURN 0
;滞在カウント残っててもだめ
SIF KDVAR:対象:水蜜_水底滞在カウント > 0
	RETURN 0

RETURN 1

;---------------------
;本体
;---------------------
@KOJO_DAILY_K75_DERIVATION_FROM_THE_DEEP(対象)
#DIM 対象
PRINTFORMW 「ただいま」
PRINTFORMW 行方不明になっていた%ANAME(対象)%が、ふらりと戻ってきた
PRINTFORMW 「急にごめんね、もう大丈夫だから」
PRINTFORMW 行方不明になる前の憔悴っぷりはどこへやら、なにかつやつやしてすらいる
PRINTFORMW どこへ行っていたのかと尋ねても、%ANAME(対象)%は首を横に振った
PRINTFORMW （言えるわけないじゃん……？　血の池地獄に沈んできてたなんて）
PRINTFORMW （あそこなら私でも『浮けない』し『溺れる』し……どっちも、普通の水じゃできないことだし）
PRINTFORMW （誰にだって、ストレス解散の方法くらいあるよねってことで、ひとつ）

CALL COLOR_PRINTW(@"%ANAME(対象)%が職場復帰しました", カラー_注意)
CALL COLOR_PRINTW(@"%ANAME(対象)%の能力・素質が変化しました", カラー_注意)

CALL SKILL_LEARN_BY_NAME(対象, スキル_ジャンル_SP, NO:対象, "幽霊船永久停泊")
SIF RESULT
	CALL COLOR_PRINTW(@"%ANAME(対象)%がスキル＜幽霊船永久停泊＞を習得しました", カラー_注意)


ABL:対象:武闘 += 10
ABL:対象:防衛 += 10
ABL:対象:知略 += 10
TALENT:対象:妖術知識 = 1
ABL:対象:妖術 += 5

TALENT:対象:男嫌い = 0
TALENT:対象:解放 = 1
TALENT:対象:Ｓ気質 = 1
TALENT:対象:Ｍ気質 = 1

CALL CHANGE_COUNTRY(対象, CFLAG:MASTER:所属)


KDVAR:対象:水蜜_仄暗い水の底から = 2

RETURN 1




