;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K97_MERCY_PLEASE_RATE(対象)
#DIM 対象
RETURN 150


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K97_MERCY_PLEASE_DECISION(対象)
#DIM 対象

;MASTERがどこかの勢力に所属
SIF !CFLAG:MASTER:所属
	RETURN 0

;3都市以上をMASTERが所有していること
SIF GET_OWN_CITY(CFLAG:MASTER:所属) < 3
	RETURN 0

;MASTERが針妙丸だと駄目
SIF NAME_TO_CHARA("針妙丸") == MASTER
	RETURN 0

SIF FLAG:クリアフラグ
	RETURN 0

;一回きり
SIF KDVAR:対象:正邪_正直な懇願
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 0, 1) && CFLAG:MASTER:所属 == CFLAG:対象:捕虜先 && !IS_FALLEN(対象)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K97_MERCY_PLEASE_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K97_MERCY_PLEASE(対象)
#DIM 対象
PRINTFORML %ANAME(MASTER)%が牢獄に向かうと、捕らえられた%ANAME(対象)%は懇願してきた
PRINTFORML 「なぁ、私のことを解放してくれないか？」
PRINTFORMW 「そしたら、お前の仲間になってやるよ。私は役に立つぞ？」
PRINTFORML 「いや、信用ならないのは分かってる！　なにせ私は天邪鬼だからな、疑う気持ちはよく分かるよ」
PRINTFORMW 「でも私も、生き残りのために必死なんだ。ここで朽ち果てるくらいなら、天邪鬼としてのありようなんてクソ食らえだ」
PRINTFORMW 「な？　な？　頼むよ、ちゃんと言うこと聞くからさ……」
PRINTFORML 天邪鬼の言うことだが、その目には真剣味がある
PRINTFORML 検討してみるのもいいかもしれないが……？
PRINTFORML

CALL ASK_YN("解放する", "断る")
IF RESULT == 0
	PRINTFORMW 「そうこなくっちゃ！　さあ、そうと決まればこんなところからは早く出してくれ」
	PRINTFORMW 「さあ、目にもの見せてやろうじゃないか、ふふふ」
	PRINTFORML 牢屋から出た%ANAME(対象)%は、ずいぶんやる気のようだ
	PRINTFORML これなら役に立ってくれるかもしれない……
	CALL COLOR_PRINTW(@"%ANAME(対象)%が仲間になりました", カラー_注意)
	CALL CHANGE_COUNTRY(対象, CFLAG:MASTER:所属, 1)
	KDVAR:対象:正邪_正直な懇願 = 1
ELSE
	PRINTFORMW 「……クソが！」
	PRINTFORMW 「このクソッタレ！　呪われちまえ！」
	PRINTFORML 天邪鬼を信用できるわけがないだろう
	PRINTFORML そう伝えると、%ANAME(対象)%は口汚くこちらを罵ってきた
	PRINTFORML 罵倒を背中に受けながら、牢獄を後にした……
	KDVAR:対象:正邪_正直な懇願 = 2
ENDIF

RETURN 1
