@KOJO_DAILY_EXIST_K9()

@KOJO_DAILY_K9()
#DIM 発生フラグ
発生フラグ = 0

;トレーニング
DAILY_NAME = トレーニング
CALL KOJO_DAILY_K9_TRAINING
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

;主の客は
DAILY_NAME = 主の客は
CALL KOJO_DAILY_K9_AFTER_REMILLIAS_INVITATION
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

DAILY_NAME = 主の主は
CALL KOJO_DAILY_K9_LOYALTY_FROM_SAKUYA
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

RETURN 発生フラグ

;----------------------------------
;トレーニング
;----------------------------------
@KOJO_DAILY_K9_TRAINING
#DIM 咲夜
#DIM 時間
#DIM 答え
#DIMS 答え文字列 = "左", "右", "上", "下"
#DIM 経験値

咲夜 = NAME_TO_CHARA("咲夜")

SIF !(CHECK_KOJO_DAILY_HAPPEN(咲夜, 40, 1, 0, 1))
	RETURN -1


;レミリア、パチュリー以外
SIF GROUPMATCH(CSTR:MASTER:99, "レミリア", "パチュリー")
	RETURN -1


;答えを出しておく
答え = RAND:4

;上がる能力も出しておく
SELECTCASE RAND:100
	CASE IS < 25
		経験値 = GET_EXP(GETNUM(ABL, "武闘"))
	CASE IS < 50
		経験値 = GET_EXP(GETNUM(ABL, "防衛"))
	CASE IS < 65
		経験値 = GET_EXP(GETNUM(ABL, "知略"))
	CASE IS < 80
		経験値 = GET_EXP(GETNUM(ABL, "政治"))
	CASE IS < 90
		経験値 = GET_EXP(GETNUM(ABL, "料理"))
	CASEELSE
		経験値 = GET_EXP(GETNUM(ABL, "歌唱"))
ENDSELECT

CALL KOJO_DAILY_START()

SELECTCASE KDVAR:咲夜:咲夜_トレーニング
	CASE 0
		PRINTFORMW %ANAME(MASTER)%が散歩をしていると、%ANAME(咲夜)%が投げナイフの訓練をしているところに出くわした
		PRINTFORMW %ANAME(咲夜)%はこちらを見つけると、訓練を中断して近づいてきた
		PRINTFORML 「%ANAME(MASTER)%。ちょうど良かったわ、ナイフの訓練をしてたのだけど、仮想敵がいないとやっぱり駄目なの」
		PRINTFORMW 「ちょっと付き合ってくれない？」
		PRINTFORMW 軽い口調で言うが、手に持ったそれは刃のついた本物のナイフだ
		PRINTFORML 失敗すれば当然怪我するだろうが……
	CASE 1
		PRINTFORMW また、%ANAME(咲夜)%のナイフの訓練に出くわした
		PRINTFORMW 「あら、ちょうど良かったわ」
		PRINTFORMW 「また付き合ってくれない？」
		PRINTFORML 失敗すれば怪我するだろうが、どうしようか……
	CASE 2
		PRINTFORMW また、%ANAME(咲夜)%のナイフの訓練に出くわした
		PRINTFORMW 「あら、ちょうど良かったわ」
		PRINTFORMW 「今度は付き合ってくれるわよね？」
		PRINTFORML 失敗すれば怪我するだろうが、どうしようか……
ENDSELECT
CALL ASK_YN("付き合う", "危ないからやめておく")
IF RESULT == 1
	PRINTFORMW 「あら、そう？　意気地なしね」
	PRINTFORMW 「仕方ない、一人で練習することにするわ」
	PRINTFORML %ANAME(咲夜)%は訓練に戻っていった……
	KDVAR:咲夜:咲夜_トレーニング = 2
ELSE
	PRINTFORMW 「ふふ、ありがとう」
	PRINTFORMW 「それじゃ、いくわよ……！」
	PRINTFORML 咲夜がナイフを投げてきた！
	CALL COLORPRINT(@"%答え文字列:答え%を選べ！", COLOR("警告"), "L")
	FOR LOCAL, 0, VARSIZE("答え文字列")
		PRINTFORML [{LOCAL}] %答え文字列:LOCAL%
	NEXT
	TINPUT 2000, -1, 1
	;失敗したパターン
	IF RESULT != 答え
		PRINTFORML %ANAME(MASTER)%は回避しようと試みた！
		PRINTFORMW だが、ナイフはものの見事に%ANAME(MASTER)%に命中した！
		PRINTFORMW 「ちょ、ちょっと大丈夫！？」
		PRINTFORMW 「ごめんなさい。……でもおかげで自信がついたわ、ありがとうね？」
		PRINTFORMW %ANAME(咲夜)%に手当してもらった……
		CALL COLORPRINT(@"%ANAME(MASTER)%は怪我をしてしまいました", COLOR("警告"), "W")
		CALL ADD_COOLTIME(MASTER, 3)
		CALL PRINT_ADD_EXP(咲夜, EXPNAME:経験値, RAND:5 + 1, 1)
	;成功
	ELSE
		PRINTFORML %ANAME(MASTER)%は回避しようと試みた！
		PRINTFORMW %ANAME(咲夜)%の放ったナイフを、見事回避してみせた！
		PRINTFORMW 「あら……結構本気で投げたのだけど、まさか避けられるなんてね」
		PRINTFORMW 「私もまだまだね。もっと練習しなくちゃ。付き合ってくれてありがとう、礼を言うわ」
		PRINTFORMW 咲夜は訓練に戻っていった……
		CALL PRINT_ADD_EXP(MASTER, EXPNAME:経験値, RAND:5 + 1, 1)
	ENDIF
ENDIF

;----------------------------------
;主の客は
;----------------------------------
@KOJO_DAILY_K9_AFTER_REMILLIAS_INVITATION
#DIM 咲夜
#DIM レミリア

咲夜 = NAME_TO_CHARA("咲夜")
レミリア = NAME_TO_CHARA("レミリア")


;咲夜と同じ勢力、咲夜が捕虜でない、100%
SIF !(CHECK_KOJO_DAILY_HAPPEN(咲夜, 1000, 1, 0))
	RETURN -1

SIF !(CHECK_KOJO_DAILY_HAPPEN(レミリア, 1000, 1, 0))
	RETURN -1

SIF IS_SLAVE(咲夜) || IS_LOVER(咲夜)
	RETURN -1

;一回きり
SIF KDVAR:咲夜:咲夜_主の客は
	RETURN -1

;レミリアからの勧誘をこなしている
SIF KDVAR:レミリア:レミリア_レミリアからの勧誘 != 1
	RETURN -1

;紅魔館メンバー
SIF GROUPMATCH(CSTR:MASTER:99, "レミリア", "パチュリー", "美鈴", "小悪魔", "フランドール")
	RETURN -1

;面識を得る
CFLAG:咲夜:面識 = 1

CALL KOJO_DAILY_START()

PRINTFORMW 「ああ、%ANAME(MASTER)%。少しいいかしら」
PRINTFORML %ANAME(咲夜)%が部屋を訪ねてきた
PRINTFORMW 「話は聞いてるわ、お嬢様の誘いを受けたそうね？　ようこそ、紅魔館へ」
PRINTFORMW 「それにあたって、一つ大事な話があるの。悪い話ではないわ」
PRINTFORMW 「ご存じの通り、私はお嬢様のメイド。お嬢様が是というなら、私にとっても是だわ」
PRINTFORMW 「……それはつまり、お嬢様が身体を許されたなら、私も許す、ということでもある」
PRINTFORMW 「お嬢様同様、私のことも、好きにしてくれていいわ」
PRINTFORML 
CALL ASK_YN("正気か？", "そうさせてもらおう")
IF RESULT == 0
	PRINTFORMW 「もちろん。遊びや酔狂で言ったりはしないわ。繰り返しになるけど、お嬢様のされることが、私のすること」
	PRINTFORMW 「ま、それだけお嬢様は%ANAME(MASTER)%を欲しがっていて、かつ繋ぎ止めたがっているということよ」
	PRINTFORML なおも躊躇する%ANAME(MASTER)%に、%ANAME(咲夜)%は呆れた顔を浮かべた
	PRINTFORML 「……別に、義務感だけから言ってるわけじゃないのよ？」
	PRINTFORMW 「私だって一人の独立した人間だわ。お嬢様だって、ただの操り人形を手元に置いたりはしないでしょうね」
	PRINTFORMW 「……お嬢様じゃないけど、私も、どうせ成すなら優秀な子のほうがいいってことよ」
	PRINTFORMW 「どうするかは、%ANAME(MASTER)%次第だけれど。私の部屋は、好きに訪ねてくれていいから」
	PRINTFORML 言うだけ言って、%ANAME(咲夜)%は立ち去った
	PRINTFORML あとには、呆然とする%ANAME(MASTER)%が残されるばかりだった……
ELSE
	PRINTFORMW 「話が早いわね。そうそう、それでいいの」
	PRINTFORMW 「それで満足して、紅魔館に残って、結果を残してくれたら十分。あなたはそのために呼ばれたんだから」
	PRINTFORMW 「それじゃ、私の部屋は好きに訪ねてくれていいから。そっちから呼んでくれてもいいわ」
	PRINTFORML 満足気に頷くと、%ANAME(咲夜)%は立ち去った
	PRINTFORML そのうちにでも呼んでやるとしよう……
ENDIF

CALL COLORPRINT(@"%ANAME(咲夜)%の合意を得ました", COLOR("注意"), "W")
CFLAG:咲夜:従属度 = MIN(CFLAG:咲夜:従属度 + 500, 1000)
CFLAG:咲夜:依存度 = MIN(CFLAG:咲夜:依存度 + 300, 500)
TALENT:咲夜:合意 = 1
TALENT:咲夜:チョロイン = 1
KDVAR:咲夜:咲夜_主の客は = 1

;----------------------------------
;主の主は
;----------------------------------
@KOJO_DAILY_K9_LOYALTY_FROM_SAKUYA
#DIM 咲夜
#DIM レミリア

咲夜 = NAME_TO_CHARA("咲夜")
レミリア = NAME_TO_CHARA("レミリア")

;咲夜と同じ勢力、咲夜が捕虜でない、100%
SIF !(CHECK_KOJO_DAILY_HAPPEN(咲夜, 1000, 1, 0))
	RETURN -1

SIF !(CHECK_KOJO_DAILY_HAPPEN(レミリア, 1000, 1, 0))
	RETURN -1

SIF IS_SLAVE(咲夜) || IS_LOVER(咲夜) || TALENT:咲夜:合意
	RETURN -1

;一回きり
SIF KDVAR:咲夜:咲夜_主の主は
	RETURN -1

;レミリアがIS_SLAVEでないとだめ
SIF !IS_SLAVE(レミリア)
	RETURN -1

;紅魔館メンバーはだめ
SIF GROUPMATCH(CSTR:MASTER:99, "レミリア", "パチュリー", "美鈴", "小悪魔", "フランドール")
	RETURN -1

;面識を得る
CFLAG:咲夜:面識 = 1

CALL KOJO_DAILY_START()
PRINTFORMW 「失礼いたします。ご主人様、少々お時間よろしいでしょうか？」
PRINTFORML うやうやしげな様子で、%ANAME(咲夜)%が部屋を訪ねてきた
PRINTFORML ご主人様とは何事かと問うと、%ANAME(咲夜)%は慇懃に語り始める
PRINTFORMW 「我があるじ、レミリア・スカーレットは、あなた様に忠誠を誓いました」
PRINTFORMW 「あるじのあるじは、またあるじです。私も、%ANAME(MASTER)%様に忠誠を誓います」
PRINTFORMW 「なんなりとご命令ください。戦も、お望みでしたら伽も、完璧にこなしてみせますわ」
PRINTFORMW 「取り急ぎは以上です。夜の慰みが必要でしたら、いつでもお呼びくださいませ」
PRINTFORML 言うだけ言って、時を止めたのか、%ANAME(咲夜)%は消えてしまった
PRINTFORML 折角の申し出だ、有効に活用させてもらおう……
CALL COLORPRINT(@"%ANAME(咲夜)%が忠誠を誓いました", COLOR("注意"), "W")
CALL COLORPRINT(@"%ANAME(咲夜)%の合意を得ました", COLOR("注意"), "W")

CFLAG:咲夜:従属度 = MAX(CFLAG:咲夜:従属度 + 1500, 1500)
CFLAG:咲夜:依存度 = MAX(CFLAG:咲夜:依存度 + 300, 300)
TALENT:咲夜:合意 = 1
TALENT:咲夜:チョロイン = 1
TALENT:咲夜:服従 = 1
