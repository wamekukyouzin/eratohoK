;─────────────────────────────────────── 
;■閨の攻め受け分岐
;　KOJO_K9_前.ERBとKOJO_K9_後.ERBで閨と判断されると呼び出される
;　閨は実行対象や主導権だけでは攻め受け関係が判断しにくいので
;　ここを通してまとめて分岐させる
;─────────────────────────────────────── 
@KOJO_K9_NIGHT(咲夜_対象, 前後)
#DIM 咲夜
#DIM 咲夜_対象
#DIMS 前後
#DIMS 咲夜口上攻受カテゴリ

IF !咲夜_対象
	咲夜_対象 = MASTER
ENDIF

;リセット
咲夜 = NAME_TO_CHARA("咲夜")
咲夜口上攻受カテゴリ '= ""
咲夜_咲夜が受け = 0

;─────────────────────────────────────── 
;●詳細カテゴリ決定
;─────────────────────────────────────── 
;咲夜が実行
IF IS_MPLY(咲夜)
	;土下座・おねだり（主導権に関わらず実行が受け）
	IF GROUPMATCH(SELECTCOM, 110, 111)
		咲夜口上攻受カテゴリ '= "咲夜受け"
	;100自慰 101顔面騎乗 102Ａ顔面騎乗 （主導権がなければ受け）
	ELSEIF GROUPMATCH(SELECTCOM, 100, 101, 102) && !IS_INITIATIVE(咲夜)
		咲夜口上攻受カテゴリ '= "咲夜受け"
	;主導権なし排泄者　130放尿する 131尿を掛ける 132口内放尿 150放屁　151顔騎放屁（主導権がなければ受け）
	ELSEIF GROUPMATCH(SELECTCOM, 130, 131, 132, 150, 151) && !IS_INITIATIVE(咲夜)
		咲夜口上攻受カテゴリ '= "咲夜受け"
	;それ以外は実行が攻め
	ELSE
		咲夜口上攻受カテゴリ '= "咲夜攻め"
	ENDIF
;咲夜が咲夜_対象
ELSEIF IS_MTAR(咲夜)
	;土下座・おねだり（主導権に関わらず対象が攻め）
	IF GROUPMATCH(SELECTCOM, 110, 111)
		咲夜口上攻受カテゴリ '= "咲夜攻め"
	;100自慰 101顔面騎乗 102Ａ顔面騎乗（主導権があれば攻め）
	ELSEIF GROUPMATCH(SELECTCOM, 100, 101, 102) && IS_INITIATIVE(咲夜)
		咲夜口上攻受カテゴリ '= "咲夜攻め"
	;主導権あり排泄指示者　130放尿する 131尿を掛ける 132口内放尿 150放屁　151顔騎放屁（主導権があれば攻め）
	ELSEIF GROUPMATCH(SELECTCOM, 130, 131, 132, 150, 151) && IS_INITIATIVE(咲夜)
		咲夜口上攻受カテゴリ '= "咲夜攻め"
	;それ以外は対象が受け
	ELSE
		咲夜口上攻受カテゴリ '= "咲夜受け"
	ENDIF
ENDIF

;─────────────────────────────────────── 
;●詳細カテゴリによる分岐
;─────────────────────────────────────── 
IF 咲夜口上攻受カテゴリ == "咲夜受け"
	;文末決定関数で使用する
	咲夜_咲夜が受け = 1
	;前文か後文か
	IF 前後 == "前"
		CALL KOJO_K9_NIGHT_BEFORE_BOTTOM(咲夜_対象)
	ELSEIF 前後 == "後"
		CALL KOJO_K9_NIGHT_AFTER_BOTTOM(咲夜_対象)
	ENDIF
ELSEIF 咲夜口上攻受カテゴリ == "咲夜攻め"
	;前文か後文か
	IF 前後 == "前"
		CALL KOJO_K9_NIGHT_BEFORE_TOP(咲夜_対象)
	ELSEIF 前後 == "後"
		CALL KOJO_K9_NIGHT_AFTER_TOP(咲夜_対象)
	ENDIF
ENDIF

;─────────────────────────────────────── 
;●終了
;─────────────────────────────────────── 
咲夜_咲夜が受け = 0
RESETCOLOR
RETURN RESULT

