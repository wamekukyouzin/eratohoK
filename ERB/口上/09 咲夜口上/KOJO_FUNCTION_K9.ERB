;─────────────────────────────────────── 
;●咲夜口上の使用可否・初期化
;─────────────────────────────────────── 
@KOJO_ASK_RESET_K9
#DIM SAVE_REDRAW
#DIM SAVE_COLOR
#DIM SAVE_LINE
#DIM 咲夜

CURRENTREDRAW
SAVE_REDRAW = RESULT
REDRAW 0
GETCOLOR
SAVE_COLOR = RESULT
RESETCOLOR

咲夜 = NAME_TO_CHARA("咲夜")

;咲夜口上用のデータを初期化
;0=未設定 1=使用しない 2=使用する
咲夜_口上デイリー入力系使用 = 0
;入力系デイリー親しい呼びかた用
咲夜_主人公への呼称 '= ""
咲夜_主人公への呼称フリガナ '= ""
;入力系デイリーえっちな呼びかた用
咲夜_淫語Ｐ '= "あれ"
咲夜_淫語Ｖ '= "なか"
咲夜_淫語Ｃ '= "そこ"
咲夜_淫語Ａ '= "そこ"
咲夜_淫語Ｂ '= "そこ"
;0=未設定 1=低頻度 2=高頻度
咲夜_淫語頻度 = 0
;プレイヤー側の初回実行判定（スキンヘッドチェック用）
咲夜_髪梳き初回 = 0
;おもいで語りデイリー用
咲夜_告白成功期 = 0
咲夜_告白成功場所 = 0
咲夜_告白成功したキャラ = 0

RESETCOLOR
PRINTL 

PRINTFORML 咲夜口上使用の設定
CALL KOJO_ASK("使用しない", "使用する")
PRINTFORMW 咲夜口上の使用を設定した

;使用しない
IF RESULT == 0
	CFLAG:咲夜:400 = 1
;使用する
ELSEIF RESULT == 1
	CFLAG:咲夜:400 = 2
	PRINTL 
	PRINTFORM 咲夜口上デイリー入力系使用の設定
	SETCOLOR カラー_選択不可
	PRINTFORML  - あなたへの優先呼称やあなた好みの淫語を入力するイベント
	RESETCOLOR
	CALL KOJO_ASK("使用しない", "使用する")
	PRINTFORMW 咲夜口上デイリー入力系使用を設定した
	;使用しない
	IF RESULT == 0
		咲夜_口上デイリー入力系使用 = 1
	;使用する
	ELSEIF RESULT == 1
		咲夜_口上デイリー入力系使用 = 2
	ENDIF
ENDIF

PRINTL 

REDRAW SAVE_REDRAW
SETCOLOR SAVE_COLOR
RETURN 0


;─────────────────────────────────────── 
;●咲夜の呼称（咲夜から咲夜_対象へ）
;　使い方：%CALLNAME_K9(咲夜_対象)%　%CALLNAME_K9(咲夜_対象, "ぁ")%
;　覚書　：第二引数に"ぁ"とつけると
;　フリガナ（CSTR:MASTER:6や咲夜_主人公への呼称フリガナや君主等への特殊呼称）がある場合
;　対応する小書き母音をつける（甘え感を出す）
;─────────────────────────────────────── 
@CALLNAME_K9(咲夜_対象, 小書き)
#FUNCTIONS
#DIM 咲夜
#DIM 咲夜_対象
#DIMS 小書き
#DIMS 表示テキスト
#DIMS 表示テキストフリガナ

;リセット
咲夜 = NAME_TO_CHARA("咲夜")
表示テキスト '= ""

;主人公で呼称指定済み
IF 咲夜_対象 == MASTER && KDVAR:咲夜:咲夜_親しい呼びかた && 咲夜_主人公への呼称 != ""
	表示テキスト '= 咲夜_主人公への呼称
	表示テキストフリガナ '= 咲夜_主人公への呼称フリガナ

;同一勢力君主の男対象
ELSEIF GET_COUNTRY_BOSS(CFLAG:咲夜:所属) == 咲夜_対象 && IS_MALE(咲夜_対象)
	表示テキスト '= "ご主人様"
	表示テキストフリガナ '= "ゴシュジンサマ"

;咲夜_対象が主人公＆咲夜が服従系陥落＆対象が男
ELSEIF 咲夜_対象 == MASTER && IS_SLAVE(咲夜) && IS_MALE(咲夜_対象)
	IF ANAME(咲夜_対象) == "あなた"
		表示テキスト '= "ご主人様"
		表示テキストフリガナ '= "ゴシュジンサマ"
	ELSE
		表示テキスト '= ANAME(咲夜_対象) + "様"
		IF CSTR:咲夜_対象:6 != ""
			表示テキストフリガナ '= CSTR:咲夜_対象:6 + "サマ"
		ELSE
			表示テキストフリガナ '= ""
		ENDIF
	ENDIF

;同一勢力君主の女対象
ELSEIF GET_COUNTRY_BOSS(CFLAG:咲夜:所属) == 咲夜_対象 && IS_FEMALE(咲夜_対象)
	IF ANAME(咲夜_対象) == "あなた"
		表示テキスト '= "お嬢様"
		表示テキストフリガナ '= "オジョウサマ"
	ELSE
		表示テキスト '= ANAME(咲夜_対象) + "様"
		IF CSTR:咲夜_対象:6 != ""
			表示テキストフリガナ '= CSTR:咲夜_対象:6 + "サマ"
		ELSE
			表示テキストフリガナ '= ""
		ENDIF
	ENDIF

;咲夜_対象が主人公＆咲夜が服従系陥落＆対象が女
ELSEIF 咲夜_対象 == MASTER && IS_SLAVE(咲夜) && IS_FEMALE(咲夜_対象)
	IF ANAME(咲夜_対象) == "あなた"
		表示テキスト '= "お嬢様"
		表示テキストフリガナ '= "オジョウサマ"
	ELSE
		表示テキスト '= ANAME(咲夜_対象) + "様"
		IF CSTR:咲夜_対象:6 != ""
			表示テキストフリガナ '= CSTR:咲夜_対象:6 + "サマ"
		ELSE
			表示テキストフリガナ '= ""
		ENDIF
	ENDIF

ELSEIF ANAME(咲夜_対象) == "レミリア"
	表示テキスト '= "お嬢様"
	表示テキストフリガナ '= "オジョウサマ"

ELSEIF ANAME(咲夜_対象) == "フラン" 
	表示テキスト '= "妹様"
	表示テキストフリガナ '= "イモウトサマ"

ELSEIF ANAME(咲夜_対象) == "パチュリー" 
	表示テキスト '= "パチュリー様"
	表示テキストフリガナ '= "パチュリーサマ"

;慰安中モブ相手
ELSEIF FLAG:調教モード == 調教_慰安 && CFLAG:(咲夜_対象):慰安モブ
	表示テキスト '= "皆様"
	表示テキストフリガナ '= "ミナサマ"

;慰安中でモブ以外のキャラには様をつけて呼ぶ
ELSEIF FLAG:調教モード == 調教_慰安 && CFLAG:(咲夜_対象):慰安参加者
	表示テキスト '= ANAME(咲夜_対象) + "様"
	IF CSTR:咲夜_対象:6 != ""
		表示テキストフリガナ '= CSTR:咲夜_対象:6 + "サマ"
	ELSE
		表示テキストフリガナ '= ""
	ENDIF

ELSEIF ANAME(咲夜_対象) == "霖之助" 
	表示テキスト '= "店主"
	表示テキストフリガナ '= "テンシュ"

;敬語の場合
ELSEIF CHECK_K9("敬語", 咲夜_対象)
	IF ANAME(咲夜_対象) == "あなた"
		IF IS_MALE(咲夜_対象)
			表示テキスト '= "貴方"
			表示テキストフリガナ '= "アナタ"
		ELSE
			表示テキスト '= "貴女"
			表示テキストフリガナ '= "アナタ"
		ENDIF
	ELSE
		表示テキスト '= ANAME(咲夜_対象) + "様"
		IF CSTR:咲夜_対象:6 != ""
			表示テキストフリガナ '= CSTR:咲夜_対象:6 + "サマ"
		ELSE
			表示テキストフリガナ '= ""
		ENDIF
	ENDIF

;それ以外
ELSE
	IF ANAME(咲夜_対象) == "あなた"
		IF IS_MALE(咲夜_対象)
			表示テキスト '= "貴方"
			表示テキストフリガナ '= "アナタ"
		ELSE
			表示テキスト '= "貴女"
			表示テキストフリガナ '= "アナタ"
		ENDIF
	ELSE
		表示テキスト '= ANAME(咲夜_対象)
		IF CSTR:咲夜_対象:6 != ""
			表示テキストフリガナ '= CSTR:咲夜_対象:6
		ELSE
			表示テキストフリガナ '= ""
		ENDIF
	ENDIF
ENDIF

IF 小書き == "ぁ" && 表示テキストフリガナ != ""
	表示テキスト '= 表示テキスト + SMALLSCRIPT(表示テキストフリガナ)
ENDIF

RETURNF 表示テキスト


;─────────────────────────────────────── 
;●咲夜口上の分岐条件まとめ
;　IF文を短縮したり見やすくするための条件まとめ
;　よく使う条件が増えたらその都度追加
;　条件=わかりやすそうな条件名 咲夜_対象=略すと主人公
;　CHECK_K9("敬語", 咲夜_対象)……咲夜が敬語を使う固定キャラや君主や服従陥落があるか等 
;　CHECK_K9("面識", 咲夜_対象)……既知と扱う固定キャラか、面識はあるかの確認
;　CHECK_K9("陥落", 咲夜_対象)……咲夜_対象が主人公か＆恋慕系服従系の陥落があるか確認する
;　CHECK_K9("紅魔館", 咲夜_対象)……紅魔館のメンバーかどうか（既知かつ好意的扱い）
;　CHECK_K9("活力")……咲夜の気力体力がある・または咲夜に主導権がある
;─────────────────────────────────────── 
@CHECK_K9(条件, 咲夜_対象)
#FUNCTION
#DIMS 条件
#DIM 咲夜_対象
#DIM 咲夜

;咲夜_対象は未指定だと主人公
IF !咲夜_対象
	咲夜_対象 = MASTER
ENDIF

咲夜 = NAME_TO_CHARA("咲夜")

;〇敬語
IF 条件 == "敬語"
	;慰安はサービスなので敬語にする
	IF FLAG:調教モード == 調教_慰安
		RETURNF 1
	;咲夜_対象があなたで咲夜が服従系陥落（デイリー「主の主は」発生済みを含む）
	ELSEIF 咲夜_対象 == MASTER && IS_SLAVE(咲夜)
		RETURNF 1
	;咲夜_対象が君主＆咲夜が君主でない＆同盟以上の関係（自勢力を含む）
	ELSEIF GET_COUNTRY_BOSS(CFLAG:咲夜_対象:所属) == 咲夜_対象 && GET_COUNTRY_BOSS(CFLAG:咲夜:所属) != 咲夜 && CHECK_COUNTRY_RELATION_F(CFLAG:咲夜_対象:所属, CFLAG:咲夜:所属) >= 2
		RETURNF 1
	;咲夜_対象がお嬢様orパチュリー様or妹様
	ELSEIF ANAME(咲夜_対象) == "レミリア" || ANAME(咲夜_対象) == "パチュリー" || ANAME(咲夜_対象) == "フラン"
		RETURNF 1
	;それ以外
	ELSE
		RETURNF 0
	ENDIF
ENDIF

;〇面識
IF 条件 == "面識"
	;咲夜_対象があなたで咲夜と面識がある（面識を得るデイリーの発生も含む）
	IF 咲夜_対象 == MASTER && CFLAG:咲夜:面識
		RETURNF 1
	;固定キャラで紅魔館キャラの場合
	ELSEIF ANAME(咲夜_対象) == "レミリア" || ANAME(咲夜_対象) == "パチュリー" || ANAME(咲夜_対象) == "フラン" || ANAME(咲夜_対象) == "小悪魔" || ANAME(咲夜_対象) == "美鈴"
		RETURNF 1
	;固定キャラで咲夜が知人として扱いそうなキャラの場合（特殊な呼び方などのあるキャラ）
	ELSEIF ANAME(咲夜_対象) == "霊夢" || ANAME(咲夜_対象) == "魔理沙" || ANAME(咲夜_対象) == "霖之助"
		RETURNF 1
	;それ以外
	ELSE
		RETURNF 0
	ENDIF
ENDIF

;〇陥落
IF 条件 == "陥落"
	;咲夜_対象があなたで恋慕系か服従系の陥落
	IF 咲夜_対象 == MASTER && ( IS_SLAVE(咲夜) || IS_LOVER(咲夜) )
		RETURNF 1
	ENDIF
ENDIF

;〇紅魔館
IF 条件 == "紅魔館"
	;咲夜_対象が固定キャラで紅魔館キャラの場合（一応咲夜自身も）
	IF ANAME(咲夜_対象) == "レミリア" || ANAME(咲夜_対象) == "パチュリー" || ANAME(咲夜_対象) == "フラン" || ANAME(咲夜_対象) == "小悪魔" || ANAME(咲夜_対象) == "美鈴" || ANAME(咲夜_対象) == "咲夜"
		RETURNF 1
	ENDIF
ENDIF

;〇活力（気力体力1/3以上または主導権持ち）
IF 条件 == "活力"
	IF BASE:咲夜:気力 > (MAXBASE:咲夜:気力 / 3) && BASE:咲夜:体力 > (MAXBASE:咲夜:体力 / 3)
		RETURNF 1
	ENDIF
	IF IS_INITIATIVE(咲夜)
		RETURNF 1
	ENDIF
ENDIF

RETURNF 0


;─────────────────────────────────────── 
;●敬語か常体語かでちょっと分岐
;　使い方：%POLITE_K9("そうよね", "そうですよね")%
;─────────────────────────────────────── 
@POLITE_K9(常体語, 敬語, 咲夜_対象)
#FUNCTIONS
#DIM 咲夜
#DIM 咲夜_対象
#DIMS 常体語
#DIMS 敬語

;咲夜_対象は未指定だと主人公
IF !咲夜_対象
	咲夜_対象 = MASTER
ENDIF

咲夜 = NAME_TO_CHARA("咲夜")

IF CHECK_K9("敬語", 咲夜_対象)
	RETURNF STRFORM(敬語)
ENDIF
RETURNF STRFORM(常体語)


;─────────────────────────────────────── 
;●咲夜の文末を決める
;　使い方：鍵括弧前は%BREAK_K9("末")%　そうでなければ%BREAK_K9("中")%
;　覚書　：文末というか読点・感嘆符・ハート
;　機嫌とか快楽度合いとかコマンド種を見て「。」つけるか「！」や「❤」つけるか決める
;　鍵括弧の直前以外は！や❤の後に全角スペースを入れるため、中か末かの指定がいる
;　促音「っ」をつけたりつけなかったりするので、元テキストの末尾につけてしまうと「っっ」になる
;─────────────────────────────────────── 
@BREAK_K9(位置, 咲夜_対象)
#FUNCTIONS
#DIMS 位置
#DIMS 区切り
#DIMS 表示テキスト
#DIM 咲夜
#DIM 咲夜_対象
#DIMS 咲夜機嫌
#DIM 咲夜_最大快楽部位の値

;咲夜_対象は未指定だと主人公
IF !咲夜_対象
	咲夜_対象 = MASTER
ENDIF

咲夜 = NAME_TO_CHARA("咲夜")
咲夜機嫌 '= TOSTR_EMOTION(咲夜)
咲夜_最大快楽部位の値 = MAX(PALAM:咲夜:快Ｃ, PALAM:咲夜:快Ｖ, PALAM:咲夜:快Ａ, PALAM:咲夜:快Ｂ, PALAM:咲夜:快Ｍ)

表示テキスト '= ""

;▼日常コマンド失敗時
IF TFLAG:18 == -1 || TFLAG:17 == -1
	表示テキスト '= SPLIT_R("……：：：：：")
	IF 位置 == "中"
		表示テキスト '= 表示テキスト + "。"
	ENDIF

;▼不機嫌
ELSEIF GROUPMATCH(咲夜機嫌,"恨","怒","憤","鬱","悲","憂","狂","恐","怯")
	表示テキスト '= SPLIT_R("……：：：：：")
	表示テキスト '= 表示テキスト + SPLIT_R("っ：：：：：")
	IF 位置 == "中"
		表示テキスト '= 表示テキスト + "。"
	ENDIF

;▼気力切れ/酔い限界
ELSEIF TCVAR:咲夜:52 || TCVAR:咲夜:53 == 1
	;咲夜_対象が主人公ではないか、陥落ではない、あるいは捕虜スカ触手系
	IF !CHECK_K9("陥落", 咲夜_対象) || ((SELECTCOM >= 80 && SELECTCOM <= 99) || (SELECTCOM >= 130 && SELECTCOM <= 159) || (SELECTCOM >= 200 && SELECTCOM <= 219))
		表示テキスト '= SPLIT_R("……：：：：：")
		表示テキスト '= 表示テキスト + SPLIT_R("っ：：：：：")
		IF 位置 == "中"
			表示テキスト '= 表示テキスト + "。"
		ENDIF
	;咲夜_対象が主人公かつ陥落、捕虜スカ触手系ではない
	ELSE
		表示テキスト '= SPLIT_R("……：：：：：")
		表示テキスト '= 表示テキスト + SPLIT_R("っ：：：：：")
		IF 位置 == "中"
			表示テキスト '= 表示テキスト + SPLIT_R("。：❤　")
		ELSE
			表示テキスト '= 表示テキスト + SPLIT_R("：❤")
		ENDIF
	ENDIF

;▼体力切れ 
ELSEIF TCVAR:咲夜:51 || (BASE:咲夜:体力 <= (MAXBASE:咲夜:体力 / 3))
	IF 咲夜_最大快楽部位の値 >= 9000
		表示テキスト '= SPLIT_R("……：")
		表示テキスト '= 表示テキスト + SPLIT_R("っ：")
		IF 位置 == "中"
			表示テキスト '= 表示テキスト + "。"
		ENDIF
	ELSE
		表示テキスト '= SPLIT_R("……：")
		表示テキスト '= 表示テキスト + SPLIT_R("っ：：：：：")
		IF 位置 == "中"
			表示テキスト '= 表示テキスト + "。"
		ENDIF
	ENDIF

;▼閨で咲夜が受け
;　受けかどうかはNIGHTのカテゴリ分岐で保存される
ELSEIF FLAG:ウフフフラグ && 咲夜_咲夜が受け
	;▽～1500　あっ……ん……
	IF 咲夜_最大快楽部位の値 < 1500
		;咲夜_対象が主人公ではないか、陥落ではない、あるいは捕虜スカ触手系
		IF !CHECK_K9("陥落", 咲夜_対象) || ((SELECTCOM >= 80 && SELECTCOM <= 99) || (SELECTCOM >= 130 && SELECTCOM <= 159) || (SELECTCOM >= 200 && SELECTCOM <= 219))
			表示テキスト '= SPLIT_R("……：")
			表示テキスト '= 表示テキスト + SPLIT_R("っ：：：：：")
			IF 位置 == "中"
				表示テキスト '= 表示テキスト + "。"
			ENDIF
		;咲夜_対象が主人公かつ陥落、捕虜スカ触手系ではない
		ELSE
			表示テキスト '= SPLIT_R("……：")
			表示テキスト '= 表示テキスト + SPLIT_R("っ：：：：：")
			IF 位置 == "中"
				表示テキスト '= 表示テキスト + SPLIT_R("。：❤　")
			ELSE
				表示テキスト '= 表示テキスト + SPLIT_R("：❤")
			ENDIF
		ENDIF

	;▽1500～5000　ああっ、う、あ❤　……っ、ん
	ELSEIF 咲夜_最大快楽部位の値 >= 1500 && 咲夜_最大快楽部位の値 < 5000
		;咲夜_対象が主人公ではないか、陥落ではない、あるいは捕虜スカ触手系
		IF !CHECK_K9("陥落", 咲夜_対象) || ((SELECTCOM >= 80 && SELECTCOM <= 99) || (SELECTCOM >= 130 && SELECTCOM <= 159) || (SELECTCOM >= 200 && SELECTCOM <= 219))
			表示テキスト '= SPLIT_R("……：：：")
			表示テキスト '= 表示テキスト + SPLIT_R("っ：：：：")
			IF 位置 == "中"
				表示テキスト '= 表示テキスト + "。"
			ENDIF
		;咲夜_対象が主人公かつ陥落、捕虜スカ触手系ではない
		ELSE
			表示テキスト '= SPLIT_R("……：：：")
			表示テキスト '= 表示テキスト + SPLIT_R("っ：：：：")
			IF 位置 == "中"
				表示テキスト '= 表示テキスト + SPLIT_R("。：❤　")
			ELSE
				表示テキスト '= 表示テキスト + SPLIT_R("：❤")
			ENDIF
		ENDIF

	;▽5000～9000　あっ。だめっ！　も、もうっ。はあぁぁん❤
	ELSEIF 咲夜_最大快楽部位の値 >= 5000 && 咲夜_最大快楽部位の値 < 9000
		;咲夜_対象が主人公ではないか、陥落ではない、あるいは捕虜スカ触手系
		IF !CHECK_K9("陥落", 咲夜_対象) || ((SELECTCOM >= 80 && SELECTCOM <= 99) || (SELECTCOM >= 130 && SELECTCOM <= 159) || (SELECTCOM >= 200 && SELECTCOM <= 219))
			表示テキスト '= SPLIT_R("っ：：：")
			IF 位置 == "中"
				表示テキスト '= 表示テキスト + SPLIT_R("。：。：。：！　")
			ELSE
				表示テキスト '= 表示テキスト + SPLIT_R("：：：！")
			ENDIF
		;咲夜_対象が主人公かつ陥落、捕虜スカ触手系ではない
		ELSE
			表示テキスト '= SPLIT_R("っ：")
			IF 位置 == "中"
				表示テキスト '= 表示テキスト + SPLIT_R("。：。：。：！　：❤　：❤　")
			ELSE
				表示テキスト '= 表示テキスト + SPLIT_R("：：：！：❤：❤")
			ENDIF
		ENDIF

	;▽9000～　いくうううう❤　いっちゃうう～っ！
	ELSE
		;咲夜_対象が主人公ではないか、陥落ではない、あるいは捕虜スカ触手系
		IF !CHECK_K9("陥落", 咲夜_対象) || ((SELECTCOM >= 80 && SELECTCOM <= 99) || (SELECTCOM >= 130 && SELECTCOM <= 159) || (SELECTCOM >= 200 && SELECTCOM <= 219))
			表示テキスト '= SPLIT_R("――：～：ー：……：：：：：：：：：：：：：：：")
			表示テキスト '= 表示テキスト + SPLIT_R("っ：っ：っ：")
			IF 位置 == "中"
				表示テキスト '= 表示テキスト + SPLIT_R("。：！　")
			ELSE
				表示テキスト '= 表示テキスト + SPLIT_R("：！")
			ENDIF
		;咲夜_対象が主人公かつ陥落、捕虜スカ触手系ではない
		ELSE
			表示テキスト '= SPLIT_R("――：～：ー：……：：：：：：：：：：：：：：：")
			表示テキスト '= 表示テキスト + SPLIT_R("っ：っ：っ：")
			IF 位置 == "中"
				表示テキスト '= 表示テキスト + SPLIT_R("。：！　：！　：❤　：❤　：❤　")
			ELSE
				表示テキスト '= 表示テキスト + SPLIT_R("：！：！：❤：❤：❤")
			ENDIF
		ENDIF
	ENDIF

;▼閨で咲夜が攻め
ELSEIF FLAG:ウフフフラグ
	;▽～5000
	IF 咲夜_最大快楽部位の値 < 5000
		;咲夜_対象が主人公ではないか、陥落ではない、あるいは捕虜スカ触手系
		IF !CHECK_K9("陥落", 咲夜_対象) || ((SELECTCOM >= 80 && SELECTCOM <= 99) || (SELECTCOM >= 130 && SELECTCOM <= 159) || (SELECTCOM >= 200 && SELECTCOM <= 219))
			表示テキスト '= SPLIT_R("……：……っ：：：：：：：")
			IF 位置 == "中"
				表示テキスト '= 表示テキスト + "。"
			ENDIF
		;咲夜_対象が主人公かつ陥落、捕虜スカ触手系ではない
		ELSE
			表示テキスト '= SPLIT_R("……：：：：：")
			IF 位置 == "中"
				表示テキスト '= 表示テキスト + SPLIT_R("。：❤　")
			ELSE
				表示テキスト '= 表示テキスト + SPLIT_R("：❤")
			ENDIF
		ENDIF

	;▽5000～9000
	ELSEIF 咲夜_最大快楽部位の値 >= 5000 && 咲夜_最大快楽部位の値 < 9000
		;咲夜_対象が主人公ではないか、陥落ではない、あるいは捕虜スカ触手系
		IF !CHECK_K9("陥落", 咲夜_対象) || ((SELECTCOM >= 80 && SELECTCOM <= 99) || (SELECTCOM >= 130 && SELECTCOM <= 159) || (SELECTCOM >= 200 && SELECTCOM <= 219))
			表示テキスト '= SPLIT_R("……っ：っ：：：：：：：：")
			IF 位置 == "中"
				表示テキスト '= "。"
			ENDIF
		;咲夜_対象が主人公かつ陥落、捕虜スカ触手系ではない
		ELSE
			表示テキスト '= SPLIT_R("っ：：：：：")
			IF 位置 == "中"
				表示テキスト '= SPLIT_R("。：❤　")
			ELSE
				表示テキスト '= SPLIT_R("：❤")
			ENDIF
		ENDIF

	;▽9000～
	ELSE
		;咲夜_対象が主人公ではないか、陥落ではない、あるいは捕虜スカ触手系
		IF !CHECK_K9("陥落", 咲夜_対象) || ((SELECTCOM >= 80 && SELECTCOM <= 99) || (SELECTCOM >= 130 && SELECTCOM <= 159) || (SELECTCOM >= 200 && SELECTCOM <= 219))
			表示テキスト '= SPLIT_R("っ：：：：：")
			IF 位置 == "中"
				表示テキスト '= 表示テキスト + SPLIT_R("。：。：。：。：。：！")
			ELSE
				表示テキスト '= 表示テキスト + SPLIT_R("：：：：：！")
			ENDIF
		;咲夜_対象が主人公かつ陥落、捕虜スカ触手系ではない
		ELSE
			表示テキスト '= SPLIT_R("っ：：：：：")
			IF 位置 == "中"
				表示テキスト '= 表示テキスト + SPLIT_R("。：。：。：！：❤　：❤　：❤　")
			ELSE
				表示テキスト '= 表示テキスト + SPLIT_R("：：：！：❤：❤：❤")
			ENDIF
		ENDIF
	ENDIF

;▼日常
ELSE
	;咲夜_対象が主人公ではないか、陥落ではない
	IF !CHECK_K9("陥落", 咲夜_対象)
		表示テキスト '= SPLIT_R("……：：：：：")
		IF 位置 == "中"
			表示テキスト '= 表示テキスト + "。"
		ENDIF
	;咲夜_対象が主人公かつ陥落
	ELSE
		表示テキスト '= SPLIT_R("……：：：：：")
		IF 位置 == "中"
			表示テキスト '= 表示テキスト + SPLIT_R("。：❤　")
		ELSE
			表示テキスト '= 表示テキスト + SPLIT_R("：❤")
		ENDIF
	ENDIF
ENDIF

RETURNF 表示テキスト

;─────────────────────────────────────── 
;●咲夜の淫語
;　使い方：%CALL_DIRTY_K9("Ｐ", "ぁ")%
;　覚書　：第二引数に"ぁ"をつけるとプレイヤーの入力した淫語に
;　小書き母音をつけて甘えボイス感を出す
;─────────────────────────────────────── 
@CALL_DIRTY_K9(部位, 小書き)
#FUNCTIONS
#DIMS 部位
#DIMS 小書き
#DIMS 表示テキスト

表示テキスト '= ""

;ロードデータ用初期化
IF 咲夜_淫語Ｐ == ""
	咲夜_淫語Ｐ '= "あれ"
	咲夜_淫語Ｖ '= "なか"
	咲夜_淫語Ｃ '= "そこ"
	咲夜_淫語Ａ '= "そこ"
	咲夜_淫語Ｂ '= "そこ"
ENDIF

IF 小書き == "ぁ"
	IF 部位 == "Ｐ"
		IF 咲夜_淫語頻度 != 2
			SELECTCASE RAND:3
				CASE 1
					表示テキスト '= 咲夜_淫語Ｐ + SMALLSCRIPT(咲夜_淫語Ｐ)
				CASEELSE
					表示テキスト '= "あれぇ"
			ENDSELECT
		ELSE
			表示テキスト '= 咲夜_淫語Ｐ + SMALLSCRIPT(咲夜_淫語Ｐ)
		ENDIF
	ELSEIF 部位 == "Ｖ"
		IF 咲夜_淫語頻度 != 2
			SELECTCASE RAND:3
				CASE 1
					表示テキスト '= 咲夜_淫語Ｖ + SMALLSCRIPT(咲夜_淫語Ｖ)
				CASEELSE
					表示テキスト '= "なかぁ"
			ENDSELECT
		ELSE
			表示テキスト '= 咲夜_淫語Ｖ + SMALLSCRIPT(咲夜_淫語Ｖ)
		ENDIF
	ELSEIF 部位 == "Ｃ"
		IF 咲夜_淫語頻度 != 2
			SELECTCASE RAND:3
				CASE 1
					表示テキスト '= 咲夜_淫語Ｃ + SMALLSCRIPT(咲夜_淫語Ｃ)
				CASEELSE
					表示テキスト '= "そこぉ"
			ENDSELECT
		ELSE
			表示テキスト '= 咲夜_淫語Ｃ + SMALLSCRIPT(咲夜_淫語Ｃ)
		ENDIF
	ELSEIF 部位 == "Ａ"
		IF 咲夜_淫語頻度 != 2
			SELECTCASE RAND:3
				CASE 1
					表示テキスト '= 咲夜_淫語Ａ + SMALLSCRIPT(咲夜_淫語Ｖ)
				CASEELSE
					表示テキスト '= "そこぉ"
			ENDSELECT
		ELSE
			表示テキスト '= 咲夜_淫語Ａ + SMALLSCRIPT(咲夜_淫語Ａ)
		ENDIF
	ELSEIF 部位 == "Ｂ"
		IF 咲夜_淫語頻度 != 2
			SELECTCASE RAND:3
				CASE 1
					表示テキスト '= 咲夜_淫語Ｂ + SMALLSCRIPT(咲夜_淫語Ｂ)
				CASEELSE
					表示テキスト '= "そこぉ"
			ENDSELECT
		ELSE
			表示テキスト '= 咲夜_淫語Ｂ + SMALLSCRIPT(咲夜_淫語Ｂ)
		ENDIF
	ENDIF
ELSE
	IF 部位 == "Ｐ"
		IF 咲夜_淫語頻度 != 2
			SELECTCASE RAND:3
				CASE 1
					表示テキスト '= 咲夜_淫語Ｐ
				CASEELSE
					表示テキスト '= "それ"
			ENDSELECT
		ELSE
			表示テキスト '= 咲夜_淫語Ｐ
		ENDIF
	ELSEIF 部位 == "Ｖ"
		IF 咲夜_淫語頻度 != 2
			SELECTCASE RAND:3
				CASE 1
					表示テキスト '= 咲夜_淫語Ｖ
				CASEELSE
					表示テキスト '= "なか"
			ENDSELECT
		ELSE
			表示テキスト '= 咲夜_淫語Ｖ
		ENDIF
	ELSEIF 部位 == "Ｃ"
		IF 咲夜_淫語頻度 != 2
			SELECTCASE RAND:3
				CASE 1
					表示テキスト '= 咲夜_淫語Ｃ
				CASEELSE
					表示テキスト '= "そこ"
			ENDSELECT
		ELSE
			表示テキスト '= 咲夜_淫語Ｃ
		ENDIF
	ELSEIF 部位 == "Ａ"
		IF 咲夜_淫語頻度 != 2
			SELECTCASE RAND:3
				CASE 1
					表示テキスト '= 咲夜_淫語Ａ
				CASEELSE
					表示テキスト '= "そこ"
			ENDSELECT
		ELSE
			表示テキスト '= 咲夜_淫語Ａ
		ENDIF
	ELSEIF 部位 == "Ｂ"
		IF 咲夜_淫語頻度 != 2
			SELECTCASE RAND:3
				CASE 1
					表示テキスト '= 咲夜_淫語Ｂ
				CASEELSE
					表示テキスト '= "そこ"
			ENDSELECT
		ELSE
			表示テキスト '= 咲夜_淫語Ｂ
		ENDIF
	ENDIF
ENDIF

RETURNF 表示テキスト

;─────────────────────────────────────── 
;●咲夜の既成事実ごとのおもてなし　%CAKE_K9(MARK:咲夜:既成事実)%
;　生菓子は控え目で焼き菓子中心、中世～近代西洋イメージ
;　レミリアが紅茶好きなので英国系そうだが吸血鬼のイメージは東側らしいので曖昧
;─────────────────────────────────────── 
@CAKE_K9(ARG:0)
#FUNCTIONS
#DIMS 菓子
菓子 = 
;DATAFORM文にしてみたら元テキストのカラーが変わってしまうようなのでそのまま
IF ARG:0 < 1
	;まだあまり準備していない
	SELECTCASE RAND:4
		CASE 1
			菓子 = ワッフル
		CASE 2
			菓子 = ファッジ
		CASE 3
			菓子 = ショートブレッド
		CASEELSE
			菓子 = プディング
	ENDSELECT
ELSEIF ARG:0 < 2
	;たまに準備するようになってきた
	SELECTCASE RAND:11
		CASE 1
			菓子 = ワッフル
		CASE 2
			菓子 = ファッジ
		CASE 3
			菓子 = ショートブレッド
		CASE 4
			菓子 = プディング
		CASE 5
			菓子 = タフィー
		CASE 6
			菓子 = マカロン
		CASE 7
			菓子 = チョコレート
		CASE 8
			菓子 = ガレット
		CASE 9
			菓子 = コンフィズリー
		CASE 10
			菓子 = ワッフル
		CASEELSE
			菓子 = フィナンシェ
	ENDSELECT
ELSE
	;時を止めて作ってでも
	SELECTCASE RAND:27
		CASE 1
			菓子 = ワッフル
		CASE 2
			菓子 = ファッジ
		CASE 3
			菓子 = ショートブレッド
		CASE 4
			菓子 = プディング
		CASE 5
			菓子 = タフィー
		CASE 6
			菓子 = マカロン
		CASE 7
			菓子 = チョコレート
		CASE 8
			菓子 = ガレット
		CASE 9
			菓子 = コンフィズリー
		CASE 10
			菓子 = ワッフル
		CASE 11
			菓子 = フィナンシェ
		CASE 12
			菓子 = アップルクランブル
		CASE 13
			菓子 = ミンスパイ
		CASE 14
			菓子 = フルーツタルト
		CASE 15
			菓子 = ガトーショコラ
		CASE 16
			菓子 = チーズケーキ
		CASE 17
			菓子 = バターケーキ
		CASE 18
			菓子 = チェリーパイ
		CASE 19
			菓子 = パウンドケーキ
		CASE 20
			菓子 = クッキー
		CASE 21
			菓子 = フルーツタルト
		CASE 22
			菓子 = マロングラッセ
		CASE 23
			菓子 = ダックワーズ
		CASE 24
			菓子 = ガトーショコラ
		CASE 25
			菓子 = シャーベット
		CASE 26
			菓子 = ビスキュイ
		CASEELSE
			菓子 = マドレーヌ
	ENDSELECT
ENDIF
RETURNF 菓子


