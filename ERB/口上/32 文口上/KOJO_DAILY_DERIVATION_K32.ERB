@KOJO_DAILY_DERIVATION_K32()

;水遊びの派生
DAILY_NAME = お見舞い
CALL KOJO_DAILY_ERIVATION_K32_MIZUASOBI()


;----------------------------------
;水遊びの派生イベント
;----------------------------------
@KOJO_DAILY_ERIVATION_K32_MIZUASOBI()
#DIM 文
文 = NAME_TO_CHARA("文")

;文が同じ勢力で、捕虜でない
SIF !(CHECK_KOJO_DAILY_HAPPEN(文, 1000, 1, 0, 1))
	RETURN -1

;文のデイリー「水遊び」で遊んでいない、またはイベント終了済みなら駄目
SIF KDVAR:文:文_水遊び != 1
	RETURN -1

CALL KOJO_DAILY_START()

PRINTFORML %ANAME(MASTER)%が寝込んでいると、部屋の扉がノックされた
PRINTFORMW 入ってきたのは%ANAME(文)%だった
PRINTFORMW 「あー……%ANAME(MASTER)%、具合はどうですか？」
PRINTFORMW 「その、すみません、私が無理言って誘ったせいで風邪なんて」
PRINTFORML
CALL ASK_YN("ずいぶんしおらしいな", @"%ANAME(文)%のせいではない")
SELECTCASE RESULT
	CASE 0
		CFLAG:文:好感度 += 100
		PRINTFORML %ANAME(文)%にしてはずいぶんしおらしい態度じゃないか
		PRINTFORMW そういってからかうと、%ANAME(文)%は口を尖らせた
		PRINTFORMW 「失敬な。私にも申し訳ないと思う気持ちくらいありますよ」
		PRINTFORMW 「まぁ、軽口がたたけるだけの元気はあるってことですかね」
		PRINTFORMW 「それより、お腹は空いていませんか？　お粥を作ってきてあげましたよ。栄養重視なので、味は保証しませんが」
	CASE 1
		PRINTFORMW 「そう言ってもらえると、ありがたいですね」
		PRINTFORML 口ではそう言う%ANAME(文)%だったが、その表情はどこか曇っていた
		PRINTFORMW 「あの、お腹は空いていませんか？　お粥を作ってきました。栄養重視なので、味は保証しませんが」
ENDSELECT
PRINTFORML
CALL ASK_YN("ありがたく受け取る", "腹は減っていない")
SELECTCASE RESULT
	CASE 0
		PRINTFORML ありがたい。ちょうど腹が減っていたところだった
		PRINTFORML %ANAME(文)%によそってもらい、粥を口に運んだ
		PRINTFORMW 「どうでしょうか……？」
		PRINTFORML
		CALL ASK_YN("うまい", "まずい")
		SELECTCASE RESULT
			CASE 0
				PRINTFORML 味は保証しないと言っていたが、正直、美味い
				PRINTFORML そう伝えると、%ANAME(文)%の表情はずいぶん明るくなった
				PRINTFORMW 「ふふ、そうでしょうそうでしょう、この私が作ったんですからね」
				PRINTFORMW 「ささ、食べるものは食べたんですから、ゆっくり寝て英気を養ってくださいな」
				PRINTFORML %ANAME(文)%に無理矢理に寝かしつけられた……
				CALL COLORPRINT("お粥のおかげで、風邪が完治しました", カラー_注意, "W")
				CALL COLORPRINT("クールタイムが0になりました", カラー_注意, "W")
				CFLAG:文:好感度 += 300
				COOLTIME:MASTER:0 = 0
			CASE 1
				PRINTFORML 作ってもらってなんだが、正直、まずい
				PRINTFORML そう伝えると、%ANAME(文)%は顔を真っ赤にした
				PRINTFORMW 「むむむ……！　人がせっかく心配して作ってあげたというのに、なんという言い方ですか！」
				PRINTFORMW 「見てなさいよ、絶対に%ANAME(MASTER)%を唸らせる粥を作ってきてあげますからね！」
				PRINTFORML ぷんすかと怒りながら、%ANAME(文)%は部屋を出て行った……
				CALL COLORPRINT(@"%ANAME(文)%が料理の特訓をしたようです", カラー_注意, "W")
				CALL COLORPRINT(@"%ANAME(文)%の料理が5成長しました", カラー_注意, "W")
				CFLAG:文:好感度 -= 300
				ABL:文:料理 += 5
		ENDSELECT
	CASE 1
		PRINTFORMW 「む、そうですか……」
		PRINTFORMW 「それは仕方ないですね。では、ゆっくり寝て英気を養ってくださいな」
		PRINTFORMW %ANAME(文)%は部屋を後にした……
ENDSELECT
KDVAR:文:文_水遊び = 2
