@KOJO_DAILY_EXIST_K51()

@KOJO_DAILY_K51()
#DIM 発生フラグ
発生フラグ = 0

;たずねびと
DAILY_NAME = たずねびと
CALL KOJO_DAILY_K51_WHERE_IS_TENSHI
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1
RETURN 発生フラグ
;------------------------------------
;おうちかえして
;-----------------------------------
@KOJO_DAILY_K51_WHERE_IS_TENSHI
#DIM 衣玖
#DIM 天子
衣玖 = NAME_TO_CHARA("衣玖")
天子 = NAME_TO_CHARA("天子")

;衣玖が同勢力、捕虜でなく面識ありで50%
SIF !(CHECK_KOJO_DAILY_HAPPEN(衣玖, 1000, 1, 0, 1))
	RETURN -1

;天子が別勢力
SIF !(CHECK_KOJO_DAILY_HAPPEN(天子, 1000, 0))
	RETURN -1

;発生済みなら駄目
SIF KDVAR:衣玖:衣玖_たずねびと
	RETURN -1

;MASTERが天子なら駄目
SIF MASTER == 天子
	RETURN -1

;天子が死んでたら駄目
SIF CFLAG:天子:特殊状態 == 2
	RETURN -1

CALL KOJO_DAILY_START()
PRINTFORML 部屋で仕事をしていると、%ANAME(衣玖)%が訪ねてきた
PRINTFORMW 「%ANAME(MASTER)%さん、実はお願いがありまして」
PRINTFORM 「私が目付役をしている、総領娘様……
CALL COLORPRINT(NAME_FORMAL(天子), COLOR("注意"))
PRINTFORMW のことですが」
PRINTFORMW 「%PRONOUN(天子)%を探し、私の目の届くところに確保していただきたいのです」
PRINTFORML 確保とはずいぶん物々しい言い方だ
PRINTFORML わけを問うと、%ANAME(衣玖)%は言葉を濁した
PRINTFORML 「その……なんといいますか、総領娘様は非常に奔放な方でして……」
PRINTFORMW 「%PRONOUN(天子)%を野放しにしておくのは……トラブルの元になるといいますか」
IF CFLAG:天子:面識
	PRINTFORMW 確かに、%PRONOUN(天子)%の性格を考えると、それは明らかなことだ……
ELSE
	PRINTFORMW %PRONOUN(天子)%と面識はないが、%ANAME(衣玖)%がここまで言うからにはそうなのだろう……
ENDIF
PRINTFORMW 「もちろん、作戦などもあるでしょうし、無理にとは言えません」
PRINTFORMW 「ですが、どうか考えていただけないでしょうか？」
PRINTFORML 
CALL ASK_YN("わかった", "無理だ")
SELECTCASE RESULT
	CASE 0
		PRINTFORMW 「ありがとうございます。あちこちに頭を下げにいかずに済みそうです」
		PRINTFORMW 「では、すみませんがよろしくお願いします。私も可能な限り協力しますから」
		PRINTFORML 恭しく礼をし、%ANAME(衣玖)%は立ち去った……
		KDVAR:衣玖:衣玖_たずねびと = 1
	CASE 1
		PRINTFORMW 「そうですか……」
		PRINTFORMW 「仕方がありませんね。総領娘様のことは自分で探すことにします。お忙しいところ失礼しました」
		PRINTFORML %ANAME(衣玖)%は立ち去った……
		KDVAR:衣玖:衣玖_たずねびと = 2
ENDSELECT
