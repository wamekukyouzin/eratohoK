@EVENT_KOJO_DAILY()
#DIM EVENT_NUM
#DIMS NAME_ENG
#DIMS NAME_JAP
#DIM EVENT_COUNT
#DIM EVENT_CHARA, VARSIZE("SHAFFLE_ARRAY")
#DIM EVENT_ARRAY, VARSIZE("SHAFFLE_ARRAY")
#DIM 対象
#DIM 対象NO

SIF !IS_DAILY_EVENT_HAPPEN(MASTER)
	RETURN 0

SIF FLAG:観戦モード
	RETURN 0

SIF DAILY_CANCEL
	RETURN 0

CALL KOJO_DAILY_DERIVATION()

DAILY_CANCEL = 0
EVENT_COUNT = KOJO_DAILY_EVENT_COUNT


;口上用デイリー MASTERのものは発動しない
;順番決定
CALL FISHER_YATES_SHAFFLE(CHARANUM)

ARRAYCOPY "SHAFFLE_ARRAY", "EVENT_CHARA"

FOR LOCAL, 0, CHARANUM
	対象 = EVENT_CHARA:LOCAL
	対象NO = NO:対象
	SIF !IS_TOHO_CHARA(対象)
		CONTINUE

	SIF KOJO_DAILY_EVENT_NUM:対象NO:デイリー_通常 == 0
		CONTINUE

	EVENT_NUM = KOJO_DAILY_EVENT_NUM:対象NO:デイリー_通常

	CALL FISHER_YATES_SHAFFLE(EVENT_NUM)
	ARRAYCOPY "SHAFFLE_ARRAY", "EVENT_ARRAY"

	FOR LOCAL:1, 0, EVENT_NUM
		SIF EVENT_COUNT == 0
			BREAK
		NAME_ENG '= KOJO_DAILY_EVENT_NAME_ENG:対象NO:デイリー_通常:(EVENT_ARRAY:(LOCAL:1))
		NAME_JAP '= KOJO_DAILY_EVENT_NAME:対象NO:デイリー_通常:(EVENT_ARRAY:(LOCAL:1))

		SIF KOJO_DAILY_GET_DISABLE_CONFIG(対象, NAME_ENG)
			CONTINUE

		TRYCCALLFORM KOJO_DAILY_K{対象NO}_%NAME_ENG%_RATE(対象)
			SIF RESULT * KOJO_DAILY_EVENT_ODDS <= RAND:1000
				CONTINUE

			;放浪中の拠点フェイズデイリーは特殊な関数が必要
			IF !CFLAG:MASTER:所属
				RESULT = 0
				TRYCALLFORM KOJO_DAILY_K{対象NO}_%NAME_ENG%_ALLOW_WHEN_WANDERING()
				SIF RESULT == 0
					CONTINUE
			ENDIF

			RESULT = 1
			TRYCALLFORM KOJO_DAILY_K{対象NO}_%NAME_ENG%_DECISION(対象)
			SIF RESULT == 0
				CONTINUE

			VARSET DAILY_TARGET, -1
			VARSET DAILY_TARGET_NUM
			RESULT = 1
			TRYCALLFORM KOJO_DAILY_K{対象NO}_%NAME_ENG%_SETTARGET(対象)
			SIF RESULT == 0
				CONTINUE

			RESULTS = 
			TRYCALLFORM KOJO_DAILY_K{対象NO}_%NAME_ENG%_NAME(対象)
			IF RESULTS == ""
				CALL KOJO_DAILY_START(NAME_JAP, 対象)
			ELSE
				CALL KOJO_DAILY_START(RESULTS, 対象)
			ENDIF
			CALLFORM KOJO_DAILY_K{対象NO}_%NAME_ENG%(対象)

			IF RESULT == 1
				EVENT_COUNT --
			ENDIF
			SIF DAILY_CANCEL
				RETURN
		CATCH
;DEBUGPRINTFORML KOJO_DAILY_{対象NO}_%NAME_ENG%_RATEみつからず
		ENDCATCH
	NEXT
NEXT



;共通デイリーイベント：派生処理
@KOJO_DAILY_DERIVATION()
#DIM EVENT_NUM
#DIMS NAME_ENG
#DIMS NAME_JAP
#DIM EVENT_CHARA, VARSIZE("SHAFFLE_ARRAY")
#DIM EVENT_ARRAY, VARSIZE("SHAFFLE_ARRAY")
#DIM 対象
#DIM 対象NO

SIF !IS_DAILY_EVENT_HAPPEN(MASTER)
	RETURN 0

SIF FLAG:観戦モード
	RETURN 0

SIF DAILY_CANCEL
	RETURN 0


DAILY_CANCEL = 0

;口上用デイリー MASTERのものは発動しない
;順番決定
CALL FISHER_YATES_SHAFFLE(CHARANUM)

ARRAYCOPY "SHAFFLE_ARRAY", "EVENT_CHARA"

FOR LOCAL, 0, CHARANUM
	対象 = EVENT_CHARA:LOCAL
	対象NO = NO:対象
	SIF !IS_TOHO_CHARA(対象)
		CONTINUE

	EVENT_NUM = KOJO_DAILY_EVENT_NUM:対象NO:デイリー_通常派生

	FOR LOCAL:1, 0, EVENT_NUM
		NAME_ENG '= KOJO_DAILY_EVENT_NAME_ENG:対象NO:デイリー_通常派生:(LOCAL:1)
		NAME_JAP '= KOJO_DAILY_EVENT_NAME:対象NO:デイリー_通常派生:(LOCAL:1	)

		RESULT = 0
		TRYCCALLFORM KOJO_DAILY_K{対象NO}_DERIVATION_%NAME_ENG%_DISABLE(対象)
			SIF RESULT == 1
				CONTINUE

			;放浪中の拠点フェイズデイリーは特殊な関数が必要
			IF !CFLAG:MASTER:所属
				RESULT = 0
				TRYCALLFORM KOJO_DAILY_K{対象NO}_DERIVATION_%NAME_ENG%_ALLOW_WHEN_WANDERING()
				SIF RESULT == 0
					CONTINUE
			ENDIF


			TRYCALLFORM KOJO_DAILY_K{対象NO}_DERIVATION_%NAME_ENG%_DECISION(対象)
			SIF RESULT == 0
				CONTINUE

			VARSET DAILY_TARGET, -1
			VARSET DAILY_TARGET_NUM
			RESULT = 1
			TRYCALLFORM KOJO_DAILY_K{対象NO}_DERIVATION_%NAME_ENG%_SETTARGET(対象)
			SIF RESULT == 0
				CONTINUE

			RESULTS = 
			TRYCALLFORM KOJO_DAILY_K{対象NO}_DERIVATION_%NAME_ENG%_NAME(対象)
			IF RESULTS == ""
				CALL KOJO_DAILY_START_DERIVATION(NAME_JAP, 対象)
			ELSE
				CALL KOJO_DAILY_START_DERIVATION(RESULTS, 対象)
			ENDIF

			CALLFORM KOJO_DAILY_K{対象NO}_DERIVATION_%NAME_ENG%(対象)
			SIF DAILY_CANCEL
				RETURN
		CATCH
;DEBUGPRINTFORML KOJO_DAILY_K{対象NO}_DERIVATION_%NAME_ENG%_DECISIONみつからず
		ENDCATCH
	NEXT
NEXT
