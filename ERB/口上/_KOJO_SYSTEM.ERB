;口上に関する処理

;--------------------------------------------------
;キャラARG:0の口上ナンバーを返す
;東方キャラ……NOがそのまま返る
;汎用キャラ……口上パターン番号が返る
;--------------------------------------------------
@KOJO_NO(ARG:0)
#FUNCTION
IF IS_TOHO_CHARA(ARG:0) || IS_SP_CHARA(ARG:0)
	RETURNF NO:(ARG:0)
ENDIF
IF CFLAG:(ARG:0):汎用口上パターン >= 1000 && CFLAG:(ARG:0):汎用口上パターン <= 1999
	RETURNF CFLAG:(ARG:0):汎用口上パターン
ENDIF
RETURNF 999

;--------------------------------------------------
;状況に応じた付加文字列を返す
;ARG:0に1を設定すると、分岐が細かくなる
;--------------------------------------------------
@KOJO_STATE(ARG:0 = 0)
#FUNCTIONS
IF ARG:0
	;会いに行く
	IF FLAG:調教モード == 0
		RETURNF "A1"
	;閨に呼ぶ
	ELSEIF FLAG:調教モード == 1
		RETURNF "A2"
	;子育て
	ELSEIF FLAG:調教モード == 3
		RETURNF "A3"
	;捕虜会話
	ELSEIF FLAG:調教モード == 6
		RETURNF "A4"
	;捕虜の調教
	ELSEIF FLAG:調教モード == 2
		RETURNF "B"
	;捕虜逆調教(通常)
	ELSEIF FLAG:調教モード == 5
		RETURNF "C"
	;捕虜逆調教(外来)
	ELSEIF FLAG:調教モード == 4
		RETURNF "D"
	ENDIF
ELSE
	;会いに行く・閨に呼ぶ・子育て・捕虜会話
	IF GROUPMATCH(FLAG:調教モード, 0, 1, 3, 6)
		RETURNF "A"
	;捕虜の調教
	ELSEIF FLAG:調教モード == 2
		RETURNF "B"
	;捕虜逆調教(通常)
	ELSEIF FLAG:調教モード == 5
		RETURNF "C"
	;捕虜逆調教(外来)
	ELSEIF FLAG:調教モード == 4
		RETURNF "D"
	ENDIF
ENDIF

RETURNF "X"

;--------------------------------------------------
;口上の存在判定  口上が存在すれば1を、存在しなければ0を返す
;ARG:0にはキャラのNOを指定する
;--------------------------------------------------
@IS_KOJO(ARG:0)
;イベントキャラは口上を表示しない
IF ARG:0 >= 2000
	RETURN 0
ENDIF

;口上を表示しない設定なら常に0を返す
IF (ARG:0 <= 100 && CONFIG:40 == 1) || (ARG:0 >= 101 && CONFIG:41 == 1)
	RETURN 0
ENDIF

TRYCCALLFORM KOJO_EXIST_K{ARG:0}
	RETURN 1
CATCH
ENDCATCH
RETURN 0

;--------------------------------------------------
;調教開始時の口上
;--------------------------------------------------
@EVENTTRAIN
;現在のターゲットを記録
LOCAL:0 = TARGET

;相手が一人の場合のみ口上呼び出し 捕虜調教の場合は人数制限を無視してメインターゲットの口上を表示 逆調教,特殊勢力逆調教の場合は全てじょうじ
;IF TFLAG:44 == 1 || GROUPMATCH(FLAG:調教モード,2,4,5)
FOR LOCAL:0, 0, CHARANUM
		;調教参加フラグが立っていれば口上呼び出し(捕虜調教の場合はメインターゲットのみ)
	IF CFLAG:(LOCAL:0):調教参加フラグ && (FLAG:調教モード != 2 || FINDELEMENT(PRISONER_TARGET, LOCAL:0) != -1)
		TARGET = LOCAL:0
		LOCAL:5 = KOJO_NO(TARGET)

		CALL IS_KOJO(LOCAL:5)
		IF RESULT
			TRYCALLFORM KOJO_TRAIN_START_K{LOCAL:5}
			TRYCALLFORM KOJO_TRAIN_START_%KOJO_STATE(1)%_K{LOCAL:5}
		ENDIF
	ENDIF
NEXT
;ENDIF

;ターゲットを戻す
TARGET = LOCAL:0

;--------------------------------------------------
;調教終了時の口上
;--------------------------------------------------
@EVENTEND
;現在のターゲットを記録
LOCAL:0 = TARGET

;相手が一人の場合のみ口上呼び出し 捕虜調教の場合は人数制限を無視してメインターゲットの口上を表示 逆調教,特殊勢力逆調教の場合は全てじょうじ
IF TFLAG:44 == 1 || GROUPMATCH(FLAG:調教モード,2,4,5)
	FOR LOCAL:0, 0, CHARANUM
		;調教参加フラグが立っていれば口上呼び出し(捕虜調教の場合はメインターゲットのみ)
		IF CFLAG:(LOCAL:0):調教参加フラグ && (FLAG:調教モード != 2 || FINDELEMENT(PRISONER_TARGET, LOCAL:0) != -1)
			TARGET = LOCAL:0
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				TRYCALLFORM KOJO_TRAIN_END_K{LOCAL:5}
				TRYCALLFORM KOJO_TRAIN_END_%KOJO_STATE(1)%_K{LOCAL:5}
			ENDIF
		ENDIF
	NEXT
ENDIF

;ターゲットを戻す
TARGET = LOCAL:0

;--------------------------------------------------
;コマンド実行前の口上
;戻り値に1を設定すると地の文がカットされる
;--------------------------------------------------
@KOJO_COM_BEFORE
;現在のターゲットを記録
LOCAL:0 = TARGET

;戻り値のデフォルト値を0に設定
LOCAL:1 = 0

;主人公がプレイヤーの場合
IF IS_MPLY(MASTER)
	FOR LOCAL, 0, MTAR_NUM
		TARGET = MTAR:(LOCAL)
		SIF TARGET == -1
			CONTINUE
		;失神・睡眠・離脱状態なら口上を非表示
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_TARGET_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_TARGET_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
;主人公がターゲットの場合
ELSEIF IS_MTAR(MASTER)
	FOR LOCAL, 0, MPLY_NUM
		TARGET = MPLY:(LOCAL)
		SIF TARGET == -1
			CONTINUE
		;失神・睡眠・離脱状態なら口上を非表示
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_PLAYER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_PLAYER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
;プレイヤーにもターゲットにも主人公を含まない
ELSE
	FOR LOCAL, 0, MPLY_NUM
		TARGET = MPLY:LOCAL
		SIF TARGET:LOCAL == -1
			CONTINUE
		;失神・睡眠・離脱状態なら口上を非表示
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)
			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_PLAYER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_PLAYER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
	FOR LOCAL, 0, MTAR_NUM
		TARGET = MTAR:LOCAL
		SIF TARGET:LOCAL == -1
			CONTINUE
		;失神・睡眠・離脱状態なら口上を非表示
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)
			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_TARGET_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_TARGET_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
ENDIF

;ターゲットを戻す
TARGET = LOCAL:0

RETURN LOCAL:1

;--------------------------------------------------
;コマンド実行時の口上
;--------------------------------------------------
@KOJO_COM
;現在のターゲットを記録
LOCAL:0 = TARGET

;戻り値のデフォルト値を0に設定
LOCAL:1 = 0

;主人公がプレイヤーの場合
IF IS_MPLY(MASTER)
	FOR LOCAL, 0, MTAR_NUM
		TARGET = MTAR:(LOCAL)
		SIF TARGET:LOCAL == -1
			CONTINUE
		;失神・睡眠・離脱状態なら口上を非表示
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_TARGET_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_TARGET_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
;主人公がターゲットの場合
ELSEIF IS_MTAR(MASTER)
	FOR LOCAL, 0, MPLY_NUM
		TARGET = MPLY:(LOCAL)
		SIF TARGET:LOCAL == -1
			CONTINUE
		;失神・睡眠・離脱状態なら口上を非表示
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_PLAYER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_PLAYER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
;プレイヤーにもターゲットにも主人公を含まない
ELSE
	FOR LOCAL, 0, MPLY_NUM
		TARGET = MPLY:LOCAL
		SIF TARGET:LOCAL == -1
			CONTINUE
		;失神・睡眠・離脱状態なら口上を非表示
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)
			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_PLAYER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_PLAYER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
	FOR LOCAL, 0, MTAR_NUM
		TARGET = MTAR:LOCAL
		SIF TARGET:LOCAL == -1
			CONTINUE
		;失神・睡眠・離脱状態なら口上を非表示
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)
			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_TARGET_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_TARGET_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
ENDIF

;ターゲットを戻す
TARGET = LOCAL:0

RETURN LOCAL:1

;-------------------------------------------------
;コマンド実行後の口上
;パラメータの変化や絶頂の有無を参照可能
;-------------------------------------------------
@KOJO_COM_AFTER
;現在のターゲットを記録
LOCAL:0 = TARGET

;戻り値のデフォルト値を0に設定
LOCAL:1 = 0

;主人公がプレイヤーの場合
IF IS_MPLY(MASTER)
	FOR LOCAL, 0, MTAR_NUM
		TARGET = MTAR:(LOCAL)
		;失神・睡眠・離脱状態なら口上を非表示
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_TARGET_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_TARGET_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT

;主人公がターゲットの場合
ELSEIF IS_MTAR(MASTER)
	FOR LOCAL, 0, MPLY_NUM
		TARGET = MPLY:(LOCAL)
		;失神・睡眠・離脱状態なら口上を非表示
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_PLAYER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_PLAYER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
;プレイヤーにもターゲットにも主人公を含まない
ELSE
	FOR LOCAL, 0, MPLY_NUM
		TARGET = MPLY:LOCAL
		;失神・睡眠・離脱状態なら口上を非表示
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)
			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_PLAYER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_PLAYER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
	FOR LOCAL, 0, MTAR_NUM
		TARGET = MTAR:LOCAL
		;失神・睡眠・離脱状態なら口上を非表示
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)
			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_TARGET_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_TARGET_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
ENDIF


;ターゲットを戻す
TARGET = LOCAL:0

RETURN LOCAL:1

;-------------------------------------------------
;各種イベントの口上
;対象となるキャラの番号をARG:0に代入
;中身はARG:1に入れる数値で判定
;-------------------------------------------------
@KOJO_EVENT(ARG:0, ARG:1)
;対象キャラが主人公なら表示せずに戻る
IF ARG:0 == MASTER
	RETURN
ENDIF

LOCAL:1 = 0

;現在のターゲットを記録してからターゲットをARG:0に変更
LOCAL:0 = TARGET
TARGET = ARG:0
LOCAL:5 = KOJO_NO(TARGET)

CALL IS_KOJO(LOCAL:5)
IF RESULT
	RESULT = 0
	TRYCALLFORM KOJO_EVENT_K{LOCAL:5}, ARG:1
	IF RESULT
		LOCAL:1 = 1
	ENDIF
ENDIF

IF !LOCAL:1
	;デフォルトメッセージ
	CALL KOJO_EVENT_DEFAULT(ARG:1)
ENDIF

;ターゲットを元に戻す
TARGET = LOCAL:0

;-------------------------------------------------
;単体エンディング口上
;対象となるキャラの番号をARG:0に代入
;-------------------------------------------------
@KOJO_SINGLE_ENDING(ARG:0)
LOCAL:1 = 0

;現在のターゲットを記録してからターゲットをARG:0に変更
LOCAL:0 = TARGET
TARGET = ARG:0
LOCAL:5 = KOJO_NO(TARGET)

CALL IS_KOJO(LOCAL:5)
IF RESULT
	RESULT = 0
	TRYCALLFORM KOJO_SINGLE_ENDING_K{LOCAL:5}
	LOCAL:1 = RESULT
ENDIF

;ターゲットを元に戻す
TARGET = LOCAL:0

RETURN LOCAL:1

;-------------------------------------------------
;斬首エンディング口上
;対象となるキャラの番号をARG:0に代入
;-------------------------------------------------
@KOJO_DEAD_ENDING(ARG:0)
LOCAL:1 = 0

;現在のターゲットを記録してからターゲットをARG:0に変更
LOCAL:0 = TARGET
TARGET = ARG:0
LOCAL:5 = KOJO_NO(TARGET)

CALL IS_KOJO(LOCAL:5)
IF RESULT
	RESULT = 0
	TRYCALLFORM KOJO_DEAD_ENDING_K{LOCAL:5}
	LOCAL:1 = RESULT
ENDIF

;ターゲットを元に戻す
TARGET = LOCAL:0

RETURN LOCAL:1

;-------------------------------------------------
;キャラARG:0のコマンド実行済みフラグをONにする(＝初回判定をOFF)
;SELECTCOMとプレイヤー、ターゲット、主導権を元に自動で設定
;-------------------------------------------------
@SET_COM_FIRST_FLAG(ARG:0)
LOCAL:0 = SELECTCOM
IF LOCAL:0 < 0 || LOCAL:0 > 999
	RETURN
ENDIF

;コマンド番号20～29は特殊判定(プレイヤー・ターゲットの区別がないため、両方のフラグを立てる)
IF LOCAL:0 >= 20 && LOCAL:0 <= 29
	IF !IS_MPLY(ARG:0) && !IS_MTAR(ARG:0)
		;プレイヤーでもターゲットでもないときは無視
		RETURN
	ENDIF

	IF IS_INITIATIVE(ARG:0)
		LOCAL:0 += 2000
	ELSE
		LOCAL:0 += 0
	ENDIF

	LOCAL:1 = LOCAL:0 + 1000

	LOCAL:2 = LOCAL:0 / 50 + 2000
	LOCAL:3 = LOCAL:0 % 50
	LOCAL:4 = LOCAL:1 / 50 + 2000
	LOCAL:5 = LOCAL:1 % 50

	;使用フラグ 2000～2079
	CFLAG:(ARG:0):(LOCAL:2) |= (1 << LOCAL:3)
	CFLAG:(ARG:0):(LOCAL:4) |= (1 << LOCAL:5)

ELSE
	IF IS_MPLY(ARG:0)
		LOCAL:0 += 1000
	ELSEIF IS_MTAR(ARG:0)
		LOCAL:0 += 0
	ELSE
		;プレイヤーでもターゲットでもないときは無視
		RETURN
	ENDIF

	IF IS_INITIATIVE(ARG:0)
		LOCAL:0 += 2000
	ELSE
		LOCAL:0 += 0
	ENDIF

	LOCAL:1 = LOCAL:0 / 50 + 2000
	LOCAL:2 = LOCAL:0 % 50

	;使用フラグ 2000～2079
	CFLAG:(ARG:0):(LOCAL:1) |= (1 << LOCAL:2)
ENDIF

;-------------------------------------------------
;コマンドの初回判定
;現在のTARGETの実行済みフラグを調べる
;ARG:0に0を設定すると主導権別に、1を設定すると主導権によらずに判定する
;-------------------------------------------------
@IS_COM_FIRST(ARG:0 = 0)
#FUNCTION
LOCAL:0 = SELECTCOM
IF LOCAL:0 < 0 || LOCAL:0 > 999
	RETURNF 0
ENDIF

IF IS_MPLY(TARGET)
	LOCAL:0 += 1000
ELSEIF IS_MTAR(TARGET)
	LOCAL:0 += 0
ELSE
	RETURNF 0
ENDIF

IF ARG:0
	LOCAL:1 = LOCAL:0 / 50 + 2000
	LOCAL:2 = LOCAL:0 / 50 + 2040
	LOCAL:3 = LOCAL:0 % 50

	IF (CFLAG:(LOCAL:1) & (1 << LOCAL:3)) || (CFLAG:(LOCAL:2) & (1 << LOCAL:3))
		RETURNF 0
	ENDIF

ELSE
	IF IS_INITIATIVE(TARGET)
		LOCAL:0 += 2000
	ELSE
		LOCAL:0 += 0
	ENDIF

	LOCAL:1 = LOCAL:0 / 50 + 2000
	LOCAL:2 = LOCAL:0 % 50

	IF CFLAG:(LOCAL:1) & (1 << LOCAL:2)
		RETURNF 0
	ENDIF
ENDIF
RETURNF 1

;-------------------------------------------------
;デートイベント用の関数。ARG:0はキャラ番号で、ARG:1は行き先。
;呼び出されたDATE_EVENT_K内で、自作したイベント本体の関数を呼び出してください
;行き先を参照する場合はDATE_EVENT_Kに渡ってるARG:1を利用すると良いでしょう。
;-------------------------------------------------	
@KOJO_DATE_EVENT(ARG:0, ARG:1)
LOCAL:5 = KOJO_NO(ARG:0)
CALL IS_KOJO(LOCAL:5)
IF RESULT
	TRYCCALLFORM DATE_EVENT_K{LOCAL:5}(ARG:1)
		RETURN RESULT
	CATCH 
		RETURN 0
	ENDCATCH
ELSE
	RETURN 0
ENDIF

;-------------------------------------------------
;ARG:0が口上デイリーイベントを実施できるかの簡易チェック関数。
;細かな条件は自分で設定してね
;ARG:1 0 MASTERと別勢力である 1 MASTERと同一勢力である　-1 どちらでもよい
;ARG:2 0 捕虜でない 1 捕虜である -1 どちらでもよい
;ARG:3 0 面識がない 1 面識がある -1 どちらでもよい
;-------------------------------------------------
@CHECK_KOJO_DAILY_HAPPEN(ARG:0, ARG:1 = -1, ARG:2 = -1, ARG:3 = -1)
#FUNCTION
#DIM EVENT_ODDS
EVENT_ODDS = KOJO_DAILY_EVENT_ODDS

;MASTERならダメ
SIF ARG:0 == MASTER
	RETURNF 0

;範囲外はもちろんアウト
SIF ARG:0 < 0 || CHARANUM <= ARG:0
	RETURNF 0
;死んでたら駄目
SIF CFLAG:(ARG:0):特殊状態 == 特殊状態_死亡
	RETURNF 0
;未登場もだめ
SIF CFLAG:(ARG:0):所属 == 0 && CFLAG:(ARG:0):特殊状態 == 0 && !CFLAG:(ARG:0):捕虜先
	RETURNF 0
;所属判定
SELECTCASE ARG:1
	CASE -1
	CASE 0
		SIF CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属
			RETURNF 0
	CASE 1
		SIF CFLAG:(ARG:0):所属 != CFLAG:MASTER:所属
			RETURNF 0
	CASEELSE
		RETURNF 0
ENDSELECT
;捕虜判定
SELECTCASE ARG:2
	CASE -1
	CASE 0
		SIF CFLAG:(ARG:0):捕虜先
			RETURNF 0
	CASE 1
		SIF !CFLAG:(ARG:0):捕虜先
			RETURNF 0
	CASEELSE
		RETURNF 0
ENDSELECT
;面識判定
SELECTCASE ARG:3
	CASE -1
	CASE 0
		SIF CFLAG:(ARG:0):面識
			RETURNF 0
	CASE 1
		SIF !CFLAG:(ARG:0):面識
			RETURNF 0
	CASEELSE
		RETURNF 0
ENDSELECT
RETURNF 1
