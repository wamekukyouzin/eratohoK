@CONFIG_KOJO_DAILY
#DIM カウンタ
#DIM FIRST_LINE
CALL SINGLE_DRAWLINE
PRINTFORML 各口上のデイリーイベントの発生を制御できます（クリックでONOFF）
PRINTFORML 現在、キャラごとにまるっとONにするかOFFにするかのみ指定可能です
CALL SINGLE_DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP
REDRAW 0
カウンタ = 0
FOR LOCAL, 0, 201
	CALL IS_KOJO(LOCAL)
	IF RESULT
		TRYCCALLFORM KOJO_DAILY_EXIST_K{LOCAL}
			SIF KOJO_DAILY_DISABLE:(LOCAL)
				SETCOLOR カラー_選択不可
			PRINTBUTTON @"%"[" + ANAME(LOCAL) + "]", 20, LEFT%", LOCAL + 100
			RESETCOLOR
			カウンタ ++
			IF カウンタ % 4 == 0
				PRINTL
			ENDIF
		CATCH
			CONTINUE
		ENDCATCH
	ENDIF
NEXT
PRINTL
CALL SINGLE_DRAWLINE
PRINTBUTTON "[決定]", 0
PRINT     
PRINTBUTTON "[全ON]", 1
PRINT     
PRINTBUTTON "[全OFF]", 2
PRINTL
$INPUT_LOOP
INPUT
IF RESULT == 0
	REDRAW 1
	RETURN
ELSEIF GROUPMATCH(RESULT, 1, 2)
	LOCAL:1 = RESULT
	FOR LOCAL, 0, 201
		CALL IS_KOJO(LOCAL)
		IF RESULT
			TRYCCALLFORM KOJO_DAILY_EXIST_K{LOCAL}
				KOJO_DAILY_DISABLE:(LOCAL) = LOCAL:1 -1
			CATCH
			ENDCATCH
		ENDIF
	NEXT
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSE
	LOCAL:2 = RESULT - 100
	TRYCCALLFORM KOJO_DAILY_EXIST_K{LOCAL:2}
		KOJO_DAILY_DISABLE:(LOCAL:2) = !KOJO_DAILY_DISABLE:(LOCAL:2)
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	CATCH
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDCATCH
ENDIF
