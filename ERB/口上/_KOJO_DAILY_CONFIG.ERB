;-------------------------------------------------
;desc  :口上デイリーのコンフィグ設定
;-------------------------------------------------
@CONFIG_KOJO_DAILY
#DIM 対象
#DIM FIRST_LINE, 2
FIRST_LINE:0 = LINECOUNT

CALL ASK_MULTI("通常の設定", "全口上デイリーオン", "全口上デイリーオフ", "戻る")

SELECTCASE RESULT
	CASE 1
		PRINTFORML 全口上デイリーをオンにしますか？
		CALL ASK_YN()
		SIF RESULT == 1
			RESTART
		CALL SET_ALL_KOJO_DAILY(1)
		RESTART
	CASE 2
		PRINTFORML 全口上デイリーをオフにしますか？
		CALL ASK_YN()
		SIF RESULT == 1
			RESTART
		CALL SET_ALL_KOJO_DAILY(0)
		RESTART
	CASE 3
		RETURN
ENDSELECT

FIRST_LINE:1 = LINECOUNT
$SHOW_LOOP

CALL SINGLE_DRAWLINE
PRINTFORML 各口上のデイリーイベントの発生を制御できます（クリックでONOFF）
CALL SINGLE_DRAWLINE

CALL SELECT_CHARA_LIST("CONFIG_KOJO_DAILY", "NONE", "DEFAULT")
対象 = RESULT

IF RESULT == -1
	CLEARLINE FIRST_LINE:0 - LINECOUNT
	RESTART
ENDIF

CALL CONFIG_KOJO_DAILY_EACH(対象)

CLEARLINE FIRST_LINE:1 - LINECOUNT
GOTO SHOW_LOOP


@SET_ALL_KOJO_DAILY(オンオフ)
#DIM オンオフ
#DIM 配列要素
#DIMS EVENT_NAME_ENG

配列要素 = デイリー_通常
FOR LOCAL, 0, CHARANUM
	SIF !IS_FIXED_CHARA(LOCAL)
		CONTINUE
	FOR LOCAL:1, 0, VARSIZE("KOJO_DAILY_EVENT_NAME_ENG", 2)
		EVENT_NAME_ENG = %KOJO_DAILY_EVENT_NAME_ENG:(NO:LOCAL):配列要素:(LOCAL:1)%
		CALL KOJO_DAILY_SET_DISABLE(LOCAL, EVENT_NAME_ENG, !オンオフ)
	NEXT
NEXT

;-------------------------------------------------
;desc  :各キャラごとの口上デイリー設定
;param :対象:対象のキャラ番号
;-------------------------------------------------
@CONFIG_KOJO_DAILY_EACH(対象)
#DIM FIRST_LINE
#DIM 対象
#DIM 配列要素
#DIM EVENT_NUM
#DIM オフ
#DIMS EVENT_NAME_JAP
#DIMS EVENT_NAME_ENG
#DIM 対象NO

配列要素 = デイリー_通常
対象NO = NO:対象
EVENT_NUM = KOJO_DAILY_EVENT_NUM:対象NO:配列要素
FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTFORML %ANAME(対象)%のコンフィグ
CALL SINGLE_DRAWLINE

FOR LOCAL, 0, EVENT_NUM
	EVENT_NAME_JAP = %KOJO_DAILY_EVENT_NAME:対象NO:配列要素:LOCAL%
	EVENT_NAME_ENG = %KOJO_DAILY_EVENT_NAME_ENG:対象NO:配列要素:LOCAL%
	オフ = KOJO_DAILY_GET_DISABLE_CONFIG(対象, EVENT_NAME_ENG)
	PRINTFORML [{LOCAL}] - %EVENT_NAME_JAP, 40, LEFT% - \@ オフ ? オフ # オン\@
NEXT

CALL SINGLE_DRAWLINE
PRINTFORML [97] 全てオン
PRINTFORML [98] 全てオフ
PRINTFORML [99] 戻る

INPUT 

IF RESULT == 99
	RETURN
ELSEIF RESULT == 97
	FOR LOCAL, 0, EVENT_NUM
		EVENT_NAME_ENG = %KOJO_DAILY_EVENT_NAME_ENG:対象NO:配列要素:LOCAL%
		CALL KOJO_DAILY_SET_DISABLE(対象, EVENT_NAME_ENG, 0)
	NEXT
	GOTO SET_END
ELSEIF RESULT == 98
	FOR LOCAL, 0, EVENT_NUM
		EVENT_NAME_ENG = %KOJO_DAILY_EVENT_NAME_ENG:対象NO:配列要素:LOCAL%
		CALL KOJO_DAILY_SET_DISABLE(対象, EVENT_NAME_ENG, 1)
	NEXT
	GOTO SET_END
ENDIF

SIF !INRANGE(RESULT, 0, EVENT_NUM)
	GOTO NOT_FOUND

EVENT_NAME_ENG = %KOJO_DAILY_EVENT_NAME_ENG:対象NO:配列要素:RESULT%

オフ = KOJO_DAILY_GET_DISABLE_CONFIG(対象, EVENT_NAME_ENG)
CALL KOJO_DAILY_SET_DISABLE(対象, EVENT_NAME_ENG, !オフ)

$SET_END
$NOT_FOUND

CLEARLINE LINECOUNT - FIRST_LINE
RESTART

@SELECT_CHARA_LIST_SHOW_LOGIC_CONFIG_KOJO_DAILY(対象)
#DIM 対象
RETURN IS_FIXED_CHARA(対象)
