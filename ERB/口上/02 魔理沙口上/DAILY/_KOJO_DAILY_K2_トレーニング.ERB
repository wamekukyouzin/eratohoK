;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K2_TRAINING_RATE(対象)
#DIM 対象
RETURN 40

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K2_TRAINING_DECISION(対象)
#DIM 対象
RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K2_TRAINING_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K2_TRAINING(対象)
#DIM 対象
#DIM 時間
#DIM 答え
#DIMS 答え文字列 = "左", "右", "上", "下"
#DIM 経験値

;答えを出しておく
答え = RAND:4

;上がる能力も出しておく
SELECTCASE RAND:100
	CASE IS < 25
		経験値 = GET_EXP(GETNUM(ABL, "武闘"))
	CASE IS < 50
		経験値 = GET_EXP(GETNUM(ABL, "防衛"))
	CASE IS < 65
		経験値 = GET_EXP(GETNUM(ABL, "知略"))
	CASE IS < 80
		経験値 = GET_EXP(GETNUM(ABL, "政治"))
	CASE IS < 90
		経験値 = GET_EXP(GETNUM(ABL, "料理"))
	CASEELSE
		経験値 = GET_EXP(GETNUM(ABL, "歌唱"))
ENDSELECT


SELECTCASE KDVAR:対象:魔理沙_トレーニング
	CASE 0
		PRINTFORMW %ANAME(MASTER)%が散歩をしていると、%ANAME(対象)%が魔法をしているところに出くわした
		PRINTFORMW %ANAME(対象)%はこちらを見つけると、訓練を中断して近づいてきた
		PRINTFORMW 「あー、秘密の特訓のつもりだったんだが……」
		PRINTFORML 「まぁいいや、仮想敵がいれば、訓練もはかどるだろ」
		PRINTFORMW 「ちょっと付き合ってくれないか？」
		PRINTFORMW 軽い口調で言うが、使うのは本当の魔法だ
		PRINTFORML 失敗すれば当然怪我するだろうが……
	CASE 1
		PRINTFORMW また、%ANAME(対象)%の特訓に出くわした
		PRINTFORMW 「おっ、ちょうどいいや」
		PRINTFORMW 「また付き合ってくれないか？」
		PRINTFORML 失敗すれば怪我するだろうが、どうしようか……
	CASE 2
		PRINTFORMW また、%ANAME(対象)%の特訓に出くわした
		PRINTFORMW 「おっ、ちょうどいいや」
		PRINTFORMW 「今度は付き合ってくれるよな？」
		PRINTFORML 失敗すれば怪我するだろうが、どうしようか……
ENDSELECT
CALL ASK_YN("付き合う", "危ないからやめておく")
IF RESULT == 1
	PRINTFORMW 「えー？　なんだよ、意気地なしだな」
	PRINTFORMW 「しょうがないな、一人で練習するかぁ」
	PRINTFORML %ANAME(対象)%は訓練に戻っていった……
	KDVAR:対象:魔理沙_トレーニング = 2
ELSE
	PRINTFORMW 「よっしゃ、そうと決まれば話は早い」
	PRINTFORMW 「そこに立ってくれ。……そうだ、そこだ」
	PRINTFORMW 「よし、いくぞ！」
	PRINTFORML %ANAME(対象)%がマジックナパームを放ってきた！
	CALL COLOR_PRINTL(@"%答え文字列:答え%を選べ！", カラー_警告)
	FOR LOCAL, 0, VARSIZE("答え文字列")
		PRINTFORML [{LOCAL}] %答え文字列:LOCAL%
	NEXT
	TINPUT 2000, -1, 1
	;失敗したパターン
	IF RESULT != 答え
		PRINTFORML %ANAME(MASTER)%は回避しようと試みた！
		PRINTFORMW だが、弾幕はものの見事に%ANAME(MASTER)%に命中した！
		PRINTFORMW 「お、おいおい、大丈夫か！？」
		PRINTFORMW 「あー、なんかごめんな……まぁ、おかげで自信がついたよ。ありがとな」
		PRINTFORMW %ANAME(対象)%に手当してもらった……
		CALL COLOR_PRINTW(@"%ANAME(MASTER)%は怪我をしてしまいました", カラー_警告)
		CALL ADD_COOLTIME(MASTER, 3)
		CALL PRINT_ADD_EXP(対象, EXPNAME:経験値, RAND:5 + 1, 1)
		CALL TRAIN_AUTO_ABLUP(対象)
	;成功
	ELSE
		PRINTFORML %ANAME(MASTER)%は回避しようと試みた！
		PRINTFORMW %ANAME(対象)%の放った弾幕を、見事回避してみせた！
		PRINTFORMW 「おっと……わりと本気だったんだけど、避けられるとはな」
		PRINTFORMW 「私もまだまだだな。付き合ってくれてありがとな、礼を言うぜ」
		PRINTFORMW %ANAME(対象)%は訓練に戻っていった……
		CALL PRINT_ADD_EXP(MASTER, EXPNAME:経験値, RAND:5 + 1, 1)
		CALL TRAIN_AUTO_ABLUP(MASTER)
	ENDIF
	KDVAR:対象:魔理沙_トレーニング = 1
ENDIF

CALL TRAIN_AUTO_ABLUP(対象)

RETURN 1
