;------------------------------------
;そのキャラがデイリーイベントをもつか判断する関数
;KOJO_EXISTとは別にこの関数も必要になるので注意
;------------------------------------
@KOJO_DAILY_EXIST_K40()


;------------------------------------
;そのキャラのデイリー本体を呼び出すハブとなる関数
;------------------------------------
@KOJO_DAILY_K40()
#DIM 発生フラグ
発生フラグ = 0

;デイリーイベント
DAILY_NAME = 新兵器
CALL KOJO_DAILY_K40_NEW_WEAPON
SIF RESULT != -1
	発生フラグ = 1
SIF DAILY_CANCEL
	RETURN 1

;;デイリーイベント
;DAILY_NAME = デイリーイベントの名前
;CALL KOJO_DAILY_KXX_NAMAE_OF_DAILY_EVENT
;SIF RESULT != -1
;	発生フラグ = 1
;SIF DAILY_CANCEL
;	RETURN 1

RETURN 発生フラグ


;------------------------------------
;デイリーイベント本体
;創意工夫
;発生判定に失敗した場合（イベントが発生しない場合）はマイナス１を返すこと
;-----------------------------------
@KOJO_DAILY_K40_NEW_WEAPON
#DIM にとり

にとり = NAME_TO_CHARA("にとり")

;確率はダミー
SIF !CHECK_KOJO_DAILY_HAPPEN(にとり, 1000, 1, 0, 1)
	RETURN -1

SIF 50 + (KDVAR:にとり:にとり_新兵器開発 > 0) * 100 < RAND:1000
	RETURN -1

SIF KDVAR:にとり:にとり_新兵器開発 == -1
	RETURN -1


SIF !TALENT:MASTER:技師
	RETURN -1

SIF GET_EMPTY_SKILL_SLOT(にとり, スキル_ジャンル_SP) == -1
	RETURN -1


CALL KOJO_DAILY_START

IF KDVAR:にとり:にとり_新兵器開発 == 0
	PRINTFORMW 「ねぇねぇ、%ANAME(MASTER)%、手伝ってほしいことがあるんだ」
	PRINTFORMW %ANAME(MASTER)%が仕事をしていると、%ANAME(にとり)%が声をかけてきた
	PRINTFORMW 「実は最近、新しい兵器の開発をしてるんだけど、これがなかなか難航してるんだよね」
	PRINTFORMW 「そういうわけだから、知恵とアイデアと手を貸してくれると嬉しいんだけど……」
ELSE
	PRINTFORMW 「やぁやぁ%ANAME(MASTER)%。悪いんだけど、例の兵器開発の件、また手伝ってくれないかな？」
ENDIF
PRINTFORMW どうしようか……
PRINTFORML
CALL ASK_YN("手伝う", "今は忙しい")

IF RESULT == 1
	PRINTFORMW 「えっ、そうなの？　それなら仕方ないかな」
	PRINTFORMW 「それじゃ私一人で頑張るかなー、うーん」
	PRINTFORMW %ANAME(にとり)%は立ち去った……
	KDVAR:にとり:にとり_新兵器開発 = -1
	RETURN
ENDIF

PRINTFORMW 「うんうん、そうこなくっちゃね」
PRINTFORMW 「それじゃあ私の工房に案内するから、一つ頼むよ！」
PRINTFORMW
PRINTFORMW ……日がくれるまで、%ANAME(にとり)%に手伝わされた……

IF KDVAR:にとり:にとり_新兵器開発 * 100 > RAND:1000
	PRINTFORMW ……だが、そのおかげで兵器が開発した！
	PRINTFORMW 「やったやった！　やっと完成したよ！」
	PRINTFORMW 「ありがとう%ANAME(MASTER)%、私一人じゃどれくらいかかってたかわかんないよ！」
	PRINTFORMW %ANAME(にとり)%から深く感謝されたようだ
	PRINTFORMW 随分疲れたが、この笑顔が見られたならよしとするか……
	CALL COLORPRINT(@"%ANAME(にとり)%がスキル「撃て！バブルドラゴン」を使用可能になりました", COLOR("注意"), "W")
	CALL COLORPRINT(@"%ANAME(にとり)%の武闘が5上昇しました", COLOR("注意"), "W")
	ABL:にとり:武闘 += 5
	KDVAR:にとり:にとり_新兵器開発 = -1
	CALL SKILL_LEARN_BY_NAME(にとり, スキル_ジャンル_SP, NO:にとり, "撃て！バブルドラゴン")
ELSE
	PRINTFORMW ……しかし、開発は終わらなかった
	PRINTFORMW 「うーん、もう少し時間が必要かな」
	PRINTFORMW 「もし良かったら、次も手伝ってくれると嬉しいな」
	CALL ADD_COOLTIME(MASTER, 2)
	KDVAR:にとり:にとり_新兵器開発 = 1
ENDIF

