;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K72_SMITHING_RATE(対象)
#DIM 対象
RETURN 35

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K72_SMITHING_DECISION(対象)
#DIM 対象

SIF KDVAR:対象:小傘_たたら
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K72_SMITHING_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K72_SMITHING(対象)
#DIM 対象

KDVAR:対象:小傘_たたら = 1

PRINTFORMW 武器の手入れ中、%ANAME(対象)%に話しかけられた
PRINTFORMW 「あれ、%ANAME(MASTER)%。どうしたのそれ？」
PRINTFORMW 武器の手入れをしているのだと返すと、%ANAME(対象)%は笑みを浮かべた
PRINTFORMW 「それならわちきに任せてよ！　こう見えても鍛冶仕事は得意だからさ」
PRINTFORML 胸を張って自慢げに言う%ANAME(対象)%だが……
PRINTFORML 
CALL ASK_YN("任せる", "やめておく")

IF RESULT == 1
	PRINTFORML 心配だ。やめておくことにした
	PRINTFORMW 「えー？　なんでなんで？」
	PRINTFORMW 「まぁ、それならしょうがないけど」
	PRINTFORMW %ANAME(対象)%は不満げだった……
	CFLAG:対象:好感度 -= 100
	RETURN 1
ENDIF

PRINTFORML 「任せてちょうだい！　ちょっと待っててね？」
PRINTFORMW %ANAME(対象)%は%ANAME(MASTER)%の武器を持って自分の部屋に戻っていった……
PRINTFORML ・
PRINTFORML ・
PRINTFORMW ・
PRINTFORMW 「お待たせ！　こんなもんでいいかな？」
PRINTFORML %ANAME(MASTER)%の武器は以前よりずっと扱いやすくなっていた
PRINTFORMW %ANAME(対象)%の腕は確かだったようだ
PRINTFORMW 「えへへ、どう？　すごいでしょ、驚いた？」
PRINTFORMW 「また手入れすることがあったら言ってね、やってあげるから！」
PRINTFORMW %ANAME(対象)%に礼を言っておいた……

CALL COLOR_PRINTW(@"%ANAME(MASTER)%の武闘が3上がった", カラー_注意)
ABL:MASTER:武闘 += 3
IF !TALENT:対象:技師
	CALL COLOR_PRINTW(@"%ANAME(対象)%は＜技師＞を得た", カラー_注意)
	TALENT:対象:技師 = 1
ENDIF

RETURN 1
