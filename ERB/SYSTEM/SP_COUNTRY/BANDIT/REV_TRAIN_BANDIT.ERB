@REV_TRAIN_BANDIT
#DIM 候補, 3000
#DIM 候補数
#DIM 参加者, 8
#DIM 参加者数
#DIM 野盗勢力
#DIM ループ数
VARSET 候補, -1
VARSET 候補数
VARSET 参加者, -1
VARSET 参加者数

野盗勢力 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_野盗))
;初回
IF FLAG:逆調教回数 == 0
	IF SP_COUNTRY_START:特殊勢力スタート_士官 == 1
		PRINTFORMW 部屋にどやどやと入ってきた野盗たちは、%ANAME(MASTER)%の姿を見て爆笑した
		PRINTFORMW なに自分の勢力にとっ捕まってるんだお前はと、口々に囃したてる
		PRINTFORMW ひとしきり笑った後、野盗たちは%ANAME(MASTER)%の縄を解き、牢から出した……
		CALL COLORPRINT (@"%ANAME(MASTER)%は野盗に復帰しました", COLOR("注意"), "L")
		CALL CHANGE_COUNTRY(MASTER, CFLAG:MASTER:捕虜先, 1)
		RETURN
	ELSEIF SP_COUNTRY_START:特殊勢力スタート_奴隷 == 2
		PRINTFORMW 部屋にどやどやと入ってきた野盗たちは、%ANAME(MASTER)%の姿を見てニヤニヤと笑う
		PRINTFORMW 奴隷生活が忘れられなくて戻ってきちまったのかよ、と
		PRINTFORMW どうやら、%ANAME(MASTER)%が奴隷になることを認めれば、牢から出すつもりでいるようだ……
		PRINTFORML
		CALL ASK_MULTI("再び奴隷になる", "奴隷になるなんてごめんだ")
		IF RESULT == 0
			PRINTFORMW そう来なくっちゃなと笑い、野盗たちは%ANAME(MASTER)%の縄を解いた
			PRINTFORMW じゃあ早速奴隷の仕事をしてもらおうかと、彼らは自らのペニスを露出させる
			PRINTFORMW そして%ANAME(MASTER)%は、しばしの間、奴隷としての仕事に従事した……
			CALL FUCK_GANGBANG(MASTER, GET_SPERM_ID("野盗"), @"野盗の\@ RAND:2 ? ペニス # 唇\@", "野盗")
			CALL CHANGE_COUNTRY(MASTER, CFLAG:MASTER:捕虜先, 1)
			RETURN 1
		ELSE
			PRINTFORMW もうお前たちのいいなりになんてならない……%ANAME(MASTER)%は毅然と言う
			PRINTFORMW 野盗たちはそれを笑い捨て、立場ってもんを教えてやるよと、%ANAME(MASTER)%に群がり始めた……
		ENDIF
	ENDIF
ENDIF

IF FLAG:逆調教回数 != 0 && FLAG:逆調教回数 % 4 == 0
	PRINTFORMW 部屋にどやどやと入ってきた野盗たちは、%ANAME(MASTER)%の姿を見てニヤニヤと笑う
	PRINTFORMW 奴隷根性が染みついたやつなら、出してやってもいいんだけどなー、などと言い出した……
	PRINTFORMW どうやら、%ANAME(MASTER)%が奴隷になることを認めれば、牢から出すつもりでいるようだ……
	PRINTFORML
	CALL ASK_MULTI("奴隷になる", "奴隷になるなんてごめんだ")
	IF RESULT == 0
		PRINTFORMW そう来なくっちゃなと笑い、野盗たちは%ANAME(MASTER)%の縄を解いた
		PRINTFORMW じゃあ早速奴隷の仕事をしてもらおうかと、彼らは自らのペニスを露出させる
		PRINTFORMW そして%ANAME(MASTER)%は、しばしの間、奴隷としての仕事に従事した……
		CALL FUCK_GANGBANG(MASTER, GET_SPERM_ID("野盗"), @"野盗の\@ RAND:2 ? ペニス # 唇\@", "野盗")
		CALL CHANGE_COUNTRY(MASTER, CFLAG:MASTER:捕虜先, 1)
		FOR LOCAL:0, 0, CHARANUM
			IF TALENT:(LOCAL:0):特殊勢力素質 == 特殊勢力_野盗
				TALENT:(LOCAL:0):合意 = 1
				CFLAG:(LOCAL:0):2 = MAX(CFLAG:(LOCAL:0):2, 500)
				CFLAG:(LOCAL:0):3 = MAX(CFLAG:(LOCAL:0):3, 500)
			ENDIF
		NEXT
		IF RAND:3
			CALL SET_PIERCE_RANKED(MASTER, 0, ピアス_ラビア, ピアス_ペニス, ピアス_クリトリス, ピアス_乳首, ピアス_へそ, ピアス_耳, ピアス_舌, ピアス_鼻)
			SIF RESULT != -1
				CALL COLORPRINT(@"%ANAME(MASTER)%の%GET_PIERCE_NAME(RESULT)%に、服従の証のピアスが取り付けられた……", COLOR("桃"), "W")
		ELSE
			IF GET_INMOU(MASTER) >= 陰毛_標準
				CALL COLORPRINT(@"服従の証として、%ANAME(MASTER)%の陰毛はハート型に整えられた……", COLOR("桃"), "W")
				TALENT:MASTER:陰毛目標値 = 陰毛_ハート型
				TALENT:MASTER:陰毛現在値 = 陰毛_ハート型
			ELSE
				CALL COLORPRINT(@"服従の証として、%ANAME(MASTER)%の陰毛は永久脱毛された……", COLOR("桃"), "W")
				TALENT:MASTER:陰毛目標値 = 陰毛_パイパン
				TALENT:MASTER:陰毛現在値 = 陰毛_パイパン
			ENDIF
		ENDIF
		RETURN 1
	ELSE
		PRINTFORMW だれがお前たちの奴隷になどと……%ANAME(MASTER)%は毅然と言う
		PRINTFORMW 野盗たちはそれを笑い捨て、立場ってもんを教えてやるよと、%ANAME(MASTER)%に群がり始めた……
	ENDIF
ENDIF
;全員の調教参加フラグを解除
CVARSET CFLAG, 6, 0

;1/3の確率で野盗に寝返ったキャラが調教者になる　ただしマスターが男なら確実に来る
IF IS_MALE(MASTER) || RAND:3 
	LOCAL:1 = 0
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):面識 && !IS_SP_CHARA(LOCAL:0) && CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:捕虜先 && GETBIT(TALENT:(LOCAL:0):特殊勢力陥落系, 特殊勢力_野盗) && !RAND:2
			候補:候補数 = LOCAL:0
			候補数 ++
		ENDIF
	NEXT
	IF 候補数
		CALL FISHER_YATES_SHAFFLE(候補数)
		FOR LOCAL, 0, MIN(候補数, 4)
			参加者:参加者数 = 候補:(SHAFFLE_ARRAY:(LOCAL:0))
			CFLAG:(参加者:参加者数):調教参加フラグ = 1
			参加者数 ++
		NEXT
		IF 参加者数
			LOCALS:0 = 
			FOR LOCAL:0, 0, 参加者数
				IF LOCALS:0 == ""
					LOCALS:0 = %ANAME(参加者:LOCAL)%
				ELSE
					LOCALS:0 = %LOCALS:0%、%ANAME(参加者:LOCAL)%
				ENDIF
			NEXT
			CALL SINGLE_DRAWLINE
			PRINTFORML 調教の時間が近づくと、%ANAME(MASTER)%はいつものように調教部屋で拘束され、
			PRINTFORMW あとは男達に汚されるのを待つのみとなった
			IF 参加者数
				PRINTFORMW やってきた野盗は、%ANAME(MASTER)%が良く見知った相手を連れてきていた
				PRINTFORMW %LOCALS:0%…
				PRINTFORMW 頑丈な首輪が嵌められている以外は皆一糸まとわぬ姿で、全身に卑猥な落書きを施されている
				IF IS_FEMALE(参加者:0)
					PRINTFORMW 事前にたっぷりと犯されてきたのだろう、顔も乳房も両穴も、既に愛液や精液でどろどろになっている……
				ELSE
					PRINTFORMW 事前にたっぷりと犯されてきたのだろう、あちこちを白濁で汚されている……
				ENDIF
				PRINTFORMW 首輪を野盗は強引に引っ張るが、%ANAME(参加者:0)%\@ 参加者数 > 1 ? たち # \@は嫌がるどころか、従順についていく……
				PRINTFORMW 野盗はニヤニヤと笑いながら、お前の知り合いをここでヤってやるよと言い始めた…
				PRINTFORMW これから行われることを、%ANAME(参加者:0)%\@ 参加者数 > 1 ? たち # \@は理解したのだろう。股を開き、腰をくねらせ、淫らな言葉で肉棒をねだり始めた……
			ENDIF
			FLAG:特殊勢力逆調教知り合いフラグ = 1
		ENDIF
	ENDIF
ENDIF
IF 参加者数
	ループ数 = MIN(4, VARSIZE("参加者") - 参加者数)
ELSE
	ループ数 = 4
ENDIF

FOR LOCAL:0, 0, ループ数
	;確率で犬豚が対象に
	IF !RAND:20
		FOR LOCAL:1, 0, CHARANUM
			IF CFLAG:(LOCAL:1):所属 == 野盗勢力 && IS_ANIMAL(LOCAL:1) && !CFLAG:(LOCAL:1):調教参加フラグ
				参加者:参加者数 =  LOCAL:1
				参加者数 ++
				CFLAG:(LOCAL:1):調教参加フラグ = 1
				BREAK
			ENDIF
		NEXT
		CALL ADD_ANIMAL(@"\@ RAND:2 ? 犬 # 豚 \@", LOCAL:6)
		CFLAG:(CHARANUM -1):調教参加フラグ = 1
		参加者:参加者数 =  CHARANUM - 1
		参加者数 ++
	;普通に野盗を選出
	ELSE
		FOR LOCAL:1, 0, CHARANUM
			IF TALENT:(LOCAL:1):特殊勢力素質 == 特殊勢力_野盗 && !CFLAG:(LOCAL:1):調教参加フラグ && CFLAG:(LOCAL:1):所属 == 野盗勢力
				参加者:参加者数 =  LOCAL:1
				参加者数 ++
				CFLAG:(LOCAL:1):調教参加フラグ = 1
				CFLAG:(LOCAL:1):TEXT_LAST非表示 = 1
				CFLAG:(LOCAL:1):能力固定フラグ = 1
				BREAK
			ENDIF
		NEXT
	ENDIF
NEXT

FOR LOCAL:0, 0, 参加者数
	CFLAG:(参加者:(LOCAL:0)):調教参加フラグ = 1
NEXT
;逆調教モード
FLAG:調教モード = 4
;ウフフモード
FLAG:ウフフフラグ = 1
;アイテムを一通り追加
ITEM:A_ローター = 1
ITEM:A_バイブ = 1
ITEM:A_アナルバイブ = 1
ITEM:A_ペニスバンド = 1
ITEM:A_オナホール = 1
ITEM:A_クリキャップ = 1
ITEM:A_ニプルキャップ = 1
ITEM:A_針 = 1
ITEM:A_鞭 = 1
ITEM:A_目隠し = 1
ITEM:A_鼻フック = 1
ITEM:A_口枷 = 1
ITEM:A_麻薬 = 99
ITEM:A_媚薬 = 99
ITEM:A_排卵誘発剤 = 99
;回数をカウント
FLAG:逆調教回数 ++
;解放カウンタをリセット
CFLAG:MASTER:解放カウンタ = 0
