;--------------------------------------
;蜂起イベントが発生するかの判定
;--------------------------------------
@BANDIT_RISE_RATE()
#DIM 野盗
野盗 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_野盗))

;そもそも出現させなかったら駄目
SIF !SP_COUNTRY_RANK:(特殊勢力_野盗)
	RETURN 0

;まだ存在しない（ランクは設定したが、初期で出現させなかった場合）とき
IF 野盗 == -1
	IF DAY > SLG_PP:1 && (DAY - SLG_PP:1) * 15
		CALL BANDIT_INIT(1)
		SIF !RESULT && BANDIT_HAS_INITIAL_CITIES
			CALL BANDIT_RISE
		SIF !RESULT
			RETURN 1
	ENDIF
	RETURN 0
ENDIF

SIF GET_OWN_CITY(野盗) > 0
	RETURN 0

SIF DAY < SLG_PP:1
	RETURN 0

SIF DAILY_GET_DISABLE_CONFIG("BANDIT_RISE")
	RETURN 0

IF RAND:1000 >= DVAR:野盗_蜂起カウンタ * 5 - MIN(DAY / 2, 50)
	DVAR:野盗_蜂起カウンタ ++
	RETURN 0
ENDIF
RETURN 1


;--------------------------------------
;野盗蜂起イベント
;--------------------------------------
@BANDIT_RISE(都市 = 0)
#DIM 野盗
#DIM 都市

野盗 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_野盗))

IF 野盗 == -1
	CALL BANDIT_INIT()
	野盗 = RESULT
ENDIF

SIF 野盗 == 0
	RETURN 0

CALL SP_COUNTRY_RISE(野盗, 都市)

SIF !RESULT
	RETURN 0

FOR LOCAL, 0, MAX_UNIT
	CALL CLEAR_UNIT(野盗, LOCAL, 1)
NEXT

CALL INIT_SP_COUNTRY(野盗)

COUNTRY_IS_CLOSED:野盗 = 0
DVAR:野盗_蜂起カウンタ = 0

RETURN 野盗

;---------------------
;蜂起イベント用都市簒奪関数
;---------------------
@BANDIT_RISE_TAKEOVER_CITY(勢力, 初期都市)
#DIM 勢力
#DIM 初期都市
#DIM 簒奪数
#DIM 接続先

;処理が特殊なので、RISE_MSGでなくここでメッセージ表示
CALL SINGLE_DRAWLINE
PRINTFORML
SETCOLOR カラー_警告
PRINTFORMW 野盗が蜂起しました！
RESETCOLOR

CALL BANDIT_RISE_ARREST(勢力, CITY_OWNER:初期都市)
CALL TAKEOVER_CITY_FROM_NO(勢力, 初期都市)

簒奪数 = 1

;初期都市を中心に、接続されている都市を奪う
;中継点がある場合は80%
FOR LOCAL, 0, VARSIZE("CITY_ROUTE", 1)

	;接続先を取得
	接続先 = CITY_ROUTE:初期都市:(LOCAL:0)
	SIF 接続先 == 0
		BREAK

	SELECTCASE CITY_TYPE:接続先
		;接続先が都市
		;OWNERの所有都市数を調べ、2以上なら奪う
		CASE 0
			IF !IS_COUNTRY(CITY_OWNER:接続先)
				CALL TAKEOVER_CITY_FROM_NO(勢力, 接続先)
				簒奪数 ++
			ELSE
				IF GET_OWN_CITY(CITY_OWNER:接続先) >= 2
					CALL BANDIT_RISE_ARREST(勢力, CITY_OWNER:接続先)
					CALL TAKEOVER_CITY_FROM_NO(勢力, 接続先)
					簒奪数 ++
				ENDIF
			ENDIF
		;接続先が中継地
		;さらに接続している先を取得し、同様の処理
		CASE 1
			SIF RAND:100 > 80
				CONTINUE
			FOR LOCAL:2, 0, VARSIZE("CITY_ROUTE", 1)
				LOCAL:3  = CITY_ROUTE:接続先:(LOCAL:2)
				SIF LOCAL:3 == 0
					BREAK
				IF LOCAL:3 != 初期都市
					IF !IS_COUNTRY(CITY_OWNER:(LOCAL:3))
						CALL TAKEOVER_CITY_FROM_NO(勢力, LOCAL:3)
						簒奪数 ++
						BREAK
					ELSE
						IF GET_OWN_CITY(CITY_OWNER:(LOCAL:3)) >= 2
							CALL BANDIT_RISE_ARREST(勢力, CITY_OWNER:(LOCAL:3))
							CALL TAKEOVER_CITY_FROM_NO(勢力, LOCAL:3)
							簒奪数 ++
							BREAK
						ENDIF
					ENDIF
				ENDIF
			NEXT
	ENDSELECT
NEXT

RETURN 簒奪数

;-------------------------------
;奪った都市の所属武将をかっぱらう処理（無所属都市でなければ）
;-------------------------------
@BANDIT_RISE_ARREST(勢力, 対象)
#DIM 勢力
#DIM 対象
VARSET LOCAL

SIF !IS_COUNTRY(対象)
	RETURN -1

FOR LOCAL, 0, CHARANUM
	IF CFLAG:LOCAL:所属 == 対象 && !CFLAG:LOCAL:捕虜先 && IS_FEMALE(LOCAL) && GET_COUNTRY_BOSS(対象) != LOCAL && !IS_SP_CHARA(LOCAL) && !TALENT:LOCAL:特殊勢力素質 && !RAND:4
		CALL COLOR_PRINTW(@"野盗は%ANAME(LOCAL)%を性奴隷として拉致しました", カラー_警告)
		CALL CAPTURE(LOCAL, 勢力)
		BREAK
	ENDIF
NEXT


;---------------------
;蜂起イベント用メッセージ関数
;ダミー
;---------------------
@BANDIT_RISE_MSG(勢力, 対象)
#DIM 勢力
#DIM 対象
RETURN 1
