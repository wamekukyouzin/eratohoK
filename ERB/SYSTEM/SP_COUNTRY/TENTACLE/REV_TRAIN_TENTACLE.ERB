@REV_TRAIN_TENTACLE
#DIM 候補, 3000
#DIM 候補数
#DIM 参加者, 8
#DIM 参加者数
#DIM 触手
#DIM ループ数
VARSET 候補, -1
VARSET 候補数
VARSET 参加者, -1
VARSET 参加者数

触手 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_触手))

;四回ごとに誘われる
IF FLAG:逆調教回数 > 0 && FLAG:逆調教回数 % 4 == 0
	PRINTFORML 調教の時間が近づくと、%ANAME(MASTER)%はいつものように触手どもに拘束され、
	PRINTFORMW あとは触手に自我を乗っ取られた連中に汚されるのを待つのみとなった
	PRINTFORML やってきた触手兵たちは、%ANAME(MASTER)%に何もしなかった
	PRINTFORMW 自ら触手に身体を明け渡し、母体となれば、これ以上の調教はしないつもりのようだが……
	PRINTFORMW どうするべきだろうか……
	CALL ASK_YN("諦めて受け入れる（苗床化)", "そんなのはごめんだ")
	IF RESULT == 0
		PRINTFORML %ANAME(MASTER)%が頷くと、触手は一斉に%ANAME(MASTER)%へと群がった
		FOR LOCAL, 0, 5
			CALL FUCK_SP(MASTER, "欲望, 奉仕, 性交, 性技, 精愛, 排泄, Ｖ拡張, Ａ拡張, Ｃ, Ｖ, Ａ, Ｂ, Ｍ, 口淫, キス, 緊縛, 苦痛快楽, マゾ, Ｖ性交, Ａ性交", "処女喪失, 膣内射精, Ａ処女喪失, キス喪失, 腸内射精, 口内射精", 触手, GET_SPERM_ID("触手"), "触手", "触手", "", 喪失_輪姦)
		NEXT
		SETCOLOR COLOR("注意")
		PRINTFORMW %ANAME(MASTER)%は触手に寄生されました
		RESETCOLOR
		SETBIT TALENT:MASTER:特殊勢力陥落系, 特殊勢力_触手
		CALL CHANGE_COUNTRY(MASTER, 触手, 1)
		FOR LOCAL:0, 0, CHARANUM
			IF TALENT:(LOCAL:0):特殊勢力素質 == 特殊勢力_触手
				TALENT:(LOCAL:0):合意 = 1
				CFLAG:(LOCAL:0):2 = MAX(CFLAG:(LOCAL:0):2, 500)
				CFLAG:(LOCAL:0):3 = MAX(CFLAG:(LOCAL:0):3, 500)
			ENDIF
		NEXT
		RETURN 1
	ELSE
		PRINTFORML そんなことが認められるか
		PRINTFORML 拒否の言葉を理解しているのか、触手兵たちは%ANAME(MASTER)%の言葉を受け、再び動き始めた
		PRINTFORMW 彼らはそのまま、%ANAME(MASTER)%に群がっていく……
	ENDIF
ENDIF
;全員の調教参加フラグを解除
CVARSET CFLAG, 6, 0

;1/2の確率で触手に寝返った顔見知りのキャラが調教者になる
IF RAND:2
	LOCAL:1 = 0
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):面識 && !IS_SP_CHARA(LOCAL) && CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:捕虜先 && GETBIT(TALENT:(LOCAL:0):特殊勢力陥落系, 特殊勢力_触手) && !RAND:2
			候補:候補数 = LOCAL:0
			候補数 ++
		ENDIF
	NEXT
	IF 候補数
		CALL FISHER_YATES_SHAFFLE(候補数)
		FOR LOCAL, 0, MIN(候補数, 4)
			参加者:参加者数 = 候補:(SHAFFLE_ARRAY:(LOCAL:0))
			CFLAG:(参加者:参加者数):調教参加フラグ = 1
			参加者数 ++
		NEXT
		IF 参加者数
			LOCALS:0 = 
			FOR LOCAL:0, 0, 参加者数
				IF LOCALS:0 == ""
					LOCALS:0 = %ANAME(参加者:LOCAL)%
				ELSE
					LOCALS:0 = %LOCALS:0%、%ANAME(参加者:LOCAL)%
				ENDIF
			NEXT
			DRAWLINE
			PRINTFORML 調教の時間が近づくと、%ANAME(MASTER)%はいつものように触手どもに拘束され、
			PRINTFORMW あとは触手に自我を乗っ取られた連中に汚されるのを待つのみとなった
			IF 参加者数
				PRINTFORMW やってきた触手兵の中に、%ANAME(MASTER)%が良く見知った相手が混じっていた
				PRINTFORMW %LOCALS:0%…
				PRINTFORMW 虚ろな表情を浮かべている。その瞳からは理性がうかがえない
				PRINTFORMW %ANAME(MASTER)%は必死に呼びかけるが、やはり無反応だった
				PRINTFORMW どうやら触手に寄生され、自我を喪失してしまっているようだ……
				PRINTFORMW %ANAME(参加者:0)%\@ 参加者数 > 1 ? たち # \@は他の触手兵とともに、%ANAME(MASTER)%に群がりはじめた……
			ENDIF
			FLAG:特殊勢力逆調教知り合いフラグ = 1
		ENDIF
	ENDIF
ENDIF
IF 参加者数
	ループ数 = MIN(4, VARSIZE("参加者") - 参加者数)
ELSE
	ループ数 = 4
ENDIF

FOR LOCAL:0, 0, ループ数
	FOR LOCAL:1, 0, CHARANUM
		IF TALENT:(LOCAL:1):特殊勢力素質 == 特殊勢力_触手 && !CFLAG:(LOCAL:1):調教参加フラグ && CFLAG:(LOCAL:1):所属 == 触手
			参加者:参加者数 =  LOCAL:1
			参加者数 ++
			CFLAG:(LOCAL:1):調教参加フラグ = 1
			CFLAG:(LOCAL:1):TEXT_LAST非表示 = 1
			CFLAG:(LOCAL:1):能力固定フラグ = 1
			BREAK
		ENDIF
	NEXT
NEXT

FOR LOCAL:0, 0, 参加者数
	CFLAG:(参加者:(LOCAL:0)):調教参加フラグ = 1
NEXT
;逆調教モード
FLAG:調教モード = 4
;ウフフモード
FLAG:ウフフフラグ = 1
;アイテムは追加されない
ITEM:A_媚薬 = 99
ITEM:A_排卵誘発剤 = 99
;回数をカウント
FLAG:逆調教回数 ++
;解放カウンタをリセット
CFLAG:MASTER:解放カウンタ = 0

