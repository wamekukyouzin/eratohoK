@REV_TRAIN_SUCCUBUS
#DIM 候補, 3000
#DIM 候補数
#DIM 参加者, 8
#DIM 参加者数
#DIM サキュバス
#DIM ループ数
VARSET 候補, -1
VARSET 候補数
VARSET 参加者, -1
VARSET 参加者数

サキュバス = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_サキュバス))

;四回ごとに誘われる
IF FLAG:逆調教回数 > 0 && FLAG:逆調教回数 % 4 == 0
	PRINTFORML 調教の時間が近づくと、%ANAME(MASTER)%はいつものように夢の世界で拘束され、
	PRINTFORMW あとはサキュバス達に弄ばれるのを待つのみとなった
	PRINTFORML やってきたサキュバスは、そろそろ素直になってくれたかしらと訪ねてきた
	PRINTFORMW 私たちの仲間になれば、こういうコトもし放題よ、と……
	PRINTFORML このまま拒否し続けても、こいつらは自分を弄び続けるだろう
	PRINTFORML 首を縦に振れば、いくらかは楽になれるのかもしれない
	PRINTFORMW どうするべきだろうか……
	CALL ASK_YN("諦めて受け入れる（夢魔化)", "こんなやつらの仲間なんてごめんだ")
	IF RESULT == 0
		PRINTFORML %ANAME(MASTER)%が頷くと、サキュバスたちは歓迎してくれた
		CALL COLORPRINT(@"そして%ANAME(MASTER)%を、夢魔として生まれ変わらせた……", COLOR("桃"), "W")
		TALENT:MASTER:陰毛目標値 = 陰毛_ハート型
		TALENT:MASTER:陰毛現在値 = 陰毛_ハート型
		SETCOLOR COLOR("注意")
		PRINTFORMW %ANAME(MASTER)%は夢魔になりました
		RESETCOLOR
		SETBIT TALENT:MASTER:特殊勢力陥落系, 特殊勢力_サキュバス
		CALL CHANGE_COUNTRY(MASTER, サキュバス, 1)
		FOR LOCAL:0, 0, CHARANUM
			IF TALENT:(LOCAL:0):特殊勢力素質 == 特殊勢力_サキュバス
				TALENT:(LOCAL:0):合意 = 1
				CFLAG:(LOCAL:0):2 = MAX(CFLAG:(LOCAL:0):2, 500)
				CFLAG:(LOCAL:0):3 = MAX(CFLAG:(LOCAL:0):3, 500)
			ENDIF
		NEXT
		RETURN 1
	ELSE
		PRINTFORML そう、と、%ANAME(MASTER)%の拒否を特に気にした風でもなくサキュバスは受け止める
		PRINTFORML ならもっと気持ちいいことを教えてあげなくちゃねと、その顔は淫蕩な笑みを浮かべた……
		PRINTFORMW 彼女らはそのまま、%ANAME(MASTER)%に群がっていく……
	ENDIF
ENDIF
;全員の調教参加フラグを解除
CVARSET CFLAG, 6, 0

;1/3の確率でサキュバスに寝返った顔見知りのキャラが調教者になる　ただしマスターが男なら確実に来る
IF IS_MALE(MASTER) || RAND:3 
	LOCAL:1 = 0
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):面識 && !IS_SP_CHARA(LOCAL:0) && CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:捕虜先 && GETBIT(TALENT:(LOCAL:0):特殊勢力陥落系, 特殊勢力_サキュバス) && !RAND:2
			候補:候補数 = LOCAL:0
			候補数 ++
		ENDIF
	NEXT
	IF 候補数
		CALL FISHER_YATES_SHAFFLE(候補数)
		FOR LOCAL, 0, MIN(候補数, 4)
			参加者:参加者数 = 候補:(SHAFFLE_ARRAY:(LOCAL:0))
			CFLAG:(参加者:参加者数):調教参加フラグ = 1
			参加者数 ++
		NEXT
		IF 参加者数
			LOCALS:0 = 
			FOR LOCAL:0, 0, 参加者数
				IF LOCALS:0 == ""
					LOCALS:0 = %ANAME(参加者:LOCAL)%
				ELSE
					LOCALS:0 = %LOCALS:0%、%ANAME(参加者:LOCAL)%
				ENDIF
			NEXT
			CALL SINGLE_DRAWLINE
			PRINTFORML 調教の時間が近づくと、%ANAME(MASTER)%はいつものように調教部屋で拘束され、
			PRINTFORMW あとは彼女達に、弄ばれるのを待つのみとなった
			IF 参加者数
				PRINTFORMW やってきたサキュバスは、%ANAME(MASTER)%が良く見知った相手を連れてきていた
				PRINTFORMW %LOCALS:0%…
				PRINTFORMW 元の%ANAME(参加者:0)%ではない。どうやら夢魔になり果ててしまったようだ
				PRINTFORMW 淫蕩な視線は、%ANAME(MASTER)%のからだに注がれている……
				PRINTFORMW サキュバスはニコニコ笑いながら、お知り合いとシたほうが気持ちよくなれるわ、と言ってきた
				PRINTFORMW かつて%ANAME(参加者:0)%\@ 参加者数 > 1 ? たち # \@が%ANAME(MASTER)%と知り合い「だった」ことを、掴んでいるようだ……
				PRINTFORMW さあ楽しみましょう。サキュバスがそのように言うと、%ANAME(参加者:0)%\@ 参加者数 > 1 ? たち # \@は%ANAME(MASTER)%に群がった……
			ENDIF
			FLAG:特殊勢力逆調教知り合いフラグ = 1
		ENDIF
	ENDIF
ENDIF
IF 参加者数
	ループ数 = MIN(4, VARSIZE("参加者") - 参加者数)
ELSE
	ループ数 = 4
ENDIF

FOR LOCAL:0, 0, ループ数
	FOR LOCAL:1, 0, CHARANUM
		IF TALENT:(LOCAL:1):特殊勢力素質 == 特殊勢力_サキュバス && !CFLAG:(LOCAL:1):調教参加フラグ && CFLAG:(LOCAL:1):所属 == サキュバス
			参加者:参加者数 =  LOCAL:1
			参加者数 ++
			CFLAG:(LOCAL:1):調教参加フラグ = 1
			CFLAG:(LOCAL:1):TEXT_LAST非表示 = 1
			CFLAG:(LOCAL:1):能力固定フラグ = 1
			BREAK
		ENDIF
	NEXT
NEXT

FOR LOCAL:0, 0, 参加者数
	CFLAG:(参加者:(LOCAL:0)):調教参加フラグ = 1
NEXT
;逆調教モード
FLAG:調教モード = 4
;ウフフモード
FLAG:ウフフフラグ = 1
;アイテムを一通り追加
ITEM:A_ローター = 1
ITEM:A_バイブ = 1
ITEM:A_アナルバイブ = 1
ITEM:A_ペニスバンド = 1
ITEM:A_オナホール = 1
ITEM:A_クリキャップ = 1
ITEM:A_ニプルキャップ = 1
ITEM:A_針 = 1
ITEM:A_鞭 = 1
ITEM:A_目隠し = 1
ITEM:A_鼻フック = 1
ITEM:A_口枷 = 1
ITEM:A_媚薬 = 99
ITEM:A_排卵誘発剤 = 99
;回数をカウント
FLAG:逆調教回数 ++
;解放カウンタをリセット
CFLAG:MASTER:解放カウンタ = 0

