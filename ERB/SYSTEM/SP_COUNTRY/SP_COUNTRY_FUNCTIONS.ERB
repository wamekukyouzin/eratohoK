;---------------------------
;対象キャラが対象勢力による調教でFORCE_FUCKされたときに、CFLAGが減少するか
;---------------------------
@SP_TRAIN_IS_DECREASE_CFLAG(対象, 勢力番号)
#FUNCTION
#DIM 対象
#DIM 勢力番号
SIF !IS_SP_COUNTRY(勢力番号)
	RETURNF 0
LOCAL = SP_COUNTRY_TO_CONST(勢力番号)

SIF 対象 == MASTER || FLAG:観戦モード
	RETURNF 0
;士官か奴隷としてスタートし、対象が堕ちている
SIF GROUPMATCH(SP_COUNTRY_START:LOCAL, 特殊勢力スタート_士官, 特殊勢力スタート_奴隷) && GETBIT(TALENT:対象:特殊勢力陥落系, LOCAL)
	RETURNF 0
;MASTERが対象勢力に所属している、または捕虜で対象が堕ちている
SIF CFLAG:MASTER:所属 == 勢力番号 || (CFLAG:MASTER:捕虜先 == 勢力番号 && GETBIT(TALENT:対象:特殊勢力陥落系, LOCAL))
	RETURNF 0

RETURNF 1

@SP_COUNTRY_ABL_MODIFIER(勢力定数)
#FUNCTION
#DIM 勢力定数
SELECTCASE SP_COUNTRY_RANK:勢力定数
	CASE 5
		RETURNF 10
	CASE 4
		RETURNF 7
	CASE 3
		RETURNF 5
	CASE 2
		RETURNF 3
	CASE 1
		RETURNF 2
ENDSELECT

;-------------------------------------------------
;渡されたIDのキャラの特殊勢力素質に対応する特殊精液を返す
;-------------------------------------------------	
@SP_CHARA_TO_SPERM_ID(ID)
#FUNCTION
#DIM ID
;SP_COUNTRY_NAMEの各インデックスと、各特殊勢力素質の値が、同じ特殊勢力を示していることを利用する
RETURNF GET_SPERM_ID(SP_COUNTRY_NAME:(TALENT:ID_TO_CHARA(ID):特殊勢力素質))

;-------------------------------------------------
;渡された勢力番号が特殊勢力であるかを判断する
;-------------------------------------------------	
@IS_SP_COUNTRY(勢力)
#FUNCTION
#DIM 勢力
SIF 勢力 < 0 || 勢力 >= MAX_COUNTRY
	RETURNF 0
RETURNF (FINDELEMENT(SP_COUNTRY_ID, COUNTRY_EVENT_ID:勢力) != -1 ? 1 # 0)

;-------------------------------------------------
;対象が渡された勢力に調教されうるかを判断する
;-------------------------------------------------	
@IS_TRAIN_BY_SP_COUNTRY(対象, 勢力番号)
#FUNCTION
#DIM 対象
#DIM 勢力番号
;その勢力所属でも、その勢力の捕虜でもないキャラは駄目（あたりまえ）
SIF CFLAG:対象:所属 != 勢力番号 && CFLAG:対象:捕虜先 != 勢力番号
	RETURNF 0
;その勢力に所属はしているが、その勢力以外に捕虜にされているなら駄目
SIF CFLAG:対象:所属 == 勢力番号 && !GROUPMATCH(CFLAG:対象:捕虜先, 勢力番号, 0)
	RETURNF 0
;特殊勢力のキャラは駄目
SIF TALENT:対象:特殊勢力素質
	RETURNF 0
SIF IS_ANIMAL(対象)
	RETURNF 0
SIF CFLAG:対象:行動不能状態 == 行動不能_子供
	RETURNF 0
;MASTERは士官スタートなら抜ける
SIF 対象 == MASTER && GROUPMATCH(SP_COUNTRY_START:SP_COUNTRY_TO_CONST(勢力番号), 特殊勢力スタート_士官, 特殊勢力スタート_君主)
	RETURNF 0
;軟禁中はスキップ
SIF CFLAG:対象:捕虜先 == 勢力番号 && CFLAG:対象:監禁状態
	RETURNF 0
;自勢力以外をはじく設定
SIF CONFIG:81 == 1 && CFLAG:MASTER:所属 != 勢力番号
	RETURNF 0
;自勢力をはじく設定
SIF CONFIG:81 == 2 && CFLAG:MASTER:所属 == 勢力番号
	RETURNF 0
;テキスト非表示な設定ならスキップ
SIF	!TRAIN_IS_SHOW_TEXT(対象)
	RETURNF 0
;その勢力に所属するMASTER以外の堕ちていないキャラはスキップ
;綺麗なまま捕らえておきたい人向け
;	SIF !GETBIT(TALENT:対象:特殊勢力陥落系, COUNTRY_TO_CONST(勢力番号)) && CFLAG:対象:所属 == 勢力番号 && 対象 != MASTER
;		RETURNF 0
;対象勢力に所属して、しかも堕ちているキャラは2/3でカット（いちいち出るとうっとうしいので）
SIF CFLAG:対象:所属 == 勢力番号 && GETBIT(TALENT:対象:特殊勢力陥落系, SP_COUNTRY_TO_CONST(勢力番号)) && RAND:3
	RETURNF 0
RETURNF 1

;-------------------------------------------------
;対象が渡された勢力に寝返るかどうか判断する
;-------------------------------------------------	
@IS_BETRAY_TO_SP_COUNTRY(対象, 勢力番号)
#FUNCTION
#DIM 対象
#DIM 勢力番号
#DIM 勢力定数
勢力定数 = SP_COUNTRY_TO_CONST(勢力番号)
;堕ちていないなら、ターン数不足なら駄目
SIF !GETBIT(TALENT:対象:特殊勢力陥落系, 勢力定数) && SP_TRAIN_COUNT:対象:勢力定数 < TRAIN_FALLEN_TERM:3
	RETURNF 0
;堕ちていて、その勢力に士官しているならスキップ（既に寝返っているので。士官でない場合、一度よそに登用されるかなにかしているので例外）
SIF CFLAG:対象:所属 == 勢力番号 && GETBIT(TALENT:対象:特殊勢力陥落系, 勢力定数)
	RETURNF 0
RETURNF 1

;-------------------------------------------------
;渡されたキャラが特殊勢力の陥落系素質を持っているかを判断する
;-------------------------------------------------	
@IS_FALLEN_TO_SP_COUNTRY(対象)
#FUNCTION
#DIM 対象
RETURNF (TALENT:対象:特殊勢力陥落系 != 0)

;-------------------------------------------------
;主人公が特殊勢力に陥落した場合の処理
;-------------------------------------------------	
@PLAYER_FALLEN_TO_SP_COUNTRY(勢力)
#DIM 勢力
FOR LOCAL, 0, CHARANUM
	IF CFLAG:LOCAL:所属 == 勢力
		TALENT:LOCAL:合意 = 1
		CFLAG:LOCAL:好感度 = MAX(CFLAG:LOCAL:好感度, 500)
		CFLAG:LOCAL:従属度 = MAX(CFLAG:LOCAL:好感度, 500)
		CFLAG:LOCAL:依存度 = MAX(CFLAG:LOCAL:好感度, 500)
	ENDIF
NEXT

;-------------------------------------------------
;渡されたキャラが特殊勢力のキャラであるかどうかを判断する
;-------------------------------------------------	
@IS_SP_COUNTRY_CHARA(対象)
#FUNCTION
#DIM 対象
RETURNF TALENT:対象:特殊勢力素質


;---------------------------------------------------
;与えられた勢力番号を、対応する定数割り当てに変換する
;勢力番号だけ分かる状態で定数割り当てが拾いたいときに
;---------------------------------------------------
@SP_COUNTRY_TO_CONST(勢力番号)
#FUNCTION
#DIM 勢力番号
SIF 勢力番号 < 0 || 勢力番号 >= MAX_COUNTRY
	RETURNF 0
RETURNF (FINDELEMENT(SP_COUNTRY_ID, COUNTRY_EVENT_ID:勢力番号) != -1 ? FINDELEMENT(SP_COUNTRY_ID, COUNTRY_EVENT_ID:勢力番号) # 0)

;---------------------------------------------------
;与えられたEVENTIDを、対応する定数割り当てに変換する
;おそらく使わない
;---------------------------------------------------
@SP_COUNTRY_ID_TO_COSNT(EVENTID)
#FUNCTION
#DIM EVENTID
SIF GET_COUNTRY_FROM_ID(EVENTID) != -1
	RETURNF SP_COUNTRY_TO_CONST(GET_COUNTRY_FROM_ID(EVENTID))
RETURNF 0


;---------------------------------------------------
;蜂起するか判定する汎用関数
;---------------------------------------------------
@CHECK_SP_COUNTRY_RISE()
#FUNCTION
#DIM 勢力数
#DIM 基礎確率

基礎確率 = 50
勢力数 = 0

SIF DAY < SLG_PP:1
	RETURNF 0

基礎確率 += (DAY - SLG_PP:1) * 10

FOR LOCAL, 0, MAX_COUNTRY
	SIF IS_SP_COUNTRY(LOCAL) && COUNTRY_EVENT_ID:LOCAL != SP_COUNTRY_ID:特殊勢力_野盗
		勢力数 ++
NEXT

基礎確率 /= (勢力数 + 1)

RETURNF 基礎確率 > RAND:1000


;-------------------------------------------------
;desc  :指定したIDの特殊勢力を生成する
;param :勢力ID:COUNTRY_EVENT_ID
;return:0:生成失敗
;return:勢力番号
;-------------------------------------------------
@CREATE_SP_COUNTRY(勢力定数)
#DIM 勢力定数
#DIM 勢力

勢力 = GET_NEW_COUNTRY()

SIF 勢力 == -1
	RETURN 0

SELECTCASE 勢力定数
	CASE 特殊勢力_野盗
		COUNTRY_COLOR:勢力 = 0x800000
	CASE 特殊勢力_ホフゴブリン
		COUNTRY_COLOR:勢力 = 0x2B5C1B
	CASE 特殊勢力_外来人
		COUNTRY_COLOR:勢力 = 0x483D8B
	CASE 特殊勢力_触手
		COUNTRY_COLOR:勢力 = 0xFF1493
	CASE 特殊勢力_自警団
		COUNTRY_COLOR:勢力 = 0x33CC66
	CASE 特殊勢力_サキュバス
		COUNTRY_COLOR:勢力 = 0x8B008B
	CASE 特殊勢力_狂信者
		COUNTRY_COLOR:勢力 = 0xB43104
ENDSELECT

COUNTRY_SOLDIER:勢力 = 2000 * POWER(2, SP_COUNTRY_RANK:勢力定数) + MIN(DAY / 2 * 1000, 10000)
COUNTRY_EVENT_ID:勢力 = SP_COUNTRY_ID:勢力定数
MONEY:勢力 = SUMARRAY(CITY_ECONOMY) / 500

;ボス生成
CALL ADD_SP_CHARA(勢力, 1)

COUNTRY_BOSS:勢力 = GET_ID(RESULT)

FOR LOCAL, 0, SP_COUNTRY_RANK:勢力定数 * 2
	CALL ADD_SP_CHARA(勢力, 0, LOCAL)
NEXT

RETURN 勢力


;-------------------------------------------------
;desc  :特殊勢力キャラのひな型を追加する。ADD_VOID_CHARAのラッパー。
;param :勢力:勢力番号
;param :ボス:ボスを生成する
;param :ループ:通常キャラ生成時、FORループのループ変数を渡す（野盗A, BのAとかBとかの部分の処理用）
;return:生成したキャラ番号
;-------------------------------------------------
@ADD_SP_CHARA(勢力, ボス, ループ)
#DIM 勢力
#DIM 勢力ID
#DIM 勢力定数
#DIM ボス
#DIM ループ
#DIM 対象

勢力ID = COUNTRY_EVENT_ID:勢力
勢力定数 = SP_COUNTRY_TO_CONST(勢力)

CALL ADD_VOID_CHARA()
対象 = RESULT

;名前設定
IF ボス
	SELECTCASE SP_COUNTRY_BOSSNAME:勢力定数
		CASE 0
			SELECTCASE 勢力定数
				CASE 特殊勢力_野盗
					NAME:対象     = 野盗
					CALLNAME:対象 = 野盗
					CSTR:対象:1   = 野盗
					CSTR:対象:3   = ヤトウ
					CSTR:対象:4   = ヤトウ
					CSTR:対象:6   = ヤトウ
				CASE 特殊勢力_ホフゴブリン
					NAME:対象     = ホフゴブリン
					CALLNAME:対象 = ホフゴブリン
					CSTR:対象:1   = ホフゴブリン
					CSTR:対象:3   = ホフゴブリン
					CSTR:対象:4   = ホフゴブリン
					CSTR:対象:6   = ホフゴブリン
				CASE 特殊勢力_外来人
					NAME:対象     = 外来人
					CALLNAME:対象 = 外来人
					CSTR:対象:1   = 外来人
					CSTR:対象:3   = ガイライジン
					CSTR:対象:4   = ガイライジン
					CSTR:対象:6   = ガイライジン
				CASE 特殊勢力_触手
					NAME:対象     = 触手兵
					CALLNAME:対象 = 触手兵
					CSTR:対象:1   = 触手兵
					CSTR:対象:3   = ショクシュヘイ
					CSTR:対象:4   = ショクシュヘイ
					CSTR:対象:6   = ショクシュヘイ
				CASE 特殊勢力_自警団
					NAME:対象     = 自警団員
					CALLNAME:対象 = 自警団員
					CSTR:対象:1   = 自警団員
					CSTR:対象:3   = ジケイダンイン
					CSTR:対象:4   = ジケイダンイン
					CSTR:対象:6   = ジケイダンイン
				CASE 特殊勢力_サキュバス
					NAME:対象     = サキュバス
					CALLNAME:対象 = サキュバス
					CSTR:対象:1   = サキュバス
					CSTR:対象:3   = サキュバス
					CSTR:対象:4   = サキュバス
					CSTR:対象:6   = サキュバス
				CASE 特殊勢力_狂信者
					NAME:対象     = 狂信者
					CALLNAME:対象 = 狂信者
					CSTR:対象:1   = 狂信者
					CSTR:対象:3   = キョウシンシャ
					CSTR:対象:4   = キョウシンシャ
					CSTR:対象:6   = キョウシンシャ
			ENDSELECT
		CASE 1
			SELECTCASE 勢力定数
				CASE 特殊勢力_野盗
					NAME:対象     = 野盗首領
					CALLNAME:対象 = 野盗首領
					CSTR:対象:1   = 野盗首領
					CSTR:対象:3   = ヤトウシュリョウ
					CSTR:対象:4   = ヤトウシュリョウ
					CSTR:対象:6   = ヤトウシュリョウ
				CASE 特殊勢力_ホフゴブリン
					NAME:対象     = ゴブリンキング
					CALLNAME:対象 = ゴブリンキング
					CSTR:対象:1   = ゴブリンキング
					CSTR:対象:3   = ゴブリンキング
					CSTR:対象:4   = ゴブリンキング
					CSTR:対象:6   = ゴブリンキング
				CASE 特殊勢力_外来人
					NAME:対象     = 研究室長
					CALLNAME:対象 = 研究室長
					CSTR:対象:1   = 研究室長
					CSTR:対象:3   = ケンキュウシツチョウ
					CSTR:対象:4   = ケンキュウシツチョウ
					CSTR:対象:6   = ケンキュウシツチョウ
				CASE 特殊勢力_触手
					NAME:対象     = 触手兵長
					CALLNAME:対象 = 触手兵長
					CSTR:対象:1   = 触手兵長
					CSTR:対象:3   = ショクシュヘイチョウ
					CSTR:対象:4   = ショクシュヘイチョウ
					CSTR:対象:6   = ショクシュヘイチョウ
				CASE 特殊勢力_自警団
					NAME:対象     = 自警団長
					CALLNAME:対象 = 自警団長
					CSTR:対象:1   = 自警団長
					CSTR:対象:3   = ジケイダンチョウ
					CSTR:対象:4   = ジケイダンチョウ
					CSTR:対象:6   = ジケイダンチョウ
				CASE 特殊勢力_サキュバス
					NAME:対象     = サキュバスクイーン
					CALLNAME:対象 = サキュバスクイーン
					CSTR:対象:1   = サキュバスクイーン
					CSTR:対象:3   = サキュバスクイーン
					CSTR:対象:4   = サキュバスクイーン
					CSTR:対象:6   = サキュバスクイーン
				CASE 特殊勢力_狂信者
					NAME:対象     = 教祖
					CALLNAME:対象 = 教祖
					CSTR:対象:1   = 教祖
					CSTR:対象:3   = キョウソ
					CSTR:対象:4   = キョウソ
					CSTR:対象:6   = キョウソ
			ENDSELECT
		CASE 2
			TARGET = 対象
			IF 勢力定数 == 特殊勢力_サキュバス
				CALL SET_RANDOM_NAME_K(勢力定数)
			ELSE
				CALL SET_RANDOM_NAME_J(0)
				NAME:対象 = %CSTR:対象:1%
				CALLNAME:対象 = %CSTR:対象:1%
			ENDIF
		CASE 3
			NAME:対象 = %SP_COUNTRY_BOSSNAME_FIRST_NAME:勢力定数%
			CALLNAME:対象 = %SP_COUNTRY_BOSSNAME_FIRST_NAME:勢力定数%
			CSTR:対象:0 = %SP_COUNTRY_BOSSNAME_LAST_NAME:勢力定数%
			CSTR:対象:1 = %SP_COUNTRY_BOSSNAME_FIRST_NAME:勢力定数%
	ENDSELECT
ELSE
	SELECTCASE 勢力定数
		CASE 特殊勢力_野盗
			NAME:対象     = 野盗
			CALLNAME:対象 = 野盗
			CSTR:対象:1   = 野盗
			CSTR:対象:3   = ヤトウ
			CSTR:対象:4   = ヤトウ
			CSTR:対象:6   = ヤトウ
		CASE 特殊勢力_ホフゴブリン
			NAME:対象     = ホフゴブリン
			CALLNAME:対象 = ホフゴブリン
			CSTR:対象:1   = ホフゴブリン
			CSTR:対象:3   = ホフゴブリン
			CSTR:対象:4   = ホフゴブリン
			CSTR:対象:6   = ホフゴブリン
		CASE 特殊勢力_外来人
			NAME:対象     = 外来人
			CALLNAME:対象 = 外来人
			CSTR:対象:1   = 外来人
			CSTR:対象:3   = ガイライジン
			CSTR:対象:4   = ガイライジン
			CSTR:対象:6   = ガイライジン
		CASE 特殊勢力_触手
			NAME:対象     = 触手兵
			CALLNAME:対象 = 触手兵
			CSTR:対象:1   = 触手兵
			CSTR:対象:3   = ショクシュヘイ
			CSTR:対象:4   = ショクシュヘイ
			CSTR:対象:6   = ショクシュヘイ
		CASE 特殊勢力_自警団
			NAME:対象     = 自警団員
			CALLNAME:対象 = 自警団員
			CSTR:対象:1   = 自警団員
			CSTR:対象:3   = ジケイダンイン
			CSTR:対象:4   = ジケイダンイン
			CSTR:対象:6   = ジケイダンイン
		CASE 特殊勢力_サキュバス
			NAME:対象     = サキュバス
			CALLNAME:対象 = サキュバス
			CSTR:対象:1   = サキュバス
			CSTR:対象:3   = サキュバス
			CSTR:対象:4   = サキュバス
			CSTR:対象:6   = サキュバス
		CASE 特殊勢力_狂信者
			NAME:対象     = 狂信者
			CALLNAME:対象 = 狂信者
			CSTR:対象:1   = 狂信者
			CSTR:対象:3   = キョウシンシャ
			CSTR:対象:4   = キョウシンシャ
			CSTR:対象:6   = キョウシンシャ
	ENDSELECT
	NAME:対象     = %NAME:対象%%UNICODE(0xFF21 + ループ)%
	CALLNAME:対象 = %CALLNAME:対象%%UNICODE(0xFF21 + ループ)%
ENDIF

MAXBASE:対象:体力 = 3000
MAXBASE:対象:気力 = 3000
MAXBASE:対象:精神力 = 1500
BASE:対象:体力 = MAXBASE:対象:体力
BASE:対象:気力 = MAXBASE:対象:気力
BASE:対象:精神力 = MAXBASE:対象:精神力
CFLAG:対象:所属 = 勢力

TALENT:対象:絶倫 = 1
TALENT:対象:特殊勢力素質 = 勢力定数
TALENT:対象:陰毛現在値 = 陰毛_標準
TALENT:対象:陰毛目標値 = 陰毛_標準
TALENT:対象:Ｖ締まり = GET_DEFAULT_TIGHTNESS("普通")
TALENT:対象:Ａ締まり = GET_DEFAULT_TIGHTNESS("普通")

IF ボス
	SIF GROUPMATCH(勢力定数, 特殊勢力_サキュバス)
		TALENT:対象:性別 = 2
ELSE
	IF 勢力定数 == 特殊勢力_サキュバス
		SELECTCASE RAND:100
			CASE IS < 40
				TALENT:対象:性別 = 1
			CASE IS < 80
				TALENT:対象:性別 = 2
			CASE IS < 90
				TALENT:対象:性別 = 4
			CASE IS < 100
				TALENT:対象:性別 = 5
		ENDSELECT
	ELSEIF GROUPMATCH(勢力定数, 特殊勢力_外来人, 特殊勢力_自警団, 特殊勢力_触手, 特殊勢力_狂信者)
		SELECTCASE RAND:100
			CASE IS < 60
				TALENT:対象:性別 = 0
			CASE IS < 90
				TALENT:対象:性別 = 1
			CASE IS < 100
				TALENT:対象:性別 = 2
		ENDSELECT
	ENDIF
ENDIF

TARGET = 対象
SIF IS_FEMALE(対象)
	CALL RANDOM_CHARA_BUSTSIZE()
SIF HAS_PENIS(対象)
	CALL RANDOM_CHARA_PENISSIZE()


IF 勢力定数 == 特殊勢力_外来人
	CALL SKILL_LEARN(対象, FINDELEMENT(SKILL_GENRE_ENG, "BASE"), SKILL_SP_COUNTRY_NO:勢力定数, 0)
ELSE
	CALL SKILL_LEARN(対象, FINDELEMENT(SKILL_GENRE_ENG, "SP"), SKILL_SP_COUNTRY_NO:勢力定数, 0)
ENDIF

SELECTCASE 勢力定数
	CASE 特殊勢力_野盗
		ABL:対象:Ｃ感 = 3
		ABL:対象:欲望 = 5
		ABL:対象:性技 = 3
		ABL:対象:性知識 = 5
		ABL:対象:奉仕 = 3
		ABL:対象:性交 = 5
		ABL:対象:露出 = 2
		ABL:対象:射精 = 3
		ABL:対象:排泄 = 2
		ABL:対象:主導度Ｕ = 500
		ABL:対象:主導度Ｎ = 300
		ABL:対象:倒錯度 = 300
		EXP:対象:絶頂経験 = 500
	CASE 特殊勢力_ホフゴブリン
		ABL:対象:Ｃ感 = 4
		ABL:対象:欲望 = 6
		ABL:対象:性技 = 4
		ABL:対象:性知識 = 5
		ABL:対象:奉仕 = 2
		ABL:対象:性交 = 4
		ABL:対象:露出 = 1
		ABL:対象:射精 = 4
		ABL:対象:排泄 = 2
		ABL:対象:主導度Ｕ = 700
		ABL:対象:主導度Ｎ = 300
		ABL:対象:倒錯度 = 300
		EXP:対象:絶頂経験 = 500
	CASE 特殊勢力_外来人
		ABL:対象:Ｃ感 = 3
		IF IS_FEMALE(対象)
			ABL:対象:Ｂ感 = 2
			ABL:対象:Ｖ感 = 2
			ABL:対象:Ａ感 = 2
			ABL:対象:Ｍ感 = 3
		ENDIF
		ABL:対象:欲望 = 5
		ABL:対象:性技 = 4
		ABL:対象:性知識 = 5
		ABL:対象:奉仕 = 4
		ABL:対象:性交 = 2
		ABL:対象:露出 = 3
		IF HAS_PENIS(対象)
			ABL:対象:射精 = 3
		ENDIF
		ABL:対象:排泄 = 2
		ABL:対象:主導度Ｕ = 700
		ABL:対象:主導度Ｎ = 300
		ABL:対象:倒錯度 = 600
		EXP:対象:絶頂経験 = 500
	CASE 特殊勢力_触手
		ABL:対象:Ｃ感 = 4
		IF IS_FEMALE(対象)
			ABL:対象:Ｂ感 = 3
			ABL:対象:Ｖ感 = 4
		ENDIF
		ABL:対象:Ａ感 = 2
		ABL:対象:Ｍ感 = 3
		ABL:対象:欲望 = 7
		ABL:対象:性技 = 1
		ABL:対象:性知識 = 5
		ABL:対象:奉仕 = 2
		ABL:対象:性交 = 6
		ABL:対象:露出 = 1
		IF HAS_PENIS(対象)
			ABL:対象:射精 = 6
		ENDIF
		ABL:対象:噴乳 = 1
		ABL:対象:排泄 = 2
		ABL:対象:主導度Ｕ = 500
		ABL:対象:主導度Ｎ = 300
		ABL:対象:倒錯度 = 300
		EXP:対象:絶頂経験 = 500
	CASE 特殊勢力_自警団
		ABL:対象:Ｃ感 = 2
		IF IS_FEMALE(対象)
			ABL:対象:Ｂ感 = 2
			ABL:対象:Ｖ感 = 2
			ABL:対象:Ａ感 = 1
			ABL:対象:Ｍ感 = 2
		ENDIF
		ABL:対象:欲望 = 3
		ABL:対象:性技 = 2
		ABL:対象:性知識 = 3
		ABL:対象:奉仕 = 2
		ABL:対象:性交 = 1
		ABL:対象:露出 = 1
		IF HAS_PENIS(対象)
			ABL:対象:射精 = 2
		ENDIF
		ABL:対象:排泄 = 1
		ABL:対象:主導度Ｕ = RAND(-300, 300)
		ABL:対象:主導度Ｎ = RAND(-300, 300)
		ABL:対象:倒錯度 = RAND:300
		EXP:対象:絶頂経験 = 100
	CASE 特殊勢力_サキュバス
		ABL:対象:Ｃ感 = 3
		IF IS_FEMALE(対象)
			ABL:対象:Ｂ感 = 3
			ABL:対象:Ｖ感 = 3
		ENDIF
		ABL:対象:Ａ感 = 3
		ABL:対象:Ｍ感 = 3
		ABL:対象:欲望 = 5
		ABL:対象:性技 = 4
		ABL:対象:性知識 = 5
		ABL:対象:奉仕 = 4
		ABL:対象:性交 = 3
		ABL:対象:精愛 = 3
		ABL:対象:レズ = 3
		ABL:対象:露出 = 2
		ABL:対象:排泄 = 2
		ABL:対象:肝臓 = 3
		ABL:対象:主導度Ｕ = 500
		ABL:対象:主導度Ｎ = 300
		ABL:対象:倒錯度 = 300
		EXP:対象:絶頂経験 = 500
	CASE 特殊勢力_狂信者
		ABL:対象:Ｃ感 = 3
		ABL:対象:欲望 = 5
		IF IS_FEMALE(対象)
			ABL:対象:Ｂ感 = 3
			ABL:対象:Ｖ感 = 4
			ABL:対象:Ａ感 = 1
			ABL:対象:Ｍ感 = 3
		ENDIF
		ABL:対象:性技 = 4
		ABL:対象:性知識 = 5
		ABL:対象:奉仕 = 2
		ABL:対象:性交 = 5
		ABL:対象:露出 = 2
		IF HAS_PENIS(対象)
			ABL:対象:射精 = 4
		ENDIF
		ABL:対象:排泄 = 2
		ABL:対象:主導度Ｕ = 700
		ABL:対象:主導度Ｎ = 300
		ABL:対象:倒錯度 = 600
		EXP:対象:絶頂経験 = 500
ENDSELECT

ABL:対象:肝臓 = 3

;ベース100, 難度依存20(難度3で100)、ランダム期待値20 = 合計220
;四能力平均ちょい上
SELECTCASE 勢力定数
	CASE 特殊勢力_野盗
		ABL:対象:武闘 = 30 + 5 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:防衛 = 30 + 5 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:知略 = 20 + 5 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:政治 = 20 + 5 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
	CASE 特殊勢力_ホフゴブリン
		ABL:対象:武闘 = 40 + 6 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:防衛 = 30 + 5 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:知略 = 10 + 4 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:政治 = 20 + 5 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
	CASE 特殊勢力_外来人
		ABL:対象:武闘 = 10 + 4 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:防衛 = 20 + 4 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:知略 = 40 + 6 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:政治 = 30 + 6 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
	CASE 特殊勢力_触手
		ABL:対象:武闘 = 45 + 7 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:防衛 = 30 + 5 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:知略 = 15 + 4 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:政治 = 10 + 4 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
	CASE 特殊勢力_自警団
		ABL:対象:武闘 = 20 + 4 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:防衛 = 40 + 6 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:知略 = 20 + 5 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:政治 = 20 + 5 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
	CASE 特殊勢力_サキュバス
		ABL:対象:武闘 = 15 + 4 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:防衛 = 40 + 6 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:知略 = 30 + 6 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:政治 = 15 + 4 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
	CASE 特殊勢力_狂信者
		ABL:対象:武闘 = 20 + 4 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:防衛 = 20 + 4 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:知略 = 30 + 6 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
		ABL:対象:政治 = 30 + 6 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
ENDSELECT

ABL:対象:歌唱 = 10 + 3 * SP_COUNTRY_ABL_MODIFIER(勢力定数)
ABL:対象:料理 = 10 + 3 * SP_COUNTRY_ABL_MODIFIER(勢力定数)

ABL:対象:武闘 += RAND:10
ABL:対象:防衛 += RAND:10
ABL:対象:知略 += RAND:10
ABL:対象:政治 += RAND:10

IF ボス
	ABL:対象:武闘 += 10
	ABL:対象:防衛 += 10
	ABL:対象:知略 += 10
	ABL:対象:政治 += 10
	ABL:対象:歌唱 += 5
	ABL:対象:料理 += 5
ENDIF

SELECTCASE 勢力定数
	CASE 特殊勢力_野盗
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_上服, "服")
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_下服, "ズボン")
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_下着下, "トランクス")
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_靴下, "靴下")
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_靴, "靴")
	CASE 特殊勢力_ホフゴブリン
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_下着下, "腰布")
	CASE 特殊勢力_外来人
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_上服, "ワイシャツ")
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_下服, "スラックス")
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_上着, "白衣")
		IF IS_FEMALE(対象)
			CALL SET_CLOTH_BY_NAME(対象, 衣装部位_下着上, "ブラ")
			CALL SET_CLOTH_BY_NAME(対象, 衣装部位_下着下, "パンティ")
		ELSE
			CALL SET_CLOTH_BY_NAME(対象, 衣装部位_下着下, "トランクス")
		ENDIF
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_靴下, "靴下")
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_靴, "靴")
	CASE 特殊勢力_触手
		;何も着ない
	CASE 特殊勢力_自警団
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_上服, "服")
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_下服, "ズボン")
		IF IS_FEMALE(対象)
			CALL SET_CLOTH_BY_NAME(対象, 衣装部位_下着上, "ブラ")
			CALL SET_CLOTH_BY_NAME(対象, 衣装部位_下着下, "パンティ")
		ELSE
			CALL SET_CLOTH_BY_NAME(対象, 衣装部位_下着下, "トランクス")
		ENDIF
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_靴下, "靴下")
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_靴, "靴")
	CASE 特殊勢力_サキュバス
		;何も着ない
	CASE 特殊勢力_狂信者
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_上下服, "修道服")
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_靴下, "靴下")
		CALL SET_CLOTH_BY_NAME(対象, 衣装部位_靴, "靴")
ENDSELECT

RETURN 対象
