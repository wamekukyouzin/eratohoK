;-------------------------------------------------
;「スキル習得」のリスト１入力処理
;-------------------------------------------------
@CUSTOM_SKILL_SETTING(対象)
#DIM 対象
#DIM FIRST_LINE
#DIM ジャンル
VARSET SHOP_SKILL_PAGE, 1

CALL CUSTOM_SKILL_CREATE_LIST(対象)

REDRAW 0
FIRST_LINE = LINECOUNT

$SHOW_LOOP

CALL SINGLE_DRAWLINE
CALL CUSTOM_SKILL_PRINT_TABS(ジャンル)

IF ジャンル == SKILL_GENRE_NUM
	CALL CUSTOM_SKILL_PRINT_FORGET_LIST(対象)
ELSE
	CALL CUSTOM_SKILL_PRINT_LEARN_LIST(ジャンル,対象)
ENDIF
CALL SINGLE_DRAWLINE
PRINTBUTTON "[戻る]", 99999

INPUT 99999


SELECTCASE RESULT
	CASE 0 TO 3000
		IF ジャンル != SKILL_GENRE_NUM
			CALL CUSTOM_LEARN_SKILL(対象, ジャンル, RESULT)
		ELSE
			CALL CUSTOM_FORGET_SKILL(対象, RESULT)
		ENDIF
		CALL CUSTOM_SKILL_CREATE_LIST(対象)
CASE 4000 TO (4000 + SKILL_GENRE_NUM)
		ジャンル = RESULT - 4000
	;タブ切り替え処理
	CASE 5000
		SHOP_SKILL_PAGE:ジャンル --
	CASE 5001
		SHOP_SKILL_PAGE:ジャンル ++
	;戻る
	CASE 99999
		RETURN 0
ENDSELECT
CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

;----------------------------
;スキル習得をこころみる
;----------------------------
@CUSTOM_LEARN_SKILL(対象, ジャンル, 番号)
#DIM 対象
#DIM 教師
#DIM 番号
#DIM ジャンル
#DIM スキル
#DIM スキル位置
#DIM レベル

CALL CUSTOM_SKILL_LEARN(対象, ジャンル, SHOP_SKILL_NO_LIST:ジャンル:番号, SHOP_SKILL_ID_LIST:ジャンル:番号)

;いっぱいならだめ
IF RESULT == 0
	PRINTFORML (このジャンルのスキルは既に覚えきっています)
	PRINTFORMW (新たなものを覚えるには、何かを忘れる必要があります)
	RETURN 
ENDIF



;----------------------------
;スキル忘却をこころみる
;----------------------------
@CUSTOM_FORGET_SKILL(対象, 番号)
#DIM 対象
#DIM 番号
#DIM ジャンル
#DIM スキル
#DIM スキル位置
#DIM レベル
#DIMS スキル名

ジャンル = 番号 / MAX_SKILL_SLOT
スキル = 番号 % MAX_SKILL_SLOT

;該当スキルの習得判定
IF CUSTOM_SKILL_NO:対象:ジャンル:スキル < 0 || CUSTOM_SKILL_ID:対象:ジャンル:スキル < 0
	PRINTFORML (そのスキルは覚えていません)
	RETURN
ENDIF

CUSTOM_SKILL_NO:対象:ジャンル:スキル = -1
CUSTOM_SKILL_ID:対象:ジャンル:スキル = -1

;----------------------------
;スキルタブ描画
;----------------------------
@CUSTOM_SKILL_PRINT_TABS(ジャンル)
#DIM ジャンル
FOR LOCAL, 0, SKILL_GENRE_NUM
	IF ジャンル == LOCAL
		SETCOLOR カラー_選択中
	ENDIF
	PRINTBUTTON @"[%SKILL_GENRE:LOCAL%]", 4000 + LOCAL
	PRINT  
	RESETCOLOR
NEXT
IF ジャンル == LOCAL
	SETCOLOR カラー_選択中
ENDIF
PRINTBUTTON @"[忘れる]", 4000 + LOCAL
RESETCOLOR
PRINTL

;----------------------------
;習得可能スキル描画
;----------------------------
@CUSTOM_SKILL_PRINT_LEARN_LIST(ジャンル, 対象)
#DIM 対象
#DIM ジャンル
#DIM スキル
#DIM 最大ページ
#DIM CONST 項目数 = 10
#DIM 空きスロット
#DIM レベル
#DIM 描画行

最大ページ = SHOP_SKILL_NUM:ジャンル / 項目数 + 1

IF SHOP_SKILL_PAGE:ジャンル <= 0
	SHOP_SKILL_PAGE:ジャンル = 最大ページ
ELSEIF SHOP_SKILL_PAGE:ジャンル > 最大ページ
	SHOP_SKILL_PAGE:ジャンル = 1
ENDIF

空きスロット = CUSTOM_GET_EMPTY_SKILL_SLOT(対象, ジャンル)

CALL SINGLE_DRAWLINE
PRINTFORM -スキルの習得-
IF 空きスロット == -1
	CALL COLORPRINT(@"  このジャンルのスキルスロットがいっぱいです", カラー_注意, "L")
ELSE
	PRINTL
ENDIF
CALL SINGLE_DRAWLINE

描画行 = 0
FOR スキル, (SHOP_SKILL_PAGE:ジャンル - 1) * 項目数, MIN(SHOP_SKILL_PAGE:ジャンル * 項目数, SHOP_SKILL_NUM:ジャンル)

	CALLFORM SKILL_{SHOP_SKILL_NO_LIST:ジャンル:スキル}_%SKILL_GENRE_ENG:ジャンル%_{SHOP_SKILL_ID_LIST:ジャンル:スキル}_LEVEL
	レベル = RESULT

	CALL SINGLE_DRAWLINE

	;習得ボタンは一番左
	IF 空きスロット == -1
		SETCOLOR カラー_選択不可
		PRINTPLAIN [習得]
		RESETCOLOR
	ELSE
		PRINTBUTTON "[習得]", スキル
	ENDIF
	;項目描画
	CALL CUSTOM_SKILL_EXPLANATION(ジャンル, SHOP_SKILL_NO_LIST:ジャンル:スキル, SHOP_SKILL_ID_LIST:ジャンル:スキル)
	描画行 ++
NEXT

IF 項目数 - 描画行 > 0
	FOR LOCAL, 描画行, 項目数
		PRINTL
	NEXT
ENDIF

CALL SINGLE_DRAWLINE
PRINTFORML {SHOP_SKILL_PAGE:ジャンル} / {最大ページ}
CALL SINGLE_DRAWLINE
PRINTBUTTON "[前のページ]", 5000
PRINTBUTTON "[次のページ]", 5001
PRINTL


;----------------------------
;忘却スキル描画
;----------------------------
@CUSTOM_SKILL_PRINT_FORGET_LIST(対象)
#DIM 対象
#DIM ジャンル
#DIM ジャンル数
#DIM スキル数
#DIM スキル
#DIM 最大ページ
#DIM CONST 項目数 = 10
#DIMS アクセサ
#DIMS キャラ名
#DIMS スキル名
#DIM レベル
#DIMS 説明
#DIM 描画行

ジャンル数 = SKILL_GENRE_NUM

;ページ数出す用
スキル数 = 0
FOR ジャンル, 0, ジャンル数
	FOR スキル, 0, MAX_SKILL_SLOT
		SIF CUSTOM_SKILL_NO:対象:ジャンル:スキル >= 0 && CUSTOM_SKILL_ID:対象:ジャンル:スキル >= 0
			スキル数 ++
	NEXT
NEXT

最大ページ = スキル数 / 項目数 + 1

IF SHOP_SKILL_PAGE:ジャンル数 <= 0
	SHOP_SKILL_PAGE:ジャンル数 = 最大ページ
ELSEIF SHOP_SKILL_PAGE:ジャンル数 > 最大ページ
	SHOP_SKILL_PAGE:ジャンル数 = 1
ENDIF

CALL SINGLE_DRAWLINE
PRINTFORML -スキルの忘却-
CALL SINGLE_DRAWLINE

描画行 = 0
FOR ジャンル, 0, SKILL_GENRE_NUM
	FOR スキル, 0, MAX_SKILL_SLOT

		;存在判定
		TRYCCALLFORM SKILL_{CUSTOM_SKILL_NO:対象:ジャンル:スキル}_%SKILL_GENRE_ENG:ジャンル%_{CUSTOM_SKILL_ID:対象:ジャンル:スキル}_EXIST
		CATCH
			CONTINUE
		ENDCATCH

		CALL SINGLE_DRAWLINE
		PRINTBUTTON "[忘却]", ジャンル * MAX_SKILL_SLOT + スキル
		CALL CUSTOM_SKILL_EXPLANATION(ジャンル, CUSTOM_SKILL_NO:対象:ジャンル:スキル, CUSTOM_SKILL_ID:対象:ジャンル:スキル)
		PRINTL
		描画行 ++
	NEXT
NEXT

IF 項目数 - 描画行 > 0
	FOR LOCAL, 描画行, 項目数
		PRINTL
	NEXT
ENDIF

CALL SINGLE_DRAWLINE
PRINTFORML {SHOP_SKILL_PAGE:ジャンル数} / {最大ページ}
CALL SINGLE_DRAWLINE
PRINTBUTTON "[前のページ]", 5000
PRINTBUTTON "[次のページ]", 5001
PRINTL


;----------------------------
;あるキャラが習得できるスキルリスト生成
;----------------------------
@CUSTOM_SKILL_CREATE_LIST(対象)
#DIM キャラ
#DIM ジャンル
#DIM スキルID
#DIM 対象
#DIM 対象スキル
VARSET SHOP_SKILL_NO_LIST
VARSET SHOP_SKILL_ID_LIST
VARSET SHOP_SKILL_NUM

FOR キャラ, 0, MAX_FIXED_CHARA
	;0番は汎用なのでスキップ　本人もスキップ
	IF キャラ != 0
		SIF !EXISTCSV(キャラ)	
			CONTINUE
	ENDIF
	FOR ジャンル, 0, SKILL_GENRE_NUM
		FOR スキルID, 0, MAX_SKILLS
			TRYCCALLFORM SKILL_{キャラ}_%SKILL_GENRE_ENG:ジャンル%_{スキルID}_EXIST
				;所持してたらダメ
				SIF CUSTOM_CHECK_SKILL_DUPLICATE(対象, ジャンル, キャラ, スキルID)
					GOTO CANCEL
				SHOP_SKILL_NO_LIST:ジャンル:(SHOP_SKILL_NUM:ジャンル) = キャラ
				SHOP_SKILL_ID_LIST:ジャンル:(SHOP_SKILL_NUM:ジャンル) = スキルID
				SHOP_SKILL_NUM:ジャンル ++
			CATCH
				$CANCEL
			ENDCATCH
		NEXT
	NEXT
NEXT

CALL SINGLE_DRAWLINE

;---------------------------------------
;指定したスキルをもう覚えているかを調べる
;---------------------------------------
@CUSTOM_CHECK_SKILL_DUPLICATE(対象, ジャンル, スキルNO, スキルID)
#FUNCTION
#DIM 対象
#DIM ジャンル
#DIM スキルID
#DIM スキルNO
#DIM スキル

FOR スキル, 0, MAX_SKILL_SLOT
	SIF CUSTOM_SKILL_NO:対象:ジャンル:スキル == スキルNO && CUSTOM_SKILL_ID:対象:ジャンル:スキル == スキルID
		RETURNF 1
NEXT

RETURNF 0

;---------------------------------
;空きスロットにスキルのセット
;習得できれば1が戻る　空きがなければ0
;---------------------------------
@CUSTOM_SKILL_LEARN(対象, ジャンル, スキルNO, スキルID)
#DIM 対象
#DIM ジャンル
#DIM スキルNO
#DIM スキルID
#DIM 空きスロット

;いっぱいならだめ
空きスロット = CUSTOM_GET_EMPTY_SKILL_SLOT(対象, ジャンル)
SIF 空きスロット == -1
	RETURN 0

;重複してたらダメでしょ
SIF CUSTOM_CHECK_SKILL_DUPLICATE(対象, ジャンル, スキルNO, スキルID)
	RETURN 0

CUSTOM_SKILL_NO:対象:ジャンル:空きスロット = スキルNO
CUSTOM_SKILL_ID:対象:ジャンル:空きスロット = スキルID

RETURN 1

;---------------------------------
;空きスロットの取得　空きがなければ-1
;---------------------------------
@CUSTOM_GET_EMPTY_SKILL_SLOT(対象, ジャンル)
#FUNCTION
#DIM 対象
#DIM ジャンル
#DIM 空きスロット
;空きスロットを探す
FOR 空きスロット, 0, MAX_SKILL_SLOT
	IF CUSTOM_SKILL_NO:対象:ジャンル:空きスロット < 0 && CUSTOM_SKILL_ID:対象:ジャンル:空きスロット < 0
		空きスロット --
		BREAK
	ENDIF
NEXT

;いっぱいならだめ
SIF 空きスロット == MAX_SKILL_SLOT
	RETURNF -1

RETURNF 空きスロット

;---------------------------------
;スキルの説明
;---------------------------------
@CUSTOM_SKILL_EXPLANATION(ジャンル, スキルNO, スキルID, スペース = 0)
#DIM ジャンル
#DIM スキルNO
#DIM スキルID
#DIMS アクセサ
#DIMS キャラ名
#DIMS スキル名
#DIMS レベル
#DIMS 説明
#DIMS 発動率
#DIM スペース
;描画内容を用意
アクセサ = SKILL_{スキルNO}_%SKILL_GENRE_ENG:ジャンル%_{スキルID}_
CALLFORM %アクセサ%NAME
スキル名 = %(STRLENS(RESULTS:0) > 20 ? TOHALF(RESULTS:0) # RESULTS:0)%
IF スキルNO == 0
	キャラ名 = 汎用
ELSEIF スキルNO > 10000
	キャラ名 = 特殊
ELSE
	キャラ名 = %CSVCSTR(スキルNO, 99)%
ENDIF
CALLFORM %アクセサ%LEVEL
レベル '= TOSTR(RESULT)
TRYCCALLFORM %アクセサ%CANT_TELL
	レベル '= "EX"
CATCH
ENDCATCH
CALLFORM %アクセサ%EXPLANATION
説明 = %RESULTS%

IF ジャンル == スキル_ジャンル_PASSIVE
	発動率 =  --
ELSE
	CALLFORM %アクセサ%RATE_SHOW
	IF TOINT(RESULTS) == 0
		発動率 = %RESULTS%
	ELSE
		発動率 = %DECIMAL_STRING(TOINT(RESULTS), 1)%
	ENDIF
ENDIF

PRINTFORML %スキル名, 35, LEFT%LV%レベル, 3, LEFT% 発動率%発動率, 6, LEFT%%SKILL_GENRE:ジャンル, 6, LEFT%%キャラ名, MAX_CHARANAME_LENGTH/2, LEFT%
PRINTFORML %TOSTR_SPACE(スペース)%%説明%


