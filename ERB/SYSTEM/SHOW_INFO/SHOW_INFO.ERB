;情報表示に関わる関数

@SHOW_INFO_WITH_UI(対象, ページ移動, 調教中 = 0)
#DIM 対象
#DIM ページ移動
#DIM 調教中
#DIM FIRST_LINE
#DIM 現好みLV
#DIM 経験数

FIRST_LINE = LINECOUNT

;主人公なら性癖設定画面の表示を禁止
SIF 対象 == MASTER && FLAG:能力表示モード == 1
	FLAG:能力表示モード = 0
;固定キャラ以外は口上デイリーの表示を禁止
SIF !IS_FIXED_CHARA(対象) && FLAG:能力表示モード == 7
	FLAG:能力表示モード = 0

;対象キャラの情報を表示
CALL SINGLE_DRAWLINE
CALL SHOW_INFO(対象, 調教中)
CALL SINGLE_DRAWLINE

REDRAW 0

PRINTBUTTON "10[戻る]", 10
PRINT   
CALL PRINTBUTTON_EX(" 0[基本]", 0, 1, FLAG:能力表示モード == 0)
CALL PRINTBUTTON_EX(" 1[性癖]", 1,対象 != MASTER, FLAG:能力表示モード == 1)
CALL PRINTBUTTON_EX(" 2[能力]", 2, 1, FLAG:能力表示モード == 2)
CALL PRINTBUTTON_EX(" 3[家系]", 3, 1, FLAG:能力表示モード == 3)
CALL PRINTBUTTON_EX(" 4[スキル]", 4, 1, FLAG:能力表示モード == 4)
CALL PRINTBUTTON_EX(" 5[衣装]", 5, 1, FLAG:能力表示モード == 5)
CALL PRINTBUTTON_EX(" 6[説明]", 6, 1, FLAG:能力表示モード == 6)
CALL PRINTBUTTON_EX(" 7[デイリー]", 7, IS_FIXED_CHARA(対象), FLAG:能力表示モード == 7)


IF ページ移動
	PRINTL
	PRINTBUTTON " 8[前のキャラ]", 8
	PRINTBUTTON " 9[次のキャラ]", 9
ENDIF
PRINTL 

$INPUT_LOOP
INPUT

IF FLAG:能力表示モード == 0
	IF INRANGE(RESULT - 100, 0, 11)
		INVERTBIT SHOW_INFO_FOLD, RESULT - 100
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ENDIF
ELSEIF FLAG:能力表示モード == 1
	;「性癖設定」専用の入力処理
	IF RESULT == 101
		FLAG:性癖設定ページ数 --
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ELSEIF RESULT == 102
		FLAG:性癖設定ページ数 ++
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ELSEIF RESULT >= 3000 && RESULT < 6000
		現好みLV = COM_TENDENCY:(対象):(RESULT - 3000)
		経験数 = COM_EXP:(対象):(RESULT - 3000) /10
		IF (現好みLV == 0 && 経験数 >= 10) || (現好みLV == 1 && 経験数 >= 35) || (現好みLV == 2 && 経験数 >= 100)
			COM_TENDENCY:(対象):(RESULT - 3000) ++
		ENDIF
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ELSEIF RESULT >= 6000 && RESULT < 9000
		IF COM_TENDENCY:(対象):(RESULT - 6000) >= 1
			COM_TENDENCY:(対象):(RESULT - 6000) --
		ENDIF
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ELSEIF GROUPMATCH(RESULT, 10000, 11000, 12000, 13000, 14000, 15000)
		CALL 性癖一括変更(対象, RESULT)
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ENDIF
ELSEIF FLAG:能力表示モード == 4 && RESULT >= 100
	LOCAL = RESULT - 100
	IF LOCAL == 1 && IS_COUNTRY(CFLAG:MASTER:所属) && CFLAG:MASTER:所属 == CFLAG:対象:所属 && !CFLAG:対象:捕虜先
		CALL SHOP_LIFE_SWAP_SKILL(対象)
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ENDIF
ELSEIF FLAG:能力表示モード == 7 && RESULT >= 100
	LOCAL = RESULT - 100
	IF LOCAL == 98
		CALL SET_ALL_KOJO_DAILY_EACH(対象, 0)
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ELSEIF LOCAL == 99
		CALL SET_ALL_KOJO_DAILY_EACH(対象, 1)
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ELSE
		printformw {local}
		CALL TOGGLE_KOJO_DAILY(対象, LOCAL)
		PRINTFORMW {RESULT}
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ENDIF
ELSEIF FLAG:能力表示モード == 5 && GROUPMATCH(CFLAG:MASTER:所属, CFLAG:対象:所属, CFLAG:対象:捕虜先)
	IF INRANGE(RESULT, 100, 100 + CLOTH_PART_NUM - 1)
		CALL CLOTH_CHANGE_MENU(対象, RESULT - 100)
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ENDIF
ENDIF

IF RESULT == 10
	REDRAW 1
	RETURN
ELSEIF RESULT >= 0 && RESULT <= 6 && FLAG:能力表示モード != RESULT && !(RESULT == 1 && 対象 == MASTER)
	FLAG:能力表示モード = RESULT
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ELSEIF RESULT == 7 && IS_FIXED_CHARA(対象)
	FLAG:能力表示モード = RESULT
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
;前のキャラ
ELSEIF RESULT == 8 && ページ移動
	LOCAL:0 = 0
	LOCAL:1 = 0
	WHILE 1
		LOCAL:2 = SHOP_CHARA_LIST:(LOCAL:0)
		IF LOCAL:1
			IF LOCAL:2 != -1
				対象 = SHOP_CHARA_LIST:(LOCAL:0)
				BREAK
			ENDIF
		ELSEIF LOCAL:2 == 対象
			LOCAL:1 = 1
		ENDIF
		LOCAL:0 --
		SIF LOCAL:0 < 0
			LOCAL:0 = CHARANUM - 1
	WEND
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
;後ろのキャラ
ELSEIF RESULT == 9 && ページ移動
	LOCAL:0 = 0
	LOCAL:1 = 0
	WHILE 1
		LOCAL:2 = SHOP_CHARA_LIST:(LOCAL:0)
		IF LOCAL:1
			IF LOCAL:2 != -1
				対象 = SHOP_CHARA_LIST:(LOCAL:0)
				BREAK
			ENDIF
		ELSEIF LOCAL:2 == 対象
			LOCAL:1 = 1
		ENDIF
		LOCAL:0 ++
		SIF LOCAL:0 >= CHARANUM
			LOCAL:0 = 0
	WEND
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
;★
ELSEIF RESULT == 1000
	FLAG:能力表示モード = 10
	RESTART
ENDIF
GOTO INPUT_LOOP

;-------------------------------------------------
;ARG:0番のキャラの性別情報を表示（右詰め）
;ARG:1が真なら一文字表示
;ARG:2は詰め方向(FALSE = 左, TRUE = 右)
;ARG:3は詰め字数
;-------------------------------------------------
@PRINT_SEX(ARG:0, ARG:1 = 0, ARG:2 = 0, ARG:3 = 0)
LOCAL:0 = GETCOLOR()

SELECTCASE TALENT:(ARG:0):性別
	CASE 1
		SETCOLOR カラー_女
		LOCALS:1 = \@(!ARG:1) ? 女性 # 女\@
	CASE 0
		SETCOLOR カラー_男
		IF !IS_ANIMAL(ARG:0)
			LOCALS:1 = \@(!ARG:1) ? 男性 # 男\@
		ELSE
			LOCALS:1 = \@(!ARG:1) ? オス # 雄\@
		ENDIF
	CASE 2
		SETCOLOR カラー_女双
		LOCALS:1 = \@(!ARG:1) ? 女性(双) # 双\@
	CASE 3
		SETCOLOR カラー_男双
		LOCALS:1 = \@(!ARG:1) ? 男性(双) # 双\@
	CASE 4
		SETCOLOR カラー_男の娘
		LOCALS:1 = \@(!ARG:1) ? 男の娘 # 男\@
	CASE 5
		SETCOLOR カラー_男の娘双
		LOCALS:1 = \@(!ARG:1) ? 男娘(双) # 双\@
	CASEELSE
		SETCOLOR カラー_性別不明
		LOCALS:1 = \@(!ARG:1) ? 不明 # ？\@
ENDSELECT

CALL SETCOLORBYSEX(ARG:0)
IF !ARG:2
	PRINTFORM %LOCALS:1, ARG:3, LEFT%
ELSE
	PRINTFORM %LOCALS:1, ARG:3, RIGHT%
ENDIF

SETCOLOR LOCAL:0

;-------------------------------------------------
;ARG:0番のキャラの情報を表示
;-------------------------------------------------
@SHOW_INFO(ARG:0, 調教中 = 0)
#DIM 調教中
IF FLAG:能力表示モード == 1
	CALL SHOW_TENDENCY_SETTING(ARG:0)
ELSEIF FLAG:能力表示モード == 2
	CALL SHOW_ABL_DETAIL(ARG:0)
ELSEIF FLAG:能力表示モード == 3
	CALL SHOW_PREGNANT_RECORD(ARG:0)
ELSEIF FLAG:能力表示モード == 4
	CALL SHOW_SKILLS(ARG:0)
ELSEIF FLAG:能力表示モード == 5
	CALL SHOW_CLOTH_SETTING(ARG:0, GROUPMATCH(CFLAG:MASTER:所属, CFLAG:(ARG:0):所属, CFLAG:(ARG:0):捕虜先) && !調教中)
ELSEIF FLAG:能力表示モード == 6
	PRINTFORML %NAME_FORMAL(ARG:0)%
	CALL SINGLE_DRAWLINE
	CALL SHOW_INFO_ERO_DESCRIPTION(ARG:0)
ELSEIF FLAG:能力表示モード == 7
	PRINTFORML %NAME_FORMAL(ARG:0)%の口上デイリー
	CALL SINGLE_DRAWLINE
	CALL LISTUP_KOJO_DAILY_EACH(ARG:0, 100)
	PRINTBUTTON "[全てオン]", 198
	PRINTL
	PRINTBUTTON "[全てオフ]", 199
	PRINTL
ELSEIF FLAG:能力表示モード == 10
	CALL SHOW_RELATION_LIST(ARG:0)
ELSE
	CALL SHOW_HEADER(ARG:0)
	IF 調教中
		;PRINTL ◇ 今回の調教 ◇
		CALL SMALL_TITLE("今回")
		CALL NEW_PRINT_ORGASM(ARG:0)
		PRINTL 
	ENDIF
	CALL SMALL_TITLE("基礎", 100)
	SIF !GETBIT(SHOW_INFO_FOLD, 0)
		CALL SHOW_BASE(ARG:0)
	PRINTL 
	CALL SMALL_TITLE("関係", 111)
	SIF !GETBIT(SHOW_INFO_FOLD, 11)
		CALL SHOW_RELATION(ARG:0)
	PRINTL 
	CALL SMALL_TITLE("戦闘", 101)
	SIF !GETBIT(SHOW_INFO_FOLD, 1)
		CALL NEW_PRINT_SLG_ABL(ARG:0)
	PRINTL 
	CALL SMALL_TITLE("閨事", 102)
	SIF !GETBIT(SHOW_INFO_FOLD, 2)
		CALL NEW_PRINT_ABL(ARG:0)
	PRINTL 
	CALL SMALL_TITLE("素質", 103)
	SIF !GETBIT(SHOW_INFO_FOLD, 3)
		CALL NEW_PRINT_TALENT(ARG:0)
	PRINTL 
	CALL SMALL_TITLE("経験", 104)
	SIF !GETBIT(SHOW_INFO_FOLD, 4)
		CALL NEW_PRINT_EXP(ARG:0)
	PRINTL 
	CALL SMALL_TITLE("親交", 105)
	SIF !GETBIT(SHOW_INFO_FOLD, 5)
		CALL SHOW_FRIENDSHIP(ARG:0)
	PRINTL 
	CALL SMALL_TITLE("初体験", 106)
	IF !GETBIT(SHOW_INFO_FOLD, 6)
		CALL NEW_PRINT_SEXUAL_EXPERIENCE(ARG:0)
		CALL SMALL_TITLE("最近")
		CALL NEW_PRINT_LAST_EXPERIENCE(ARG:0)
	ENDIF
	PRINTL 
	CALL SMALL_TITLE("陥落", 107)
	IF !GETBIT(SHOW_INFO_FOLD, 7)
		CALL NEW_PRINT_SPECIAL_TALENT(ARG:0)
		CALL SMALL_TITLE("淫乱")
		CALL NEW_PRINT_SEX_TALENT(ARG:0)
		CALL SMALL_TITLE("ﾃﾞｲﾘｰ")
		CALL NEW_PRINT_DAILY_TALENT(ARG:0)
		CALL SMALL_TITLE("特殊")
		CALL NEW_PRINT_FALLEN_TALENT(ARG:0)
	ENDIF
	PRINTL 
	CALL SMALL_TITLE("続柄", 108)
	SIF !GETBIT(SHOW_INFO_FOLD, 8)
		CALL SHOW_PARENTS(ARG:0)
	PRINTL 
	CALL SMALL_TITLE("身体", 109)
	SIF !GETBIT(SHOW_INFO_FOLD, 9)
		CALL SHOW_BODY(ARG:0)
	PRINTL 
	CALL SMALL_TITLE("他", 110)
	SIF !GETBIT(SHOW_INFO_FOLD, 10)
		CALL SHOW_OTHER_INFO(ARG:0)
ENDIF

;-------------------------------------------------
;キャラARG:0のスキル一覧表示
;-------------------------------------------------
@SHOW_SKILLS(ARG:0)
PRINTFORML ◆%ANAME(ARG:0)%のスキル◆
LOCAL:2 = 0
FOR LOCAL, 0, SKILL_GENRE_NUM
	FOR LOCAL:1, 0, MAX_SKILL_SLOT
		TRYCCALLFORM SKILL_{SKILL_NO_SLOT:(ARG:0):LOCAL:(LOCAL:1)}_%SKILL_GENRE_ENG:LOCAL%_{SKILL_ID_SLOT:(ARG:0):LOCAL:(LOCAL:1)}_EXIST
		CATCH
			CONTINUE
		ENDCATCH
		CALL COLOR_LINE
		SETCOLOR カラー_選択不可
		PRINT   ・ 
		RESETCOLOR
		CALL SKILL_EXPLANATION(LOCAL, SKILL_NO_SLOT:(ARG:0):LOCAL:(LOCAL:1), SKILL_ID_SLOT:(ARG:0):LOCAL:(LOCAL:1), 5)
		LOCAL:2 = 1
	NEXT
NEXT
IF !LOCAL:2
	PRINTFORML 
ELSE
	CALL SINGLE_DRAWLINE
	CALL PRINTBUTTON_EXL("[並べ替え]", 101, IS_COUNTRY(CFLAG:MASTER:所属) && CFLAG:MASTER:所属 == CFLAG:(ARG:0):所属 && !CFLAG:(ARG:0):捕虜先)
ENDIF

;-------------------------------------------------
;キャラARG:0の性癖設定画面  表示処理のみ
;-------------------------------------------------
@SHOW_TENDENCY_SETTING(ARG:0)
;1ページに表示するコマンド数の設定
LOCAL:5 = 18

;表示されないコマンドの数
LOCAL:6 = 48

;定義されているコマンドの数を数える
LOCAL:12 = 0
FOR LOCAL:0, 0, VARSIZE("TRAINNAME")
	IF TRAINNAME:(LOCAL:0) != ""
		LOCAL:12 ++
	ENDIF
NEXT

LOCAL:12 -= LOCAL:6

;ページ数の計算
FLAG:性癖設定最大ページ = MAX(LOCAL:12 - 1, 0) / LOCAL:5 + 1
FLAG:性癖設定ページ数 = LIMIT(FLAG:性癖設定ページ数, 1, FLAG:性癖設定最大ページ)

PRINTFORML ◆%ANAME(ARG:0)%の性癖設定◆
CALL SINGLE_DRAWLINE
PRINTL   各コマンドの実行回数の確認と好みLvの変更が可能です
PRINTL   好みLvが高いほど実行時に中毒充足のソースが多く追加され、おまかせ・逆調教時の実行確率が増加します
PRINTL   好みLvを上げるのに必要な実行回数は  Lv0→1:10回  Lv1→2:35回  Lv2→3:100回
CALL SINGLE_DRAWLINE
PRINTL       < コマンド名称 >      < プレイヤー >      < ターゲット >      <   第三者   >
PRINT 　　　　　　  　　　　　　　　　
PRINTBUTTON "[+]", 10000
PRINTBUTTON "[-]", 11000
PRINT 　　　　　　　
PRINTBUTTON "[+]", 12000
PRINTBUTTON "[-]", 13000
PRINT 　　　　　　　
PRINTBUTTON "[+]", 14000
PRINTBUTTON "[-]", 15000	
PRINTL 
LOCAL:13 = (FLAG:性癖設定ページ数 - 1) * LOCAL:5
LOCAL:14 = FLAG:性癖設定ページ数 * LOCAL:5
LOCAL:15 = 0
LOCAL:16 = 0
FOR LOCAL:0, 0, 290
	IF TRAINNAME:(LOCAL:0) != ""
		IF LOCAL:15 >= LOCAL:13 && LOCAL:15 < LOCAL:14
			PRINT   
			IF COM_EXP:(ARG:0):(LOCAL:0) + COM_EXP:(ARG:0):(LOCAL:0 + 1000) + COM_EXP:(ARG:0):(LOCAL:0 + 2000) >= 1
				PRINTFORM %TRAINNAME:(LOCAL:0), 20, RIGHT%
				CALL PRINT_TENDENCT_CHANGE_BUTTON(ARG:0, LOCAL:0)
				CALL PRINT_TENDENCT_CHANGE_BUTTON(ARG:0, LOCAL:0 + 1000)
				CALL PRINT_TENDENCT_CHANGE_BUTTON(ARG:0, LOCAL:0 + 2000)
			ELSE
				CALL CHECK_COM_KNOWLEDGE(ARG:0, LOCAL:0)
				IF RESULT
					SETCOLOR 0x808080
					PRINTFORM %TRAINNAME:(LOCAL:0), 20, RIGHT%        未経験    知識あり
				ELSE
					SETCOLOR 0x505050
					PRINTFORM %TRAINNAME:(LOCAL:0), 20, RIGHT%        未経験    知識なし
				ENDIF
				RESETCOLOR
			ENDIF
			PRINTL 
			LOCAL:16 ++
		ENDIF
		LOCAL:15 ++
	ENDIF
NEXT
FOR LOCAL:0, LOCAL:16, LOCAL:5
	PRINTL 
NEXT
CALL SINGLE_DRAWLINE
PRINT   
IF FLAG:性癖設定ページ数 >= 2
	PRINTBUTTON "101[←前のページ]", 101
ELSE
	SETCOLOR カラー_選択不可
	PRINT 101[←前のページ]
	RESETCOLOR
ENDIF
PRINTFORM         page{FLAG:性癖設定ページ数, 2, RIGHT}/{FLAG:性癖設定最大ページ, 2, LEFT}       
IF FLAG:性癖設定ページ数 < FLAG:性癖設定最大ページ
	PRINTBUTTON "102[次のページ→]", 102
ELSE
	SETCOLOR カラー_選択不可
	PRINT 102[次のページ→]
	RESETCOLOR
ENDIF
PRINTL 

;-------------------------------------------------
;性癖設定画面における好みLv変更ボタンの描画 ARG:0=キャラ番号、ARG:1=配列番号
;-------------------------------------------------
@PRINT_TENDENCT_CHANGE_BUTTON(ARG:0, ARG:1)
PRINTFORM {COM_EXP:(ARG:0):(ARG:1) / 10, 8, RIGHT}回 

SELECTCASE COM_TENDENCY:(ARG:0):(ARG:1)
	CASE 1
		SETCOLOR 0x00FF00
	CASE 2
		SETCOLOR カラー_オレンジ
	CASE 3
		SETCOLOR 0xFF00FF
ENDSELECT
PRINTFORM Lv{COM_TENDENCY:(ARG:0):(ARG:1)}
RESETCOLOR

LOCAL:0 = COM_TENDENCY:(ARG:0):(ARG:1)
LOCAL:1 = COM_EXP:(ARG:0):(ARG:1) / 10
IF (LOCAL:0 <= 0 && LOCAL:1 >= 10) || (LOCAL:0 <= 1 && LOCAL:1 >= 35) || (LOCAL:0 <= 2 && LOCAL:1 >= 100)
	PRINTBUTTON "[+]", ARG:1 + 3000
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAIN [+]
	RESETCOLOR
ENDIF
IF LOCAL:0 >= 1
	PRINTBUTTON "[-]", ARG:1 + 6000
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAIN [-]
	RESETCOLOR
ENDIF

;-------------------------------------------------
;ARG:0番のキャラの名前を表示
;-------------------------------------------------
@SHOW_HEADER(ARG:0)
;名前の表示
IF CSTR:(ARG:0):0 == "" && CSTR:(ARG:0):1 == "" && NAME:(ARG:0) == CALLNAME:(ARG:0)
	PRINTFORM 〇%ANAME(ARG:0), 30, LEFT%
ELSE
	;姓名
	IF !TALENT:(ARG:0):姓名逆転
		LOCALS:0 =%CSTR:(ARG:0):0%
		SIF CSTR:(ARG:0):5 != "" && CSTR:(ARG:0):0 != CSTR:(ARG:0):5
			LOCALS:0 = %LOCALS:0%(%TOHALF(CSTR:(ARG:0):5)%)
		SIF LOCALS:0 != ""
			LOCALS:0 = %LOCALS:0 + " "%
		LOCALS:0 = %LOCALS:0%%CSTR:(ARG:0):1%
		SIF CSTR:(ARG:0):6 != "" && CSTR:(ARG:0):1 != CSTR:(ARG:0):6
			LOCALS:0 = %LOCALS:0%(%TOHALF(CSTR:(ARG:0):6)%)
	ELSE
		LOCALS:0 =%CSTR:(ARG:0):1%
		SIF CSTR:(ARG:0):6 != "" && CSTR:(ARG:0):1 != CSTR:(ARG:0):6
			LOCALS:0 = %LOCALS:0%(%TOHALF(CSTR:(ARG:0):6)%)
		SIF LOCALS:0 != ""
			LOCALS:0 = %LOCALS:0 + " "%
		LOCALS:0 = %LOCALS:0%%CSTR:(ARG:0):0%
		SIF CSTR:(ARG:0):5 != "" && CSTR:(ARG:0):0 != CSTR:(ARG:0):5
			LOCALS:0 = %LOCALS:0%(%TOHALF(CSTR:(ARG:0):5)%)
	ENDIF

	;字
	IF CSTR:(ARG:0):2 != ""
		LOCALS:0 = %LOCALS:0% %CSTR:(ARG:0):2%
		SIF CSTR:(ARG:0):7 != ""
			LOCALS:0 = %LOCALS:0%(%TOHALF(CSTR:(ARG:0):7)%)
	ENDIF
	PRINTFORM 〇%LOCALS:0, 40, LEFT%
ENDIF

LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:(ARG:0):所属)
IF LOCAL:4 >= 0
	LOCALS:0 = %ANAME(LOCAL:4), MAX_CHARANAME_LENGTH, LEFT%
ELSE
	LOCALS:0 = %"----", MAX_CHARANAME_LENGTH, LEFT%
ENDIF
PRINTFORM 所属:%LOCALS:0%
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:(ARG:0):捕虜先)
IF LOCAL:4 >= 0
	LOCALS:0 = %ANAME(LOCAL:4)%
ELSE
	LOCALS:0 = %"----"%
ENDIF
IF LOCAL:4 >= 0
	IF CFLAG:(ARG:0):軟禁中 == 0
		LOCALS:0 = %LOCALS:0+"(監禁)"%
	ELSE
		LOCALS:0 = %LOCALS:0+"(軟禁)"%
	ENDIF
ENDIF
PRINTFORM 捕虜:%LOCALS:0, MAX_CHARANAME_LENGTH+6, LEFT%

CALL PRINT_SEX(ARG:0, 0, 0, 10)

LOCALS:0 = 区分:
IF ARG:0 != MASTER
	IF IS_FIXED_CHARA(ARG:0)
		SETCOLOR カラー_注意
		LOCALS:0 = 固定
	ELSEIF IS_RANDOM_CHARA(ARG:0)
		SETCOLOR カラー_シアン
		LOCALS:0 = 汎用
	ELSEIF IS_CHILD(ARG:0)
		SETCOLOR 0x00FF80
		LOCALS:0 = 子供
	ELSEIF TALENT:(ARG:0):カスタムキャラ
		SETCOLOR カラー_オレンジ
		LOCALS:0 = カスタム
	ELSE
		SETCOLOR カラー_オレンジ
		LOCALS:0 = 特殊
	ENDIF
ENDIF
PRINTFORM %LOCALS:0%
RESETCOLOR

PRINTL


CALL SHOW_TAG(ARG:0)
PRINTL

CALL SINGLE_DRAWLINE

;-------------------------------------------------
;ARG:0番のキャラのタグを表示
;-------------------------------------------------
@SHOW_TAG(ARG:0)
FOR LOCAL, 0, MAX_TAG
	SIF TAG:(ARG:0):LOCAL == -1
		CONTINUE
	PRINTPLAINFORM [%TAG_NAME:(TAG:(ARG:0):LOCAL)%] 
NEXT

;-------------------------------------------------
;ARG:0番のキャラの両親を表示
;-------------------------------------------------
@SHOW_PARENTS(ARG:0)

LOCAL:2 = ID_TO_CHARA(CFLAG:(ARG:0):父親)
LOCAL:3 = ID_TO_CHARA(CFLAG:(ARG:0):母親)
IF LOCAL:2 >= 0
	PRINTFORM 父:%ANAME(LOCAL:2), MAX_CHARANAME_LENGTH, LEFT%
;特殊キャラ
ELSE
	PRINTFORM 父:%ID_TO_SP_NAME(CFLAG:(ARG:0):父親), MAX_CHARANAME_LENGTH, LEFT%
ENDIF

IF LOCAL:3 >= 0
	PRINTFORM %SMALL_TITLE_SPACE(1)%母:%ANAME(LOCAL:3), MAX_CHARANAME_LENGTH, LEFT%
ELSE
	PRINTFORM %SMALL_TITLE_SPACE(1)%母:%ID_TO_SP_NAME(CFLAG:(ARG:0):母親), MAX_CHARANAME_LENGTH, LEFT%
ENDIF
PRINTL

;-------------------------------------------------
;ARG:0番のキャラのBASE関係を表示
;-------------------------------------------------
@SHOW_BASE(ARG:0)

PRINTFORM 体力:
CALL PRINT_GRADATIONBAR(BASE:(ARG:0):体力, MAXBASE:(ARG:0):体力, 10, 0xFF5E7E, , 1)
PRINTFORM {BASE:(ARG:0):体力,6, RIGHT}/{MAXBASE:(ARG:0):体力, 6, RIGHT}

PRINTFORM %SMALL_TITLE_SPACE(1)%昼主導度 
CALL PRINT_SLIDER(-1000, 1000, ABL:(ARG:0):主導度Ｎ, 30, 0xC040C0, 0x40C040)
PRINTFORM {ABL:(ARG:0):主導度Ｎ, 5} 

IF ABL:(ARG:0):主導度Ｎ <= -500
	PRINT 超弱気で
ELSEIF ABL:(ARG:0):主導度Ｎ <= -300
	PRINT 弱気で
ELSEIF ABL:(ARG:0):主導度Ｎ <= -100
	PRINT 落ち着いた
ELSEIF ABL:(ARG:0):主導度Ｎ < 100
	PRINT 友達感覚で
ELSEIF ABL:(ARG:0):主導度Ｎ < 300
	PRINT 元気のいい
ELSEIF ABL:(ARG:0):主導度Ｎ < 500
	PRINT 強気で
ELSE
	PRINT 超強気で
ENDIF

PRINTL 

PRINTFORM %SMALL_TITLE_SPACE()%気力:
CALL PRINT_GRADATIONBAR(BASE:(ARG:0):気力, MAXBASE:(ARG:0):気力, 10, 0x60A74E)
PRINTFORM {BASE:(ARG:0):気力,6, RIGHT}/{MAXBASE:(ARG:0):気力, 6, RIGHT}

PRINTFORM %SMALL_TITLE_SPACE(1)%夜倒錯度 
CALL PRINT_SLIDER(0, 1000, ABL:(ARG:0):倒錯度, 30, 0x4040C0, 0x4040C0)
PRINTFORM {ABL:(ARG:0):倒錯度, 5} 

IF ABL:(ARG:0):倒錯度 >= 800
	PRINT 変態的な
ELSEIF ABL:(ARG:0):倒錯度 >= 500
	PRINT 倒錯的な
ELSEIF ABL:(ARG:0):倒錯度 >= 300
	PRINT ちょっと刺激的な
ELSEIF ABL:(ARG:0):倒錯度 >= 100
	PRINT 好奇心旺盛な
ELSE
	PRINT 甘々な
ENDIF

PRINTL 

PRINTFORM %SMALL_TITLE_SPACE()%精神:
CALL PRINT_GRADATIONBAR(BASE:(ARG:0):精神力, MAXBASE:(ARG:0):精神力, 10, 0x5E9AFF)
PRINTFORM {BASE:(ARG:0):精神力,6, RIGHT}/{MAXBASE:(ARG:0):精神力, 6, RIGHT}

PRINTFORM %SMALL_TITLE_SPACE(1)%夜主導度 
CALL PRINT_SLIDER(-1000, 1000, ABL:(ARG:0):主導度Ｕ, 30, 0xC040C0, 0x40C040)
PRINTFORM {ABL:(ARG:0):主導度Ｕ, 5} 

IF ABL:(ARG:0):主導度Ｕ <= -500
	PRINT ドＭ
ELSEIF ABL:(ARG:0):主導度Ｕ <= -300
	PRINT Ｍ
ELSEIF ABL:(ARG:0):主導度Ｕ <= -100
	PRINT 微Ｍ
ELSEIF ABL:(ARG:0):主導度Ｕ < 100
	PRINT リバ
ELSEIF ABL:(ARG:0):主導度Ｕ < 300
	PRINT 微Ｓ
ELSEIF ABL:(ARG:0):主導度Ｕ < 500
	PRINT Ｓ
ELSE
	PRINT ドＳ
ENDIF

PRINTL 

;-------------------------------------------------
;ARG:0番のキャラの外交印象を表示
;-------------------------------------------------
@SHOW_RELATION(ARG:0)

SIF FLAG:観戦モード
	RETURN

LOCAL = GET_COUNTRY_BOSS(CFLAG:(ARG:0):所属)
LOCAL:1 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)

PRINTFORM %ANAME(MASTER), MAX_CHARANAME_LENGTH, LEFT%

SIF LOCAL != LOCAL:1 && LOCAL:1 != MASTER && LOCAL:1 != -1
	PRINTFORM %ANAME(LOCAL:1), MAX_CHARANAME_LENGTH, LEFT%

SIF IS_COUNTRY(CFLAG:(ARG:0):所属) && LOCAL != MASTER && LOCAL != (ARG:0) && LOCAL != -1
	PRINTFORM %ANAME(LOCAL), MAX_CHARANAME_LENGTH, LEFT%

PRINTL

PRINTFORM %SMALL_TITLE_SPACE()%好
CALL PRINT_GRADATIONBAR(REL_LIKE:(ARG:0):MASTER, 1500, 10, 0xFF5E7E)
PRINTFORM %TOSTR_SPACE(MAX_CHARANAME_LENGTH - 12)%

IF LOCAL != LOCAL:1 && LOCAL:1 != MASTER && LOCAL:1 != -1
	PRINTFORM 好
	CALL PRINT_GRADATIONBAR(REL_LIKE:(ARG:0):(LOCAL:1), 1500, 10, 0xFF5E7E)
	PRINTFORM %TOSTR_SPACE(MAX_CHARANAME_LENGTH - 12)%
ENDIF

IF IS_COUNTRY(CFLAG:(ARG:0):所属) && LOCAL != MASTER && LOCAL != (ARG:0) && LOCAL != -1
	PRINTFORM 好
	CALL PRINT_GRADATIONBAR(REL_LIKE:(ARG:0):LOCAL, 1500, 10, 0xFF5E7E)
	PRINTFORM %TOSTR_SPACE(MAX_CHARANAME_LENGTH - 12)%
ENDIF



PRINTL

PRINTFORM %SMALL_TITLE_SPACE()%嫌
CALL PRINT_GRADATIONBAR(REL_HATE:(ARG:0):MASTER, 1500, 10, 0x5E9AFF)
PRINTFORM %TOSTR_SPACE(MAX_CHARANAME_LENGTH - 12)%

IF LOCAL != LOCAL:1 && LOCAL:1 != MASTER && LOCAL:1 != -1
	PRINTFORM 嫌
	CALL PRINT_GRADATIONBAR(REL_HATE:(ARG:0):(LOCAL:1), 1500, 10, 0x5E9AFF)
	PRINTFORM %TOSTR_SPACE(MAX_CHARANAME_LENGTH - 12)%
ENDIF

IF IS_COUNTRY(CFLAG:(ARG:0):所属) && LOCAL != MASTER && LOCAL != (ARG:0) && LOCAL != -1
	PRINTFORM 嫌
	CALL PRINT_GRADATIONBAR(REL_HATE:(ARG:0):LOCAL, 1500, 10, 0x5E9AFF)
	PRINTFORM %TOSTR_SPACE(MAX_CHARANAME_LENGTH - 12)%
ENDIF

PRINTL



;↓バーが小さくてわかりにくいようでしたら戻してください
;（精神力ゲージの表示。捕虜時限定）
;IF CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):軟禁中 == 0 || CFLAG:(ARG:0):外交調教カウンタ
;	PRINTFORM   精神力 
;	CALL PRINT_SHORT_MIND(ARG:0)
;	PRINTL 
;ENDIF

;-------------------------------------------------
;ARG:0番のキャラとの親交の深さを表示
;-------------------------------------------------
@SHOW_FRIENDSHIP(ARG:0)

PRINTFORML 好感度:{CFLAG:(ARG:0):好感度, 12, LEFT}     依存度:{CFLAG:(ARG:0):依存度, 12, LEFT}     従属度:{CFLAG:(ARG:0):従属度, 12, LEFT}     支配度:{CFLAG:(ARG:0):支配度, 12, LEFT}

;-------------------------------------------------
;ARG:0番のキャラの肉体について表示
;-------------------------------------------------
@SHOW_BODY(ARG:0)

CALL SHOW_BODYSIZE(ARG:0)

PRINTFORM %SMALL_TITLE_SPACE()%
PRINTFORM 体格:%TOSTR_BODYSIZE(TALENT:(ARG:0):体格), 10, LEFT%
PRINTFORM 体型:%TOSTR_BODYTYPE(TALENT:(ARG:0):体型), 10, LEFT%
PRINTL

PRINTFORM %SMALL_TITLE_SPACE()%
SIF HAS_VAGINA(ARG:0)
	PRINTFORM 乳房:%TOSTR_BUSTSIZE(GET_BUSTSIZE(ARG:0)), 10, LEFT%
SIF HAS_PENIS(ARG:0)
	PRINTFORM 陰茎:%TOSTR_PENISSIZE(GET_PENISSIZE(ARG:0)), 10, LEFT%
PRINTFORM   尻:%TOSTR_HIPSIZE(GET_HIPSIZE(ARG:0)), 10, LEFT%

IF HAS_VAGINA(ARG:0)
	PRINTFORM 膣締:%TOSTR_TIGHTNESS(TALENT:(ARG:0):Ｖ締まり), 10, LEFT% 
ENDIF
PRINTFORM 尻締:%TOSTR_TIGHTNESS(TALENT:(ARG:0):Ａ締まり), 10, LEFT% 
PRINTFORM 陰毛:
CALL COLOR_PRINT(@"%GET_INMOU_NAME(TALENT:(ARG:0):陰毛現在値), 10, LEFT%", TALENT:(ARG:0):陰毛現在値 == 陰毛_ハート型 ? カラー_ピンク # GETDEFCOLOR())
PRINTFORM 腋毛:
CALL PRINT_ARMPIT(ARG:0)
PRINTL

PRINTFORM %SMALL_TITLE_SPACE()%髪色:
CALL PRINT_HAIR_COLOR(TALENT:(ARG:0):髪色)
PRINTFORM 髪長:%TOSTR_HAIR_LENGTH(TALENT:(ARG:0):髪の長さ), 10, LEFT% 
PRINTL

SIF HAS_VAGINA(ARG:0)
	PRINTFORML %SMALL_TITLE_SPACE()%危険日:%TOFULL(TOSTR(TALENT:(ARG:0):危険日))%, %TOFULL(TOSTR(TALENT:(ARG:0):危険日 + 5))%

;-------------------------------------------------
;ARG:0番のキャラのその他分類しにくいもんについて表示
;-------------------------------------------------

@SHOW_OTHER_INFO(ARG:0)

LOCAL:1 = 0
PRINTFORM 嗜好:
FOR LOCAL, 0, VARSIZE("SEXUAL_PREFERENCE_EXP")
	IF GETBIT(SEXUAL_PREFERENCE:(ARG:0):0, LOCAL)
		PRINTFORM <%SEXUAL_PREFERENCE_STR:LOCAL%>
		LOCAL:1 = 1
	ENDIF
NEXT
IF LOCAL:1 == 0
	PRINTFORM なし
ENDIF
PRINTL

PRINTFORM %SMALL_TITLE_SPACE()%タトゥ:
CALL PRINT_TATTOO(ARG:0)
PRINTL

PRINTFORM %SMALL_TITLE_SPACE()%ピアス:
CALL PRINT_PIERCE(ARG:0)
PRINTL 

LOCAL:0 = 0

IF TALENT:(ARG:0):妊娠
	SETCOLOR カラー_ピンク
	PRINTFORM %SMALL_TITLE_SPACE()%
	IF CFLAG:(ARG:0):行動不能状態 == 行動不能_臨月
		PRINTFORM 臨月:
	ELSE
		PRINTFORM 妊娠:
	ENDIF
	PRINTFORM \@ID_TO_CHARA(CFLAG:(ARG:0):子の父親) != -1 ? %ANAME(ID_TO_CHARA(CFLAG:(ARG:0):子の父親))% # %GET_SPERM_NAME(CFLAG:(ARG:0):子の父親)% \@
	PRINTFORM ({CFLAG:(ARG:0):妊娠期間}/\@ GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_良質母胎) ? 3 # 7\@)
	RESETCOLOR
	LOCAL:0 = 1
ENDIF

IF TALENT:(ARG:0):母乳体質
	IF !LOCAL:0
		PRINTFORM %SMALL_TITLE_SPACE()%
	ELSE
		PRINTFORM  
	ENDIF
	SETCOLOR カラー_ピンク
	PRINTFORM <母乳体質>
	SIF CFLAG:(ARG:0):一時的母乳体質
		PRINTFORM (一時的)
	LOCAL:0 = 1
	RESETCOLOR
ENDIF

SIF LOCAL:0
	PRINTL

;-------------------------------------------------
;ARG:0番のキャラの経験を表示
;-------------------------------------------------
@NEW_PRINT_EXP(ARG:0)
LOCAL:1 = 0
FOR LOCAL:0, 0, 100
	;並べ直したEXPをLOCAL:2に代入
	LOCAL:2 = GET_SORTED_EXP(LOCAL:0)

	;対応するEXPがなければスキップ
	IF LOCAL:2 < 0
		CONTINUE
	ENDIF

	IF EXP:(ARG:0):(LOCAL:2) > 0
		;改行判定
		IF LOCAL:1 > 0 && LOCAL:1 % 4 == 0
			PRINTL 
			PRINTFORM %SMALL_TITLE_SPACE()%
		;最初の一回目だけインデントなし
		ELSEIF LOCAL:1 == 0
			PRINTFORM 
		;改行しない場合
		ELSE
			PRINTFORM %SMALL_TITLE_SPACE(1)%
		ENDIF
		LOCAL:1 ++

		CALL VALUE_FORM, EXP:(ARG:0):(LOCAL:2), "経験"
		PRINTFORM %EXPNAME:(LOCAL:2), 12, LEFT%:%RESULTS, 6% 
	ENDIF
NEXT

IF LOCAL:1 == 0
	PRINTFORM なし
ENDIF
PRINTL 

;------------------------
;ARG:0番のキャラの初めての相手を表示
;-------------------------
@NEW_PRINT_SEXUAL_EXPERIENCE(ARG:0)

PRINTFORM Ｍ:%SEXUAL_EXPERIENCE:(ARG:0):初体験_キス, 27, LEFT% 

IF HAS_PENIS(ARG:0)
	PRINTFORM Ｐ:%SEXUAL_EXPERIENCE:(ARG:0):初体験_童貞, 18, LEFT% 
ENDIF

IF HAS_VAGINA(ARG:0)
	PRINTFORM Ｖ:%SEXUAL_EXPERIENCE:(ARG:0):初体験_処女, 18, LEFT% 
ENDIF

PRINTFORM Ａ:%SEXUAL_EXPERIENCE:(ARG:0):初体験_アナル処女, 18, LEFT% 
PRINTL

;------------------------
;ARG:0番のキャラの初めての相手を表示
;-------------------------
@NEW_PRINT_LAST_EXPERIENCE(ARG:0)

PRINTFORM Ｍ:%SEXUAL_LAST_EXPERIENCE:(ARG:0):初体験_キス, 27, LEFT% 

IF HAS_PENIS(ARG:0)
	PRINTFORM Ｐ:%SEXUAL_LAST_EXPERIENCE:(ARG:0):初体験_童貞, 18, LEFT% 
ENDIF

IF HAS_VAGINA(ARG:0)
	PRINTFORM Ｖ:%SEXUAL_LAST_EXPERIENCE:(ARG:0):初体験_処女, 18, LEFT% 
ENDIF

PRINTFORM Ａ:%SEXUAL_LAST_EXPERIENCE:(ARG:0):初体験_アナル処女, 18, LEFT% 
PRINTL

;-------------------------------------------------
;ARG:0番のキャラのSLG系能力を表示
;-------------------------------------------------
@NEW_PRINT_SLG_ABL(ARG:0)
;表示するABLの並び順リスト(10ごとに改行)
#DIM SHOW_LIST, 30
VARSET SHOW_LIST, -1
SHOW_LIST:0 = GETNUM(ABL, "武闘")
SHOW_LIST:1 = GETNUM(ABL, "防衛")
SHOW_LIST:2 = GETNUM(ABL, "知略")
SHOW_LIST:3 = GETNUM(ABL, "政治")
SHOW_LIST:4 = GETNUM(ABL, "妖術")
SHOW_LIST:5 = GETNUM(ABL, "料理")
SHOW_LIST:6 = GETNUM(ABL, "歌唱")
SHOW_LIST:7 = GETNUM(ABL, "野心")

PRINT 評価:
CALL TMP_PRINT_CHARA_STARS(ARG:0)

PRINTFORML %TOSTR_SPACE(7)%成長型: %TOSTR_GROWTYPE(TALENT:(ARG:0):300)%タイプ(%TOSTR_GROWTYPE_B(TALENT:(ARG:0):300)%に適正)

PRINTFORM %SMALL_TITLE_SPACE(1)%
FOR LOCAL:0, 0, VARSIZE("SHOW_LIST")
	;対応するABLがなければスキップ
	SIF SHOW_LIST:(LOCAL:0) < 0
		CONTINUE
	;改行
	IF LOCAL:0 > 0 && LOCAL:0 % 4 == 0
		PRINTL 
		PRINTFORM %SMALL_TITLE_SPACE(1)%
	ENDIF

	LOCAL:2 = SHOW_LIST:(LOCAL:0)

	;3桁までは右詰め、それ以上は左詰めで表示
	IF ABL:(ARG:0):(LOCAL:2) <= 99
		PRINTFORM %SMALL_TITLE_SPACE(1)%%ABLNAME:(LOCAL:2), 6, LEFT%:{ABL:(ARG:0):(LOCAL:2), 3} 
	ELSE
		PRINTFORM %SMALL_TITLE_SPACE(1)%%ABLNAME:(LOCAL:2), 6, LEFT%:{ABL:(ARG:0):(LOCAL:2), 4, LEFT}
	ENDIF
	;ランクづけ
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ARG:0):(LOCAL:2))
NEXT
PRINTL 
;PRINTFORML     性格: %TOSTR_CHARATYPE(TALENT:(ARG:0):301)%

;-------------------------------------------------
;ARG:0番のキャラの能力を表示
;-------------------------------------------------
@NEW_PRINT_ABL(ARG:0)
;表示するABLの並び順リスト(10ごとに改行)
#DIM SHOW_LIST, 100

VARSET SHOW_LIST, -1


SHOW_LIST:0 = 0
SHOW_LIST:1 = 1
SHOW_LIST:2 = 2
SHOW_LIST:3 = 3
SHOW_LIST:4 = 4
SHOW_LIST:5 = 12
SHOW_LIST:6 = 13
SHOW_LIST:7 = 14
SHOW_LIST:8 = 20
SHOW_LIST:9 = 21
SHOW_LIST:10 = 22
SHOW_LIST:11 = 23
SHOW_LIST:12 = 24
SHOW_LIST:13 = 25
SHOW_LIST:14 = 26
SHOW_LIST:15 = 27
SHOW_LIST:16 = 28
SHOW_LIST:17 = 29
SHOW_LIST:18 = 30
SHOW_LIST:19 = 31
SHOW_LIST:20 = 33
SHOW_LIST:21 = 34
SHOW_LIST:22 = 35
SHOW_LIST:23 = 32
FOR LOCAL:0, 0, 100
	;対応するABLがなければスキップ
	SIF SHOW_LIST:(LOCAL:0) < 0
		CONTINUE
	;改行
	IF LOCAL:0 > 0 && LOCAL:0 % 6 == 0
		PRINTL 
		PRINTFORM %SMALL_TITLE_SPACE()%
	;最初の1回目はインデントなし
	ELSEIF LOCAL:0 == 0
		PRINTFORM 
	;改行なし
	ELSE
		PRINTFORM %SMALL_TITLE_SPACE(1)%
	ENDIF

	LOCAL:2 = SHOW_LIST:(LOCAL:0)

	;3桁までは右詰め、それ以上は左詰めで表示
	IF ABL:(ARG:0):(LOCAL:2) <= 99
		PRINTFORM %ABLNAME:(LOCAL:2), 6, LEFT%:{ABL:(ARG:0):(LOCAL:2), 3} 
	ELSE
		PRINTFORM %ABLNAME:(LOCAL:2), 6, LEFT%:{ABL:(ARG:0):(LOCAL:2), 4, LEFT}
	ENDIF
	;ランクづけ
	IF LOCAL:2 == GETNUM(ABL, "性知識")
		CALL PRINT_ALPHABET_RANK(ランク_性知識, ABL:(ARG:0):(LOCAL:2))
	ELSE
		CALL PRINT_ALPHABET_RANK(ランク_その他, ABL:(ARG:0):(LOCAL:2))
	ENDIF
NEXT
PRINTL 

;-------------------------------------------------
;数値をスライダとして表示する関数
;ARG:0=スライダの最小値、ARG:1=スライダの最大値、ARG:2=スライダの値、ARG:3=スライダの幅、ARG:4=バーの色(正)、ARG:5=バーの色(負)
;-------------------------------------------------
@PRINT_SLIDER(ARG:0, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5)
;スライダの位置を四捨五入で求める
LOCAL:2 = LIMIT((ARG:2 - ARG:0) * (ARG:3 * 2 + 1) / ((ARG:1 - ARG:0) * 2), 0, ARG:3 - 1)

FOR LOCAL:0, 0, ARG:3
	IF LOCAL:0 == LOCAL:2
		PRINTFORM %UNICODE(0x2588)%
	ELSE
		LOCAL:3 = ARG:0 + (ARG:1 - ARG:0) * (LOCAL:0 * 2 + 1) / (ARG:3 * 2)

		IF LOCAL:3 >= 0
			SETCOLOR ARG:4
		ELSE
			SETCOLOR ARG:5
		ENDIF

		FONTSTYLE 4
		IF LOCAL:0 == 0
			PRINTFORM %UNICODE(0x25E3)%
		ELSEIF LOCAL:0 == ARG:3 - 1
			PRINTFORM %UNICODE(0x25E2)%
		ELSE
			PRINTFORM  
		ENDIF
		FONTSTYLE 0
	ENDIF
	RESETCOLOR
NEXT

;-------------------------------------------------
;ARG:0番のキャラの素質を表示
;-------------------------------------------------
@NEW_PRINT_TALENT(ARG:0)
#DIMS TN

LOCAL:1 = 0
FOR LOCAL:0, 1, 300
	IF TALENT:(ARG:0):(LOCAL:0)
		TN = %TALENTNAME:(LOCAL:0)%

		SIF TN == ""
			CONTINUE

		;非表示素質のスキップ
		IF !HAS_VAGINA(ARG:0) && (TN == "処女" || TN == "淫壷" || TN == "Ｖ敏感" || TN == "Ｖ鈍感")
			CONTINUE
		ELSEIF TN == "童貞" || TN == "ペニスサイズ"
			CONTINUE
		ELSEIF TN == "バストサイズ"
			CONTINUE
		ELSEIF HAS_VAGINA(ARG:0) && TN == "孕み可能"
			CONTINUE
		ELSEIF TN == "処女" || TN == "アナル処女" || TN == "キス未経験"　|| TN == "童貞"
			CONTINUE
		ELSEIF TN == "恋慕" || TN == "親愛" || TN == "服従" || TN == "隷属" || TN == "恋人" || TN == "烙印" || TN == "主人" || TN == "所有者"
			CONTINUE
		ENDIF

		IF (TN == "正妻" || TN == "妾") && IS_HUSBAND(ARG:0)
			TN = 夫
		ELSEIF TN == "処女" && TALENT:(ARG:0):(LOCAL:0) == 2
			TN = 無自覚非処女
		ELSEIF TN == "童貞" && TALENT:(ARG:0):(LOCAL:0) == 2
			TN = 無自覚非童貞
		ENDIF

		;改行判定
		LOCAL:3 = STRLENS(TN) + 3
		LOCAL:1 += LOCAL:3
		IF LOCAL:1 >= 95
			LOCAL:1 = LOCAL:3
			PRINTL 
			PRINTFORM %SMALL_TITLE_SPACE()%
		ENDIF

		PRINTFORM <%TN%> 
	ENDIF
NEXT
PRINTL 




;-------------------------------------------------
;ARG:0番の陥落・特殊勢力陥落・デイリー陥落素質を一覧表示
;-------------------------------------------------
@NEW_PRINT_SPECIAL_TALENT(ARG:0)
#DIM 表示
#DIMS TN
#DIM 表示素質 = GETNUM(TALENT, "恋慕"), GETNUM(TALENT, "親愛"), GETNUM(TALENT, "服従"), GETNUM(TALENT, "隷属"), GETNUM(TALENT, "恋人"), GETNUM(TALENT, "烙印"), GETNUM(TALENT, "主人"), GETNUM(TALENT, "所有者")
LOCAL:1 = 0
表示 = 0
FOR LOCAL:0, 0, VARSIZE("表示素質")
	IF TALENT:(ARG:0):(表示素質:LOCAL)
		TN = %TALENTNAME:(表示素質:LOCAL)%
		;改行判定
		LOCAL:3 = STRLENS(TN) + 3
		LOCAL:1 += LOCAL:3
		IF LOCAL:1 >= 95
			LOCAL:1 = LOCAL:3
			PRINTL 
			PRINTFORM %SMALL_TITLE_SPACE()%
		ENDIF
		PRINTFORM <%TN%> 
		表示 = 1
	ENDIF
NEXT
SIF !表示
	PRINTFORM なし
PRINTL

@NEW_PRINT_SEX_TALENT(ARG:0)
#DIM 表示
#DIMS TN
LOCAL:1 = 0
表示 = 0
FOR LOCAL:0, 0, VARSIZE("SEX_TALENT_NAME")
	IF GETBIT(TALENT:(ARG:0):淫乱系, LOCAL) && SEX_TALENT_NAME:(LOCAL:0) != ""
		TN = %SEX_TALENT_NAME:(LOCAL:0)%
		;改行判定
		LOCAL:3 = STRLENS(TN) + 3
		LOCAL:1 += LOCAL:3
		IF LOCAL:1 >= 95
			LOCAL:1 = LOCAL:3
			PRINTL 
			PRINTFORM %SMALL_TITLE_SPACE()%
		ENDIF
		PRINTFORM <%TN%> 
		表示 = 1
	ENDIF
NEXT
SIF !表示
	PRINTFORM なし
PRINTL

@NEW_PRINT_DAILY_TALENT(ARG:0)
#DIM 表示
#DIMS TN
LOCAL:1 = 0
表示 = 0
FOR LOCAL:0, 0, VARSIZE("DAILY_TALENT_NAME")
	IF GETBIT(TALENT:(ARG:0):デイリー系, LOCAL) && DAILY_TALENT_NAME:LOCAL != ""
		TN = %DAILY_TALENT_NAME:(LOCAL:0)%
		;改行判定
		LOCAL:3 = STRLENS(TN) + 3
		LOCAL:1 += LOCAL:3
		IF LOCAL:1 >= 95
			LOCAL:1 = LOCAL:3
			PRINTL 
			PRINTFORM %SMALL_TITLE_SPACE()%
		ENDIF
		PRINTFORM <%TN%> 
		表示 = 1
	ENDIF
NEXT
SIF !表示
	PRINTFORM なし
PRINTL

@NEW_PRINT_FALLEN_TALENT(ARG:0)
#DIM 表示
#DIMS TN
LOCAL:1 = 0
表示 = 0
FOR LOCAL:0, 0, MAX_SP_COUNTRY
	IF GETBIT(TALENT:(ARG:0):特殊勢力陥落系, LOCAL) && SP_FALLEN_TALENT_NAME:(LOCAL:0) != ""
		TN = %SP_FALLEN_TALENT_NAME:(LOCAL:0)%
		;改行判定
		LOCAL:3 = STRLENS(TN) + 3
		LOCAL:1 += LOCAL:3
		IF LOCAL:1 >= 95
			LOCAL:1 = LOCAL:3
			PRINTL 
			PRINTFORM %SMALL_TITLE_SPACE()%
		ENDIF
		PRINTFORM <%TN%> 
		表示 = 1
	ENDIF
NEXT
SIF !表示
	PRINTFORM なし
PRINTL

;-------------------------------------------------
;ARG:0番の今回の調教での射精・射精され・絶頂回数を表示
;-------------------------------------------------
@NEW_PRINT_ORGASM(対象)
#DIM 対象
#DIM 部位
#DIM 表示フラグ
表示フラグ = 0

FOR 部位, 0, CUM_SLOT_NUM
	SIF CUM_GET_COUNT:対象:部位 <= 0
		CONTINUE
	IF 表示フラグ == 0
		PRINTFORM 今回受け止めた精液:
		表示フラグ = 1
	ENDIF
	SIF GROUPMATCH(部位, 射精部位_膣内, 射精部位_アナル)
		SETCOLOR カラー_ピンク
	PRINTFORM %CUM_SLOT_NAME:部位, 8, RIGHT%:{CUM_GET_COUNT:対象:部位, 4, RIGHT}
	RESETCOLOR
NEXT
IF 表示フラグ
	PRINTL 
	PRINTFORM %SMALL_TITLE_SPACE()%
	表示フラグ = 0
ENDIF

FOR 部位, 0, CUM_SLOT_NUM
	SIF CUM_TO_COUNT:対象:部位 <= 0
		CONTINUE
	IF 表示フラグ == 0
		PRINTFORM 今回解き放った精液:
		表示フラグ = 1
	ENDIF

	SIF GROUPMATCH(部位, 射精部位_膣内, 射精部位_アナル)
		SETCOLOR カラー_ピンク
	PRINTFORM %CUM_SLOT_NAME:部位, 8, RIGHT%:{CUM_TO_COUNT:対象:部位, 4, RIGHT}
	RESETCOLOR
NEXT
IF 表示フラグ
	PRINTL 
	PRINTFORM %SMALL_TITLE_SPACE()%
	表示フラグ = 0
ENDIF

FOR 部位, 0, VARSIZE("EX")
	SIF EX:対象:部位 <= 0
		CONTINUE
	IF 表示フラグ == 0
		PRINTFORM 今回の絶頂強度:
		表示フラグ = 1
	ENDIF
	PRINTFORM %EXNAME:部位, 8, RIGHT%:{EX:対象:部位, 4, RIGHT}
NEXT
PRINTL

;-------------------------------------------------
;数値表示変更
;-------------------------------------------------
@VALUE_FORM(ARG, ARGS)
VARSET LOCAL, 0
VARSET LOCALS, ""

IF ARGS == "珠"
	IF ARG <= 9999999
		RESULTS = {ARG}
		RETURN
	ENDIF
ELSEIF ARGS == "経験"
	IF ARG <= 999999
		RESULTS = {ARG}
		RETURN
	ENDIF
ELSE
	IF ARG <= 99999999
		RESULTS = {ARG}
		RETURN
	ENDIF
ENDIF

LOCAL:0 = 10000000000000000
LOCAL:1 = 1000000000000
LOCAL:2 = 100000000
LOCAL:3 = 10000
LOCAL:4 = 1
LOCALS:0 = 京
LOCALS:1 = 兆
LOCALS:2 = 億
LOCALS:3 = 万

FOR LOCAL:98, 0, 4
	IF ARG >= LOCAL:(LOCAL:98)
		LOCAL:99 = LOCAL:98
		BREAK
	ENDIF
NEXT

LOCAL:10 = ARG / LOCAL:(LOCAL:99)
LOCAL:11 = (ARG % LOCAL:(LOCAL:99)) / LOCAL:(LOCAL:99 + 1)

IF ARGS == "珠" || ARGS == "経験"
	RESULTS = {LOCAL:10}%LOCALS:(LOCAL:99)%
ELSE
	RESULTS = {LOCAL:10}%LOCALS:(LOCAL:99)%{LOCAL:11}%LOCALS:(LOCAL:99 + 1)%
ENDIF

;-------------------------------------------------
;ARG:0番のキャラの能力成長状況を詳細表示
;-------------------------------------------------
@SHOW_ABL_DETAIL(ARG:0)
;表示するABLの並び順リスト(10ごとに改行)
#DIM SHOW_LIST, 100

VARSET SHOW_LIST, -1

SHOW_LIST:0 = 50
SHOW_LIST:1 = 51
SHOW_LIST:2 = 52
SHOW_LIST:3 = 53
SHOW_LIST:4 = 54
SHOW_LIST:5 = 60
SHOW_LIST:6 = 61
SHOW_LIST:10 = 0
SHOW_LIST:11 = 1
SHOW_LIST:12 = 2
SHOW_LIST:13 = 3
SHOW_LIST:14 = 4

SHOW_LIST:20 = 12
SHOW_LIST:21 = 13
SHOW_LIST:22 = 20
SHOW_LIST:23 = 21
SHOW_LIST:24 = 22
SHOW_LIST:25 = 23
SHOW_LIST:30 = 24
SHOW_LIST:31 = 25
SHOW_LIST:32 = 26
SHOW_LIST:33 = 27
SHOW_LIST:34 = 28
SHOW_LIST:35 = 29
SHOW_LIST:40 = 30
SHOW_LIST:41 = 31
SHOW_LIST:42 = 33
SHOW_LIST:43 = 34
SHOW_LIST:44 = 35
SHOW_LIST:45 = 32


PRINTFORML ◆%ANAME(ARG:0)%の能力成長状況◆
LOCAL:1 = 0
FOR LOCAL:0, 0, 100
	;対応するABLがなければスキップ
	IF SHOW_LIST:(LOCAL:0) >= 0
		;改行判定
		IF LOCAL:1 > 0 && LOCAL:1 % 2 == 0
			PRINTL 
		ENDIF
		LOCAL:1 ++

		LOCAL:2 = SHOW_LIST:(LOCAL:0)

		PRINTFORM %ABLNAME:(LOCAL:2), 8%:{ABL:(ARG:0):(LOCAL:2), 4}

		RESULT = 0
		;必要な経験値の計算
		TRYCALLFORM CALC_ABLUP_EXP{LOCAL:2}(ARG:0)
		IF RESULT >= 1 && LOCAL:2 < 9999
			LOCAL:5 = RESULT
			RESULTS = 
			;能力に対応する経験値の名称を取得
			TRYCALLFORM ABLUP_EXPNAME{LOCAL:2}
			LOCAL:6 = GETNUM(EXP, RESULTS)
			IF LOCAL:6 >= 0
				LOCAL:7 = EXP:(ARG:0):(LOCAL:6) * 20 / LOCAL:5
				IF LOCAL:7 >= 17
					LOCAL:8 = 0x00FF00
				ELSEIF LOCAL:7 >= 12
					LOCAL:8 = カラー_注意
				ELSE
					LOCAL:8 = 0xC0C0C0
				ENDIF
				FONTSTYLE 4
				FOR LOCAL:4, 0, 20
					IF LOCAL:4 < LOCAL:7
						SETCOLOR LOCAL:8
					ELSE
						SETCOLOR 0x404040
					ENDIF
					PRINTFORM  
				NEXT
				FONTSTYLE 0
				RESETCOLOR
			ENDIF
		ELSE
			SETCOLOR 0x8080D0
			FONTSTYLE 4
			PRINT        
			FONTSTYLE 0
			PRINT  最大 
			FONTSTYLE 4
			PRINT        
			FONTSTYLE 0
			RESETCOLOR
		ENDIF
		PRINT   
	ENDIF
NEXT
PRINTL 

;-------------------------------------------------
;ARG:0番の他キャラに対する好感度・敵対値を一覧表示
;-------------------------------------------------
@SHOW_RELATION_LIST(ARG:0)
FOR LOCAL:0, 0, CHARANUM
	PRINTFORM %ANAME(LOCAL:0), 8, RIGHT%
	PRINTFORM   {REL_LIKE:(ARG:0):(LOCAL:0), 5, RIGHT}
	PRINTFORM   {REL_HATE:(ARG:0):(LOCAL:0), 5, RIGHT}
	PRINTL 
NEXT
