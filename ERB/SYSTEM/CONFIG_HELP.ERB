@CREATE_HELP_MAP()
VARSET HELP_CONTENT_NAME
VARSET HELP_CONTENT_NAME_ENG
VARSET HELP_CONTENT_NUM

CALL REGISTER_HELP("ゲームの流れ"           , "ABOUT_GAME_FLOW")
CALL REGISTER_HELP("能力値の意味"           , "ABOUT_ABL")
CALL REGISTER_HELP("素質の意味"          	, "ABOUT_TALENT")
CALL REGISTER_HELP("拠点フェイズのコマンド" , "ABOUT_LIFE_PHASE_COMMAND")
CALL REGISTER_HELP("戦略フェイズのコマンド" , "ABOUT_SLG_PHASE_COMMAND")
CALL REGISTER_HELP("初心者向けガイド"       , "QUICKSTART")
CALL REGISTER_HELP("基本戦術"               , "BASIC_STRATEGY")
CALL REGISTER_HELP("士官を増やす"           , "GET_OFFICER")
CALL REGISTER_HELP("クールタイム"           , "ABOUT_COOLTIME")
CALL REGISTER_HELP("外交について"           , "ABOUT_DIPLOMACY")
CALL REGISTER_HELP("捕虜調教のやりかた"     , "HOW_TO_TRAIN_HOSTAGE")
CALL REGISTER_HELP("士官の陥落"             , "GET_TALENT")
CALL REGISTER_HELP("陥落素質"               , "SPECIAL_TALENT")
CALL REGISTER_HELP("デイリーイベント"       , "ABOUT_DAILY_EVENT")
CALL REGISTER_HELP("士官の鍛えかた"         , "HOW_TO_BUILD_CHARA")
CALL REGISTER_HELP("士官がすぐ怒って帰る"   , "THEY_GET_MAD")
CALL REGISTER_HELP("士官がすぐ気絶する"     , "THEY_EASYLY_GET_UNCONSCIOUS")
CALL REGISTER_HELP("好感度補正"             , "ABOUT_LOVE_POWER")
CALL REGISTER_HELP("士官が捕虜になった"     , "THEY_GET_CAPTURED")
CALL REGISTER_HELP("各勢力にすごく嫌われる" , "THEY_HATE_ME")
CALL REGISTER_HELP("特殊な勢力"             , "SP_COUNTRY")
CALL REGISTER_HELP("兵が足りない"           , "SOLDIER_DOESNT_INCREASE")
CALL REGISTER_HELP("外交要求が通らない"     , "THEY_WONT_ACCEPT_REQUEST")
CALL REGISTER_HELP("謀反と蜂起"             , "THEY_REBEL_AGAINST_ME")
CALL REGISTER_HELP("子育てについて"         , "HOW_TO_KOSODATE")
CALL REGISTER_HELP("注意すべきキャラ"       , "BEWARE_HER")

@REGISTER_HELP(HELPNAME, HELPNAME_ENG, HELPPAGENUM)
#DIMS HELPNAME
#DIMS HELPNAME_ENG
#DIM HELPPAGENUM
HELP_CONTENT_NAME:HELP_CONTENT_NUM '= HELPNAME
HELP_CONTENT_NAME_ENG:HELP_CONTENT_NUM '= HELPNAME_ENG
HELP_CONTENT_NUM ++
;-------------------------------------------------
;攻略情報メニュー
;-------------------------------------------------
@CONFIG_HELPMENU
#DIM FIRST_LINE
#DIM FIRST_LINE2

;今見てるメニューのページ
#DIM MENUPAGE
;今見てるヘルプのHELP_CONTENT_系変数におけるインデックス
#DIM HELPINDEX
;今見てるヘルプ項目のページ
#DIM HELPPAGE
;現在のREDRAWを保存
#DIM NOWREDRAW

;メニューのページ数
#DIM PAGENUM

;ヘルプの行数
#DIM MAXHELPLINE
;ページあたりのコンテンツ数
#DIM CONTENTPERPAGE

FIRST_LINE = LINECOUNT
MENUPAGE = 1
HELPINDEX = 0
HELPPAGE = 1
CURRENTREDRAW
NOWREDRAW = RESULT:0

CONTENTPERPAGE = 5
PAGENUM = (HELP_CONTENT_NUM - 1) / CONTENTPERPAGE + 1

;カスタマイズ用……ヘルプのページ当たり行数
MAXHELPLINE = 20

VARSET HELP_CONTENT_LINE
VARSET HELP_CONTENT_LINE_NUM

;初期化処理
CALLFORM PREPARE_HELP_CONTENT_%HELP_CONTENT_NAME_ENG:HELPINDEX%()

$SHOW_LOOP
REDRAW 0

CALL SINGLE_DRAWLINE
PRINT ●攻略情報●
PRINTBUTTON "    1000[前のページ]", 1000
PRINTFORM     ページ {MENUPAGE} / {PAGENUM}
PRINTBUTTON "    1001[次のページ]", 1001
PRINTL 

CALL SINGLE_DRAWLINE

FOR LOCAL, CONTENTPERPAGE * (MENUPAGE - 1), MIN(HELP_CONTENT_NUM, CONTENTPERPAGE * MENUPAGE)
	CALL HELP_COLORBUTTON(HELPINDEX, LOCAL, HELP_CONTENT_NAME:LOCAL)
NEXT

IF LINECOUNT - FIRST_LINE < CONTENTPERPAGE + 3
	LOCAL:1 = (CONTENTPERPAGE + 3) - (LINECOUNT - FIRST_LINE)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTL 
	NEXT
ENDIF

CALL SINGLE_DRAWLINE
FIRST_LINE2 = LINECOUNT
CALL SHOW_HELP_CONTENT_VIEW(HELPINDEX, HELPPAGE, MAXHELPLINE)

IF LINECOUNT - FIRST_LINE2 < MAXHELPLINE + 3
	LOCAL:1 = MAXHELPLINE - (LINECOUNT - FIRST_LINE2)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTL 
	NEXT
ENDIF
CALL SINGLE_DRAWLINE
PRINTBUTTON "    2000[前のページ]", 2000
PRINTFORM     ページ {HELPPAGE} / {(HELP_CONTENT_LINE_NUM -1 )/ MAXHELPLINE + 1}
PRINTBUTTON "    2001[次のページ]", 2001
PRINTL
CALL SINGLE_DRAWLINE
PRINTBUTTON " 999[      戻る]", 999
PRINTL 

$INPUT_LOOP
INPUT

IF RESULT == 999
	REDRAW NOWREDRAW
	CLEARLINE LINECOUNT - FIRST_LINE
	RETURN

ELSEIF RESULT == 1000
	MENUPAGE -= 1
	IF MENUPAGE < 1
		MENUPAGE = PAGENUM
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE

ELSEIF RESULT == 1001
	MENUPAGE += 1
	IF MENUPAGE > PAGENUM
		MENUPAGE = 1
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE

ELSEIF RESULT == 2000
	HELPPAGE --
	SIF HELPPAGE < 1
		HELPPAGE = (HELP_CONTENT_LINE_NUM - 1) / MAXHELPLINE + 1
	CLEARLINE LINECOUNT - FIRST_LINE

ELSEIF RESULT == 2001
	HELPPAGE ++
	SIF HELPPAGE > (HELP_CONTENT_LINE_NUM - 1) / MAXHELPLINE + 1
		HELPPAGE = 1
	CLEARLINE LINECOUNT - FIRST_LINE
ELSEIF INRANGE(RESULT, 0, HELP_CONTENT_NUM - 1)
	HELPINDEX = RESULT
	HELPPAGE = 1
	VARSET HELP_CONTENT_LINE
	VARSET HELP_CONTENT_LINE_NUM
	CALLFORM PREPARE_HELP_CONTENT_%HELP_CONTENT_NAME_ENG:HELPINDEX%()
	CLEARLINE LINECOUNT - FIRST_LINE
ELSE
	GOTO INPUT_LOOP
ENDIF
GOTO SHOW_LOOP

;-------------------------------------------------
;ヘルプのコンテント部分の表示
;-------------------------------------------------
@SHOW_HELP_CONTENT_VIEW(HELPINDEX, HELPPAGE, MAXHELPLINE)
#DIM HELPINDEX
#DIM HELPPAGE
#DIM MAXHELPLINE

SIF !INRANGE(HELPINDEX, 0, HELP_CONTENT_NUM - 1)
	HELPINDEX = 0
SIF HELPPAGE > (HELP_CONTENT_LINE_NUM - 1) / MAXHELPLINE + 1
	HELPPAGE = 1
SIF HELPPAGE < 1
	HELPPAGE = (HELP_CONTENT_LINE_NUM - 1) / MAXHELPLINE + 1

PRINTFORML %HELP_CONTENT_NAME:HELPINDEX%
CALL SINGLE_DRAWLINE
FOR LOCAL, (HELPPAGE - 1) * MAXHELPLINE, HELPPAGE * MAXHELPLINE
	CALL PRINT_HELP_CONTENT(HELP_CONTENT_LINE:LOCAL, LOCAL == 0)
NEXT


;-------------------------------------------------
;ヘルプの中身を追加する関数
;-------------------------------------------------
@ADD_HELP_LINE(LINE)
#DIMS LINE
HELP_CONTENT_LINE:HELP_CONTENT_LINE_NUM '= LINE
HELP_CONTENT_LINE_NUM ++

;-------------------------------------------------
;ヘルプの各行を描画する関数
;-------------------------------------------------
@PRINT_HELP_CONTENT(ORIGSTR, INDENTRESET)
#DIMS ORIGSTR
#DIMS SEPSTR, 1000
#DIM SHORTHANDFLAG, 1000
#DIM POINTER
#DIM SEPNUM
#DIM SHORTHANDPOINT,3
#DIM SHORTHANDAREA, 2
#DIM SHORTHANDTYPE
#DIM CONST ショートハンド_ランク = 1
#DIM CONST ショートハンド_コンテント = 2
#DIM CONST ショートハンド_数値 = 3
#DIMS CONST SHORTHAND_START_CHARA = "", "<", "`", "'"
#DIMS CONST SHORTHAND_END_CHARA = "", ">", "`", "'"
#DIM INDENTFLAG
#DIM INDENTRESET

VARSET SEPSTR
VARSET SHORTHANDFLAG
VARSET POINTER
VARSET SEPNUM
VARSET SHORTHANDPOINT
VARSET SHORTHANDAREA
VARSET SHORTHANDTYPE

PRINT    

;空文字列またはインデントリセットの場合、インデントを解除する
IF ORIGSTR == "" || INDENTRESET == 1
	INDENTFLAG = 0
	IF ORIGSTR == ""
		PRINTFORML
		RETURN
	ENDIF
ENDIF


WHILE 1

	FOR LOCAL, 0, VARSIZE("SHORTHANDPOINT")
		SHORTHANDPOINT:LOCAL = STRFINDU(ORIGSTR, SHORTHAND_START_CHARA:(LOCAL + 1), POINTER)
	NEXT

	;もうショートハンドがない
	IF ALLSAMES(SHORTHANDPOINT:0, SHORTHANDPOINT:1, SHORTHANDPOINT:2)
		SUBSTRINGU ORIGSTR, POINTER, -1
		IF RESULTS != ""
			SEPSTR:SEPNUM = %RESULTS%
			SEPNUM ++
		ENDIF
		BREAK
	ENDIF

	FOR LOCAL, 0, VARSIZE("SHORTHANDPOINT")
		SIF SHORTHANDPOINT:LOCAL == -1
			SHORTHANDPOINT:LOCAL = __INT_MAX__
	NEXT

	;ショートハンドがある場合
	SHORTHANDAREA:0 = MINARRAY(SHORTHANDPOINT)
	;ショートハンドの種類を取得
	SHORTHANDTYPE = FINDELEMENT(SHORTHAND_START_CHARA, SUBSTRINGU(ORIGSTR, SHORTHANDAREA:0, 1))
	;ショートハンドの終了位置を取得
	SHORTHANDAREA:1 = STRFINDU(ORIGSTR, SHORTHAND_END_CHARA:SHORTHANDTYPE, SHORTHANDAREA:0 + 1)

	SUBSTRINGU ORIGSTR, POINTER, SHORTHANDAREA:0 - POINTER
	SEPSTR:SEPNUM = %RESULTS%
	SEPNUM ++
	SUBSTRINGU ORIGSTR, SHORTHANDAREA:0 + 1, SHORTHANDAREA:1 - SHORTHANDAREA:0 - 1
	SEPSTR:SEPNUM = %RESULTS%
	SHORTHANDFLAG:SEPNUM = SHORTHANDTYPE
	SEPNUM ++
	POINTER = SHORTHANDAREA:1 + 1
WEND


SIF INDENTFLAG && FINDELEMENT(SHORTHANDFLAG, ショートハンド_コンテント) == -1
	PRINTFORM %HELP_CONTENTS_TOSTR_SPACE()%


FOR LOCAL, 0, SEPNUM
	SELECTCASE SHORTHANDFLAG:LOCAL
		CASE ショートハンド_コンテント
			INDENTFLAG = 1
			PRINTFORM %HELP_CONTENTS(SEPSTR:LOCAL)%
		CASE ショートハンド_数値
			PRINTFORM %HELP_NUMBERCOLOR(SEPSTR:LOCAL)%
		CASE ショートハンド_ランク
			PRINTFORM %HELP_RANKCOLOR(SEPSTR:LOCAL)%
		CASEELSE
			PRINTFORM %SEPSTR:LOCAL%
	ENDSELECT
NEXT

PRINTL

;-------------------------------------------------
;デザイン統一用関数
;表示中のメニューを色つきにするボタン
; ARG:0 = 表示中のページ
; ARG:1 = ボタン番号
; ARGS:2 = タイトル
;-------------------------------------------------
@HELP_COLORBUTTON(ARG:0, ARG:1, ARGS:2)
IF ARG:0 == ARG:1
	SETCOLOR カラー_選択中
ENDIF
PRINTFORML [{ARG:1, 2}] %ARGS:2%
RESETCOLOR
RETURN

;-------------------------------------------------
;デザイン統一用式中関数
;素質名等を左側にタイトルとして表示するとき
; ARGS:0 = タイトル
; %HELP_CONTENTS("恋慕")%
;-------------------------------------------------
@HELP_CONTENTS(ARGS:0)
#FUNCTIONS
SETCOLOR カラー_選択不可
PRINT ・ 
RESETCOLOR
LOCALS:0 = %ARGS:0%
PRINTFORM %LOCALS:0, 14, LEFT%
SETCOLOR カラー_選択不可
PRINT  - 
RESETCOLOR
RETURNF

;-------------------------------------------------
;デザイン統一用式中関数
;素質名等を左側にタイトルとして表示するとき
;説明文が二行にまたがる場合に左側に空けるスペース
; %HELP_CONTENTS_TOSTR_SPACE()%
;-------------------------------------------------
@HELP_CONTENTS_TOSTR_SPACE()
#FUNCTIONS
PRINTFORM %TOSTR_SPACE(20)%
RETURNF

;-------------------------------------------------
;デザイン統一用式中関数
;数字を強調する
; ARGS:0 = 数字
; %HELP_RANKカラー_S%
;-------------------------------------------------
@HELP_NUMBERCOLOR(ARGS:0)
#FUNCTIONS
SETCOLOR カラー_選択中
PRINTFORM %ARGS:0%
RESETCOLOR
RETURNF

;-------------------------------------------------
;デザイン統一用式中関数
;ランク表示に色をつける（@PRINT_ALPHABET_RANKに準拠）
; ARGS:0 = ランク（全角）
; %HELP_RANKカラー_S%
;-------------------------------------------------
@HELP_RANKCOLOR(ARGS:0)
#FUNCTIONS

SETCOLOR GET_RANK_COLOR(ARGS:0)
PRINTFORM %ARGS:0%
RESETCOLOR
RETURNF

