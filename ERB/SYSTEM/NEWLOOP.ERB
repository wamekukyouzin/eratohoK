
;-------------------------------------------------
;ARG:0 = 1 で周回モード
;-------------------------------------------------
@NEWLOOP(ARG:0=0)

;変数の初期化
CALL NEWLOOP_RESET_VARS

;周回数が1増える
FLAG:周回数++

;悪の栄えたためしなしとかで戻ったとき用
VARSET SP_COUNTRY_RANK

;特殊キャラすべて削除
CALL NEWLOOP_DELETE_ALL_SP_CHARA(ARG:0)

;シナリオ選択から勢力選択まで
CALL STARTUP_SCENARIO

IF RESULT == -1
	RETURN 0
ELSEIF RESULT == 0
	FLAG:周回数 --
	RESTART
ENDIF

CALL EXTRA_SETTING

;特殊勢力の設定
CALL SP_COUNTRY_SETTING
CALL SP_COUNTRY_START_INIT

;初期ボーナス
IF !FLAG:観戦モード && FLAG:あなた新規作成 && MASTER == NAME_TO_CHARA("あなた")
	CALL NEWLOOP_ANATA_BONUS
	FLAG:あなた新規作成 = 0
ENDIF

;もろもろ終わってからのリセット処理
CALL NEWLOOP_ADDITIONAL_RESET_VARS

RETURN 1


;-------------------------------------
;シナリオ側で特殊キャラを生成しており、かつ一週目、「戻る」したときのため、特殊キャラを全て削除する
;-------------------------------------
@NEWLOOP_DELETE_ALL_SP_CHARA(ARG:0)
LOCAL:2 = CHARANUM
FOR LOCAL:0, 0, LOCAL:2
	LOCAL:1 = LOCAL:2 - LOCAL:0 - 1
	IF (IS_SP_CHARA(LOCAL:1) && MASTER != LOCAL:1) || TALENT:(LOCAL:1):カスタムキャラ == 2
		CALL DELETE_CHARA(LOCAL:1)
	ENDIF
NEXT


;-------------------------------------
;新しく周回する時用、変数のリセット処理
;-------------------------------------
@NEWLOOP_RESET_VARS
;日付を1日目に戻す
DAY = 1

;触手部屋の管理者を初期化
FLAG:触手部屋管理者 = 0

;フェイズを拠点フェイズにする
TIME = 0

;シナリオ専用変数をリセット
VARSET SCVAR, 0

;デイリーイベント変数をリセット
VARSET DVAR, 0
VARSET DSTR, 
;口上用デイリーイベント変数をリセット
FOR LOCAL, 0, VARSIZE("KDVAR")
	CVARSET KDVAR, LOCAL, 0
	CVARSET KDSTR, LOCAL, 
NEXT

;特殊勢力関係の変数をリセット
VARSET SP_COUNTRY_START
VARSET SP_COUNTRY_RANK
VARSET SP_COUNTRY_APPEARED
VARSET SP_COUNTRY_BOSSNAME
VARSET SP_COUNTRY_BOSSNAME_LAST_NAME
VARSET SP_COUNTRY_BOSSNAME_FIRST_NAME
VARSET SP_COUNTRY_INITIAL_RISE
VARSET SP_COUNTRY_GENDER, -1
VARSET BANDIT_HAS_INITIAL_CITIES

FOR LOCAL, 0, VARSIZE("SP_TRAIN_COUNT")
	CVARSET SP_TRAIN_COUNT, LOCAL, 0
NEXT

;捕虜について削除
VARSET PRISONER_TARGET, -1

;スカウト回数をリセット
VARSET SCOUT_COUNT, 0

;特別訓練回数をリセット
VARSET SPECIAL_TRAIN_COUNT, 0

;変数の初期化
TARGET = -1
ASSI = -1

;クリアフラグを解除
FLAG:クリアフラグ = 0
FLAG:クリア直後フラグ = 0
FLAG:強制エンドフラグ = 0
SHOP_TIME = 0

;観戦モードフラグを0
FLAG:観戦モード = 0

;SHOW_INFOの初期画面を基本情報にする
FLAG:能力表示モード = 0


;SLG関連の変数をクリア
STATS_TIME = 0
VARSET STATS_BOSS_ID_LIST, 0
VARSET STATS_GDP, 0
VARSET STATS_ARMY, 0
VARSET CITY_ROUTE, 0
VARSET CITY_TYPE, 0
VARSET CITY_ECONOMY, 0
VARSET CITY_SOLDIER, 0
VARSET CITY_GUARD, 0
VARSET CITY_OWNER, 0
VARSET CITY_COMMANDER, 0
VARSET CITY_DEVELOPMENT, 0
VARSET COUNTRY_BOSS, 0
VARSET COUNTRY_COLOR, 0
VARSET COUNTRY_SOLDIER, 0
VARSET COUNTRY_AI_TYPE, 0
VARSET COUNTRY_NOTARGET_TERM, 0
VARSET COUNTRY_IS_CLOSED, 0
VARSET COUNTRY_EVENT_ID, 0
VARSET AUTO_INVEST_PRICE, 0
VARSET UNIT_SOLDIER,0
VARSET UNIT_POSITION,0
VARSET UNIT_TARGET,0
VARSET UNIT_COMMANDER,0
VARSET TECHNOLOGY_STATUS, 0
VARSET TECHNOLOGY_RESEARCH_TARGET, -1
VARSET TECHNOLOGY_RESEARCH_PROGRESS, 0
VARSET CITY_ANALYZED, 0
VARSET TREATY_A_TERM, 0
VARSET TREATY_U_TERM, 0
VARSET TREATY_U_TARGET, 0
VARSET TREATY_C_TERM, 0
VARSET DIPLOMACY_HATE, 0
VARSET COUNTRY_NOTDIPLOMACY_TERM, 0
VARSET TREATY_A_COUNTRY
VARSET TREATY_U_COUNTRY
VARSET TREATY_C_COUNTRY

VARSET TMP_COUNTRY_RELATION
VARSET TMP_COUNTRY_RELATION_TERM
VARSET TMP_COUNTRY_IS_NEIBORING
VARSET TMP_COUNTRY_BOSS_NUMBER
VARSET TMP_COUNTRY_BOSS_NAME_SHORT
VARSET TMP_COUNTRY_UNION_TARGET
VARSET TMP_UNIT_ONCITY_CNT
VARSET TMP_UNIT_ONCITY_NUM
VARSET TMP_SUM_POLITICS_POWER
VARSET TMP_SUM_COOKING_POWER
VARSET TMP_COUNTRY_STARS
VARSET TMP_CHARA_STARS
VARSET TMP_CONNECTED_TO_US
VARSET TMP_CONNECTED_TO_US_ONLY
VARSET TMP_CONNECTED_TO_ENEMY
VARSET TMP_ENCLAVE_NUM
VARSET TMP_ENCLAVE_SIZE
VARSET TMP_MAX_ENCLAVE_SIZE
VARSET TMP_OFFENSIVE_IMPORTANCE
VARSET TMP_DEFFENSIVE_IMPORTANCE
VARSET TMP_CITY_RISK
VARSET TMP_IS_INVADABLE
VARSET TMP_IS_BYPASS
VARSET TMP_OWN_CITY
VARSET TMP_SUM_ECONOMY
VARSET TMP_SUM_ECONOMY_LIMIT
VARSET TMP_SUM_SOLDIER

;ランダム系シナリオ等でPLACEMENT呼び出し後に「戻る」したときのため、キャラの所属を消去
FOR LOCAL, 0, CHARANUM
	CFLAG:LOCAL:所属 = 0
	CFLAG:LOCAL:特殊状態 = 0
	CFLAG:LOCAL:捕虜先 = 0
NEXT

;シナリオ12「悪の栄えたためしなし」でPLACEMENT呼び出し後に「戻る」したときのため、出現フラグをリセット
VARSET SP_COUNTRY_APPEARED, 0

;マップIDをリセット
MAPID '= "DEFAULT"

;各勢力の国庫を空に（プレイヤーの懐はそのまま）
VARSET MONEY, 0, 1


;-------------------------------------------------
;もろもろの処理後に行う変数リセット処理
;-------------------------------------------------
@NEWLOOP_ADDITIONAL_RESET_VARS
;開始時の国庫を設定
;シナリオに応じて
FOR LOCAL:0, 1, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL:0)
		CONTINUE
	MONEY:(LOCAL:0) += GET_SUM_ECONOMY(LOCAL:0) * 100 / (100 * 100)
NEXT

FOR LOCAL, 0, CHARANUM
	SIF TALENT:LOCAL:カスタムキャラ == 2
		TALENT:LOCAL:カスタムキャラ = 1
NEXT

;奴隷市場の商品リストを更新
CALL SLAVEMARKET_PREPARE_GOODS
;スカウトの対象リストを更新
CALL SCOUT_PREPARE_CANDIDATES
;放浪中のキャラが「会いに行く」で表示されるようリスト作成
CALL MEET_CHECK_CANDIDATES
;特別訓練の価格を更新
CALL CALC_SPECIAL_TRAIN_PRICE
;このターンの特別訓練実施回数をリセット
SPECIAL_TRAIN_COUNT_THIS_TURN = 0

;主人公の所属勢力の政策を経済に
SIF IS_COUNTRY(CFLAG:MASTER:所属)
	COUNTRY_POLICY:(CFLAG:MASTER:所属) = 政策_経済


;-------------------------------------------------
;シナリオに関連するスタートアップ処理
;-------------------------------------------------
@STARTUP_SCENARIO
;シナリオ選択
CALL SELECT_SCENARIO

SIF RESULT == 0
	RETURN -1

TRYCALLFORM SCENARIO_NAME_%SCENARIOID%
LOCALS '= RESULTS
PRINTFORML シナリオ 「%LOCALS%」 で開始します

;シナリオマップ選択処理
TRYCALLFORM SCENARIO_MAPSELECT_%SCENARIOID%

;マップ初期化
CALL MAPSETUP(1)
CALL MAP_INIT

;シナリオイントロ
TRYCALLFORM SCENARIO_INTRO_%SCENARIOID%

PRINTFORML このシナリオで遊びますか？
CALL ASK_YN()
SIF RESULT == 1
	RETURN 0

;シナリオ初期勢力配置
TRYCCALLFORM SCENARIO_PLACEMENT_%SCENARIOID%
CATCH
	CALL SCENARIO_PLACEMENT_DEFAULT
ENDCATCH

;カスタムキャラの設定
CALL SETUP_SPECIAL_CHARACTERS

$SELECT_COUNTRY

;勢力選択
CALL SELECT_COUNTRY

;戻った場合
SIF RESULT == 0
	RETURN 0


;観戦モードの場合、前の週でキャラを変更したとき用に、「あなた」と主人公を入れ替えて戻す処理だけは必要
IF FLAG:観戦モード
	CALL SWAP_CHARA(MASTER, NAME_TO_CHARA("あなた"))
ELSE
	CALL SELECT_MASTER
	;戻った場合
	SIF RESULT == 0
		GOTO SELECT_COUNTRY
ENDIF

;旗揚げスタートなら、勢力を設定
IF FLAG:OP選択勢力 == -4
	CALL START_WITH_NEW_COUNTRY
	SIF RESULT == -1
		RETURN 0
ENDIF
;その他勢力選択後にシナリオ側でやりたいこと
TRYCALLFORM SCENARIO_%SCENARIOID%_AFTERSELECT

RETURN 1


;-------------------------------------------------
;シナリオ選択
;-------------------------------------------------
@SELECT_SCENARIO
#DIM IN
CALL CREATE_SCENARIO_INDEX
PRINTL ★★シナリオを選択して下さい★★
CALL SINGLE_DRAWLINE
FOR LOCAL:0, 0, SCENARIO_NUMBER
	SIF SCENARIO_SELECT:(LOCAL:0) == -1
		BREAK

	LOCALS:0  '= ""
	LOCALS:0  += "[●"
	RESULTS:0 '= ""
	TRYCALLFORM SCENARIO_NAME_%SCENARIO_ID:(LOCAL:0)%
	LOCALS:0 = %LOCALS:0%%RESULTS:0, 26, LEFT%
	LOCALS:0 += "]"
	LOCALS:0 = {SCENARIO_SELECT:(LOCAL:0),3 , RIGHT}%LOCALS:0%

	PRINTBUTTON LOCALS:0, SCENARIO_SELECT:(LOCAL:0)
	PRINTL
NEXT

IF FLAG:周回数 == 1
	CALL SINGLE_DRAWLINE
	PRINTBUTTON "99[戻る]", 99
ENDIF

$INPUT_LOOP
INPUT
IN = RESULT
FOR LOCAL:0 , 0, SCENARIO_NUMBER
	IF IN == 99 && FLAG:周回数 == 1
		RETURN 0
	ENDIF
	IF IN == SCENARIO_SELECT:(LOCAL:0)
		SCENARIOID '= SCENARIO_ID:LOCAL
		RETURN 1
	ENDIF
NEXT
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;勢力選択
;-------------------------------------------------
@SELECT_COUNTRY
#DIM FIRST_LINE
#DIM 選択勢力
#DIM ボス

CALL TMP_PREPARE_COUNTRY_STARS

選択勢力 = 0
FIRST_LINE = LINECOUNT
$SHOW_LOOP

CALL SINGLE_DRAWLINE
PRINT ★★所属勢力選択★★
PRINTPLAIN   
PRINTBUTTON "[勢力一覧]", 96
PRINTPLAIN   
PRINTBUTTON "[ランダム]", 97
PRINTPLAIN   
PRINTBUTTON "[観戦]", 98
PRINTPLAIN   
PRINTBUTTON "[放浪]", 99
IF GET_NEW_COUNTRY() != -1
	PRINTPLAIN   
	PRINTBUTTON "[特殊]", 100
	PRINTPLAIN   
	PRINTBUTTON "[旗揚げ]", 101
ENDIF
PRINTPLAIN   
PRINTBUTTON "999[戻る]", 999
PRINTL 
CALL SINGLE_DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL DRAWMAP
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE

ボス = ID_TO_CHARA(COUNTRY_BOSS:選択勢力)

IF ボス < 0
	PRINTL 都市をクリックすることで、その勢力の情報を得ることができます
	PRINTL 
	PRINTL 
	PRINTL 
	PRINTL 
	PRINTL
	PRINTL 
ELSE
	CALL COLOR_PRINT(@"この都市は", ,@"<%NAME_FORMAL(ボス)%>", COUNTRY_COLOR:選択勢力, "の勢力下にあります")
	PRINTFORM %TOSTR_SPACE(4)%
	PRINTBUTTON "[勢力情報]", 102
	PRINTL
	;星表示
	CALL SINGLE_DRAWLINE
	PRINTFORM 評価:
	CALL TMP_PRINT_COUNTRY_STARS(選択勢力)
	PRINTL
	;所属キャラ一覧表示
	CALL SINGLE_DRAWLINE
	PRINTFORML 都市数: {GET_OWN_CITY(選択勢力), 3, LEFT} 経済力: {GET_SUM_ECONOMY(選択勢力) / 100, 7, LEFT} 防衛力: {GET_SUM_GUARD(選択勢力), 7, LEFT}
	PRINTFORM 所属キャラ:

	;ボスだけ特殊表示
	CALL COLOR_PRINT(@"%ANAME(ボス)%   ", COUNTRY_COLOR:選択勢力)
	LOCAL:1 = STRLENS(LOCALS:0)

	FOR LOCAL, 0, CHARANUM
		SIF LOCAL == ボス || CFLAG:LOCAL:所属 != 選択勢力
			CONTINUE
		PRINTFORM %ANAME(LOCAL)%   
	NEXT
	PRINTL

	;その勢力がプレイ可能か調べる。未定義なら全勢力プレイ可能とする
	TRYCCALLFORM IS_COUNTRY_PLAYABLE_%SCENARIOID%(選択勢力)
		IF !RESULT
			CALL COLOR_PRINT("このシナリオでは、その勢力でプレイすることはできません", カラー_注意)
		ELSE
			PRINTBUTTON " 0[この勢力で開始]", 0
			PRINTL
		ENDIF
	CATCH
		PRINTBUTTON " 0[この勢力で開始]", 0
		PRINTL
	ENDCATCH
ENDIF

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	IF ボス >= 0
		TRYCCALLFORM IS_COUNTRY_PLAYABLE_%SCENARIOID%(選択勢力)
			SIF RESULT
				GOTO SELECTABLE
			PRINTFORMW このシナリオでは、その勢力でプレイすることはできません
		CATCH
			$SELECTABLE
			FLAG:OP選択勢力 = 選択勢力
			PRINTFORMW %ANAME(ボス)%勢力で開始します
			RETURN 1
		ENDCATCH
	ENDIF
ELSEIF RESULT == 96
	CALL SHOW_COUNTRY_DATA
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 97
	CALL FISHER_YATES_SHAFFLE(MAX_COUNTRY)
	FOR LOCAL, 0, MAX_COUNTRY
		選択勢力 = SHAFFLE_ARRAY:LOCAL
		SIF !IS_COUNTRY(選択勢力)
			CONTINUE
		ボス = GET_COUNTRY_BOSS(選択勢力)
		TRYCCALLFORM IS_COUNTRY_PLAYABLE_%SCENARIOID%(選択勢力)
			SIF RESULT
				GOTO SELECTABLE
		CATCH
			GOTO SELECTABLE
		ENDCATCH
	NEXT
	THROW そんなはずはない
ELSEIF RESULT == 98
	FLAG:OP選択勢力 = -1
	FLAG:観戦モード = 1
	TIME = 1
	PRINTFORMW 観戦モードで開始します
	RETURN 1
ELSEIF RESULT == 99
	FLAG:OP選択勢力 = 0
	TIME = 0
	PRINTFORMW 放浪状態から開始します
	RETURN 1
ELSEIF RESULT == 100 && GET_NEW_COUNTRY() != -1
	FLAG:OP選択勢力 = -3
	CALL SP_COUNTRY_START_SELECT()
	IF RESULT == 0
		FLAG:OP選択勢力 = 0
		RETURN 0
	ENDIF
	RETURN 1
ELSEIF RESULT == 101 && GET_NEW_COUNTRY() != -1
	FLAG:OP選択勢力 = -4
	PRINTFORMW 旗揚げして開始します
	RETURN 1
ELSEIF RESULT == 102
	CALL SHOW_COUNTRY_INFO(選択勢力)
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 999
	RETURN 0
;都市をクリック
ELSEIF RESULT >= 1000 && RESULT < 1000 + MAX_CITY
	LOCAL:3 = RESULT - 1000
	LOCAL:4 = CITY_OWNER:(LOCAL:3)
	IF IS_COUNTRY(LOCAL:4)
		選択勢力 = LOCAL:4
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ELSEIF GET_RELAYNAME(LOCAL:3) != "無名" && LOCAL:4 == 0
		選択勢力 = 0
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;特殊勢力の選択
;-------------------------------------------------
@SP_COUNTRY_START_SELECT()
#DIM FIRST_LINE
PRINTFORMW 特殊な勢力で開始します
PRINTFORMW どれで開始しますか？

FOR LOCAL, 0, MAX_SP_COUNTRY
	SIF SP_COUNTRY_NAME:LOCAL != ""
		PRINTFORML [{LOCAL}] %SP_COUNTRY_NAME:LOCAL%
NEXT
CALL SINGLE_DRAWLINE
PRINTFORML [999] 戻る

$INPUT_LOOP2
INPUT

LOCAL = RESULT

SIF LOCAL == 999
	RETURN 0

SIF SP_COUNTRY_NAME:LOCAL == ""
	GOTO INPUT_LOOP2

PRINTFORML どういう境遇でスタートしますか？
CALL ASK_MULTI("士官(調教を受けない)", "奴隷(最初から合意もち)", "捕虜(無所属・捕虜からスタート)", "君主")
SELECTCASE RESULT
	CASE 0
		PRINTFORMW 士官としてスタートします
	CASE 1
		PRINTFORMW 奴隷としてスタートします
	CASE 2
		PRINTFORMW 捕虜としてスタートします
	CASE 3
		PRINTFORMW 君主としてスタートします
ENDSELECT

SP_COUNTRY_START:LOCAL = RESULT + 1

;この時点で設定していない（シナリオの初期設定時点で設定していない）なら決めておく
IF SP_COUNTRY_RANK:LOCAL == 0
	FIRST_LINE = LINECOUNT
	$SHOW_LOOP
	CALL SP_COUNTRY_START_SETTING(LOCAL)
ENDIF

RETURN 1


;-------------------------------------------------
;主人公選択
;-------------------------------------------------
@SELECT_MASTER
#DIM 旧MASTERID
#DIM 選択MASTERID
#DIM 引継ぎ使用可
#DIM 戻る使用不可
#DIM FIRST_LINE
VARSET LOCAL
LOADGLOBAL

;星評価を用意
CALL TMP_PREPARE_CHARA_STARS()

VARSET 旧MASTERID
VARSET 選択MASTERID

戻る使用不可 = 0
FOR LOCAL, 0, MAX_SP_COUNTRY
	SIF SP_COUNTRY_START:LOCAL > 0
		戻る使用不可 = 1
NEXT

IF FLAG:周回数 > 1
	旧MASTERID = GET_ID(MASTER)
	;勢力君主でないか、君主でも自分が収める勢力でスタートするなら引き継いで利用できる。ただし特殊勢力素質がある場合は問答無用でNG。また、主人公が「あなた」の場合、作成済みであること
	引継ぎ使用可 = (CFLAG:MASTER:所属 == FLAG:OP選択勢力 || GET_COUNTRY_BOSS(CFLAG:MASTER:所属) != MASTER) && TALENT:MASTER:特殊勢力素質 == 0 && (MASTER != NAME_TO_CHARA("あなた") || FLAG:あなた作成済み)
ELSE
	旧MASTERID = 0
	引継ぎ使用可 = 1
ENDIF

FIRST_LINE = LINECOUNT
$SHOW_LOOP

CALL SINGLE_DRAWLINE
PRINTL ★★主人公を選択して下さい★★

SIF FLAG:OP選択勢力 <= 0
	PRINTL ※特殊なスタートを選択しているので、既存キャラ一覧に君主以外の全キャラが表示されます
;二週目以降で選択勢力と異なる勢力の君主の場合、前週と同じ選択をさせるとまずいので選択不可とする
IF 旧MASTERID > 0 && !引継ぎ使用可
	IF MASTER == NAME_TO_CHARA("あなた") && !FLAG:あなた作成済み
		;まだ作成してない場合はわざわざ表示しなくていいや
	ELSEIF TALENT:MASTER:特殊勢力素質
		CALL COLOR_PRINTL("前の週で特殊勢力固有のキャラを利用したため、「前週と同じ」が利用できません", カラー_注意)
	ELSE
		CALL COLOR_PRINTL(@"※前周の主人公%ANAME(MASTER)%が別勢力で君主をしているため、「前週と同じ」が利用できません", カラー_注意)
	ENDIF
ENDIF
CALL SINGLE_DRAWLINE

;ここから登録キャラの表示
PRINTBUTTON "[あなたを新規作成]", 0
IF FIND_CHARADATA(TOSTR(-2))
	PRINTBUTTON "[保存済みのデータを利用]", 97
ELSEIF FLAG:あなた作成済み && MASTER != NAME_TO_CHARA("あなた")
	PRINTL
	PRINTBUTTON @"[%ANAME(NAME_TO_CHARA("あなた"))%を選択]", 2
ENDIF
IF FINDELEMENT(SP_COUNTRY_START, 特殊勢力スタート_君主) != -1
	PRINTL
	PRINTBUTTON "[特殊勢力固有の君主を利用]", 1
	PRINTFORM ※次の周回に引き継げません
ENDIF
;二週目以降で引継ぎが使用できるなら
IF 旧MASTERID > 0 && 引継ぎ使用可
	PRINTL
	PRINTBUTTON @"[前週の主人公 %ANAME(MASTER)% でプレイ]", 98
ENDIF

PRINTL 
CALL SINGLE_DRAWLINE
IF FLAG:OP選択勢力 > 0
	PRINTBUTTON "[勢力所属のキャラを使用]", 99
	PRINTL 
	CALL SINGLE_DRAWLINE
ELSE
	PRINTBUTTON "[君主以外のキャラを使用]", 99
	PRINTL
	CALL SINGLE_DRAWLINE
ENDIF

IF !戻る使用不可
	PRINTBUTTON "[戻る]", 9999
	PRINTL
ENDIF

$INPUT_LOOP
INPUT

;新規作成
IF RESULT == 0
	LOCAL:1 = NAME_TO_CHARA("あなた")
	CSTR:(LOCAL:1):0 = 
	CSTR:(LOCAL:1):1 = あなた
	CSTR:(LOCAL:1):98 = 
	FOR LOCAL, 0, MAX_TAG
		TAG:(LOCAL:1):LOCAL = -1
	NEXT
	CALL INIT_CHARA(LOCAL:1)
	CALL MASTER_EDIT(LOCAL:1)
	選択MASTERID = GET_ID(RESULT)
	FLAG:あなた新規作成 = 1
ELSEIF RESULT == 97
	LOCAL:1 = NAME_TO_CHARA("あなた")
	CALL LOAD_CUSTOM_DATA(-2)
	LOCAL:2 = RESULT
	;カスタムキャラのCSTR:99は消えているため復元
	CSTR:(LOCAL:2):99 = あなた
	TALENT:(LOCAL:2):カスタムキャラ = 0
	;旧「あなた」と読み込んできた「あなた」の入れ替え
	SWAP CFLAG:(LOCAL:1):キャラＩＤ, CFLAG:(LOCAL:2):キャラＩＤ
	選択MASTERID = GET_ID(LOCAL:2)
	FLAG:あなた新規作成 = 1
	NO:(LOCAL:2) = NO:(LOCAL:1)
	NO:(LOCAL:1) = __INT_MAX__
	CALL SWAP_CHARA(LOCAL:1, LOCAL:2)
	CALL DELETE_CHARA(LOCAL:2)
;固有君主利用
ELSEIF RESULT == 1 && FINDELEMENT(SP_COUNTRY_START, 特殊勢力スタート_君主) != -1
	SP_COUNTRY_START_CHANGE_BOSS = 0
	CALL SWAP_CHARA(MASTER, NAME_TO_CHARA("あなた"))
	RETURN 1
ELSEIF RESULT == 2 && FLAG:あなた作成済み && MASTER != NAME_TO_CHARA("あなた")
	選択MASTERID = GET_ID(NAME_TO_CHARA("あなた"))
;前週と同じ
ELSEIF RESULT == 98 && 旧MASTERID > 0
	IF !引継ぎ使用可
		PRINTFORMW 前周の主人公が、選択した勢力以外の勢力の君主です。「前週と同じ」は利用できません
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF
	選択MASTERID = GET_ID(MASTER)
ELSEIF RESULT == 9999 && !戻る使用不可
	RETURN 0
;既存のキャラを選択
ELSEIF RESULT == 99
	CALL SINGLE_DRAWLINE()
	CALL SELECT_CHARA_LIST_ONLY_LOGIC_SLG("SELECT_MASTER", "NONE")
	IF RESULT == -1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF
	選択MASTERID = GET_ID(RESULT)
ELSE
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF


CALL TMP_PREPARE_CHARA_STARS()

CALL SINGLE_DRAWLINE
CALL SHOW_INFO(ID_TO_CHARA(選択MASTERID))
CALL SINGLE_DRAWLINE

PRINTBUTTON " [このキャラを主人公にして開始]", 0
PRINTL 
PRINTBUTTON " [戻る]", 9
PRINTL 

REDRAW 0

$INPUT_LOOP2
INPUT

;
SELECTCASE RESULT
	;このキャラを主人公にする
	CASE 0
		SIF FLAG:あなた新規作成
			FLAG:あなた作成済み = 1
		REDRAW 1
		;二週目以降なら、MASTERと「あなた」を入れ替えて元に戻す
		;MASTERが「あなた」以外だと、並びがぐちゃぐちゃになってしまうので（IDの仕組みがあるし問題ないと思うけど）
		IF 旧MASTERID > 0
			CALL SWAP_CHARA(MASTER, NAME_TO_CHARA("あなた"))
			CALL NEWGAME_SWAP_RELATIONS(ID_TO_CHARA(旧MASTERID))
		ENDIF
		;MASTERと選択したMASTERを入れ替える
		CALL SWAP_CHARA(MASTER, ID_TO_CHARA(選択MASTERID))

		;主人公の所属を選択した勢力にする
		;既存勢力以外のスタートを選んでいる場合はとりあえず0にしておく
		CALL CHANGE_COUNTRY(MASTER, MAX(FLAG:OP選択勢力, 0), 1)
		;放浪する可能性があるのでへし折る
		CFLAG:MASTER:特殊状態 = 0

		;体力気力精神力の現在値を最大値に変更
		FOR LOCAL:2, 0, VARSIZE("BASE")
			BASE:MASTER:(LOCAL:2) = MAXBASE:MASTER:(LOCAL:2)
		NEXT

		;特殊勢力スタートでかつ君主を挿げ替えていた場合、後々処理が必要になるのでフラグを立てておく
		SIF FINDELEMENT(SP_COUNTRY_START, 特殊勢力スタート_君主) != -1
			SP_COUNTRY_START_CHANGE_BOSS = 1

		;主人公を変更した、あるいは「あなた」を新規に作成した場合は陥落済みフラグリセット（ボーナスを再び受け取れるようにするため）
		IF 選択MASTERID != 旧MASTERID || FLAG:あなた新規作成
			FLAG:陥落人数 = 0
			CVARSET CFLAG, GETNUM(CFLAG, "陥落済み"), 0
			;利殖対策として、好感度等まっさらな状態でないキャラが一人でもいたら、ボーナス受領数はリセットしない
			LOCAL:1 = 0
			FOR LOCAL, 0, CHARANUM
				IF !(ALLSAMES(0, CFLAG:LOCAL:好感度, CFLAG:LOCAL:従属度, CFLAG:LOCAL:依存度, CFLAG:LOCAL:支配度) || IS_FALLEN(LOCAL)) && GET_ID(LOCAL) != 旧MASTERID
					LOCAL:1 = 1
					BREAK
				ENDIF
			NEXT
			SIF !LOCAL:1
				FLAG:陥落ボーナス受領数 = 0

			;元主人公がカスタムキャラだった場合、ここで削除（ここ以降に削除するとこはない）
			SIF ID_TO_CHARA(旧MASTERID) != -1 && TALENT:(ID_TO_CHARA(旧MASTERID)):カスタムキャラ
				CALL DELETE_CHARA(ID_TO_CHARA(旧MASTERID))

		ENDIF

		RETURN 1
	;戻る
	CASE 9
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
ENDSELECT
CLEARLINE 1
GOTO INPUT_LOOP2


@SELECT_CHARA_LIST_SHOW_LOGIC_SELECT_MASTER(対象)
#DIM 対象
;どこかの勢力を選択した場合は、そこ所属のキャラ以外は弾く。
SIF FLAG:OP選択勢力 > 0 && CFLAG:対象:所属 != FLAG:OP選択勢力
	RETURN 0
;特殊なスタートを選択した場合、君主は弾く。
SIF FLAG:OP選択勢力 <= 0 && GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
	RETURN 0
;「あなた」をまだ作ってないならダメ
SIF !FLAG:あなた作成済み && 対象 == NAME_TO_CHARA("あなた")
	RETURN 0
RETURN 1

;-------------------------------------------------
;君主である固定キャラ使用時に、他勢力との関係をMASTERに移しておく関数
;SWAP_CHARAと併用する
;ARG:0 = 交換対象
;-------------------------------------------------
@NEWGAME_SWAP_RELATIONS(ARG:0)
#DIM REL_LIKE_COPY,1
#DIM REL_HATE_COPY,1
FOR LOCAL, 0, 1000
	REL_LIKE_COPY = REL_LIKE:(ARG:0):LOCAL
	REL_HATE_COPY = REL_HATE:(ARG:0):LOCAL
	REL_LIKE:(ARG:0):LOCAL = REL_LIKE:MASTER:LOCAL
	REL_HATE:(ARG:0):LOCAL = REL_HATE:MASTER:LOCAL
	REL_LIKE:MASTER:LOCAL = REL_LIKE_COPY
	REL_HATE:MASTER:LOCAL = REL_HATE_COPY
NEXT
FOR LOCAL, 0, CHARANUM
	REL_LIKE_COPY = REL_LIKE:(LOCAL):MASTER
	REL_HATE_COPY = REL_HATE:(LOCAL):MASTER
	REL_LIKE:(LOCAL):MASTER = REL_LIKE:(LOCAL):(ARG:0)
	REL_HATE:(LOCAL):MASTER = REL_HATE:(LOCAL):(ARG:0)
	REL_LIKE:(LOCAL):(ARG:0) = REL_LIKE_COPY
	REL_HATE:(LOCAL):(ARG:0) = REL_HATE_COPY
NEXT


;-------------------------------------------------
;旗揚げスタート時用、勢力の設定
;-------------------------------------------------
@START_WITH_NEW_COUNTRY
#DIM FIRST_LINE
#DIM 勢力
#DIM 都市
#DIM 対象
#DIM 消去行
#DIM 士官, 3000
#DIM ランダム可能
ランダム可能 = 0
FOR LOCAL, 1, CITY_NUM + 1
	IF !IS_COUNTRY(CITY_OWNER:LOCAL)
		ランダム可能 = 1
		BREAK
	ENDIF
NEXT

勢力 = GET_NEW_COUNTRY()

CALL SINGLE_DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP
CALL RESET_CITY_COLOR
PRINTL
CALL SINGLE_DRAWLINE
PRINTL 旗揚げする都市を選択してください
PRINTL 勢力が所有している都市は、その勢力の所有都市数が1の場合は選択できません
CALL PRINTBUTTON_EX("[ランダム]", 1000, ランダム可能)
PRINTBUTTON "[戻る]", 0
PRINTL
CALL SINGLE_DRAWLINE
PRINTL 


$INPUT_LOOP_CITY
INPUT

IF RESULT == 0
	RETURN -1
ELSEIF RESULT == 1000 && ランダム可能
	WHILE 1
		都市 = RAND(1, 1  + CITY_NUM + 1)
		SIF !IS_COUNTRY(CITY_OWNER:都市)
			BREAK
	WEND
	REDRAW 1
	PRINTFORMW %GET_RELAYNAME(都市)%で旗揚げします	
ELSEIF RESULT >= 1001 && RESULT <= 1001 + CITY_NUM
	都市 = RESULT - 1000
	;指定された都市が存在し、無所属である場合
	IF GET_RELAYNAME(都市) != "無名" && CITY_TYPE:(都市) == 0 && (CITY_OWNER:(都市) == 0 || GET_OWN_CITY(CITY_OWNER:(都市)) > 1)
		REDRAW 1
		PRINTFORMW %GET_RELAYNAME(都市)%で旗揚げします
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_CITY
	ENDIF
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP_CITY
ENDIF

CALL SINGLE_DRAWLINE
PRINTL 自勢力の士官として加えるキャラを4名まで選択できます
PRINTL なお、君主は選択できません
CALL SINGLE_DRAWLINE
CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(4, "START_WITH_NEW_COUNTRY", "NONE")

FLAG:OP選択勢力 = 勢力
CITY_OWNER:都市 = 勢力
COUNTRY_BOSS:勢力 = GET_ID(MASTER)
COUNTRY_COLOR:勢力 = TALENT:MASTER:髪色 == 0 ? 0x404040 # TALENT:MASTER:髪色
;経済規模に対する最大兵数の半分を得る
COUNTRY_SOLDIER:勢力 = GET_SUM_ECONOMY(勢力) / 20
CITY_SOLDIER:都市 = 500
CALL CHANGE_COUNTRY(MASTER, 勢力, 1)
FOR LOCAL, 0, SELECTED_CHARA_NUM
	CALL CHANGE_COUNTRY(SELECTED_CHARA:LOCAL, 勢力, 1)
NEXT
REDRAW 1
RETURN

;-------------------------------------------------
;旗揚げスタート時用、SELECT_CHARA_LIST_MULTIの表示ロジック
;君主と「あなた」と主人公以外の固定キャラ
;-------------------------------------------------
@SELECT_CHARA_LIST_SHOW_LOGIC_START_WITH_NEW_COUNTRY(対象)
#DIM 対象
RETURN GET_COUNTRY_BOSS(CFLAG:対象:所属) != 対象 && !TALENT:対象:特殊勢力素質 && 対象 != NAME_TO_CHARA("あなた") && 対象 != MASTER
;-------------------------------------------------
;「あなた」を新規に作成した場合のボーナス
;-------------------------------------------------
@NEWLOOP_ANATA_BONUS
#DIM FIRST_LINE
#DIM 選択ボーナス
FIRST_LINE = LINECOUNT
CALL SINGLE_DRAWLINE
CALL COLOR_PRINTL("あなた新規作成ボーナス！", カラー_注意)
PRINTFORML 「あなた」を新規に作成して主人公にしたので、ボーナスがもらえます
PRINTFORML どれにしますか？(どれも後から手に入ります)
{
	CALL ASK_MULTI(
		"素質【酒豪】",                 "素質【天の御遣い】",           "素質【君主の器】", "素質【求心力】", "素質【妖術知識】と妖術ステータス10",
		"素質【技師】",                 "スキル【超成長力】【努力家】", "素質【絶倫】",     "金5万",       "アイテム【催眠術】",
		"【チョロイン】【合意】3人分", "お手軽調教セット",             "いらない")
}
選択ボーナス = RESULT
SELECTCASE 選択ボーナス
	CASE 0
		PRINTFORML 酒に非常に強くなる素質「酒豪」にしますか？
		PRINTFORML (通常取得には大量の飲酒が必要なため、かかる金と手間はボーナス中でも随一です)
	CASE 1
		PRINTFORML 好感度を得やすくなる素質「天の御遣い」にしますか？
		PRINTFORML (恋慕させるルートに入りやすくなります)
	CASE 2
		PRINTFORML 従属度を得やすくなる素質「君主の器」にしますか？
		PRINTFORML (屈服させるルートに入りやすくなります)
	CASE 3
		PRINTFORML 依存度を得やすくなる素質「求心力」にしますか？
		PRINTFORML (恋慕・従属どちらのルートでも必要な値なので便利です)
	CASE 4
		PRINTFORML 触手が扱えるようになる素質「妖術知識」と、ＳＬＧステータス「妖術」10のセットにしますか？
		PRINTFORML (妖術はとても鍛えにくいので10でも貴重です）
	CASE 5
		PRINTFORML アイテム開発が早くなり、効果が高まる素質「技師」にしますか？
		PRINTFORML (多くのアイテムは開発→購入というプロセスを経るので、持っておくと地味に便利です)
	CASE 6
		PRINTFORML ＳＬＧステータスの成長が速くなり、毎ターン経験値が入るスキル「超成長力」、
		PRINTFORML 戦闘に勝利した際に得られる経験値が増え、毎ターン経験値が入るスキル「努力家」のセットにしますか？
		PRINTFORML （育成が目に見えて楽になるので、ひとかどの武将・知将になりたいならぜひ欲しいスキルです）
	CASE 7
		PRINTFORML 調教による体力・気力の減少が非常に少なくなる素質「絶倫」にしますか？
		PRINTFORML （調教中に主人公の体力が尽きると調教が終了してしまいます）
		PRINTFORML （結構激しめに消費するので、あるとかなり便利です）
	CASE 8
		PRINTFORML 金は力。金5万にしますか？
		PRINTFORML （士官の雇用、兵の雇用、アイテム購入に訓練……金はいくらあっても足りません)
		PRINTFORML （これだけあれば、開幕から強力な士官を雇うこともできるでしょう）
	CASE 9
		PRINTFORML 発動することでコマンド実行判定にボーナスのかかる「催眠術」を習得しますか？
		PRINTFORML （購入までの前提条件が多く、最も取得の面倒なアイテムの一つです）
		PRINTFORML （練習が必要ですが、十分に鍛わるとたいていのキャラに容易に好き放題できるようになります）
	CASE 10
		PRINTFORML もってるとチョロくなる隠し素質「チョロイン」と、エロいことをする許し「合意」を3人まで付与しますか？
		PRINTFORML (おまけで、攻略に必要なパラメタである好感度・依存度・服従度・支配度を+1000しておきます)
		PRINTFORML (開幕えろいことしたい人向け)
	CASE 11
		PRINTFORML 調教アイテムの詰め合わせ、お手軽調教セットにしますか？
		PRINTFORML （ローター、バイブ、アナルバイブ、クリ/ニプルキャップ、ペニスバンド、オナホール、縄、鞭、目隠し、ローション * 10、絶倫丸 * 5、ビデオとビデオテープ * 5）
		PRINTFORML （アイテム開発にかかる時間を飛ばせます。使う相手がいないと意味ないけどネ！）
	CASE 12
		PRINTFORML マジで……？
		PRINTFORML (実はあとから別のボーナス！　とかマジでないです)
		PRINTFORML (縛りプレイ用。きみはもの好きなのか？)
ENDSELECT

CALL ASK_YN

IF RESULT == 1
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF

SELECTCASE 選択ボーナス
	CASE 0
		TALENT:MASTER:酒豪 = 1
	CASE 1
		TALENT:MASTER:天の御遣い = 1
	CASE 2
		TALENT:MASTER:君主の器 = 1
	CASE 3
		TALENT:MASTER:求心力 = 1
	CASE 4
		TALENT:MASTER:妖術知識 = 1
		ABL:MASTER:妖術 = 10
	CASE 5
		TALENT:MASTER:技師 = 1
	CASE 6
		CALL LEARN_SKILL_GENERAL_BY_NAME(MASTER, "超成長力")
		CALL LEARN_SKILL_GENERAL_BY_NAME(MASTER, "努力家")
	CASE 7
		TALENT:MASTER:絶倫 = 1
	CASE 8
		MONEY += 50000
	CASE 9
		ITEM:GETNUM(ITEM, "催眠術") = 1
	CASE 10
		PRINTFORML ちゃんと3人指定してなくても決定できちゃうので気を付けてね！
		CALL SINGLE_DRAWLINE
		CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(3, "NONE", "NONE")
		FOR LOCAL, 0, SELECTED_CHARA_NUM
			LOCAL:1 = SELECTED_CHARA:LOCAL 
			TALENT:(LOCAL:1):チョロイン = 1
			TALENT:(LOCAL:1):合意 = 1
			CFLAG:(LOCAL:1):好感度 += 1000
			CFLAG:(LOCAL:1):従属度 += 1000
			CFLAG:(LOCAL:1):依存度 += 1000
			CFLAG:(LOCAL:1):支配度 += 1000
		NEXT
	CASE 11
		ITEM:ローター            = 1
		ITEM:バイブ              = 1
		ITEM:アナルバイブ        = 1
		ITEM:クリキャップ        = 1
		ITEM:ニプルキャップ      = 1
		ITEM:ペニスバンド        = 1
		ITEM:オナホール          = 1
		ITEM:縄                  = 1
		ITEM:鞭                  = 1
		ITEM:目隠し              = 1
		ITEM:ローション         += 10
		ITEM:絶倫丸             += 5
		ITEM:ビデオカメラ        = 1
		ITEM:ビデオテープ       += 5
		ITEMSALES:ローター       = 0
		ITEMSALES:バイブ         = 0
		ITEMSALES:アナルバイブ   = 0
		ITEMSALES:クリキャップ   = 0
		ITEMSALES:ニプルキャップ = 0
		ITEMSALES:ペニスバンド   = 0
		ITEMSALES:オナホール     = 0
		ITEMSALES:縄             = 0
		ITEMSALES:鞭             = 0
		ITEMSALES:目隠し         = 0
		ITEMSALES:ビデオカメラ   = 0
	CASE 12
		PRINTFORML 物好きだなぁ
ENDSELECT

;-------------------------------------------------
;ランダムキャラと性知識の設定
;-------------------------------------------------
@EXTRA_SETTING
#DIM FIRST_LINE
#DIM ランダムキャラ
#DIM 性知識最大
#DIM 君主
FIRST_LINE = LINECOUNT
CALL SINGLE_DRAWLINE

IF FLAG:OPランダムキャラ使用 == 0
	PRINTFORM ランダムキャラ:
	CALL PRINT_SELECT_BUTTON("[使う]", 0, ランダムキャラ)
	CALL PRINT_SELECT_BUTTON("[使わない]", 1, !ランダムキャラ)
	PRINTL 
	PRINTL （非使用を推奨しています）
ENDIF

IF !FLAG:観戦モード
	PRINTFORM         性知識:
	CALL PRINT_SELECT_BUTTON("[5にする]", 10, 性知識最大)
	CALL PRINT_SELECT_BUTTON("[そのまま]", 11, !性知識最大)
	PRINTL
ENDIF

IF FLAG:OP選択勢力 > 0 && COUNTRY_BOSS:(CFLAG:MASTER:所属) != GET_ID(MASTER) && !IS_SP_COUNTRY(CFLAG:MASTER:所属)
	PRINTFORM           君主:
	CALL PRINT_SELECT_BUTTON("[君主としてスタート]", 20, 君主)
	CALL PRINT_SELECT_BUTTON("[そのまま]", 21, !君主)
	PRINTL
ENDIF

CALL SINGLE_DRAWLINE
PRINTBUTTON "[完了]", 100

INPUT

IF RESULT == 100
	IF FLAG:OPランダムキャラ使用 == -1
		FLAG:ランダムキャラ使用 = 0
	ELSEIF FLAG:OPランダムキャラ使用 == 1
		FLAG:ランダムキャラ使用 = 1
	ELSEIF ランダムキャラ
		FLAG:ランダムキャラ使用 = 1
	ELSEIF !ランダムキャラ
		FLAG:ランダムキャラ使用 = 0
	ENDIF
	IF 性知識最大
		ABL:MASTER:性知識 = 5
	ENDIF
	IF 君主
		COUNTRY_BOSS:(CFLAG:MASTER:所属) = GET_ID(MASTER)
	ENDIF
	RETURN
ELSEIF RESULT == 0 && FLAG:OPランダムキャラ使用 == 0
	ランダムキャラ = 1
ELSEIF RESULT == 1
	ランダムキャラ = 0
ELSEIF RESULT == 10
	性知識最大 = 1
ELSEIF RESULT == 11
	性知識最大 = 0
ELSEIF RESULT == 20 && FLAG:OP選択勢力 > 0 && COUNTRY_BOSS:(CFLAG:MASTER:所属) != GET_ID(MASTER) && !IS_SP_COUNTRY(CFLAG:MASTER:所属)
	君主 = 1
ELSEIF RESULT == 21
	君主 = 0
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;-------------------------------------------------
;ランダムキャラを使用するかどうか選択
;-------------------------------------------------
@SELECT_RCHARA
IF FLAG:OPランダムキャラ使用 == -1
	FLAG:ランダムキャラ使用 = 0
ELSEIF FLAG:OPランダムキャラ使用 == 1
	FLAG:ランダムキャラ使用 = 1
ELSE
	CALL SINGLE_DRAWLINE
	PRINTFORML このシナリオではランダムキャラ（ランダムな名前をもつモブ的なキャラ）ありの設定にできます
	PRINTFORML ランダムキャラを使用しますか？
	CALL ASK_YN
	IF RESULT == 0
		FLAG:ランダムキャラ使用 = 1
	ELSE
		FLAG:ランダムキャラ使用 = 0
	ENDIF
ENDIF



;-------------------------------------------------
;カスタムキャラ等の設定
;-------------------------------------------------
@SETUP_SPECIAL_CHARACTERS()
#DIM 候補, MAX_CHARA_NUM
#DIM 候補数
#DIM 配置先, MAX_CHARA_NUM
#DIM FIRST_LINE
#DIM ページ

VARSET 候補,   -1
VARSET 候補数, 0
VARSET 配置先, -1
VARSET ページ

FOR LOCAL, 1, CHARANUM
	SIF IS_FIXED_CHARA(LOCAL) || IS_SP_COUNTRY_CHARA(LOCAL) || NAME_TO_CHARA("あなた") == LOCAL || LOCAL == MASTER
		CONTINUE
	候補:候補数 = LOCAL
	候補数 ++
NEXT

FOR LOCAL, 1, MAX_CUSTOM_CHARA
	SIF !FIND_CHARADATA(TOSTR(LOCAL))
		CONTINUE
	CALL LOAD_CUSTOM_DATA(LOCAL)
	TALENT:RESULT:カスタムキャラ = 2
	候補:候補数 = RESULT
	候補数 ++
NEXT

SIF 候補数 == 0
	RETURN 0

FIRST_LINE = LINECOUNT

$SHOW_LOOP

CALL SINGLE_DRAWLINE
PRINTFORML 引き継いだキャラ・カスタムキャラの出現設定を行います
CALL SINGLE_DRAWLINE

FOR LOCAL, ページ * 20, MIN(ページ * 20 + 20, 候補数)
	PRINTFORM [{LOCAL}]%ANAME(候補:LOCAL)% - 
	IF 配置先:LOCAL == -1
		PRINTFORML 登場しない
	ELSEIF 配置先:LOCAL == 0
		PRINTFORML 放浪
	ELSE
		PRINTFORML %ANAME(GET_COUNTRY_BOSS(配置先:LOCAL))%
	ENDIF
NEXT
CALL SINGLE_DRAWLINE
PRINTBUTTON "[前のページ]", 10000
PRINTFORM {ページ+1}/{候補数 / 20 + 1}
PRINTBUTTON "[次のページ]", 10001
PRINTL
CALL SINGLE_DRAWLINE
PRINTBUTTON "[一括]", 10003
PRINTL
CALL SINGLE_DRAWLINE
PRINTBUTTON "[完了]", 10002

INPUT

IF RESULT == 10000
	ページ = ROUND_DECREMENT(ページ, 0, 候補数 / 20)
ELSEIF RESULT == 10001
	ページ = ROUND_INCREMENT(ページ, 0, 候補数 / 20)
ELSEIF RESULT == 10003
	FOR LOCAL, 0, MAX_COUNTRY
		SIF !IS_COUNTRY(LOCAL)
			CONTINUE
		PRINTFORML [{LOCAL}] %ANAME(GET_COUNTRY_BOSS(LOCAL))%
	NEXT
	PRINTBUTTON "[放浪]", 0
	PRINTL
	PRINTBUTTON "[登場しない]", -1
	PRINTL
	PRINTBUTTON "[中止]", 99
	INPUT
	IF RESULT == 99
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ELSEIF (INRANGE(RESULT, 1, MAX_COUNTRY - 1) && IS_COUNTRY(RESULT)) || GROUPMATCH(RESULT, 0, -1)
		LOCAL:1 = RESULT
		FOR LOCAL, 0, 候補数
			配置先:LOCAL = LOCAL:1
		NEXT
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 10002
	FOR LOCAL, 候補数 - 1, -1,  -1
		IF 配置先:LOCAL == -1
			CALL DELETE_CHARA(候補:LOCAL)
		ELSEIF 配置先:LOCAL == 0
			CFLAG:(候補:LOCAL):所属 = 0
			CFLAG:(候補:LOCAL):特殊状態 = 特殊状態_放浪
		ELSE
			CFLAG:(候補:LOCAL):所属 = 配置先:LOCAL
			CFLAG:(候補:LOCAL):特殊状態 = 0
		ENDIF
	NEXT
	RETURN
ENDIF

IF INRANGE(RESULT, 0, 候補数)
	LOCAL:1 = RESULT
	FOR LOCAL, 0, MAX_COUNTRY
		SIF !IS_COUNTRY(LOCAL)
			CONTINUE
		PRINTFORML [{LOCAL}] %ANAME(GET_COUNTRY_BOSS(LOCAL))%
	NEXT
	PRINTBUTTON "[放浪]", 0
	PRINTL
	PRINTBUTTON "[登場しない]", -1
	PRINTL
	INPUT
	IF GROUPMATCH(RESULT, 0, -1)
		配置先:(LOCAL:1) = RESULT
	ELSEIF IS_COUNTRY(RESULT)
		配置先:(LOCAL:1) = RESULT
	ENDIF
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP
