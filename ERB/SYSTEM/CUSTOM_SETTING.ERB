;-------------------------------------------------
;キャラARG:0にARG:1番目のカスタムデータを適用
;-------------------------------------------------
@APPLY_CUSTOM_DATA(ARG:0, ARG:1)
;基本名(NAME)
NAME:(ARG:0) = %CUSTOM_NAME:(ARG:1):0%
;呼び名(CALLNAME)（旧真名）
CALLNAME:(ARG:0) = %CUSTOM_NAME:(ARG:1):1%
;姓
CSTR:(ARG:0):0 = %CUSTOM_NAME:(ARG:1):2%
;名
CSTR:(ARG:0):1 = %CUSTOM_NAME:(ARG:1):3%
;字
CSTR:(ARG:0):2 = %CUSTOM_NAME:(ARG:1):4%
;基本名(NAME)のフリガナ
CSTR:(ARG:0):3 = %CUSTOM_NAME:(ARG:1):5%
;呼び名(CALLNAME)のフリガナ
CSTR:(ARG:0):4 = %CUSTOM_NAME:(ARG:1):6%
;姓のフリガナ
CSTR:(ARG:0):5 = %CUSTOM_NAME:(ARG:1):7%
;名のフリガナ
CSTR:(ARG:0):6 = %CUSTOM_NAME:(ARG:1):8%
;字のフリガナ
CSTR:(ARG:0):7 = %CUSTOM_NAME:(ARG:1):9%
;愛称
CSTR:(ARG:0):98 = %CUSTOM_NAME:(ARG:1):10%
FOR LOCAL:0, 0, MAX_TAG
	TAG:(ARG:0):(LOCAL:0) = CUSTOM_TAG:(ARG:1):(LOCAL:0)
NEXT
FOR LOCAL:0, 0, 100
	MAXBASE:(ARG:0):(LOCAL:0) = CUSTOM_MAXBASE:(ARG:1):(LOCAL:0)
	BASE:(ARG:0):(LOCAL:0) = MAXBASE:(ARG:0):(LOCAL:0)
	ABL:(ARG:0):(LOCAL:0) = CUSTOM_ABL:(ARG:1):(LOCAL:0)
	EXP:(ARG:0):(LOCAL:0) = CUSTOM_EXP:(ARG:1):(LOCAL:0)
NEXT
FOR LOCAL:0, 0, 1000
	SIF APPLY_CUSTOM_DATA_IGNORE_TALENT(LOCAL:0)
		CONTINUE
	TALENT:(ARG:0):(LOCAL:0) = CUSTOM_TALENT:(ARG:1):(LOCAL:0)
NEXT

FOR LOCAL:0, 0, SKILL_GENRE_NUM
	FOR LOCAL:1, 0, MAX_SKILL_SLOT
		SKILL_NO_SLOT:(ARG:0):(LOCAL:0):(LOCAL:1) = CUSTOM_SKILL_NO:(ARG:1):(LOCAL:0):(LOCAL:1)
		SKILL_ID_SLOT:(ARG:0):(LOCAL:0):(LOCAL:1) = CUSTOM_SKILL_ID:(ARG:1):(LOCAL:0):(LOCAL:1)
	NEXT
NEXT

;キス未経験でないならキス経験を不明として記録
IF !TALENT:(ARG:0):キス未経験
	SEXUAL_EXPERIENCE:(ARG:0):初体験_キス = 不明
	SEXUAL_LAST_EXPERIENCE:(ARG:0):初体験_キス = 不明
ELSE
	SEXUAL_EXPERIENCE:(ARG:0):初体験_キス = ----
	SEXUAL_LAST_EXPERIENCE:(ARG:0):初体験_キス = ----
ENDIF

IF HAS_VAGINA(ARG:0) && !TALENT:(ARG:0):処女
	SEXUAL_EXPERIENCE:(ARG:0):初体験_処女 = 不明
	SEXUAL_LAST_EXPERIENCE:(ARG:0):初体験_処女 = 不明
ELSE
	TALENT:(ARG:0):処女 = 1
	SEXUAL_EXPERIENCE:(ARG:0):初体験_処女 = ----
	SEXUAL_LAST_EXPERIENCE:(ARG:0):初体験_処女 = ----
ENDIF

IF HAS_PENIS(ARG:0) && !TALENT:(ARG:0):童貞
	SEXUAL_EXPERIENCE:(ARG:0):初体験_童貞 = 不明
	SEXUAL_LAST_EXPERIENCE:(ARG:0):初体験_童貞 = 不明
ELSE
	TALENT:(ARG:0):童貞 = 1
	SEXUAL_EXPERIENCE:(ARG:0):初体験_童貞 = ----
	SEXUAL_LAST_EXPERIENCE:(ARG:0):初体験_童貞 = ----
ENDIF

IF !TALENT:(ARG:0):アナル処女
	SEXUAL_EXPERIENCE:(ARG:0):初体験_アナル処女 = 不明
	SEXUAL_LAST_EXPERIENCE:(ARG:0):初体験_アナル処女 = 不明
ELSE
	TALENT:(ARG:0):アナル処女 = 1
	SEXUAL_EXPERIENCE:(ARG:0):初体験_アナル処女 = ----
	SEXUAL_LAST_EXPERIENCE:(ARG:0):初体験_アナル処女 = ----
ENDIF

;-------------------------------------------------
;カスタムデータARG:0にARG:1番目のキャラデータを適用
;-------------------------------------------------
@REVERSE_APPLY_CUSTOM_DATA(ARG:0, ARG:1)
;基本名(NAME)
CUSTOM_NAME:(ARG:0):0 = %NAME:(ARG:1)%
;呼び名(CALLNAME)（旧真名）
CUSTOM_NAME:(ARG:0):1 = %CALLNAME:(ARG:1)%
;姓
CUSTOM_NAME:(ARG:0):2 = %CSTR:(ARG:1):0%
;名
CUSTOM_NAME:(ARG:0):3 = %CSTR:(ARG:1):1%
;字
CUSTOM_NAME:(ARG:0):4 = %CSTR:(ARG:1):2%
;基本名(NAME)のフリガナ
CUSTOM_NAME:(ARG:0):5 = %CSTR:(ARG:1):3%
;呼び名(CALLNAME)のフリガナ
CUSTOM_NAME:(ARG:0):6 = %CSTR:(ARG:1):4%
;姓のフリガナ
CUSTOM_NAME:(ARG:0):7 = %CSTR:(ARG:1):5%
;名のフリガナ
CUSTOM_NAME:(ARG:0):8 = %CSTR:(ARG:1):6%
;字のフリガナ
CUSTOM_NAME:(ARG:0):9 = %CSTR:(ARG:1):7%
;愛称
CUSTOM_NAME:(ARG:0):10 = %CSTR:(ARG:1):98%

FOR LOCAL:0, 0, MAX_TAG
	CUSTOM_TAG:(ARG:0):(LOCAL:0) = TAG:(ARG:1):(LOCAL:0)
NEXT
FOR LOCAL:0, 0, 100
	CUSTOM_MAXBASE:(ARG:0):(LOCAL:0) = MAXBASE:(ARG:1):(LOCAL:0)
	CUSTOM_ABL:(ARG:0):(LOCAL:0) = ABL:(ARG:1):(LOCAL:0)
	CUSTOM_EXP:(ARG:0):(LOCAL:0) = EXP:(ARG:1):(LOCAL:0)
NEXT
FOR LOCAL:0, 0, 300
	SIF REVERSE_APPLY_CUSTOM_DATA_IGNORE_TALENT(LOCAL:0)
		CONTINUE
	CUSTOM_TALENT:(ARG:0):(LOCAL:0) = TALENT:(ARG:1):(LOCAL:0)
NEXT

CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "成長型")   = TALENT:(ARG:1):成長型
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "性格")     = TALENT:(ARG:1):性格
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "髪色")     = TALENT:(ARG:1):髪色
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "髪の長さ") = TALENT:(ARG:1):髪の長さ
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "姓名逆転") = TALENT:(ARG:1):姓名逆転
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "Ｖ締まり") = TALENT:(ARG:1):Ｖ締まり
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "Ａ締まり") = TALENT:(ARG:1):Ａ締まり
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "陰毛現在値") = TALENT:(ARG:1):陰毛現在値
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "陰毛目標値") = TALENT:(ARG:1):陰毛目標値
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "バストサイズ") = TALENT:(ARG:1):バストサイズ
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "ペニスサイズ") = TALENT:(ARG:1):ペニスサイズ

FOR LOCAL:0, 0, SKILL_GENRE_NUM
	FOR LOCAL:1, 0, MAX_SKILL_SLOT
		CUSTOM_SKILL_NO:(ARG:0):(LOCAL:0):(LOCAL:1) = SKILL_NO_SLOT:(ARG:1):(LOCAL:0):(LOCAL:1)
		CUSTOM_SKILL_ID:(ARG:0):(LOCAL:0):(LOCAL:1) = SKILL_ID_SLOT:(ARG:1):(LOCAL:0):(LOCAL:1)
	NEXT
NEXT
SAVEGLOBAL


@REVERSE_APPLY_CUSTOM_DATA_IGNORE_TALENT(ARG:0)
#FUNCTION
{
RETURNF GROUPMATCH(ARG:0, GETNUM(TALENT, "恋慕"), GETNUM(TALENT, "親愛"), GETNUM(TALENT, "恋人"), GETNUM(TALENT, "服従"), GETNUM(TALENT, "隷属"), GETNUM(TALENT, "烙印"), GETNUM(TALENT, "親友"),
GETNUM(TALENT, "崩壊"), GETNUM(TALENT, "虚ろ"), GETNUM(TALENT, "正妻"), GETNUM(TALENT, "妾"), GETNUM(TALENT, "妊娠"), GETNUM(TALENT, "育児中"), 
GETNUM(TALENT, "触手妊娠"), GETNUM(TALENT, "Ａ触手妊娠"), GETNUM(TALENT, "子供人質"),  GETNUM(TALENT, "合意"), 
GETNUM(TALENT, "孕み可能"), GETNUM(TALENT, "母性"), GETNUM(TALENT, "危険日"), GETNUM(TALENT, "淫乱系"), GETNUM(TALENT, "特殊勢力陥落系"))
}

@APPLY_CUSTOM_DATA_IGNORE_TALENT(ARG:0)
#FUNCTION
{
RETURNF GROUPMATCH(ARG:0, GETNUM(TALENT, "恋慕"), GETNUM(TALENT, "親愛"), GETNUM(TALENT, "恋人"), GETNUM(TALENT, "服従"), GETNUM(TALENT, "隷属"), GETNUM(TALENT, "烙印"), GETNUM(TALENT, "親友"),
GETNUM(TALENT, "崩壊"), GETNUM(TALENT, "虚ろ"), GETNUM(TALENT, "正妻"), GETNUM(TALENT, "妾"), GETNUM(TALENT, "妊娠"), GETNUM(TALENT, "育児中"), 
GETNUM(TALENT, "触手妊娠"), GETNUM(TALENT, "Ａ触手妊娠"), GETNUM(TALENT, "子供人質"), GETNUM(TALENT, "合意"), 
GETNUM(TALENT, "孕み可能"), GETNUM(TALENT, "母性"), GETNUM(TALENT, "危険日"), GETNUM(TALENT, "淫乱系"), GETNUM(TALENT, "特殊勢力陥落系"))
}

;-------------------------------------------------
;カスタムデータARG:0にARG:1番目のカスタムデータをコピー
;-------------------------------------------------
@COPY_CUSTOM_DATA(ARG:0, ARG:1)
;基本名(NAME)
CUSTOM_NAME:(ARG:0):0 = %CUSTOM_NAME:(ARG:1):0%
;呼び名(CALLNAME)（旧真名）
CUSTOM_NAME:(ARG:0):1 = %CUSTOM_NAME:(ARG:1):1%
;姓
CUSTOM_NAME:(ARG:0):2 = %CUSTOM_NAME:(ARG:1):2%
;名
CUSTOM_NAME:(ARG:0):3 = %CUSTOM_NAME:(ARG:1):3%
;字
CUSTOM_NAME:(ARG:0):4 = %CUSTOM_NAME:(ARG:1):4%
;基本名(NAME)のフリガナ
CUSTOM_NAME:(ARG:0):5 = %CUSTOM_NAME:(ARG:1):5%
;呼び名(CALLNAME)のフリガ
CUSTOM_NAME:(ARG:0):6 = %CUSTOM_NAME:(ARG:1):6%
;姓のフリガナ
CUSTOM_NAME:(ARG:0):7 = %CUSTOM_NAME:(ARG:1):7%
;名のフリガナ
CUSTOM_NAME:(ARG:0):8 = %CUSTOM_NAME:(ARG:1):8%
;字のフリガナ
CUSTOM_NAME:(ARG:0):9 = %CUSTOM_NAME:(ARG:1):9%
;愛称
CUSTOM_NAME:(ARG:0):10 =%CUSTOM_NAME:(ARG:1):10%

FOR LOCAL:0, 0, MAX_TAG
	CUSTOM_TAG:(ARG:0):(LOCAL:0) = CUSTOM_TAG:(ARG:1):(LOCAL:0)
NEXT
FOR LOCAL:0, 0, 100
	CUSTOM_MAXBASE:(ARG:0):(LOCAL:0) = CUSTOM_MAXBASE:(ARG:1):(LOCAL:0)
	CUSTOM_ABL:(ARG:0):(LOCAL:0) = CUSTOM_ABL:(ARG:1):(LOCAL:0)
	CUSTOM_EXP:(ARG:0):(LOCAL:0) = CUSTOM_EXP:(ARG:1):(LOCAL:0)
NEXT
FOR LOCAL:0, 0, 1000
	CUSTOM_TALENT:(ARG:0):(LOCAL:0) = CUSTOM_TALENT:(ARG:1):(LOCAL:0)
NEXT

FOR LOCAL:0, 0, SKILL_GENRE_NUM
	FOR LOCAL:1, 0, MAX_SKILL_SLOT
		CUSTOM_SKILL_NO:(ARG:0):(LOCAL:0):(LOCAL:1) = CUSTOM_SKILL_NO:(ARG:1):(LOCAL:0):(LOCAL:1)
		CUSTOM_SKILL_ID:(ARG:0):(LOCAL:0):(LOCAL:1) = CUSTOM_SKILL_ID:(ARG:1):(LOCAL:0):(LOCAL:1)
	NEXT
NEXT
SAVEGLOBAL
;-------------------------------------------------
;キャラARG:0番目のカスタムデータを削除
;-------------------------------------------------
@DELETE_CUSTOM(ARG:0)
;カスタムデータを前に１つ詰める
FOR LOCAL:1, ARG:0, 29
	FOR LOCAL:0, 0, 100
		CUSTOM_MAXBASE:(LOCAL:1):(LOCAL:0) = CUSTOM_MAXBASE:(LOCAL:1 + 1):(LOCAL:0)
		CUSTOM_ABL:(LOCAL:1):(LOCAL:0) = CUSTOM_ABL:(LOCAL:1 + 1):(LOCAL:0)
		CUSTOM_EXP:(LOCAL:1):(LOCAL:0) = CUSTOM_EXP:(LOCAL:1 + 1):(LOCAL:0)
	NEXT
	FOR LOCAL:0, 0, 1000
		CUSTOM_TALENT:(LOCAL:1):(LOCAL:0) = CUSTOM_TALENT:(LOCAL:1 + 1):(LOCAL:0)
	NEXT
	FOR LOCAL:0, 0, 10
		CUSTOM_NAME:(LOCAL:1):(LOCAL:0) = %CUSTOM_NAME:(LOCAL:1 + 1):(LOCAL:0)%
	NEXT
	FOR LOCAL:0, 0, MAX_TAG
		CUSTOM_TAG:(LOCAL:1):(LOCAL:0) = CUSTOM_TAG:(LOCAL:1 + 1):(LOCAL:0)
	NEXT
	CUSTOM_NAMETYPE:(LOCAL:1) = CUSTOM_NAMETYPE:(LOCAL:1 + 1)
NEXT
FOR LOCAL:0, 0, MAX_TAG
	CUSTOM_TAG:29:(LOCAL:0) = -1
NEXT
FOR LOCAL:0, 0, 100
	CUSTOM_MAXBASE:29:(LOCAL:0) = 0
	CUSTOM_ABL:29:(LOCAL:0) = 0
	CUSTOM_EXP:29:(LOCAL:0) = 0
NEXT
FOR LOCAL:0, 0, 1000
	CUSTOM_TALENT:29:(LOCAL:0) = 0
NEXT
FOR LOCAL:0, 0, 10
	CUSTOM_NAME:29:(LOCAL:0) = 
NEXT
CUSTOM_NAMETYPE:29 = 0

;-------------------------------------------------
;カスタムキャラのイニシャライズ　新規作成前に呼ぶこと
;-------------------------------------------------
@INIT_CUSTOM_DATA(ARG:0)
;デフォルト能力の設定
CUSTOM_MAXBASE:(ARG:0):0 = 2000
CUSTOM_MAXBASE:(ARG:0):1 = 1500
CUSTOM_MAXBASE:(ARG:0):2 = 2500

CUSTOM_TALENT:(ARG:0):0 = 0
CUSTOM_TALENT:(ARG:0):1 = 1
CUSTOM_TALENT:(ARG:0):2 = 1
CUSTOM_TALENT:(ARG:0):3 = 1
CUSTOM_TALENT:(ARG:0):4 = 1
CUSTOM_TALENT:(ARG:0):200 = 1
CUSTOM_TALENT:(ARG:0):300 = 0
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "陰毛現在値") = 陰毛_標準
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "陰毛目標値") = 陰毛_標準
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "Ｖ締まり") = GET_DEFAULT_TIGHTNESS("普通")
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "Ａ締まり") = GET_DEFAULT_TIGHTNESS("普通")
CUSTOM_ABL:(ARG:0):14 = 5
CUSTOM_ABL:(ARG:0):50 = 60
CUSTOM_ABL:(ARG:0):51 = 60
CUSTOM_ABL:(ARG:0):52 = 60
CUSTOM_ABL:(ARG:0):53 = 60
CUSTOM_ABL:(ARG:0):60 = 25
CUSTOM_ABL:(ARG:0):61 = 25
FOR LOCAL, 0, MAX_TAG
	CUSTOM_TAG:(ARG:0):LOCAL = -1
NEXT
FOR LOCAL, 0, SKILL_GENRE_NUM
	FOR LOCAL:1, 0, MAX_SKILL_SLOT
		CUSTOM_SKILL_NO:(ARG:0):(LOCAL:0):(LOCAL:1) = -1
		CUSTOM_SKILL_ID:(ARG:0):(LOCAL:0):(LOCAL:1) = -1
	NEXT
NEXT
CUSTOM_NAME:(ARG:0):0 = カスタム太郎
CUSTOM_NAME:(ARG:0):1 = カスタム太郎
CUSTOM_NAME:(ARG:0):3 = カスタム太郎
;-------------------------------------------------
;カスタムキャラの設定
;-------------------------------------------------
@EDIT_CUSTOM_DATA(ARG:0)
#DIMS タブ = "NAME", "ABL", "TALENT", "EXTRA_INFO"
#DIMS タブ日本語 = "名前", "能力", "素質", "その他"
#DIM 表示ページ
#DIM 現在タブ
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT

;妖術が1以上なら<妖術知識>を付与
CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "妖術知識") = CUSTOM_ABL:(ARG:0):GETNUM(ABL, "妖術") >= 1


CALL SINGLE_DRAWLINE
PRINTFORML カスタムキャラ編集中 {ARG:0}番
CALL SINGLE_DRAWLINE

CALLFORM EDIT_CUSTOM_DATA_%タブ:現在タブ%_UI(ARG:0)

CALL SINGLE_DRAWLINE

FOR LOCAL, 0, VARSIZE("タブ")
	CALL PRINT_SELECT_BUTTON("[" + タブ日本語:LOCAL + "]", 10000 + LOCAL, 現在タブ == LOCAL)
	PRINTFORM  
NEXT
PRINTL

CALL SINGLE_DRAWLINE

PRINTBUTTON "[登録]", 100000
PRINTBUTTON " [キャンセル]", 100001

INPUT

IF RESULT == 100000
	SAVEGLOBAL
	RETURN 1
ELSEIF RESULT == 100001
	PRINTL 登録せずに戻ります。よろしいですか？
	CALL ASK_YN()
	IF RESULT == 0
		;修正ならグローバルを読み戻す
		LOADGLOBAL
		RETURN 0
	ENDIF
ELSEIF INRANGE(RESULT, 10000, 10000 + VARSIZE("タブ") - 1)
	現在タブ = RESULT - 10000
ELSE
	CALLFORM EDIT_CUSTOM_DATA_%タブ:現在タブ%_PROCESS(ARG:0, RESULT)
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;-------------------------------------------------
;カスタムキャラの名前編集
;-------------------------------------------------
@EDIT_CUSTOM_DATA_NAME_UI(ARG:0)

PRINT 名前の形式:
CALL PRINT_SELECT_BUTTON("[名のみ]", 1000, CUSTOM_NAMETYPE:(ARG:0) == 0)
CALL PRINT_SELECT_BUTTON("[中華風]", 1001, CUSTOM_NAMETYPE:(ARG:0) == 1)
CALL PRINT_SELECT_BUTTON("[日本風]", 1002, CUSTOM_NAMETYPE:(ARG:0) == 2)
CALL PRINT_SELECT_BUTTON("[洋風]", 1003, CUSTOM_NAMETYPE:(ARG:0) == 3)
PRINTL 
PRINTL 
SELECTCASE CUSTOM_NAMETYPE:(ARG:0)
	CASE 0
		PRINTFORML 呼称 -- %CUSTOM_NAME:(ARG:0):0, 14, LEFT%
		PRINTBUTTON "[入力]", 0
		PRINTBUTTON " [ランダム]", 201
		PRINTBUTTON " [消去]", 100
		PRINTL
	CASE 1, 2, 3
		PRINTFORML 姓 -- %CUSTOM_NAME:(ARG:0):2, 10, LEFT%%CUSTOM_NAME:(ARG:0):7, 14, LEFT%
		PRINTBUTTON "[入力]",2
		PRINTBUTTON " [ランダム]", 200
		PRINTBUTTON " [消去]", 102
		PRINTL 
		PRINTFORML 名 -- %CUSTOM_NAME:(ARG:0):3, 10, LEFT%%CUSTOM_NAME:(ARG:0):8, 14, LEFT%
		PRINTBUTTON "[入力]", 3
		PRINTBUTTON " [ランダム]", 201
		PRINTBUTTON " [消去]", 103
		PRINTL 
ENDSELECT

IF CUSTOM_NAMETYPE:(ARG:0) == 1
	PRINTFORML 字 -- %CUSTOM_NAME:(ARG:0):4, 10, LEFT%%CUSTOM_NAME:(ARG:0):9, 14, LEFT%
	PRINTBUTTON "[入力]", 4
	PRINTBUTTON " [ランダム]", 202
	PRINTBUTTON " [消去]", 104
	PRINTL 
ENDIF

PRINTFORML 愛称 -- %CUSTOM_NAME:(ARG:0):10, 14, LEFT%
PRINTBUTTON "[入力]", 10
PRINTBUTTON " [消去]", 110
PRINTL


@EDIT_CUSTOM_DATA_NAME_PROCESS(ARG:0, ARG:1)
IF INRANGE(ARG:1, 1000, 1003)
	CUSTOM_NAMETYPE:(ARG:0) = ARG:1 - 1000

;手入力
ELSEIF INRANGE(ARG:1, 0, VARSIZE("CUSTOM_NAME", 1) - 1)
	PRINTFORML 入力
	INPUTS
	CUSTOM_NAME:(ARG:0):(ARG:1) '= RESULTS

	IF GROUPMATCH(ARG:1, 0, 3)
		CUSTOM_NAME:(ARG:0):0 '= RESULTS
		CUSTOM_NAME:(ARG:0):1 '= RESULTS
		CUSTOM_NAME:(ARG:0):8 '= RESULTS
	ENDIF
	IF ARG:1 != 10
		PRINTFORML ルビ入力
		INPUTS
		CUSTOM_NAME:(ARG:0):(ARG:1 + 5) '= RESULTS
		IF GROUPMATCH(ARG:1, 0, 3)
			CUSTOM_NAME:(ARG:0):5 '= RESULTS
			CUSTOM_NAME:(ARG:0):6'= RESULTS
			CUSTOM_NAME:(ARG:0):8'= RESULTS
		ENDIF
	ENDIF

;消去
ELSEIF INRANGE(ARG:1, 100, 100 + VARSIZE("CUSTOM_NAME", 1) - 1)
	LOCAL = ARG:1 - 100
	CUSTOM_NAME:(ARG:0):LOCAL '= ""

	IF GROUPMATCH(LOCAL, 0, 3)
		CUSTOM_NAME:(ARG:0):0 '= ""
		CUSTOM_NAME:(ARG:0):1 '= ""
		CUSTOM_NAME:(ARG:0):3 '= ""

	ENDIF

	IF LOCAL != 10
		CUSTOM_NAME:(ARG:0):(LOCAL + 5) '= ""
		IF GROUPMATCH(LOCAL, 0, 3)
			CUSTOM_NAME:(ARG:0):5 '= ""
			CUSTOM_NAME:(ARG:0):6 '= ""
			CUSTOM_NAME:(ARG:0):8 '= ""
		ENDIF
	ENDIF

;性ランダム
ELSEIF ARG:1 == 200
	SELECTCASE CUSTOM_NAMETYPE:(ARG:0)
		CASE 1
			CALL GENERATE_FAMILY_NAME
		CASE 2
			CALL GENERATE_FAMILY_NAME_J
		CASE 3
			CALL GENERATE_FAMILY_NAME_K
	ENDSELECT
	CUSTOM_NAME:(ARG:0):2 '= RESULTS:0
	IF CUSTOM_NAMETYPE:(ARG:0) != 3
		CUSTOM_NAME:(ARG:0):7 '= RESULTS:1
	ELSE
		CUSTOM_NAME:(ARG:0):7 '= ""
	ENDIF

;名ランダム
ELSEIF ARG:1 == 201
	SELECTCASE CUSTOM_NAMETYPE:(ARG:0)
		CASE 1
			CALL GENERATE_FIRST_NAME
			CUSTOM_NAME:(ARG:0):0 '= RESULTS:0
			CUSTOM_NAME:(ARG:0):1 '= RESULTS:0
			CUSTOM_NAME:(ARG:0):3 '= RESULTS:0
			CUSTOM_NAME:(ARG:0):5 '= RESULTS:1
			CUSTOM_NAME:(ARG:0):6 '= RESULTS:1
			CUSTOM_NAME:(ARG:0):8 '= RESULTS:1
		CASE 0, 2
			IF GROUPMATCH(CUSTOM_TALENT:(ARG:0):0, 0, 3, 4, 5)
				CALL GENERATE_FIRST_NAME_JM
			ELSE
				CALL GENERATE_FIRST_NAME_JF
			ENDIF
			CUSTOM_NAME:(ARG:0):0 '= RESULTS:0
			CUSTOM_NAME:(ARG:0):1 '= RESULTS:0
			CUSTOM_NAME:(ARG:0):3 '= RESULTS:0
			CUSTOM_NAME:(ARG:0):5 '= RESULTS:1
			CUSTOM_NAME:(ARG:0):6 '= RESULTS:1
			CUSTOM_NAME:(ARG:0):8 '= RESULTS:1

		CASE 3
			IF GROUPMATCH(CUSTOM_TALENT:(ARG:0):0, 0, 3, 4, 5)
				CALL GENERATE_FIRST_NAME_KM
			ELSE
				CALL GENERATE_FIRST_NAME_KF
			ENDIF
			CUSTOM_NAME:(ARG:0):0 '= RESULTS:0
			CUSTOM_NAME:(ARG:0):1 '= RESULTS:0
			CUSTOM_NAME:(ARG:0):3 '= RESULTS:0
			CUSTOM_NAME:(ARG:0):5 '= ""
			CUSTOM_NAME:(ARG:0):6 '= ""
			CUSTOM_NAME:(ARG:0):8 '= ""
	ENDSELECT

ELSEIF ARG:1 == 202 && CUSTOM_NAMETYPE:(ARG:0) == 1
	CALL GENERATE_MIDDLE_NAME
	CUSTOM_NAME:(ARG:0):4 '= RESULTS:0
	CUSTOM_NAME:(ARG:0):9 '= RESULTS:1
ENDIF


;-------------------------------------------------
;カスタムキャラの能力編集
;-------------------------------------------------
@EDIT_CUSTOM_DATA_ABL_UI(ARG:0)

LOCAL:1 = 0
FOR LOCAL, 0, VARSIZE("ABL")
	SIF ABLNAME:(LOCAL) == ""
		CONTINUE
	PRINTBUTTON @"% "[" + ABLNAME:LOCAL + "]" , 10, LEFT%:", LOCAL

	IF GROUPMATCH(LOCAL, GETNUM(ABL, "武闘"), GETNUM(ABL, "防衛"), GETNUM(ABL, "知略"), GETNUM(ABL, "政治"), GETNUM(ABL, "料理"), GETNUM(ABL, "歌唱"), GETNUM(ABL, "妖術"))
		CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, CUSTOM_ABL:(ARG:0):LOCAL)
	ELSEIF LOCAL == GETNUM(ABL, "性知識")
		CALL PRINT_ALPHABET_RANK(ランク_性知識, CUSTOM_ABL:(ARG:0):LOCAL)
	ELSE
		CALL PRINT_ALPHABET_RANK(ランク_その他, CUSTOM_ABL:(ARG:0):LOCAL)
	ENDIF

	PRINTFORM  {CUSTOM_ABL:(ARG:0):LOCAL, 3, RIGHT}
	LOCAL:1 ++
	SIF LOCAL:1 != 0 && LOCAL:1 % 5 == 0
		PRINTL
NEXT
PRINTL

@EDIT_CUSTOM_DATA_ABL_PROCESS(ARG:0, ARG:1)

SIF !INRANGE(ARG:1, 0, VARSIZE("ABL") - 1)
	RETURN 0

PRINTFORML 値を入力

INPUT

SIF ARG:1 == GETNUM(ABL, "性知識") && !INRANGE(RESULT, 0, 5)
	RETURN

CUSTOM_ABL:(ARG:0):(ARG:1) = RESULT

;-------------------------------------------------
;カスタムキャラの素質編集
;-------------------------------------------------
@EDIT_CUSTOM_DATA_TALENT_UI(ARG:0)

LOCAL:1 = 0
FOR LOCAL, 0, 300
	SIF CUSTOM_DATA_TALENT_IGNORE_TALENT(LOCAL)
		CONTINUE
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	CALL PRINT_TOGGLE_BUTTON(@"%"[" + TALENTNAME:(LOCAL) + "]" , 14, LEFT%", LOCAL, CUSTOM_TALENT:(ARG:0):LOCAL == 1)
	PRINT  
	LOCAL:1 ++

	SIF LOCAL:1 != 0 && LOCAL:1 % 6 == 0
		PRINTL
NEXT
PRINTL
CALL SINGLE_DRAWLINE

;特殊処理
PRINTBUTTON "[性別]:", GETNUM(TALENT, "性別")
PRINTFORM %TOSTR_SEX(CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "性別"))%
PRINTL
PRINTBUTTON "[Ｖ締まり]:", GETNUM(TALENT, "Ｖ締まり")
PRINTFORML %TOSTR_TIGHTNESS(CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "Ｖ締まり"))%
PRINTBUTTON "[Ａ締まり]:", GETNUM(TALENT, "Ａ締まり")
PRINTFORML %TOSTR_TIGHTNESS(CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "Ａ締まり"))%
PRINTBUTTON "[陰毛]:", GETNUM(TALENT, "陰毛現在値") 
PRINTFORML %GET_INMOU_NAME(CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "陰毛現在値"))%
PRINTBUTTON "[バスト]:", GETNUM(TALENT, "バストサイズ")
PRINTFORML %TOSTR_BUSTSIZE(CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "バストサイズ"))%
PRINTBUTTON "[ペニス]:", GETNUM(TALENT, "ペニスサイズ")
PRINTFORML %TOSTR_PENISSIZE(CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "ペニスサイズ"))%
PRINTBUTTON "[髪長さ]:", GETNUM(TALENT, "髪の長さ")
PRINTFORML %TOSTR_HAIR_LENGTH(CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "髪の長さ")), 12% ({CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "髪の長さ"), 4})
PRINTBUTTON "[髪の色]:", GETNUM(TALENT, "髪色")
CALL PRINT_HAIR_COLOR(CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "髪色"))
PRINTL


@EDIT_CUSTOM_DATA_TALENT_PROCESS(ARG:0, ARG:1)

;ガード節
SIF (CUSTOM_DATA_TALENT_IGNORE_TALENT(ARG:1) && ARG:1 != GETNUM(TALENT, "性別")) || TALENTNAME:(ARG:1) == ""
	RETURN 0

;通常入力
IF INRANGE(ARG:1, 0, 299) && ARG:1 != GETNUM(TALENT, "性別")
	CUSTOM_TALENT:(ARG:0):(ARG:1) = !CUSTOM_TALENT:(ARG:0):(ARG:1)

;性別
ELSEIF ARG:1 == GETNUM(TALENT, "性別")
	CALL ASK_MULTI("男", "女", "女双", "男双", "男の娘", "男の子双")
	CUSTOM_TALENT:(ARG:0):(GETNUM(TALENT,"性別")) = RESULT

;締まり
ELSEIF GROUPMATCH(ARG:1, GETNUM(TALENT, "Ｖ締まり"), GETNUM(TALENT, "Ａ締まり"))
	CALL ASK_MULTI("崩壊", "がばがば", "ぽっかり", "ゆるゆる", "ゆるめ", "普通", "名器", "きゅっきゅっ", "きつきつ", "ぎちぎち")
	CUSTOM_TALENT:(ARG:0):(ARG:1) = RESULT * 100 + 50

;陰毛
ELSEIF ARG:1 == GETNUM(TALENT, "陰毛現在値")
	CALL ASK_MULTI("パイパン", "つるつる", "うっすら", "柔毛", "ふんわり", "標準", "多め", "ふさふさ", "もっさり", "剛毛", "尻まで", "♥")
	CUSTOM_TALENT:(ARG:0):(ARG:1) = RESULT -1
	CUSTOM_TALENT:(ARG:0):GETNUM(TALENT, "陰毛目標値") = RESULT - 1

;バスト・ペニス
ELSEIF GROUPMATCH(ARG:1, GETNUM(TALENT, "バストサイズ"), GETNUM(TALENT, "ペニスサイズ"))
	CUSTOM_TALENT:(ARG:0):(ARG:1) = ROUND_INCREMENT(CUSTOM_TALENT:(ARG:0):(ARG:1), -2, 2)

ELSEIF ARG:1 == GETNUM(TALENT, "髪の長さ")
	PRINTFORML 0～1000で入力
	INPUT
	SIF INRANGE(RESULT, 0, 1000)
		CUSTOM_TALENT:(ARG:0):(ARG:1) = RESULT

ELSEIF ARG:1 == GETNUM(TALENT, "髪色")
	PRINTFORML カラーコードを入力
	INPUTS
	SIF ISNUMERIC("0x" + RESULTS) && INRANGE(TOINT("0x" + RESULTS), 0, TOINT("0xFFFFFF"))
		CUSTOM_TALENT:(ARG:0):(ARG:1) = TOINT("0x" + RESULTS)
ENDIF


@CUSTOM_DATA_TALENT_IGNORE_TALENT(ARG:0)
#FUNCTION
{
RETURNF GROUPMATCH(ARG:0, GETNUM(TALENT, "性別"), GETNUM(TALENT, "恋慕"), GETNUM(TALENT, "親愛"), GETNUM(TALENT, "恋人"), GETNUM(TALENT, "服従"), GETNUM(TALENT, "隷属"), GETNUM(TALENT, "烙印"), GETNUM(TALENT, "親友"),
GETNUM(TALENT, "崩壊"), GETNUM(TALENT, "虚ろ"), GETNUM(TALENT, "正妻"), GETNUM(TALENT, "妾"), GETNUM(TALENT, "妊娠"), GETNUM(TALENT, "育児中"), GETNUM(TALENT, "幼児"),
GETNUM(TALENT, "触手妊娠"), GETNUM(TALENT, "Ａ触手妊娠"), GETNUM(TALENT, "子供人質"), GETNUM(TALENT, "孕み可能"), GETNUM(TALENT, "母性"), GETNUM(TALENT, "危険日"), GETNUM(TALENT, "合意"))
}


@EDIT_CUSTOM_DATA_EXTRA_INFO_UI(ARG:0)

PRINTFORM 体力:
PRINTBUTTON TOSTR(CUSTOM_MAXBASE:(ARG:0):GETNUM(BASE, "体力")), GETNUM(BASE, "体力")
PRINTL
PRINTFORM 気力:
PRINTBUTTON TOSTR(CUSTOM_MAXBASE:(ARG:0):GETNUM(BASE, "気力")), GETNUM(BASE, "気力")
PRINTL
PRINTFORM 精神力:
PRINTBUTTON TOSTR(CUSTOM_MAXBASE:(ARG:0):GETNUM(BASE, "精神力")), GETNUM(BASE, "精神力")
PRINTL

PRINTFORML タグ 最大{MAX_TAG}個まで
LOCAL:1 = 0
FOR LOCAL, 0, VARSIZE("TAG_NAME")
	LOCAL:3 = 0
	FOR LOCAL:2, 0, VARSIZE("CUSTOM_TAG", 1)
		SIF CUSTOM_TAG:(ARG:0):(LOCAL:2) == LOCAL
			LOCAL:3 = 1
	NEXT
	CALL PRINT_TOGGLE_BUTTON(@"%"[" + TAG_NAME:LOCAL + "]", 10, LEFT%", LOCAL + 100, LOCAL:3)
	LOCAL:1 ++ 
	SIF LOCAL:1 != 0 && LOCAL:1 % 5 == 0
		PRINTL 
NEXT
PRINTFORML

PRINTL
PRINTBUTTON "[スキル設定]", 1000
PRINTL

@EDIT_CUSTOM_DATA_EXTRA_INFO_PROCESS(ARG:0, ARG:1)
IF GROUPMATCH(ARG:1, GETNUM(BASE, "体力"), GETNUM(BASE, "気力"), GETNUM(BASE, "精神力"))
	PRINTFORML 値を入力
	INPUT
	SIF RESULT > 0
		CUSTOM_MAXBASE:(ARG:0):(ARG:1) = RESULT
ELSEIF INRANGE(ARG:1, 100, VARSIZE("TAG_NAME") + 100 - 1)
	LOCAL:1 = ARG:1 - 100
	FOR LOCAL, 0, MAX_TAG
		IF CUSTOM_TAG:(ARG:0):LOCAL == LOCAL:1
			CUSTOM_TAG:(ARG:0):LOCAL = -1
			RETURN
		ENDIF
	NEXT
	FOR LOCAL, 0, MAX_TAG
		IF CUSTOM_TAG:(ARG:0):LOCAL == -1
			CUSTOM_TAG:(ARG:0):LOCAL = LOCAL:1
			RETURN
		ENDIF
	NEXT
ELSEIF RESULT == 1000
	CALL CUSTOM_SKILL_SETTING(ARG:0)
ENDIF
