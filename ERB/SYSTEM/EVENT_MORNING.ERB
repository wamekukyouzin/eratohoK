;朝イベントに関する処理

;-------------------------------------------------
;朝に起こるイベント
;-------------------------------------------------
@EVENT_MORNING
;コンフィグで抑制されているなら戻る
IF CONFIG:0 == 1
	RETURN
ENDIF

;-------------------------------------------------
;特殊な朝イベント
;-------------------------------------------------
@EVENT_MORNING_SP
#DIM LIST_CHARA, MAX_CHARA_NUM
#DIM LIST_LAST

;主人公が捕虜のとき
IF CFLAG:MASTER:捕虜先 >= 1
	;外来勢力とホフゴブ勢力の番号を取得(ID:299と300)
	LOCAL:3 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_外来人))
	LOCAL:5 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:(特殊勢力_ホフゴブリン))
	;逆調教のメイン調教者のキャラ番号を取得
	LOCAL:4 = ID_TO_CHARA(FLAG:逆調教メイン調教者)

	;主人公を囚えているのが外来勢力以外で、逆調教のメイン調教者が存在し、性知識Lv3以下、捕虜でない
	IF CFLAG:MASTER:捕虜先 != LOCAL:3 && CFLAG:MASTER:捕虜先 != LOCAL:5 && LOCAL:4 >= 0 && ABL:(LOCAL:4):性知識 <= 3 && CFLAG:(LOCAL:4):所属 == CFLAG:MASTER:捕虜先 && ABL:(LOCAL:4):9 == 0
		CALL SINGLE_DRAWLINE
		;口上の表示
		CALL KOJO_EVENT(LOCAL:4, 241)

		SELECTCASE ABL:(LOCAL:4):性知識
			CASE 0
				EXP:(LOCAL:4):性知識経験値 += 200
			CASE 1
				EXP:(LOCAL:4):性知識経験値 += 50
			CASE 2
				EXP:(LOCAL:4):性知識経験値 += 20
			CASE 3
				EXP:(LOCAL:4):性知識経験値 += 8
		ENDSELECT
		CALL TRAIN_AUTO_ABLUP(LOCAL:4)
	ENDIF

;主人公が捕虜でないとき
ELSE
	;性知識Lv1～2で[恋慕]持ちのキャラは自分から性知識を学ぶ
	LIST_LAST = 0
	FOR LOCAL:0, 0, CHARANUM
		IF LOCAL:0 != MASTER && TALENT:(LOCAL:0):恋慕 && GROUPMATCH(ABL:(LOCAL:0):性知識, 1, 2) && !TALENT:(LOCAL:0):虚ろ && CFLAG:(LOCAL:0):特殊状態 == 0 && CFLAG:(LOCAL:0):捕虜先 == 0
			LIST_CHARA:(LIST_LAST) = LOCAL:0
			LIST_LAST ++
		ENDIF
	NEXT
	;人数が多いほど高確率で発生
	IF RAND:(LIST_LAST + 1) >= 1
		LOCAL:2 = LIST_CHARA:(RAND:LIST_LAST)

		CALL SINGLE_DRAWLINE
		;口上の表示
		CALL KOJO_EVENT(LOCAL:2, 240)

		EXP:(LOCAL:2):性知識経験値 += 50
		CALL TRAIN_AUTO_ABLUP(LOCAL:2)
	ENDIF

ENDIF

