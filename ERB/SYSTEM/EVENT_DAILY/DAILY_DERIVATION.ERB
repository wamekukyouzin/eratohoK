;共通デイリーイベント：派生処理
@DAILY_DERIVATION()
#DIM EVENT_NUM
#DIMS NAME_ENG
#DIMS NAME_JAP
#DIM EVENT_COUNT
#DIM EVENT_ARRAY, VARSIZE("SHAFFLE_ARRAY")

SIF !IS_DAILY_EVENT_HAPPEN(MASTER)
	RETURN 0

SIF DAILY_CANCEL
	RETURN 0

EVENT_NUM = VARSIZE("DAILY_EVENT_NAME_ENG", 1)

FOR LOCAL, 0, EVENT_NUM
	NAME_ENG = %DAILY_EVENT_NAME_ENG:デイリー_通常派生:LOCAL%
	NAME_JAP = %DAILY_EVENT_NAME:デイリー_通常派生:LOCAL%

	RESULT = 0
	TRYCCALLFORM EVENT_DAILY_DERIVATION_%NAME_ENG%_DISABLE()
		SIF RESULT == 1
			CONTINUE

		;放浪中の拠点フェイズデイリーは特殊な関数が必要
		IF !CFLAG:MASTER:所属
			RESULT = 0
			TRYCALLFORM EVENT_DAILY_DERIVATION_%NAME_ENG%_ALLOW_WHEN_WANDERING()
			SIF RESULT == 0
				CONTINUE
		ENDIF

		TRYCALLFORM EVENT_DAILY_DERIVATION_%NAME_ENG%_DECISION()
		SIF RESULT == 0
			CONTINUE

		VARSET DAILY_TARGET, -1
		VARSET DAILY_TARGET_NUM
		RESULT = 1
		TRYCALLFORM EVENT_DAILY_DERIVATION_%NAME_ENG%_SETTARGET()
		SIF RESULT == 0
			CONTINUE

		RESULTS = 
		TRYCALLFORM EVENT_DAILY_DERIVATION_%NAME_ENG%_NAME()
		IF RESULTS == ""
			CALL DAILY_START_DERIVATION(NAME_JAP)
		ELSE
			CALL DAILY_START_DERIVATION(RESULTS)
		ENDIF

		CALLFORM EVENT_DAILY_DERIVATION_%NAME_ENG%()
		SIF DAILY_CANCEL
			RETURN
	CATCH
		;DEBUGPRINTFORML EVENT_DAILY_DERIVATION_%NAME_ENG%_DECISIONみつからず
	ENDCATCH
NEXT
