;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@EVENT_DAILY_ALCOHOL_RATE()
RETURN 30


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@EVENT_DAILY_ALCOHOL_DECISION()
RETURN 1

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@EVENT_DAILY_ALCOHOL_GENRE()
RETURN デイリー_ジャンル_エロ

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@EVENT_DAILY_ALCOHOL()
#DIM 対象

PRINTFORML 少し暇ができた
PRINTFORMW 手元にはお酒があるのだが…どうしよう？
CALL ASK_MULTI("部下と飲む" ,"兵士達と宴を開く" ,"仲間を誘う" ,"一人でのんびり飲む")
IF RESULT == 3
	PRINTFORMW 一人でのんびりと飲むことにした
ELSEIF RESULT == 1
	PRINTFORMW 兵士達の日頃の労をねぎらう為に宴を開いた
	PRINTFORMW …宴は大いに盛り上がり、彼らも喜んでくれたようだ
	IF MASTER != GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
		PRINTFORMW 宴について君主の耳に入り、評価を上げた
		CFLAG:GET_COUNTRY_BOSS(CFLAG:MASTER:所属):好感度 += 100 + RAND:100
	ENDIF
ELSEIF RESULT == 0
	PRINTFORMW 部下と飲み交わすことにした
	PRINTFORMW …お酒のおかげか、腹を割って話し合えた
	SELECTCASE RAND:4
		CASE 0
			CALL PRINT_ADD_EXP(MASTER, "武闘経験値", RAND(5, 10), 1)
		CASE 1
			CALL PRINT_ADD_EXP(MASTER, "防衛経験値", RAND(5, 10), 1)
		CASE 2
			CALL PRINT_ADD_EXP(MASTER, "知略経験値", RAND(5, 10), 1)
		CASE 3
			CALL PRINT_ADD_EXP(MASTER, "政治経験値", RAND(5, 10), 1)
	ENDSELECT
	PRINTFORMW 
	IF IS_FEMALE(MASTER)
		PRINTFORMW 
		PRINTFORML 程よく酔って来た頃、彼がさりげなく身体に触れてきた
		PRINTFORMW 彼を見上げると、顔を寄せて見つめ返してきた
		CALL ASK_YN("抵抗する", "抵抗しない")
		IF RESULT == 0
			PRINTFORMW 諫める為に軽くデコピンをすると彼は残念そうに肩をすくめた
		ELSE
			PRINTFORML 程よい気分の%ANAME(MASTER)%は抵抗せずに彼に寄りかかった
			PRINTFORMW 彼は%ANAME(MASTER)%の肩を抱いて唇を塞ぎ、ゆっくりと押し倒してきた
			PRINTFORML ・
			PRINTFORML ・
			PRINTFORMW ・
			PRINTFORMW 一晩中、彼の情熱的なセックスでたっぷりと可愛がられた
			CALL FUCK(MASTER, "欲望, 奉仕, 性交, 性技, 情愛, Ｖ, Ｂ, Ｃ, Ｍ, キス, 口淫, Ｖ性交", "キス喪失, 処女喪失, 膣内射精, 口内射精", GET_SPERM_ID("兵士"), @"部下の唇", @"部下", "", "和姦")
		ENDIF
	ENDIF
ELSE
	PRINTFORML 仲間と親睦を深めることにした
	PRINTFORMW 誰を誘おう？
	CALL SELECT_CHARA_LIST_ONLY_LOGIC_SEX("DAILY_ALCOHOL", "DAILY_ALCOHOL")
	IF RESULT == -1
		PRINTFORMW やはり一人で飲むことにした
		RETURN 1
	ENDIF
	対象 = RESULT
	PRINTFORML %ANAME(対象)%を誘うことにした
	IF IS_FEMALE(対象)
		PRINTFORMW 彼女は喜んで付き合ってくれた
	ELSE
		PRINTFORMW 彼は喜んで付き合ってくれた
	ENDIF
	PRINTFORML 
	PRINTFORMW 美味しいお酒を肴に二人で楽しく飲み交わした
	CFLAG:対象:面識 = 1
	CFLAG:対象:好感度 += 50 + RAND:50
	IF HAS_PENIS(MASTER) && IS_FEMALE(対象)
		PRINTFORMW …酔いも回り、良い雰囲気になってきた
		PRINTFORML どうしよう？
		CALL ASK_YN("そっと体を寄せる", "このまま飲み続ける")
		IF RESULT == 1
			PRINTFORMW この雰囲気をまったりと楽しむことにした
			CFLAG:対象:好感度 += 50 + RAND:50
		ELSE
			PRINTFORMW あなたはそっと%ANAME(対象)%に体を寄せた
			IF !(CFLAG:対象:好感度 >= 1200 + RAND:300 + (DAY * 10) || TALENT:対象:合意 == 1)
				PRINTFORML しかし彼女はあなたの手を振り払った
				PRINTFORMW ぎこちない雰囲気になってしまった
				FOR LOCAL, 0, CHARANUM
					SIF CFLAG:(LOCAL):所属 == CFLAG:MASTER:所属 && !CFLAG:(LOCAL):捕虜先 && IS_FEMALE(LOCAL) && !IS_ANIMAL(LOCAL)
						CFLAG:(LOCAL):好感度 -= 200
				NEXT
				CALL COLOR_PRINT("話が広がり、女性陣の評価が下がった…", カラー_注意)
				PRINTFORMW 
			ELSE
				PRINTFORML 彼女は頬を赤らめつつも抵抗せずに寄りかかってきた
				PRINTFORMW あなたは%ANAME(対象)%の肩に手を廻すと、ゆっくりと押し倒した
				PRINTFORML ・
				PRINTFORML ・
				PRINTFORMW ・
				IF TALENT:対象:処女 == 1
					PRINTFORML あなたは初めての痛みでぎゅっと抱き着いてくる彼女を優しくあやすように腰を振る
					PRINTFORML 呻くだけだった%ANAME(対象)%も、身体がほぐれてくるにつれて次第に喉から甘い吐息を漏らしだす
					PRINTFORMW 少しずつ腰の動きを速めていき奥の方を突いてやると、彼女はビクンと身体を跳ねさせた
					PRINTFORML きつい膣の締まりに我慢の限界に達し、思い切り射精すると、%ANAME(対象)%は大きくのけぞりながら嬌声を上げた
					PRINTFORML …しばし絶頂の余韻に浸るように抱きしめ合った後、ペニスを引き抜くとドロリと赤と白の体液が溢れ出た
					PRINTFORMW 初めてを終えた彼女は息を荒げ、涙を流しながらも恍惚とした表情であふれる体液を眺めていた
					PRINTFORMW あなたと%ANAME(対象)%はその後も熱に浮かされた様に一晩中たっぷりと愛し合った
				ELSE
					SELECTCASE RAND:10
						CASE 0
							PRINTFORMW 可愛く嬌声を上げる彼女をたっぷりと躾けた
						CASE 1
							PRINTFORMW 昂る肉欲のまま獣の様に激しくまぐわい続けた
						CASE 2
							PRINTFORMW 情熱的に求めてくる彼女にたっぷりと搾られた
						CASE 3
							PRINTFORMW 彼女に求められるまま灼ける様な肉欲に溺れた
						CASE 4
							PRINTFORMW 激しく乱れる彼女と汗だくになって愛し合った
						CASE 5
							PRINTFORMW 挑発的な表情を見せる彼女を存分に虐め尽した
						CASE 6
							PRINTFORMW 甘い吐息を漏らす彼女をたっぷりと可愛がった
						CASE 7
							PRINTFORMW 絡みついてくる彼女にされるがままに楽しんだ
						CASE 8
							PRINTFORMW トロトロになっていた彼女を存分に可愛がった
						CASE 9
							PRINTFORMW 蕩け切った彼女の身体をたっぷりと可愛がった
					ENDSELECT
				ENDIF
				CALL FUCK(対象, "Ｃ, Ｂ, Ｖ, Ｍ, 欲望, 性交, 奉仕, 精愛, 口淫, キス, Ｖ性交", "処女喪失, キス喪失, 膣内射精", GET_ID(MASTER), @"%ANAME(MASTER)%の\@RAND:2 ? ペニス # 唇\@", ANAME(MASTER), "", "和姦")
				CALL FUCK(MASTER, "Ｃ, 射精", "童貞喪失", 0, "", "", @"%ANAME(対象)%の膣", "和姦")
			ENDIF
		ENDIF
	ENDIF
ENDIF
CALL PRINT_ADD_EXP(MASTER, "肝臓経験値", RAND(50, 100), 1)
CALL TRAIN_AUTO_ABLUP(MASTER)
RETURN 1

@SELECT_CHARA_LIST_SHOW_LOGIC_DAILY_ALCOHOL(対象)
#DIM 対象
RETURN 対象 != MASTER && CFLAG:対象:所属 == CFLAG:MASTER:所属 && CFLAG:対象:行動不能状態 != 行動不能_子供 && !IS_ANIMAL(対象) && !IS_MALE(対象)

@SELECT_CHARA_LIST_SELECT_LOGIC_DAILY_ALCOHOL(対象)
#DIM 対象
RETURN CFLAG:対象:捕虜先 == 0
