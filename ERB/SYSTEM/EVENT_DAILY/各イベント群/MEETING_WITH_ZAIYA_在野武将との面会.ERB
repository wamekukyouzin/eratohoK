;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@EVENT_DAILY_MEETING_WITH_ZAIYA_RATE()
RETURN 75


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@EVENT_DAILY_MEETING_WITH_ZAIYA_DECISION()
RETURN DAY >= SLG_PP:1 && CFLAG:MASTER:所属 && !IS_SP_COUNTRY(CFLAG:MASTER:所属)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@EVENT_DAILY_MEETING_WITH_ZAIYA_GENRE()
RETURN デイリー_ジャンル_雇用

;---------------------
;特定の条件を満たすキャラをランダムに選択する場合に利用
;他の関数は必須だが、これだけはなくてもよい　というかパフォーマンスへ影響するので不要なら作ってはならない
;対象が存在せずデイリーを開始できない場合は0を返すことでデイリーの発生をキャンセルする
;---------------------
@EVENT_DAILY_MEETING_WITH_ZAIYA_SETTARGET()
;放浪しているキャラ。
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:特殊状態 != 特殊状態_放浪
		CONTINUE
	SIF REL_LIKE:LOCAL:MASTER - REL_HATE:LOCAL:MASTER < -1000
		CONTINUE
	SIF RAND:3
		CONTINUE
	DAILY_TARGET:DAILY_TARGET_NUM = LOCAL
	DAILY_TARGET_NUM ++
NEXT

SIF DAILY_TARGET_NUM < 1
	RETURN 0

RETURN 1


;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@EVENT_DAILY_MEETING_WITH_ZAIYA()
#DIM 対象
#DIM 雇用額
#DIM FIRST_LINE

対象 = DAILY_TARGET:(RAND:DAILY_TARGET_NUM)

PRINTFORMW 在野武将の%ANAME(対象)%が面会を求めてきた……

;特殊なパターン。ただで雇うことができる
IF TALENT:対象:親愛 || TALENT:対象:隷属 || TALENT:対象:所有者
	PRINTFORML 今は放浪の身であり自由なので、ぜひ%ANAME(MASTER)%の傍においてほしい、とのことだ
	PRINTFORML 向こうから士官を求めてくるとはありがたいことだが……
	PRINTFORML 
	CALL ASK_YN("受け入れる", "拒否する")
	IF RESULT == 0
		PRINTFORML 断る道理がない
		PRINTFORMW %ANAME(対象)%を士官として迎え入れることにした……
		CALL COLOR_PRINTW(@"%ANAME(対象)%が仲間になりました", カラー_注意)
		CALL CHANGE_COUNTRY(対象, CFLAG:MASTER:所属)
	ELSE
		PRINTFORML そういうわけにはいかない
		PRINTFORML きっぱりと断った……
	ENDIF
	RETURN 1
ENDIF

;基本安い
雇用額 = SCOUT_CALC_PRICE_BASIS(対象) * 2 / 3

;恋慕等あればさらに安く
SIF IS_LOVER(対象) || IS_SLAVE(対象)
	雇用額 /= 2

CALL ICPRINT(@"今は放浪の身であり自由なのだが、自分を金<{雇用額}>で雇ってみないか、とのことだ", "L", カラー_注意)
SIF IS_FALLEN(対象)
	PRINTFORML 相手が%ANAME(MASTER)%ということで、支度金を相当控えめにしてくれたようだ
PRINTFORML 自信満々に語っているが、実力は確かだろう……

FIRST_LINE = LINECOUNT
$SHOW_LOOP

CALL SHOW_SIMPLE_INFO(対象)

PRINTL
CALL ASK_MULTI_JUDGE(@"金{雇用額}で雇う 現在:{MONEY}", MONEY >= 雇用額, "やめておく", 1, "能力表示", 2)

SELECTCASE RESULT
	CASE 0
		PRINTFORML 戦力はいくらいても多いということはない
		PRINTFORMW %ANAME(対象)%を士官として迎え入れることにした……
		CALL COLOR_PRINTW(@"%ANAME(対象)%が仲間になりました", カラー_注意)
		CALL CHANGE_COUNTRY(対象, CFLAG:MASTER:所属)
		MONEY -= 雇用額
	CASE 1
		PRINTFORML 今は必要がない
		PRINTFORML 断ることにした……
	CASE 2
		CALL SHOW_INFO_WITH_UI(対象, 0)
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
ENDSELECT

RETURN 1

@EVENT_DAILY_MEETING_WITH_ZAIYA_ALLOW_WHEN_WANDERING
RETURN 1

