;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@EVENT_DAILY_INVITATION_FROM_COUNTRY_RATE()
RETURN (ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治 + ABL:MASTER:防衛) 


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@EVENT_DAILY_INVITATION_FROM_COUNTRY_DECISION()
;すでにどこかに所属OR捕虜だと駄目
SIF CFLAG:MASTER:所属 || CFLAG:MASTER:捕虜先
	RETURN 0
SIF FLAG:クリアフラグ
	RETURN 0
;非勧誘残ってたらダメ
SIF DVAR:仕官の誘い_非勧誘残ターン
	RETURN 0
RETURN 1

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@EVENT_DAILY_INVITATION_FROM_COUNTRY_GENRE()
RETURN デイリー_ジャンル_雇用


;---------------------
;特定の条件を満たすキャラをランダムに選択する場合に利用
;他の関数は必須だが、これだけはなくてもよい　というかパフォーマンスへ影響するので不要なら作ってはならない
;対象が存在せずデイリーを開始できない場合は0を返すことでデイリーの発生をキャンセルする
;---------------------
@EVENT_DAILY_INVITATION_FROM_COUNTRY_SETTARGET()
FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL) || IS_SP_COUNTRY(LOCAL) || FINDELEMENT(COUNTRY_PLAYER_BELONGED, LOCAL) != -1
		CONTINUE
	DAILY_TARGET:DAILY_TARGET_NUM = LOCAL
	DAILY_TARGET_NUM ++
NEXT

SIF DAILY_TARGET_NUM < 1
	RETURN 0

RETURN 1

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@EVENT_DAILY_INVITATION_FROM_COUNTRY()
#DIM 支度金
#DIM 勢力
#DIM 首領



;支度金の計算
勢力 = DAILY_TARGET:(RAND:DAILY_TARGET_NUM)
首領 = GET_COUNTRY_BOSS(勢力)
支度金 = (ABL:MASTER:武闘 + ABL:MASTER:防衛 + ABL:MASTER:知略 + ABL:MASTER:政治) * 100 + RAND:1000 + RAND:(MAX(1, CFLAG:首領:好感度))

PRINTFORMW %ANAME(首領)%から、仕官しないかという誘いが来た
PRINTFORM 支度金として金
SETCOLOR カラー_注意
PRINTFORM {支度金}
RESETCOLOR
PRINTFORMW を用意しているようだ
PRINTFORML この乱世、どこかに仕えて力を蓄えておくのは一つの手だ
PRINTFORML ただし、わざわざ誘われた以上、すぐに下野することは認められない
PRINTFORM 最低でも
SETCOLOR カラー_注意
PRINTFORM 10
RESETCOLOR
PRINTFORMW 期程度は務めなくてはならない
PRINTFORML どうしようか……
PRINTFORML
CALL ASK_YN("誘いを受ける", "丁重に辞退する")
IF RESULT == 0
	PRINTFORML %ANAME(首領)%に仕えることにした
	PRINTFORMW 配下の武将たちは%ANAME(MASTER)%を歓迎しているようだ……
	PRINTFORMW 支度金{支度金}を受け取った
	CALL CHANGE_COUNTRY(MASTER, 勢力, 1)
	MONEY += 支度金
	FOR LOCAL:2, 0, CHARANUM
		IF CFLAG:(LOCAL:2):所属 == CFLAG:MASTER:所属
			CFLAG:(LOCAL:2):好感度 += RAND(300, 600)
		ENDIF
	NEXT
	PLAYER_HIRED_COUNTER = 10
ELSE
	PRINTFORMW 誘いを丁重に断った
	DVAR:仕官の誘い_非勧誘残ターン = RAND(1, 3)
ENDIF
RETURN 1

@EVENT_DAILY_INVITATION_FROM_COUNTRY_ALLOW_WHEN_WANDERING()
RETURN 1
