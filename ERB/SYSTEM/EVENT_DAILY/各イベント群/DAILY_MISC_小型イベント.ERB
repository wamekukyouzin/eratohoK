;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@EVENT_DAILY_DAILY_MISC_RATE()
RETURN 100


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@EVENT_DAILY_DAILY_MISC_DECISION()
RETURN 1

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@EVENT_DAILY_DAILY_MISC_GENRE()
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@EVENT_DAILY_DAILY_MISC()
#DIM 対象

SELECTCASE RAND:100
;能力の上昇
CASE 0 TO 21
	CALL DAILY_EVENT_RAND_CHARASELECT(0)
	IF RESULT >= 0
		対象 = RESULT
		SELECTCASE RAND:100
			CASE 0 TO 69
				LOCAL:1 = 1
			CASE 70 TO 89
				LOCAL:1 = 2
			CASE 90 TO 99
				LOCAL:1 = 3
		ENDSELECT
		SELECTCASE RAND:4
			CASE 0
				LOCALS = 武闘
				ABL:対象:武闘 += LOCAL:1
				LOCAL:2 = GETNUM(ABL, "武闘")
			CASE 1
				LOCALS = 防衛
				ABL:対象:防衛 += LOCAL:1
				LOCAL:2 = GETNUM(ABL, "防衛")
			CASE 2
				LOCALS = 知略
				ABL:対象:知略 += LOCAL:1
				LOCAL:2 = GETNUM(ABL, "知略")
			CASE 3
				LOCALS = 政治
				ABL:対象:政治 += LOCAL:1
				LOCAL:2 = GETNUM(ABL, "政治")
		ENDSELECT
		PRINTFORML %ANAME(対象)%は秘密の特訓にいそしんでいる……
		CALL COLOR_PRINTL(@"%ANAME(対象)%の%LOCALS%が{LOCAL:1}上がり、{ABL:対象:(LOCAL:2)}になった！", カラー_オレンジ)
	ENDIF

;好景気
CASE 22 TO 38
	CALL DAILY_EVENT_RAND_CITYSELECT(0)
	対象 = RESULT
	IF 対象 > 0
		PRINTFORML %CITY_NAME:対象%は好景気なようだ……
		SETCOLOR カラー_オレンジ
		CALL MODIFY_CITY_ECONOMY(対象, RAND(10000, 30000))
		RESETCOLOR
		ENDIF

;兵数が増える
CASE 39 TO 55
	LOCALS = %ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%
	LOCAL:1 = GET_OWN_CITY(CFLAG:MASTER:所属)
	LOCAL:2 = RAND(LOCAL:1 * 300, LOCAL:1 * 300 + 500)
	COUNTRY_SOLDIER:(CFLAG:MASTER:所属) += LOCAL:2
	PRINTFORML %LOCALS%の人望に、義勇軍が結成された……
	SETCOLOR カラー_オレンジ
	PRINTFORML 兵数が{LOCAL:2}増え、{COUNTRY_SOLDIER:(CFLAG:MASTER:所属)}になった！
	RESETCOLOR

;サボり魔を取得するか、削除する
CASE 56 TO 77
	CALL DAILY_EVENT_RAND_CHARASELECT(0)
	対象 = RESULT
	IF 対象 >= 0
		IF TALENT:対象:サボり魔
			PRINTFORML 最近の%ANAME(対象)%はなんだかやる気に満ちあふれている……
			SETCOLOR カラー_オレンジ
			PRINTFORML %ANAME(対象)%は【サボり魔】を克服した！
			RESETCOLOR
			TALENT:対象:サボり魔 = 0
		ELSE
			PRINTFORML 最近の%ANAME(対象)%はなんだかやる気がみられない……
			SETCOLOR カラー_警告
			PRINTFORML %ANAME(対象)%は【サボり魔】になってしまった！
			RESETCOLOR
			TALENT:対象:サボり魔 = 1
		ENDIF
	ENDIF

;ケガをするか、ケガから復帰
CASE 78 TO 82
	CALL DAILY_EVENT_RAND_CHARASELECT(0)
	対象 = RESULT
	IF 対象 >= 0
		IF COOLTIME:対象:0
			PRINTFORML %ANAME(対象)%のケガは予想以上に早く治ったようだ
			SETCOLOR カラー_オレンジ
			PRINTFORML %ANAME(対象)%はクールタイムから復帰した！
			RESETCOLOR
			COOLTIME:対象:0 = 0
		ELSE
			LOCAL:1 = RAND:3 + 1
			PRINTFORML %ANAME(対象)%は訓練中にケガをしてしまったようだ
			CALL SET_COOLTIME(対象, LOCAL:1)
		ENDIF
	ENDIF

;国家の兵力が増加する
CASE 83 TO 99
	CALL DAILY_EVENT_RAND_COUNTRYSELECT()
	対象 = RESULT
	IF 対象 >= 0 && IS_COUNTRY(対象)
		LOCAL:1 = RAND(MIN(DAY * 100, 5000), MIN(DAY * 200, 10000))
		COUNTRY_SOLDIER:(対象) += LOCAL:1
		PRINTFORML %ANAME(GET_COUNTRY_BOSS(対象))%は徴兵を行った！
		CALL COLOR_PRINTL(@"兵数が{LOCAL:1}増加した", カラー_注意)
	ENDIF
;その他=異常終了
CASEELSE
	RETURN 0
ENDSELECT
		
RETURN 1
