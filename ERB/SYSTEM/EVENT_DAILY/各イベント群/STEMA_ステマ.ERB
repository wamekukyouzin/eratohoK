;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@EVENT_DAILY_STEMA_RATE()
RETURN 70


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@EVENT_DAILY_STEMA_DECISION()
RETURN 1

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@EVENT_DAILY_STEMA_GENRE()
RETURN デイリー_ジャンル_その他

;---------------------
;特定の条件を満たすキャラをランダムに選択する場合に利用
;他の関数は必須だが、これだけはなくてもよい　というかパフォーマンスへ影響するので不要なら作ってはならない
;対象が存在せずデイリーを開始できない場合は0を返すことでデイリーの発生をキャンセルする
;---------------------
@EVENT_DAILY_STEMA_SETTARGET()
;他国に所属している自分へ恋慕or服従を持つキャラからランダム選択、対象がいない場合はイベントキャンセル
FOR LOCAL, 0, CHARANUM
	IF CFLAG:(LOCAL):所属 != CFLAG:MASTER:所属 && IS_COUNTRY(CFLAG:LOCAL:所属) && (IS_LOVER(LOCAL) || IS_SLAVE(LOCAL)) && !CFLAG:(LOCAL):捕虜先 && !IS_ANIMAL(LOCAL)
		DAILY_TARGET:DAILY_TARGET_NUM = LOCAL 
		DAILY_TARGET_NUM ++
	ENDIF
NEXT

SIF DAILY_TARGET_NUM == 0
	RETURN 0

RETURN 1

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@EVENT_DAILY_STEMA()
#DIM 対象


対象 = DAILY_TARGET:(RAND:DAILY_TARGET_NUM)

PRINTFORML %ANAME(GET_COUNTRY_BOSS(CFLAG:対象:所属))%勢力にいる%ANAME(対象)%は、同勢力の仲間に対して
PRINTFORM %ANAME(MASTER)%の事を
IF IS_SLAVE(対象)
	PRINTFORM 陶酔するような表情で熱っぽく
ELSE
	PRINTFORM 頬を赤らめながら熱心に
ENDIF
PRINTFORMW 宣伝して回っている

FOR LOCAL, 0, CHARANUM
	IF CFLAG:(LOCAL):所属 == CFLAG:(対象):所属 && !CFLAG:(LOCAL):捕虜先 && !IS_ANIMAL(LOCAL)
		CFLAG:(LOCAL):好感度 += (51 + RAND:100)
	ENDIF
NEXT
CALL CHANGE_RELATION_C_TO_O(CFLAG:対象:所属, MASTER, 50, -50)
CALL COLORPRINT(@"%ANAME(GET_COUNTRY_BOSS(CFLAG:対象:所属))%勢力に所属する武将たちの%ANAME(MASTER)%に対する評価が上がった", カラー_注意)
PRINTFORMW 

RETURN 1
