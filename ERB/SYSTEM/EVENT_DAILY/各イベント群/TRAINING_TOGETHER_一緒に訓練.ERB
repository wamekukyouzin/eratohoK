;---------------------
;発生確率(1000分率 100で10%)
;---------------------
@EVENT_DAILY_TRAINING_TOGETHER_RATE()
RETURN 30
;---------------------
;確率以外の発生判定
;---------------------
@EVENT_DAILY_TRAINING_TOGETHER_DECISION()
SIF DAY < 5
	RETURN 0
;MASTERがペニスを持っている
SIF !(HAS_PENIS(MASTER))
	RETURN 0

RETURN 1


;---------------------
;特定の条件を満たすキャラをランダムに選択する場合に利用
;他の関数は必須だが、これだけはなくてもよい　というかパフォーマンスへ影響するので不要なら作ってはならない
;対象が存在せずデイリーを開始できない場合は0を返すことでデイリーの発生をキャンセルする
;---------------------
@EVENT_DAILY_TRAINING_TOGETHER_SETTARGET()

FOR LOCAL, 0, CHARANUM
	SIF !IS_FEMALE(LOCAL)
		CONTINUE
	SIF LOCAL == MASTER
		CONTINUE
	;自国所属かつ捕虜でない
	IF CFLAG:LOCAL:所属 == CFLAG:MASTER:所属 && !CFLAG:LOCAL:捕虜先 && CFLAG:LOCAL:面識
		DAILY_TARGET:DAILY_TARGET_NUM = LOCAL
		DAILY_TARGET_NUM ++
	ENDIF
NEXT

SIF DAILY_TARGET_NUM < 1
	RETURN 0

RETURN 1

;---------------------
;ジャンル
;---------------------
@EVENT_DAILY_TRAINING_TOGETHER_GENRE()
RETURN デイリー_ジャンル_トレーナー

;---------------------
;本体
;---------------------
@EVENT_DAILY_TRAINING_TOGETHER
#DIM 対象

対象 = DAILY_TARGET:(RAND:DAILY_TARGET_NUM)

IF RAND:2 == 0
	PRINTFORMW 訓練している%ANAME(対象)%を見かけた
	PRINTFORML どうしよう？
	CALL ASK_YN("一緒に訓練する", "立ち去る")
	IF RESULT == 1
		PRINTFORMW 邪魔してもいけない、%ANAME(MASTER)%は立ち去った
	ELSE
		PRINTFORMW 一緒に訓練を申し出たら彼女は喜んで受け入れた
		PRINTFORML ・
		PRINTFORML ・
		PRINTFORMW ・
		LOCAL = (ABL:MASTER:武闘 + ABL:MASTER:防衛) * (RAND:5 + 1) - (ABL:対象:武闘 + ABL:対象:防衛) * (RAND:4 + 1)
		IF LOCAL >= 100
			PRINTFORMW 訓練は非常に捗った
			PRINTFORML お礼にと食事に誘われ、喜んで同伴した
			PRINTFORMW 食事は美味しく会話も弾み盛り上がり、午後も二人で楽しく過ごした
			CALL PRINT_ADD_EXP(対象, "武闘経験値", RAND:10 + 6, 1)
			CALL PRINT_ADD_EXP(対象, "防衛経験値", RAND:10 + 6, 1)
			CALL TRAIN_AUTO_ABLUP(対象)
			CFLAG:対象:好感度 += RAND:300 + 500
		ELSEIF LOCAL >= 0
			PRINTFORMW 充実した訓練になった
			PRINTFORMW 良い汗をかけたとタオルでぬぐいながら礼を言う彼女の笑顔がまぶしかった
			CALL PRINT_ADD_EXP(対象, "武闘経験値", RAND:5 + 3, 1)
			CALL PRINT_ADD_EXP(対象, "防衛経験値", RAND:5 + 3, 1)
			CALL TRAIN_AUTO_ABLUP(対象)
			CFLAG:対象:好感度 += RAND:100 + 300
		ELSE
			PRINTFORMW 訓練は特に滞りなく終わった
			PRINTFORMW 礼を告げる彼女と別れた
			CALL PRINT_ADD_EXP(対象, "武闘経験値", RAND:3 + 1, 1)
			CALL PRINT_ADD_EXP(対象, "防衛経験値", RAND:3 + 1, 1)
			CALL TRAIN_AUTO_ABLUP(対象)
			CFLAG:対象:好感度 += 100
		ENDIF
	ENDIF
ELSE
	PRINTFORMW 勉強している%ANAME(対象)%を見かけた
	PRINTFORML どうしよう？
	CALL ASK_YN("一緒に勉強する", "立ち去る")
	IF RESULT == 1
		PRINTFORMW 邪魔してもいけない、%ANAME(MASTER)%は立ち去った
	ELSE
		PRINTFORMW 一緒に勉強を申し出たら彼女は喜んで受け入れた
		PRINTFORML ・
		PRINTFORML ・
		PRINTFORMW ・
		LOCAL = (ABL:MASTER:知略 + ABL:MASTER:政治) * (RAND:5 + 1) - (ABL:対象:知略 + ABL:対象:政治) * (RAND:4 + 1)
		IF LOCAL >= 100
			PRINTFORMW 勉強は非常に捗った
			PRINTFORML お礼にと食事に誘われ、喜んで同伴した
			PRINTFORMW 食事は美味しく会話も弾み盛り上がり、午後も二人で楽しく過ごした
			CALL PRINT_ADD_EXP(対象, "知略経験値", RAND:10 + 6, 1)
			CALL PRINT_ADD_EXP(対象, "政治経験値", RAND:10 + 6, 1)
			CALL TRAIN_AUTO_ABLUP(対象)
			CFLAG:対象:好感度 += RAND:300 + 500
		ELSEIF LOCAL >= 0
			PRINTFORMW 充実した勉強会になった
			PRINTFORMW 一緒に勉強出来て楽しかったと礼を言う彼女の笑顔がまぶしかった
			CALL PRINT_ADD_EXP(対象, "知略経験値", RAND:5 + 3, 1)
			CALL PRINT_ADD_EXP(対象, "政治経験値", RAND:5 + 3, 1)
			CALL TRAIN_AUTO_ABLUP(対象)
			CFLAG:対象:好感度 += RAND:100 + 300
		ELSE
			PRINTFORMW 勉強会は特に滞りなく終わった
			PRINTFORMW 礼を告げる彼女と別れた
			CALL PRINT_ADD_EXP(対象, "知略経験値", RAND:3 + 1, 1)
			CALL PRINT_ADD_EXP(対象, "政治経験値", RAND:3 + 1, 1)
			CALL TRAIN_AUTO_ABLUP(対象)
			CFLAG:対象:好感度 += 100
		ENDIF
	ENDIF
ENDIF

RETURN 1
