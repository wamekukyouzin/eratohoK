;---------------------
;発生確率(1000分率 100で10%)
;---------------------
@EVENT_DAILY_LENDER_RATE()
RETURN 20

;---------------------
;確率以外の発生判定
;---------------------
@EVENT_DAILY_LENDER_DECISION()
SIF DVAR:金貸し_発生フラグ == -1
	RETURN 0

SIF DAY < 15
	RETURN 0

;返済期限がある（借りている）または現在額がある（返済中）ならスキップ
SIF !(DVAR:金貸し_返済期限 == 0 && DVAR:金貸し_残り融資額 == 0) 
	RETURN 0

RETURN 1

;---------------------
;ジャンル
;---------------------
@EVENT_DAILY_LENDER_GENRE()
RETURN デイリー_ジャンル_エロ


@EVENT_DAILY_LENDER()
#DIM LCOUNT, 3
#DIM 融資額
#DIM 返済額
#DIM 返済期限
#DIM 担保
VARSET 融資額
VARSET 返済額
VARSET 返済期限

;借りていないパターン
IF DVAR:金貸し_返済期限 == 0 && DVAR:金貸し_残り融資額 == 0 && DVAR:金貸し_融資総額 == 0
	;融資額と返済期限の計算
	;融資額は現在資金の10% + 俸給 * 期限日分
	;返済額はその2割増し
	返済期限 = RAND(5, 10)
	融資額 = MONEY / 10 + GET_SUM_ECONOMY(CFLAG:MASTER:所属) * (ABL:MASTER:武闘 + ABL:MASTER:防衛 + ABL:MASTER:知略 + ABL:MASTER:政治 + 100) / 40000 * (返済期限)
	返済額 = 融資額 * 12 / 10
	PRINTFORMW %ANAME(MASTER)%が仕事をしていると、侍従が来客を告げた
	PRINTFORMW なんでも「恰幅の良い人の良さそうな中年が来た」のだそうだ
	PRINTFORMW 何の用かと訝りながら、%ANAME(MASTER)%は、会うことにしてみた
	PRINTFORML ・
	PRINTFORML ・
	PRINTFORML ・
	PRINTFORMW 「どうも、初めまして。%ANAME(MASTER)%様ですな。お会いできて光栄ですぞ」
	PRINTFORMW 揉み手をしながら現れたのは、なるほど非常にふくよかな、いかにも人のよさげな中年男だ
	PRINTFORMW その身なりといい、決して低くない社会的立場にいることがわかる。羽振りも良さそうだ
	PRINTFORMW 「わたくし、金貸しをしておりましてな。このたびは%ANAME(MASTER)%様のお手伝いをさせていただきたく」
	PRINTFORMW つまり、資金援助の申し出だろう。といっても金貸しだ。当然、融資という形になるのだろうが……
	PRINTFORM 「そうですな、金
	CALL COLOR_PRINT(@"{融資額}", カラー_注意)
	PRINTFORM の融資で、返済額は
	CALL COLOR_PRINT(@"{返済額}", カラー_注意)
	PRINTFORM 、返済時期は
	CALL COLOR_PRINT(@"{返済期限}期", カラー_注意)
	PRINTFORMW でいかがですかな？」
	PRINTFORMW 「%ANAME(MASTER)%様も、何かとご入り用でしょう？」
	PRINTFORMW その通りだ。徴兵に訓練、外交、何事にも金は必要だ
	PRINTFORMW 借金はあまりしたくないが、生き抜くために手段は選んでいられまい
	PRINTFORMW さて、どうしようか……
	CALL SINGLE_DRAWLINE
	PRINTFORML 現在の金:{MONEY}
	CALL ASK_MULTI(@"金{融資額}を借りる", "やめておく", "うざいので斬る")
	IF RESULT == 1
		PRINTFORMW 「ふむ。必要でないとおっしゃるなら、仕方ありますまい」
		PRINTFORMW 「ではわたくしはこのへんで。また伺いますぞ」
		PRINTFORMW 男は早々に立ち去っていった……
		RETURN 1
	ELSEIF RESULT == 0
		PRINTFORMW 「契約成立ですな」
		PRINTFORM 「返済額は
		CALL COLOR_PRINT(@"{返済額}", カラー_注意)
		PRINTFORM 、返済期限は
		CALL COLOR_PRINT(@"{返済期限}期", カラー_注意)
		PRINTFORMW ですからな。お間違いないよう」
		PRINTFORMW 種々の書類に記入させたあと、男は立ち去っていった……
		PRINTFORM 金
		CALL COLOR_PRINT(@"{融資額}", カラー_注意)
		PRINTFORMW を手に入れた
		MONEY += 融資額
		DVAR:金貸し_返済期限 = 返済期限
		DVAR:金貸し_残り融資額 = 返済額
		DVAR:金貸し_融資総額 = 返済額
		RETURN 1
	ELSEIF RESULT == 2
		融資額 = RAND(100000, 300000)
		PRINTFORMW 「おやおや！　なんてことをなさるのです！　怪我をしたら困るでしょう」
		PRINTFORMW 斬りかかった%ANAME(MASTER)%だが、男は見た目から想像つかない俊敏さでかわしてみせた
		PRINTFORM 「まったく信じられない方だ。慰謝料として、金
		CALL COLOR_PRINT(@"{融資額}", カラー_注意)
		PRINTFORMW を払っていただきますぞ？
		VARSET DAILY_TARGET
		VARSET DAILY_TARGET_NUM
		CALL EVENT_DAILY_DERIVATION_LENDER_REPAYMENT_SETTARGET()
		IF DAILY_TARGET_NUM > 0
			担保 = DAILY_TARGET:(RAND:DAILY_TARGET_NUM)
		ELSE
			担保 = -1
		ENDIF
		DVAR:金貸し_発生フラグ = 1
		IF MONEY >= 融資額
			PRINTFORM 男は本当に、金
			CALL COLOR_PRINT(@"{融資額}", カラー_注意)
			PRINTFORMW をもっていってしまった……
			MONEY -= 融資額
			RETURN 1
		ELSEIF 担保 != -1
			PRINTFORMW 「……なに？　金が足りない？」
			PRINTFORMW 「では%ANAME(担保)%をうちの店で働かせていただきましょうか」
			PRINTFORMW 「なぁに、ちゃんと額ぶん働いたらお返ししますので……」
			PRINTFORMW 男はそう言って、%ANAME(担保)%を連れて行ってしまった……
			DVAR:金貸し_返済期限 = -1
			DVAR:金貸し_カタID = GET_ID(担保)
			DVAR:金貸し_融資総額 = 融資額
			DVAR:金貸し_残り融資額 = 融資額
			CALL CHANGE_COUNTRY(担保, 0)
			CFLAG:担保:特殊状態 = 0
			RETURN 1
		ELSE
			PRINTFORMW 「……なに？　金が足りない？」
			PRINTFORMW 「仕方ない、今回は不問ということにしてさしあげましょう」
			PRINTFORMW 「しかし、次はありませんからね！」
			PRINTFORMW 男は立ち去っていった……
			RETURN 1
		ENDIF
	ENDIF
ENDIF
