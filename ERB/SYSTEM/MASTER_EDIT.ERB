;-------------------------
;主人公　というか「あなた」をエディットする
;方針　MASTER_EDITは一度仮想的にキャラを生成し、そのキャラのステータスをいじる
;最終的に出来上がったキャラを「あなた」としてすげかえる
;-------------------------
@MASTER_EDIT(対象)
#DIM 対象
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT
REDRAW 0
$SHOW_LOOP
CALL MASTER_EDIT_UI(対象)

INPUT

SELECTCASE RESULT
	;名前
	CASE 0 TO 10
		CALL MASTER_EDIT_PROCESS_NAME(対象, RESULT)
	;種族
	CASE 4000 TO 4999
		CALL MASTER_EDIT_PROCESS_TAG(対象, RESULT - 4000)
	;性別
	CASE 100 TO 110
		CALL MASTER_EDIT_PROCESS_SEX(対象, RESULT - 100)
	;性経験
	CASE 200 TO 210
		CALL MASTER_EDIT_PROCESS_SEX_EXP(対象, RESULT - 200)
	;能力
	CASE 300 TO 310
		CALL MASTER_EDIT_PROCESS_SLG_ABL(対象, RESULT - 300)
	;性的能力
	CASE 1000 TO 1999
		CALL MASTER_EDIT_PROCESS_SEX_ABL(対象, RESULT - 1000)
	;素質
	CASE 2000 TO 2999
		CALL MASTER_EDIT_PROCESS_TALENT(対象, RESULT - 2000)
	;その他
	CASE 3000 TO 3999
		CALL MASTER_EDIT_PROCESS_MISC(対象, RESULT - 3000)
	;手動能力設定
	CASE 100001
		CALL MASTER_EDIT_SLG_ABL_EDIT(対象)
	CASE 100002
		SIF MASTER == NAME_TO_CHARA("あなた")
			CALL MASTER_EDIT_RELATION_SETTING(対象)
	CASE 100000
		REDRAW 1
		PRINTFORML この主人公を保存しますか？
		CALL ASK_YN()
		IF RESULT == 0
			CALL SAVE_CUSTOM_DATA(-2, 対象)
		ENDIF
		CALL MASTER_EDIT_RELATION_CONFIRM(対象)
		RETURN 対象
ENDSELECT
CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

;-------------------------
;名前処理
;-------------------------
@MASTER_EDIT_PROCESS_NAME(対象, 番号)
#DIM 対象
#DIM 番号
SELECTCASE 番号
	CASE 0
		PRINTFORML 姓を入力
		INPUTS
		CSTR:対象:0 = %RESULTS%
	CASE 1
		PRINTFORML 名を入力
		INPUTS
		CSTR:対象:1 = %RESULTS%
		NAME:対象 = %RESULTS%
		CALLNAME:対象 = %RESULTS%
	CASE 2
		PRINTFORML 愛称を入力
		INPUTS
		CSTR:対象:98 = %RESULTS%
ENDSELECT


;-------------------------
;タグ処理
;-------------------------
@MASTER_EDIT_PROCESS_TAG(対象, 番号)
#DIM 対象
#DIM 番号
SIF !INRANGE(番号, 0, VARSIZE("TAG_NAME"))
	RETURN 0
CALL INVERT_TAG(対象, 番号)

;-------------------------
;性別処理
;-------------------------
@MASTER_EDIT_PROCESS_SEX(対象, 番号)
#DIM 対象
#DIM 番号
TALENT:対象:性別 = 番号

;-------------------------
;性経験処理
;-------------------------
@MASTER_EDIT_PROCESS_SEX_EXP(対象, 番号)
#DIM 対象
#DIM 番号
SELECTCASE 番号
	CASE 0
		TALENT:対象:童貞 = !TALENT:対象:童貞
		IF TALENT:対象:童貞
			SEXUAL_EXPERIENCE:対象:初体験_童貞 = ----
			SEXUAL_LAST_EXPERIENCE:対象:初体験_童貞 = ----
		ELSE
			SEXUAL_EXPERIENCE:対象:初体験_童貞 = 不明
			SEXUAL_LAST_EXPERIENCE:対象:初体験_童貞 = 不明
		ENDIF
	CASE 1
		TALENT:対象:キス未経験 = !TALENT:対象:キス未経験
		IF TALENT:対象:キス未経験
			SEXUAL_EXPERIENCE:対象:初体験_キス = ----
			SEXUAL_LAST_EXPERIENCE:対象:初体験_キス = ----
		ELSE
			SEXUAL_EXPERIENCE:対象:初体験_キス = 不明
			SEXUAL_LAST_EXPERIENCE:対象:初体験_キス = 不明
		ENDIF
	CASE 2
		TALENT:対象:処女 = !TALENT:対象:処女
		IF TALENT:対象:処女
			SEXUAL_EXPERIENCE:対象:初体験_処女 = ----
			SEXUAL_LAST_EXPERIENCE:対象:初体験_処女 = ----
		ELSE
			SEXUAL_EXPERIENCE:対象:初体験_処女 = 不明
			SEXUAL_LAST_EXPERIENCE:対象:初体験_処女 = 不明
		ENDIF
	CASE 3
		TALENT:対象:アナル処女 = !TALENT:対象:アナル処女
		IF TALENT:対象:アナル処女
			SEXUAL_EXPERIENCE:対象:初体験_アナル処女 = ----
			SEXUAL_LAST_EXPERIENCE:対象:初体験_アナル処女 = ----
		ELSE
			SEXUAL_EXPERIENCE:対象:初体験_アナル処女 = 不明
			SEXUAL_LAST_EXPERIENCE:対象:初体験_アナル処女 = 不明
		ENDIF
ENDSELECT

;-------------------------
;SLG処理
;-------------------------
@MASTER_EDIT_PROCESS_SLG_ABL(対象, 番号)
#DIM 対象
#DIM 番号
#DIM 補正, 6
VARSET 補正

IF 番号 == 1
	TALENT:対象:成長型 = ROUND_INCREMENT(TALENT:対象:成長型, 0, 10)
ELSEIF 番号 == 0
	SELECTCASE TALENT:対象:成長型
		;万能
		;ベースとして
		;4能力期待値180
		;6能力期待値220
		CASE 0
			ABL:対象:武闘 = RAND(25, 65)
			ABL:対象:防衛 = RAND(25, 65)
			ABL:対象:知略 = RAND(25, 65)
			ABL:対象:政治 = RAND(25, 65)
			ABL:対象:料理 = RAND(10, 30)
			ABL:対象:歌唱 = RAND(10, 30)
		;知将 武闘防衛知略
		CASE 1
			ABL:対象:武闘 = RAND(30, 70)
			ABL:対象:防衛 = RAND(30, 70)
			ABL:対象:知略 = RAND(30, 70)
			ABL:対象:政治 = RAND(20, 40)
			ABL:対象:料理 = RAND(10, 30)
			ABL:対象:歌唱 = RAND(10, 30)
		;学士 知略政治
		CASE 2
			ABL:対象:武闘 = RAND(20, 50)
			ABL:対象:防衛 = RAND(20, 50)
			ABL:対象:知略 = RAND(40, 70)
			ABL:対象:政治 = RAND(40, 70)
			ABL:対象:料理 = RAND(10, 30)
			ABL:対象:歌唱 = RAND(10, 30)
		;両道 武闘防衛政治
		CASE 3
			ABL:対象:武闘 = RAND(30, 70)
			ABL:対象:防衛 = RAND(30, 70)
			ABL:対象:知略 = RAND(20, 40)
			ABL:対象:政治 = RAND(30, 70)
			ABL:対象:料理 = RAND(10, 30)
			ABL:対象:歌唱 = RAND(10, 30)
		;武官 武闘防衛
		CASE 4
			ABL:対象:武闘 = RAND(40, 70)
			ABL:対象:防衛 = RAND(40, 70)
			ABL:対象:知略 = RAND(20, 50)
			ABL:対象:政治 = RAND(20, 50)
			ABL:対象:料理 = RAND(10, 30)
			ABL:対象:歌唱 = RAND(10, 30)
		;軍師 知略
		CASE 5
			ABL:対象:武闘 = RAND(20, 60)
			ABL:対象:防衛 = RAND(20, 60)
			ABL:対象:知略 = RAND(45, 75)
			ABL:対象:政治 = RAND(20, 60)
			ABL:対象:料理 = RAND(10, 30)
			ABL:対象:歌唱 = RAND(10, 30)
		;文官　政治
		CASE 6
			ABL:対象:武闘 = RAND(20, 60)
			ABL:対象:防衛 = RAND(20, 60)
			ABL:対象:知略 = RAND(20, 60)
			ABL:対象:政治 = RAND(45, 75)
			ABL:対象:料理 = RAND(10, 30)
			ABL:対象:歌唱 = RAND(10, 30)
		;芸能
		;4能力160
		;6能力220
		CASE 7
			ABL:対象:武闘 = RAND(20, 60)
			ABL:対象:防衛 = RAND(20, 60)
			ABL:対象:知略 = RAND(20, 60)
			ABL:対象:政治 = RAND(20, 60)
			ABL:対象:料理 = RAND(10, 30)
			ABL:対象:歌唱 = RAND(30, 50)
		;料理
		CASE 8
			ABL:対象:武闘 = RAND(20, 60)
			ABL:対象:防衛 = RAND(20, 60)
			ABL:対象:知略 = RAND(20, 60)
			ABL:対象:政治 = RAND(20, 60)
			ABL:対象:料理 = RAND(30, 50)
			ABL:対象:歌唱 = RAND(10, 30)
		;天才
		;4能力220
		;6能力270
		CASE 9
			ABL:対象:武闘 = RAND(35, 65)
			ABL:対象:防衛 = RAND(35, 65)
			ABL:対象:知略 = RAND(35, 65)
			ABL:対象:政治 = RAND(35, 65)
			ABL:対象:料理 = RAND(20, 30)
			ABL:対象:歌唱 = RAND(20, 30)
		;無才 期待値120
		CASE 10
			ABL:対象:武闘 = RAND(10, 30)
			ABL:対象:防衛 = RAND(10, 30)
			ABL:対象:知略 = RAND(10, 30)
			ABL:対象:政治 = RAND(10, 30)
			ABL:対象:料理 = RAND(1, 20)
			ABL:対象:歌唱 = RAND(1, 20)		
	ENDSELECT
ENDIF

;-------------------------
;性的能力処理
;-------------------------
@MASTER_EDIT_PROCESS_SEX_ABL(対象, 番号)
#DIM 対象
#DIM 番号
{
	#DIM 編集可能能力 = 
		GETNUM(ABL, "Ｃ感"), GETNUM(ABL, "Ｂ感"), GETNUM(ABL, "Ｖ感"), GETNUM(ABL, "Ａ感"), GETNUM(ABL, "Ｍ感"),
		GETNUM(ABL, "性知識"), GETNUM(ABL, "欲望"), GETNUM(ABL, "性技"), GETNUM(ABL, "奉仕"), GETNUM(ABL, "肝臓")
}

SIF FINDELEMENT(編集可能能力, 番号) == -1
	RETURN

IF 番号 == GETNUM(ABL, "性知識")
	PRINTL 値を入力(0~5)
	INPUT
	ABL:対象:性知識 = LIMIT(RESULT, 0, 5)
ELSE
	PRINTL 値を入力(0~3)
	INPUT
	ABL:対象:番号 = LIMIT(RESULT, 0, 3)
ENDIF

;-------------------------
;素質処理
;-------------------------
@MASTER_EDIT_PROCESS_TALENT(対象, 番号)
#DIM 対象
#DIM 番号
{
	SIF !PASS_CHARA_CHECK_SHOW_TALENT(番号) || TALENTNAME:番号 == "" ||
		GROUPMATCH(番号, GETNUM(TALENT, "謎の魅力"), GETNUM(TALENT, "絶倫"), GETNUM(TALENT, "妖術知識"), GETNUM(TALENT, "天の御遣い"), GETNUM(TALENT,"技師"),
		GETNUM(TALENT, "酒豪"), GETNUM(TALENT, "君主の器"), GETNUM(TALENT, "求心力"))
}
	RETURN

TALENT:対象:番号 = !TALENT:対象:番号

;-------------------------
;そのほか処理
;-------------------------
@MASTER_EDIT_PROCESS_MISC(対象, 番号)
#DIM 対象
#DIM 番号

SELECTCASE 番号
	CASE 200
		TALENT:対象:陰毛現在値 = ROUND_INCREMENT(TALENT:対象:陰毛現在値, -1, 10)
		TALENT:対象:陰毛目標値 = TALENT:対象:陰毛現在値
	CASE 202
		TALENT:対象:腋毛現在値 = ROUND_INCREMENT(TALENT:対象:腋毛現在値, -1, 9)
		TALENT:対象:腋毛目標値 = TALENT:対象:腋毛現在値
	CASE 201
		CALL BEAUTIFY(対象)
ENDSELECT

;-------------------------
;UI部分
;-------------------------
@MASTER_EDIT_UI(対象)
#DIM 対象
CALL SINGLE_DRAWLINE
PRINTFORML 主人公の素質や能力を設定します

CALL SINGLE_DRAWLINE
PRINTFORML 名前
CALL MASTER_EDIT_NAME(対象)

CALL SINGLE_DRAWLINE
PRINTFORML 種族
CALL MASTER_EDIT_TAG(対象)

CALL SINGLE_DRAWLINE
PRINTFORML 性別
CALL MASTER_EDIT_SEX(対象)

CALL SINGLE_DRAWLINE
PRINTFORML 性経験
CALL MASTER_EDIT_SEX_EXP(対象)

CALL SINGLE_DRAWLINE
PRINTFORML SLG能力
CALL MASTER_EDIT_SLG_ABL(対象)

CALL SINGLE_DRAWLINE
PRINTFORML 性的能力
CALL MASTER_EDIT_SEX_ABL(対象)

CALL SINGLE_DRAWLINE
PRINTFORML 素質
CALL MASTER_EDIT_TALENT(対象)

CALL SINGLE_DRAWLINE
PRINTFORML その他
CALL MASTER_EDIT_MISC(対象)

CALL SINGLE_DRAWLINE
IF MASTER == NAME_TO_CHARA("あなた")
	PRINTBUTTON "[血縁設定]", 100002
	PRINTL
ENDIF
PRINTBUTTON "[完了]", 100000

;-------------------------
;名前UI
;-------------------------
@MASTER_EDIT_NAME(対象)
#DIM 対象
PRINTFORM   姓 - %CSTR:対象:0, 20, LEFT%
PRINTBUTTON "[入力]", 0
PRINTL 
PRINTFORM   名 - %CSTR:対象:1, 20, LEFT%
PRINTBUTTON "[入力]", 1
PRINTL 
PRINTFORM 愛称 - %CSTR:対象:98, 20, LEFT%
PRINTBUTTON "[入力]", 2
PRINTL
PRINTFORML ※あまり長いと表示崩れします

;-------------------------
;タグUI
;-------------------------
@MASTER_EDIT_TAG(対象)
#DIM 対象
FOR LOCAL, 0, VARSIZE("TAG_NAME")
	CALL PRINT_TOGGLE_BUTTON(@"[%TAG_NAME:LOCAL%]", 4000 + LOCAL, HAS_TAG(対象, LOCAL))
NEXT
PRINTL
PRINTFORML ※最大{MAX_TAG}個まで
PRINTFORML ※矛盾したタグが付与できる？　まぁ気にするなよ

;-------------------------
;性別UI
;-------------------------
@MASTER_EDIT_SEX(対象)
#DIM 対象
CALL PRINT_SELECT_BUTTON("[男]", 100, TALENT:対象:性別 == 0)
CALL PRINT_SELECT_BUTTON("[女]", 101, TALENT:対象:性別 == 1)
CALL PRINT_SELECT_BUTTON("[女双]", 102, TALENT:対象:性別 == 2)
CALL PRINT_SELECT_BUTTON("[男双]", 103, TALENT:対象:性別 == 3)
CALL PRINT_SELECT_BUTTON("[男娘]", 104, TALENT:対象:性別 == 4)
CALL PRINT_SELECT_BUTTON("[男娘双]", 105, TALENT:対象:性別 == 5)
PRINTL

;-------------------------
;性経験UI
;-------------------------
@MASTER_EDIT_SEX_EXP(対象)
#DIM 対象
PRINTBUTTON TALENT:対象:童貞 ? "[ 童貞 ]" # "[非童貞]", 200
PRINTBUTTON TALENT:対象:キス未経験 ? "[ キス未経験 ]" # "[キス経験済み]", 201
PRINTBUTTON TALENT:対象:処女 ? "[ 処女 ]" # "[非処女]", 202
PRINTBUTTON TALENT:対象:アナル処女 ? "[ Ａ処女 ]" # "[Ａ非処女]", 203
PRINTL

;-------------------------
;SLG能力UI
;-------------------------
@MASTER_EDIT_SLG_ABL(対象)
#DIM 対象
#DIM 表示能力 = GETNUM(ABL, "武闘"), GETNUM(ABL, "防衛"), GETNUM(ABL, "知略"), GETNUM(ABL, "政治"), GETNUM(ABL, "歌唱"), GETNUM(ABL, "料理")

PRINTBUTTON "[変更]", 301
PRINTFORM 成長型:%TOSTR_GROWTYPE(TALENT:対象:成長型)%(%TOSTR_GROWTYPE_B(TALENT:対象:成長型)%に適性)
PRINTL
PRINTBUTTON "[手動設定]", 100001
PRINT   
PRINTBUTTON "[リロール]", 300
PRINT   
FOR LOCAL, 0, VARSIZE("表示能力")
	PRINTFORM %ABLNAME:(表示能力:LOCAL)%:
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:対象:(表示能力:LOCAL))
	PRINTFORM {ABL:対象:(表示能力:LOCAL), 3, RIGHT}  
NEXT
PRINTL

;-------------------------
;性的能力UI
;-------------------------
@MASTER_EDIT_SEX_ABL(対象)
#DIM 対象
{
	#DIM 表示能力 = 
		GETNUM(ABL, "Ｃ感"), GETNUM(ABL, "Ｂ感"), GETNUM(ABL, "Ｖ感"), GETNUM(ABL, "Ａ感"), GETNUM(ABL, "Ｍ感"),
		GETNUM(ABL, "性知識"), GETNUM(ABL, "欲望"), GETNUM(ABL, "性技"), GETNUM(ABL, "奉仕"), GETNUM(ABL, "肝臓")
}

FOR LOCAL, 0, VARSIZE("表示能力")
	PRINTFORM %ABLNAME:(表示能力:LOCAL)%:
	IF 表示能力:LOCAL == GETNUM(ABL, "性知識")
		CALL PRINT_ALPHABET_RANK(ランク_性知識, ABL:対象:(表示能力:LOCAL))
	ELSE
		CALL PRINT_ALPHABET_RANK(ランク_その他, ABL:対象:(表示能力:LOCAL))
	ENDIF
	PRINTFORM {ABL:対象:(表示能力:LOCAL), 2, RIGHT}
	PRINTBUTTON "[設定]", 1000 + 表示能力:LOCAL
	PRINT   
	SIF LOCAL % 5 == 0 && LOCAL > 0
		PRINTFORML
NEXT
PRINTL
CALL KILL_EMPTY_LINE
;-------------------------
;素質UI
;-------------------------
@MASTER_EDIT_TALENT(対象)
#DIM 対象
LOCAL:1 = 0
FOR LOCAL, 0, 300
	SIF !PASS_CHARA_CHECK_SHOW_TALENT(LOCAL) || TALENTNAME:LOCAL == "" || GROUPMATCH(LOCAL, GETNUM(TALENT, "謎の魅力"), GETNUM(TALENT, "絶倫"), GETNUM(TALENT, "妖術知識"), GETNUM(TALENT, "天の御遣い"), GETNUM(TALENT,"技師"), GETNUM(TALENT, "酒豪"), GETNUM(TALENT, "合意"), GETNUM(TALENT, "君主の器"), GETNUM(TALENT, "求心力"))
		CONTINUE
	CALL PRINT_TOGGLE_BUTTON(@"%"[" + TALENTNAME:LOCAL + "]", 14, LEFT%", 2000 + LOCAL, TALENT:対象:LOCAL)
	PRINT   
	LOCAL:1 ++
	SIF LOCAL:1 % 6 == 0 && LOCAL:1 > 0
		PRINTFORML
NEXT
PRINTL

;-------------------------
;その他UI
;-------------------------
@MASTER_EDIT_MISC(対象)
#DIM 対象

PRINTFORM 体格:%TOSTR_BODYSIZE(TALENT:(対象):体格), 10, LEFT%
PRINTFORM 体型:%TOSTR_BODYTYPE(TALENT:(対象):体型), 10, LEFT%
PRINTL

SIF HAS_VAGINA(対象)
	PRINTFORM 乳房:%TOSTR_BUSTSIZE(GET_BUSTSIZE(対象)), 10, LEFT% 
SIF HAS_PENIS(対象)
	PRINTFORM 陰茎:%TOSTR_PENISSIZE(GET_PENISSIZE(対象)), 10, LEFT%
PRINTFORM   尻:%TOSTR_HIPSIZE(GET_HIPSIZE(対象)), 10, LEFT%

IF HAS_VAGINA(対象)
	PRINTFORM 膣締:%TOSTR_TIGHTNESS(TALENT:(対象):Ｖ締まり), 10, LEFT% 
ENDIF
PRINTFORM 尻締:%TOSTR_TIGHTNESS(TALENT:(対象):Ａ締まり), 10, LEFT% 
PRINTL

PRINTFORM 髪色:
CALL PRINT_HAIR_COLOR(TALENT:(対象):髪色)
PRINTFORM 髪長:%TOSTR_HAIR_LENGTH(TALENT:(対象):髪の長さ), 10, LEFT% 
PRINTL
CALL SHOW_BODYSIZE(対象)
PRINTBUTTON "[ボディサイズ設定]", 3201
PRINTL
PRINTFORM 陰毛:
PRINTBUTTON @"[%GET_INMOU_NAME(TALENT:対象:陰毛現在値)%]", 3200
PRINTFORM 腋毛:
PRINTBUTTON @"[%GET_ARMPIT_NAME(TALENT:対象:腋毛現在値)%]", 3202

PRINTL 


;-------------------------
;能力の手動設定
;-------------------------
@MASTER_EDIT_SLG_ABL_EDIT(対象)
#DIM 対象
#DIM 表示能力 = GETNUM(ABL, "武闘"), GETNUM(ABL, "防衛"), GETNUM(ABL, "知略"), GETNUM(ABL, "政治"), GETNUM(ABL, "歌唱"), GETNUM(ABL, "料理")
#DIM ポイント
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT

IF TOSTR_GROWTYPE(TALENT:対象:成長型) == "天才"
	ポイント = 200
ELSEIF TOSTR_GROWTYPE(TALENT:対象:成長型) == "無能"
	ポイント = 100
ELSE
	ポイント = 160
ENDIF

FOR LOCAL, 0, VARSIZE("表示能力")
	ポイント -= ABL:対象:(表示能力:LOCAL)
NEXT

CALL SINGLE_DRAWLINE
PRINTFORML 能力を手動で設定します
PRINTFORML ポイントが許す限り、0～100で指定できます
PRINTFORML ただしリロールの期待値のほうが、ポイント合計より上です
PRINTFORML 残りポイント:{ポイント}
CALL SINGLE_DRAWLINE
FOR LOCAL, 0, VARSIZE("表示能力")
	PRINTBUTTON @"[%ABLNAME:(表示能力:LOCAL)%]", LOCAL
	PRINTFORM    
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:対象:(表示能力:LOCAL))
	PRINTFORM {ABL:対象:(表示能力:LOCAL), 2, RIGHT}
	PRINTL 
NEXT
CALL SINGLE_DRAWLINE
PRINTFORML [99] 戻る

INPUT

IF RESULT == 99
	IF ポイント < 0
		PRINTFORMW ポイント不足です
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ENDIF
	RETURN 0
ELSEIF !INRANGE(RESULT, 0, VARSIZE("表示能力") - 1)
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF

LOCAL = RESULT
PRINTFORML %ABLNAME:(表示能力:LOCAL)%の能力値を入力(0~{MAX(ABL:対象:(表示能力:LOCAL) + ポイント, 0)})

$INPUT_LOOP

INPUT

SIF RESULT < 0 || RESULT > 100
	GOTO INPUT_LOOP

ABL:対象:(表示能力:LOCAL) = RESULT

CLEARLINE LINECOUNT - FIRST_LINE

RESTART


;-------------------------
;血縁設定
;-------------------------
@MASTER_EDIT_RELATION_SETTING(対象)
#DIM 対象
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTBUTTON "[父親の設定]", 0
IF ID_TO_CHARA(MASTER_EDIT_FATHER_CANDIDATE) > 0
	PRINTFORML  現在:%ANAME(ID_TO_CHARA(MASTER_EDIT_FATHER_CANDIDATE))%
ELSE
	PRINTFORML  現在:なし
ENDIF
PRINTBUTTON "[母親の設定]", 1
IF ID_TO_CHARA(MASTER_EDIT_MOTHER_CANDIDATE) > 0
	PRINTFORML  現在:%ANAME(ID_TO_CHARA(MASTER_EDIT_MOTHER_CANDIDATE))%
ELSE
	PRINTFORML  現在:なし
ENDIF

PRINTBUTTON "[きょうだいの設定]", 2
IF MASTER_EDIT_BROTHER_CANDIDATE_NUM > 0
	PRINTFORM 現在:
	FOR LOCAL, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM
		SIF ID_TO_CHARA(MASTER_EDIT_BROTHER_CANDIDATE:LOCAL) > 0
			PRINTFORM %ANAME(ID_TO_CHARA(MASTER_EDIT_BROTHER_CANDIDATE:LOCAL))% 
	NEXT
	PRINTFORML
ELSE
	PRINTFORML 現在:なし
ENDIF

PRINTBUTTON "[息子・娘の設定]", 3
IF MASTER_EDIT_CHILD_CANDIDATE_NUM > 0
	PRINTFORM 現在:
	FOR LOCAL, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM
		SIF ID_TO_CHARA(MASTER_EDIT_CHILD_CANDIDATE:LOCAL) > 0
			PRINTFORM %ANAME(ID_TO_CHARA(MASTER_EDIT_CHILD_CANDIDATE:LOCAL))% 
	NEXT
	PRINTFORML
ELSE
	PRINTFORML 現在:なし
ENDIF

PRINTBUTTON "[戻る]", 999

INPUT

IF RESULT == 0
	CALL SELECT_CHARA_LIST_ONLY_LOGIC_SLG("MASTER_EDIT_FATHER", "NONE")
	MASTER_EDIT_FATHER_CANDIDATE = GET_ID(RESULT)

	;排他処理
	SIF MASTER_EDIT_MOTHER_CANDIDATE == MASTER_EDIT_FATHER_CANDIDATE
		MASTER_EDIT_MOTHER_CANDIDATE = 0

	LOCAL = FINDELEMENT(MASTER_EDIT_BROTHER_CANDIDATE, MASTER_EDIT_FATHER_CANDIDATE, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM)
	IF LOCAL != -1
		ARRAYSHIFT MASTER_EDIT_BROTHER_CANDIDATE, -1, 0, LOCAL
		MASTER_EDIT_BROTHER_CANDIDATE_NUM --
	ENDIF

	LOCAL = FINDELEMENT(MASTER_EDIT_CHILD_CANDIDATE, MASTER_EDIT_FATHER_CANDIDATE, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM)
	IF LOCAL != -1
		ARRAYSHIFT MASTER_EDIT_CHILD_CANDIDATE, -1, 0, LOCAL
		MASTER_EDIT_CHILD_CANDIDATE_NUM --
	ENDIF

ELSEIF RESULT == 1
	CALL SELECT_CHARA_LIST_ONLY_LOGIC_SLG("MASTER_EDIT_MOTHER", "NONE")
	MASTER_EDIT_MOTHER_CANDIDATE = GET_ID(RESULT)

	;排他処理
	SIF MASTER_EDIT_MOTHER_CANDIDATE == MASTER_EDIT_FATHER_CANDIDATE
		MASTER_EDIT_FATHER_CANDIDATE = 0

	LOCAL = FINDELEMENT(MASTER_EDIT_BROTHER_CANDIDATE, MASTER_EDIT_MOTHER_CANDIDATE, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM)
	IF LOCAL != -1
		ARRAYSHIFT MASTER_EDIT_BROTHER_CANDIDATE, -1, 0, LOCAL
		MASTER_EDIT_BROTHER_CANDIDATE_NUM --
	ENDIF

	LOCAL = FINDELEMENT(MASTER_EDIT_CHILD_CANDIDATE, MASTER_EDIT_MOTHER_CANDIDATE, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM)
	IF LOCAL != -1
		ARRAYSHIFT MASTER_EDIT_CHILD_CANDIDATE, -1, 0, LOCAL
		MASTER_EDIT_CHILD_CANDIDATE_NUM --
	ENDIF

ELSEIF RESULT == 2
	CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(MASTER_EDIT_CANDIDATE_MAX, "MASTER_EDIT_BROTHER", "NONE")
	FOR LOCAL, 0, SELECTED_CHARA_NUM
		MASTER_EDIT_BROTHER_CANDIDATE:LOCAL = GET_ID(SELECTED_CHARA:LOCAL)
	NEXT
	MASTER_EDIT_BROTHER_CANDIDATE_NUM = SELECTED_CHARA_NUM

	;排他処理
	SIF FINDELEMENT(MASTER_EDIT_BROTHER_CANDIDATE, MASTER_EDIT_FATHER_CANDIDATE, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM) != -1
		MASTER_EDIT_FATHER_CANDIDATE = 0

	SIF FINDELEMENT(MASTER_EDIT_BROTHER_CANDIDATE, MASTER_EDIT_MOTHER_CANDIDATE, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM) != -1
		MASTER_EDIT_MOTHER_CANDIDATE = 0

	FOR LOCAL, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM
		LOCAL:1 = MASTER_EDIT_BROTHER_CANDIDATE:LOCAL
		LOCAL:2 = FINDELEMENT(MASTER_EDIT_CHILD_CANDIDATE, LOCAL:1, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM)

		IF LOCAL:2 != -1
			ARRAYSHIFT MASTER_EDIT_CHILD_CANDIDATE, -1, 0, LOCAL:2
			MASTER_EDIT_CHILD_CANDIDATE_NUM --
		ENDIF
	NEXT


ELSEIF RESULT == 3
	CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(MASTER_EDIT_CANDIDATE_MAX, "MASTER_EDIT_CHILD", "NONE")
	FOR LOCAL, 0, SELECTED_CHARA_NUM
		LOCAL:1 = SELECTED_CHARA:LOCAL
		MASTER_EDIT_CHILD_CANDIDATE:LOCAL = GET_ID(LOCAL:1)
	NEXT
	MASTER_EDIT_CHILD_CANDIDATE_NUM = SELECTED_CHARA_NUM

	;排他処理
	SIF FINDELEMENT(MASTER_EDIT_CHILD_CANDIDATE, MASTER_EDIT_FATHER_CANDIDATE, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM) != -1
		MASTER_EDIT_FATHER_CANDIDATE = 0

	SIF FINDELEMENT(MASTER_EDIT_CHILD_CANDIDATE, MASTER_EDIT_MOTHER_CANDIDATE, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM) != -1
		MASTER_EDIT_MOTHER_CANDIDATE = 0

	FOR LOCAL, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM
		LOCAL:1 = MASTER_EDIT_CHILD_CANDIDATE:LOCAL
		LOCAL:2 = FINDELEMENT(MASTER_EDIT_BROTHER_CANDIDATE, LOCAL:1, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM)

		IF LOCAL:2 != -1
			ARRAYSHIFT MASTER_EDIT_BROTHER_CANDIDATE, -1, 0, LOCAL:2
			MASTER_EDIT_BROTHER_CANDIDATE_NUM --
		ENDIF
	NEXT

ELSEIF RESULT == 999
	RETURN 0
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
RESTART


@SELECT_CHARA_LIST_SHOW_LOGIC_MASTER_EDIT_FATHER(対象)
#DIM 対象
RETURN HAS_PENIS(対象) && IS_FIXED_CHARA(対象) && 対象 != MASTER

@SELECT_CHARA_LIST_SHOW_LOGIC_MASTER_EDIT_MOTHER(対象)
#DIM 対象
RETURN HAS_VAGINA(対象) && IS_FIXED_CHARA(対象) && 対象 != MASTER

@SELECT_CHARA_LIST_SHOW_LOGIC_MASTER_EDIT_BROTHER(対象)
#DIM 対象
RETURN IS_FIXED_CHARA(対象) && 対象 != MASTER

@SELECT_CHARA_LIST_SHOW_LOGIC_MASTER_EDIT_CHILD(対象)
#DIM 対象
RETURN IS_FIXED_CHARA(対象) && 対象 != MASTER


@MASTER_EDIT_RELATION_CONFIRM(対象)
#DIM 対象
#DIM 候補, MAX_CHARA_NUM
#DIM 候補数
#DIM 父候補
#DIM 母候補
VARSET 候補, -1
VARSET 候補数, 0

父候補 = ID_TO_CHARA(MASTER_EDIT_FATHER_CANDIDATE)
母候補 = ID_TO_CHARA(MASTER_EDIT_MOTHER_CANDIDATE)

IF 父候補 >= 0
	CFLAG:対象:父親 = GET_ID(父候補)
	CALL VIRGINIZE_LOST(父候補, 初体験_童貞, 母候補 >= 0 ? ANAME(母候補) + "の膣" # "不明")
	EXP:父候補:Ｖ挿入経験 += 5
ENDIF

IF 母候補 >= 0
	CFLAG:対象:母親 = GET_ID(母候補)
	CALL VIRGINIZE_LOST(母候補, 初体験_処女, 父候補 >= 0 ? ANAME(父候補) # "不明")
	EXP:母候補:Ｖ性交経験 += 5
	EXP:母候補:膣内射精経験 += 5
	EXP:母候補:出産経験 += 1
ENDIF

IF MASTER_EDIT_BROTHER_CANDIDATE_NUM > 0
	;きょうだい候補のきょうだいも、きょうだいとして追加する
	FOR LOCAL, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM
		LOCAL:1 = ID_TO_CHARA(MASTER_EDIT_BROTHER_CANDIDATE:LOCAL)
		IF LOCAL:1 >= 0
			候補:候補数 = LOCAL:1
			候補数 ++
			FOR LOCAL:2, 0, CHARANUM
				IF IS_PURE_BROTHER(LOCAL:1, LOCAL:2) && FINDELEMENT(候補, LOCAL:2, 0, 候補数) == -1
					候補:候補数 = LOCAL:2
					候補数 ++
				ENDIF
			NEXT
		ENDIF
	NEXT

	IF 父候補 < 0 && 母候補 < 0
		FOR LOCAL, 0, SPERM_MAX_ADDRESS
			IF SPERM_NAME_ARRAY:LOCAL == ""
				LOCAL:2 = LOCAL
				LOCAL --
				BREAK
			ENDIF
		NEXT
		SIF LOCAL == SPERM_MAX_ADDRESS
			THROW SPERM_NAME_ARRAYがいっぱいです

		SPERM_NAME_ARRAY:(LOCAL:2) = 不明
		BROTHER_SPERM_FLAG:(LOCAL:2) = 1
	ENDIF

	FOR LOCAL, 0, 候補数
		CFLAG:(候補:LOCAL):父親 = 父候補 >= 0 ? GET_ID(父候補) # LOCAL:2 * -1
		CFLAG:(候補:LOCAL):母親 = 母候補 >= 0 ? GET_ID(母候補) # LOCAL:2 * -1
	NEXT
	CFLAG:対象:父親 = 父候補 >= 0 ? GET_ID(父候補) # LOCAL:2 * -1
	CFLAG:対象:母親 = 母候補 >= 0 ? GET_ID(母候補) # LOCAL:2 * -1
	VARSET 候補
	VARSET 候補数
ENDIF

IF MASTER_EDIT_CHILD_CANDIDATE_NUM > 0
	;子供候補の子供も、子供候補として追加する
	FOR LOCAL, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM
		LOCAL:1 = ID_TO_CHARA(MASTER_EDIT_CHILD_CANDIDATE:LOCAL)
		IF LOCAL:1 >= 0
			候補:候補数 = LOCAL:1
			候補数 ++
			FOR LOCAL:2, 0, CHARANUM
				IF IS_PURE_BROTHER(LOCAL:1, LOCAL:2) && FINDELEMENT(候補, LOCAL:2, 0, 候補数)
					候補:候補数 = LOCAL:2
					候補数 ++
				ENDIF
			NEXT
		ENDIF
	NEXT

	FOR LOCAL, 0, SPERM_MAX_ADDRESS
		IF SPERM_NAME_ARRAY:LOCAL == ""
			LOCAL:2 = LOCAL
			LOCAL --
			BREAK
		ENDIF
	NEXT
	SIF LOCAL == SPERM_MAX_ADDRESS
		THROW SPERM_NAME_ARRAYがいっぱいです

	SPERM_NAME_ARRAY:(LOCAL:2) = 不明
	BROTHER_SPERM_FLAG:(LOCAL:2) = 1

	FOR LOCAL, 0, 候補数
		IF IS_MALE(対象)
			CFLAG:(候補:LOCAL):父親 = GET_ID(対象)
			CFLAG:(候補:LOCAL):母親 = LOCAL:2 * -1
			CALL VIRGINIZE_LOST(対象, 初体験_童貞, "不明")
			EXP:対象:Ｖ挿入経験 += 5

		ELSE
			CFLAG:(候補:LOCAL):父親 = LOCAL:2 * -1
			CFLAG:(候補:LOCAL):母親 = GET_ID(対象)
			CALL VIRGINIZE_LOST(対象, 初体験_処女, "不明")
			EXP:対象:Ｖ性交経験 += 5
			EXP:対象:膣内射精経験 += 5
			EXP:対象:出産経験 += 1
		ENDIF
	NEXT
	VARSET 候補
	VARSET 候補数
ENDIF
