;---------------------------------------
;カスタムキャラ用メニュー
;---------------------------------------
@CUSTOM_CHARA_MENU
#DIM 対象
#DIM FIRST_LINE
#DIM ページ
LOADGLOBAL
;ランダムキャラの名前候補リストを作成
CALL CREATE_NAME_LIST
;ランク閾値の生成
CALL INIT_RANK
;能力値の最大最小の設定
CALL SET_ABL_MIN_MAX
;タトゥーのランダム入力内容リストを作成
CALL CREATE_TATTOO_LIST

;全ての東方キャラと「あなた」を作成する
FOR LOCAL:0, 0, MAX_FIXED_CHARA
	;CSVファイルの存在判定
	SIF EXISTCSV(LOCAL:0)
		CALL ADD_CHARA_FROM_CSV(LOCAL:0)
NEXT

$END_INIT
FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTFORML カスタムキャラ管理画面
CALL SINGLE_DRAWLINE

CALL LISTUP_CUSTOM_CHARACTER(0, ページ, 20)
CALL SINGLE_DRAWLINE
PRINTBUTTON "[終了]", 10000


INPUT

IF RESULT == 10000
	DELALLCHARA
	RETURN
ELSEIF RESULT == MAX_CUSTOM_CHARA
	ページ = ROUND_DECREMENT(ページ, 0, MAX_CUSTOM_CHARA / 20)
ELSEIF RESULT == MAX_CUSTOM_CHARA + 1
	ページ = ROUND_INCREMENT(ページ, 0, MAX_CUSTOM_CHARA / 20)
ELSEIF INRANGE(RESULT, 0, MAX_CUSTOM_CHARA)
	LOCAL = RESULT
	IF FIND_CHARADATA(TOSTR(LOCAL))
		CALL LOAD_CUSTOM_DATA(LOCAL)
		CALL EDIT_CUSTOM_DATA(LOCAL, RESULT)
	ELSE
		CALL INIT_CUSTOM_DATA()
		CALL EDIT_CUSTOM_DATA(LOCAL, RESULT)
	ENDIF
ENDIF
CLEARLINE FIRST_LINE - LINECOUNT
GOTO END_INIT

;---------------------------------------
;カスタムキャラのリストアップ
;---------------------------------------
@LISTUP_CUSTOM_CHARACTER(番号 = 0, ページ = 0, 最大数 = 20)
#DIM 番号
#DIM ページ
#DIM 最大数
FOR LOCAL, ページ * 最大数, MIN(ページ * 最大数 + 最大数, MAX_CUSTOM_CHARA)
	IF FIND_CHARADATA(TOSTR(LOCAL))
		CHKCHARADATA RESULTS:0
		PRINTBUTTON @"[%RESULTS:0%]", LOCAL + 番号
	ELSE
		PRINTBUTTON "[新規作成]", LOCAL + 番号
	ENDIF
	PRINTL
NEXT
PRINTBUTTON "[前のページ]", 番号 + MAX_CUSTOM_CHARA
PRINTFORM {ページ + 1}/{MAX_CUSTOM_CHARA / 最大数 + 1}
PRINTBUTTON "[次のページ]", 番号 + MAX_CUSTOM_CHARA + 1
PRINTL
