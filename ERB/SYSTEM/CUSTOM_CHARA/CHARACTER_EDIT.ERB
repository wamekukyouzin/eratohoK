@CHARACTER_EDIT(対象, 切り替え可能 = 0)
#DIM 対象
#DIM 切り替え可能
#DIM 現在タブ
#DIMS CONST タブ英名 = "NAME", "BASE", "ABL",  "TALENT", "EXP",  "MARK", "SKILL", "OTHER", "INFO"
#DIMS CONST タブ名   = "名前", "基礎", "能力", "素質",   "経験", "陥落", "スキル", "他", "情報"
#DIM FIRST_LINE, 2
#DIM CREDRAW

CREDRAW = CURRENTREDRAW()
REDRAW 0

$SHOW_LOOP

FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTFORML %ANAME(対象)%を編集中
CALL SINGLE_DRAWLINE
FIRST_LINE:1 = LINECOUNT
CALLFORM CHARACTER_EDIT_UI_%タブ英名:現在タブ%(対象)
FOR LOCAL, 0, FIRST_LINE:1 + 25 - LINECOUNT
	PRINTL
NEXT
CALL SINGLE_DRAWLINE

FOR LOCAL, 0, VARSIZE("タブ名")
	PRINTBUTTON @"[%タブ名:LOCAL%]", 10000 + LOCAL
	PRINT  
NEXT
PRINTL

IF 切り替え可能
	CALL SINGLE_DRAWLINE
	PRINTBUTTON "[前のキャラ]", 20000
	PRINT  
	PRINTBUTTON "[次のキャラ]", 20001
	PRINTL
ENDIF

CALL SINGLE_DRAWLINE
PRINTBUTTON "[戻る]", 30000

INPUT

IF RESULT == 30000
	REDRAW CREDRAW
	RETURN
ELSEIF RESULT == 20000 && 切り替え可能
	LOCAL:0 = 0
	LOCAL:1 = 0
	WHILE 1
		LOCAL:2 = SHOP_CHARA_LIST:(LOCAL:0)
		IF LOCAL:1
			IF LOCAL:2 != -1
				対象 = SHOP_CHARA_LIST:(LOCAL:0)
				BREAK
			ENDIF
		ELSEIF LOCAL:2 == 対象
			LOCAL:1 = 1
		ENDIF
		LOCAL:0 --
		SIF LOCAL:0 < 0
			LOCAL:0 = CHARANUM - 1
	WEND
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 20001 && 切り替え可能
	LOCAL:0 = 0
	LOCAL:1 = 0
	WHILE 1
		LOCAL:2 = SHOP_CHARA_LIST:(LOCAL:0)
		IF LOCAL:1
			IF LOCAL:2 != -1
				対象 = SHOP_CHARA_LIST:(LOCAL:0)
				BREAK
			ENDIF
		ELSEIF LOCAL:2 == 対象
			LOCAL:1 = 1
		ENDIF
		LOCAL:0 ++
		SIF LOCAL:0 >= CHARANUM
			LOCAL:0 = 0
	WEND
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF INRANGE(RESULT, 10000, 10000 + VARSIZE("タブ名") - 1)
	現在タブ = RESULT - 10000
	IF タブ名:現在タブ == "情報"
		現在タブ = 0
		CALL SHOW_INFO_WITH_UI(対象)
	ELSEIF タブ名:現在タブ == "スキル"
		現在タブ = 0
		CALL SHOP_LIFE_USERSHOP41(対象, 1)
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF

CALLFORM CHARACTER_EDIT_INPUT_%タブ英名:現在タブ%(対象, RESULT)

CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

