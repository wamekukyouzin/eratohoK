;-------------------------------------------------
;左カラムカテゴリ名称設定
;-------------------------------------------------
@COLUMN_LEFT_CATEGORY_NAME_SLG
COLUMN_LEFT_CATEGORY_NAME_SLG:1 '= "自動"
COLUMN_LEFT_CATEGORY_NAME_SLG:2 '= "政策"
COLUMN_LEFT_CATEGORY_NAME_SLG:3 '= "戦略"
COLUMN_LEFT_CATEGORY_NAME_SLG:4 '= "交渉"
COLUMN_LEFT_CATEGORY_NAME_SLG:5 '= "情報"
COLUMN_LEFT_CATEGORY_NAME_SLG:6 '= "人材"
COLUMN_LEFT_CATEGORY_NAME_SLG:7 '= "特殊"
COLUMN_LEFT_CATEGORY_NAME_SLG:8 '= "進行"
COLUMN_LEFT_CATEGORY_NAME_SLG:9 '= "設定"

;-------------------------------------------------
;左カラムメニューボタン
;-------------------------------------------------
@COLUMN_LEFT_PRINTBUTTON_SLG(ARG:0, ARGS:0, ARG:1 = 0)
#DIM EXTERNAL_LINK

SIF ARG:0 < 0 || 99 < ARG:0
	THROW COLUMN_LEFT_PRINTBUTTONの項番が0～99の範囲にありません

;空指定なら同幅のスペース
IF ARGS:0 == ""
	PRINTPLAINFORM %TOSTR_SPACE(12)%
;選択不可（表示が制限されていない場合）
ELSEIF !SHOP_AVAIL:(ARG:0)
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM [%ARGS:0, 10%]
	RESETCOLOR
;現在選択中
ELSEIF ARG:1
	SETCOLOR カラー_選択中
	PRINTBUTTON @"[%ARGS:0, 10%]", ARG:0
	RESETCOLOR
;通常のボタン
ELSE
	PRINTBUTTON @"[%ARGS:0, 10%]", ARG:0
ENDIF

;-------------------------------------------------
;左カラムの配列：行番号にカテゴリとメニューを保存
;-------------------------------------------------
@COLUMN_LEFT_MENULINE_SAVE_SLG
#DIM COLUMN_LEFT_CATEGORY_SAVEFLAG_SLG, 10

VARSET COLUMN_LEFT_CATEGORY_SAVEFLAG_SLG, 0
VARSET COLUMN_LEFT_CATEGORY_NUM_SLG, 0
VARSET COLUMN_LEFT_MENU_NUM_SLG, 0

CALL COLUMN_LEFT_CATEGORY_NAME_SLG

LOCAL:0 = 1
LOCAL:4 = 0
FOR LOCAL:1, 1, 8
	IF COLUMN_LEFT_CATEGORY_NAME_SLG:(LOCAL:1) != ""
		FOR LOCAL:2, 1, 10
			LOCAL:3 = LOCAL:1 * 10 + LOCAL:2
			RESULTS:0 '= ""
			TRYCALLFORM SHOP_SLG_NAME{LOCAL:3}
			IF RESULTS:0 != "" && SHOP_AVAIL:(LOCAL:3)
				IF COLUMN_LEFT_CATEGORY_SAVEFLAG_SLG:(LOCAL:1) != 1
					IF LOCAL:4 == 1
						LOCAL:0 ++
					ENDIF
					LOCAL:4 = 1
					COLUMN_LEFT_CATEGORY_NUM_SLG:(LOCAL:0) = LOCAL:1
					COLUMN_LEFT_CATEGORY_SAVEFLAG_SLG:(LOCAL:1) = 1
				ENDIF
				COLUMN_LEFT_MENU_NUM_SLG:(LOCAL:0) = LOCAL:3
				;このメニューに追加説明文があれば空行を追加する
				TRYCCALLFORM SHOP_SLG_NEXT_LINE_ADD{COLUMN_LEFT_MENU_NUM_SLG:(LOCAL:0)}
					LOCAL:0 ++
				CATCH
				ENDCATCH
				LOCAL:0 ++
			ENDIF
		NEXT
	ENDIF
NEXT

;-------------------------------------------------
;左カラムメニュー表示ボタン呼び出し ARG:0は行数
;-------------------------------------------------
@COLUMN_LEFT_MENU_SHOW_SLG(ARG:0)
#DIM SHOP_SLG_NEXT_LINE_ADD_FLAG = 0
#DIM SHOP_SLG_NAME_COLOR = 0
;固定行
SELECTCASE ARG:0
	;固定・案内
	CASE 0
		SETCOLOR カラー_注釈
		IF FLAG:観戦モード
			CALL COLOR_PRINT(" 観戦中           ", 0xB0BA90)
		ELSEIF CFLAG:MASTER:所属 == 0
			CALL COLOR_PRINT(" 放浪中           ", 0xB0BA90)
		ELSE
			CALL COLOR_PRINT(" 委任時は自動     ", 0xB0BA90)
		ENDIF
		RESETCOLOR
	;固定・開戦
	CASE 34
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW(COLUMN_LEFT_CATEGORY_NAME_SLG:8)
		TRYCALLFORM SHOP_SLG_NAME81
		CALL COLUMN_LEFT_PRINTBUTTON_SLG( 81, RESULTS:0)
	;固定・設定
	CASE 36
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW(COLUMN_LEFT_CATEGORY_NAME_SLG:9)
		CALL COLUMN_LEFT_PRINTBUTTON_SLG( 91, "設定")
	;固定・セーブ
	CASE 37
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW("")
		CALL COLUMN_LEFT_PRINTBUTTON_SLG( 92, "セーブ")
	;固定・ロード
	CASE 38
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW("")
		CALL COLUMN_LEFT_PRINTBUTTON_SLG( 93, "ロード")
	;可変・他カテゴリ名とメニューボタン
	CASEELSE
		;カテゴリ
		IF COLUMN_LEFT_CATEGORY_NUM_SLG:(ARG:0) != 0
			CALL COLUMN_LEFT_CATEGORY_NAME_SHOW(COLUMN_LEFT_CATEGORY_NAME_SLG:(COLUMN_LEFT_CATEGORY_NUM_SLG:(ARG:0)))
		ELSE
			CALL COLUMN_LEFT_CATEGORY_NAME_SHOW("")
		ENDIF
		;ひとつ前の行のメニューに追加説明文があるかチェック
		TRYCCALLFORM SHOP_SLG_NEXT_LINE_ADD{COLUMN_LEFT_MENU_NUM_SLG:(ARG:0 - 1)}
			SHOP_SLG_NEXT_LINE_ADD_FLAG = 1
		CATCH
			SHOP_SLG_NEXT_LINE_ADD_FLAG = 0
		ENDCATCH
		;右カラムに表示のあるメニューの場合
		TRYCCALLFORM SHOP_SLG_EVENTBUY_COLUMN_RIGHT_ON_CHECK{COLUMN_LEFT_MENU_NUM_SLG:(ARG:0)}
			SHOP_SLG_NAME_COLOR = 1
		CATCH
			SHOP_SLG_NAME_COLOR = 0
		ENDCATCH
		;ボタンカラー指定があれば読み込み、右カラムに表示があってもボタンをオンカラーにしない
		TRYCCALLFORM SHOP_SLG_NAME_COLOR{COLUMN_LEFT_MENU_NUM_SLG:(ARG:0)}
			SHOP_SLG_NAME_COLOR = 0
		CATCH
		ENDCATCH
		;メニューボタンがあれば表示
		IF COLUMN_LEFT_MENU_NUM_SLG:(ARG:0) != 0
			TRYCALLFORM SHOP_SLG_NAME{COLUMN_LEFT_MENU_NUM_SLG:(ARG:0)}
			CALL COLUMN_LEFT_PRINTBUTTON_SLG( COLUMN_LEFT_MENU_NUM_SLG:(ARG:0), RESULTS:0, SHOP_SLG_NAME_COLOR && FLAG:戦略フェイズ選択コマンド == COLUMN_LEFT_MENU_NUM_SLG:(ARG:0))
		;メニューボタンがなく、ひとつ前の行のメニューに追加説明文がある場合は表示
		ELSEIF SHOP_SLG_NEXT_LINE_ADD_FLAG == 1
			TRYCALLFORM SHOP_SLG_NEXT_LINE_ADD_TEXT{COLUMN_LEFT_MENU_NUM_SLG:(ARG:0 - 1)}
		;メニューボタンがなく、ひとつ前の行のメニューに追加説明文がなければ空行
		ELSE
			CALL COLUMN_LEFT_PRINTBUTTON_SLG( 0, "")
		ENDIF
ENDSELECT

RESETCOLOR
PRINT   

