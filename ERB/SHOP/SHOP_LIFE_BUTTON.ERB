;共有しているボタンやボタンに追尾させる情報

;-------------------------------------------------
;右カラムキャラリスト用のキャラボタン
;
;ボタン表示対象キャラ=キャラ番号
;選択中色表示フラグ=0:通常カラー 1:選択中色で表示
;ボタン番号加算値=NO + ボタン番号加算値（100～）
;キャラ表示可否判定結果
;	@SHOP_LIFE_CHECKCHARA～系の返り値を代入
;	メニューファイル内のボタン指定である
;	@SHOP_LIFE_LIST1_BUTTON{メニュー番号}(ARG:0, ARG:1)や
;	@SHOP_LIFE_LIST1_BUTTON{メニュー番号}(ARG:0, ARG:1)から指定するときは
;	リスト関数内で既に判定され、結果（PRINTOPTION）がARG:1に保存されている
;	ので、そのままARG:1を返すだけでいい
;行動済表示フラグ=0:行動済みマークをつける 1:行動済みマークをつけない
;上下=今上のリストか下のリストかを指定する
;	メニューファイルでボタンの追尾情報を上下のリストで分けたいときに操作
;
;メニューファイルでの使用例）
;	CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, CFLAG:(ARG:0):閨に呼ぶで選択中, SHOP_LIFE_LIST2_ADD_INPUT, ARG:1, "BOTTOM")
;リスト内での使用例）
;	CALL COLUMN_RIGHT_CHARALIST_BUTTON(NOW_CHECK_CHARA, 0, SHOP_LIFE_LIST1_ADD_INPUT, PRINTOPTION)
;-------------------------------------------------
@COLUMN_RIGHT_CHARALIST_BUTTON(ボタン表示対象キャラ, 選択中色表示フラグ = 0, ボタン番号加算値 = SHOP_LIFE_LIST1_ADD_INPUT, キャラ表示可否判定結果 = 1, 行動済表示フラグ = 0, 上下 = "")
#DIM ボタン表示対象キャラ
#DIM 選択中色表示フラグ
#DIM ボタン番号加算値
#DIM キャラ表示可否判定結果
#DIM 行動済表示フラグ
#DIMS 上下
;CHECKCHARAの戻り値が０なら表示しない
SIF キャラ表示可否判定結果 <= 0
	RETURN 0

PRINT 

SIF 選択中色表示フラグ
	SETCOLOR カラー_選択中

;通常表示
IF キャラ表示可否判定結果 == 1
	CALL PRINTBUTTON_CENTER(@"[%ANAME(ボタン表示対象キャラ), MAX_CHARANAME_LENGTH, LEFT%]", NO:(ボタン表示対象キャラ) + ボタン番号加算値, 0)
;グレー表示
ELSEIF キャラ表示可否判定結果 == 2
	CALL PRINTBUTTON_CENTER(@"[%ANAME(ボタン表示対象キャラ), MAX_CHARANAME_LENGTH, LEFT%]", NO:(ボタン表示対象キャラ) + ボタン番号加算値, 0, 0)
ENDIF

SIF 選択中色表示フラグ
	RESETCOLOR

;行動済みマーク表示
IF !行動済表示フラグ
	CALL PRINT_RESTMARK(ボタン表示対象キャラ)
	PRINT  
ENDIF

;ボタンに追加の情報を表示する指定があれば
RESULT = 0
IF 上下 == "TOP"
	TRYCCALLFORM SHOP_LIFE_BUTTON_ADD_TOP{FLAG:拠点フェイズ選択コマンド}(ボタン表示対象キャラ)
	CATCH
		TRYCALLFORM SHOP_LIFE_BUTTON_ADD{FLAG:拠点フェイズ選択コマンド}(ボタン表示対象キャラ)
	ENDCATCH
ELSEIF 上下 == "BOTTOM"
	TRYCCALLFORM SHOP_LIFE_BUTTON_ADD_BOTTOM{FLAG:拠点フェイズ選択コマンド}(ボタン表示対象キャラ)
	CATCH
		TRYCALLFORM SHOP_LIFE_BUTTON_ADD{FLAG:拠点フェイズ選択コマンド}(ボタン表示対象キャラ)
	ENDCATCH
ELSE
	TRYCALLFORM SHOP_LIFE_BUTTON_ADD{FLAG:拠点フェイズ選択コマンド}(ボタン表示対象キャラ)
ENDIF
;戻り値が１なら他の情報は表示しない
SIF RESULT
	RETURN 0

CALL IS_KOJO(NO:(ボタン表示対象キャラ))
IF RESULT
	PRINT *
ELSE
	PRINT  
ENDIF

CALL PRINT_SEX(ボタン表示対象キャラ, 1, 0, 2)

IF ボタン表示対象キャラ == MASTER
	PRINT   
ELSEIF CFLAG:(ボタン表示対象キャラ):所属 == CFLAG:MASTER:所属
	SETCOLOR 0x97f8e8
	PRINT 自
ELSEIF CFLAG:(ボタン表示対象キャラ):所属 != 0
	SETCOLOR 0x28cc28
	PRINT 他
ELSE
	SETCOLOR 0x808080
	PRINT 無
ENDIF
RESETCOLOR
PRINT  

CALL TMP_PRINT_CHARA_STARS_NUM(ボタン表示対象キャラ)
PRINT  

IF ボタン表示対象キャラ == MASTER
	PRINT   
ELSEIF CFLAG:(ボタン表示対象キャラ):捕虜先
	IF CFLAG:(ボタン表示対象キャラ):捕虜先 == CFLAG:MASTER:所属
		IF CFLAG:(ボタン表示対象キャラ):監禁状態 == 0
			SETCOLOR 0x4434E5
			PRINT 監
		ELSE
			SETCOLOR 0xA39ED7
			PRINT 軟
		ENDIF
	ELSE
		SETCOLOR 0xdf0000
		PRINT 囚
	ENDIF
ELSEIF CFLAG:(ボタン表示対象キャラ):外交調教経過ターン
	IF CFLAG:(ボタン表示対象キャラ):外交要求成功フラグ == 2
		SETCOLOR カラー_女
		PRINT 虜
	ELSE
		SETCOLOR 0x489200
		PRINT 脅
	ENDIF
ELSEIF CFLAG:(ボタン表示対象キャラ):面識
	SETCOLOR 0xD0D0D0
	PRINT 会
ELSE
	SETCOLOR 0x404040
	PRINT 会
ENDIF
RESETCOLOR
PRINT  
IF TALENT:(ボタン表示対象キャラ):崩壊
	SETCOLOR カラー_警告
	PRINT 崩
ELSEIF TALENT:(ボタン表示対象キャラ):虚ろ
	SETCOLOR カラー_警告
	PRINT 虚
ELSEIF ID_TO_CHARA(FLAG:お気に入り指定キャラ) == ボタン表示対象キャラ
	SETCOLOR カラー_注意
	PRINT 推
ELSEIF GETBIT(TALENT:(ボタン表示対象キャラ):特殊勢力陥落系, 特殊勢力_外来人)
	SETCOLOR 0x0000A0
	PRINT 外
ELSEIF GETBIT(TALENT:(ボタン表示対象キャラ):特殊勢力陥落系, 特殊勢力_ホフゴブリン)
	SETCOLOR 0x2B5C1B
	PRINT ゴ
ELSEIF GETBIT(TALENT:(ボタン表示対象キャラ):特殊勢力陥落系, 特殊勢力_野盗)
	SETCOLOR 0x800000
	PRINT 野
ELSEIF GETBIT(TALENT:(ボタン表示対象キャラ):特殊勢力陥落系, 特殊勢力_触手)
	SETCOLOR 0xFF1493
	PRINT 寄
ELSEIF GETBIT(TALENT:(ボタン表示対象キャラ):特殊勢力陥落系, 特殊勢力_自警団)
	SETCOLOR 0x33CC66
	PRINT 自
ELSEIF GETBIT(TALENT:(ボタン表示対象キャラ):特殊勢力陥落系, 特殊勢力_サキュバス)
	SETCOLOR 0x8B008B
	PRINT 夢
ELSEIF TALENT:(ボタン表示対象キャラ):親愛
	SETCOLOR 0xF5CE13
	PRINT 愛
ELSEIF TALENT:(ボタン表示対象キャラ):恋慕
	SETCOLOR 0xFF4080
	PRINT 慕
ELSEIF TALENT:(ボタン表示対象キャラ):親友
	SETCOLOR 0xFF8000
	PRINT 友
ELSEIF TALENT:(ボタン表示対象キャラ):隷属
	SETCOLOR 0x00FFFF
	PRINT 隷
ELSEIF TALENT:(ボタン表示対象キャラ):服従
	SETCOLOR 0x00D0D0
	PRINT 酔
ELSE
	PRINT   
ENDIF
RESETCOLOR

IF TALENT:(ボタン表示対象キャラ):正妻
	SETCOLOR 0xF5CE13
	PRINT 妻
ELSEIF TALENT:(ボタン表示対象キャラ):恋人
	SETCOLOR 0xFF80C0
	PRINT 恋
ELSEIF TALENT:(ボタン表示対象キャラ):烙印
	SETCOLOR 0x00FF80
	PRINT 印
ELSE
	PRINT   
ENDIF
RESETCOLOR
PRINT  

IF CFLAG:(ボタン表示対象キャラ):行動不能状態 == 4
	SETCOLOR カラー_緑
	PRINTPLAIN 幼
ELSEIF CFLAG:(ボタン表示対象キャラ):行動不能状態 == 1
	SETCOLOR カラー_妊娠
	PRINTPLAIN 臨
ELSEIF TALENT:(ボタン表示対象キャラ):妊娠
	SETCOLOR カラー_妊娠
	PRINTPLAIN 妊
ELSEIF CFLAG:(ボタン表示対象キャラ):避妊薬残ターン
	SETCOLOR カラー_妊娠
	PRINTPLAIN 避
ELSEIF IS_OVULATION(ボタン表示対象キャラ)
	SETCOLOR カラー_ピンク
	PRINTPLAIN 危
ELSEIF CFLAG:(ボタン表示対象キャラ):行動不能状態 == 2
	SETCOLOR 0xFFC0C0
	PRINTPLAIN 育
ELSEIF CFLAG:(ボタン表示対象キャラ):行動不能状態 == 3
	SETCOLOR カラー_警告
	PRINTPLAIN 傷
ELSEIF COOLTIME:(ボタン表示対象キャラ):0
	SETCOLOR カラー_警告
	PRINTFORM %TOFULL(TOSTR(COOLTIME:(ボタン表示対象キャラ):0))%
ELSE
	PRINTPLAIN   
ENDIF
RESETCOLOR

;-------------------------------------------------
;「捕虜の調教」の右カラムキャラリストボタン（上）表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD_TOP_PRISONER(ARG:0)
PRINTPLAIN  
IF ARG:0 == MASTER
	PRINT   
ELSEIF CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属
	SETCOLOR 0x97f8e8
	PRINT 自
ELSEIF CFLAG:(ARG:0):所属 != 0
	SETCOLOR 0x28cc28
	PRINT 他
ELSE
	SETCOLOR 0x808080
	PRINT 無
ENDIF
IF TALENT:(ARG:0):崩壊
	SETCOLOR カラー_警告
	PRINT 崩
ELSEIF TALENT:(ARG:0):虚ろ
	SETCOLOR カラー_警告
	PRINT 虚
ELSEIF TALENT:(ARG:0):隷属
	SETCOLOR 0x00FFFF
	PRINT 隷
ELSEIF TALENT:(ARG:0):服従
	SETCOLOR 0x00D0D0
	PRINT 酔
ELSE
	PRINT   
ENDIF
IF TALENT:(ARG:0):正妻
	SETCOLOR 0xF5CE13
	PRINT 妻
ELSEIF TALENT:(ARG:0):恋人
	SETCOLOR 0xFF80C0
	PRINT 恋
ELSEIF TALENT:(ARG:0):烙印
	SETCOLOR 0x00FF80
	PRINT 印
ELSE
	PRINT   
ENDIF
RESETCOLOR

IF CFLAG:(ARG:0):行動不能状態 == 4
	SETCOLOR カラー_緑
	PRINTPLAIN 幼
ELSEIF CFLAG:(ARG:0):行動不能状態 == 1
	SETCOLOR カラー_妊娠
	PRINTPLAIN 臨
ELSEIF TALENT:(ARG:0):妊娠
	SETCOLOR カラー_妊娠
	PRINTPLAIN 妊
ELSEIF CFLAG:(ARG:0):避妊薬残ターン
	SETCOLOR カラー_妊娠
	PRINTPLAIN 避
ELSEIF IS_OVULATION(ARG:0)
	SETCOLOR カラー_ピンク
	PRINTPLAIN 危
ELSEIF CFLAG:(ARG:0):行動不能状態 == 2
	SETCOLOR 0xFFC0C0
	PRINTPLAIN 育
ELSEIF CFLAG:(ARG:0):行動不能状態 == 3
	SETCOLOR カラー_警告
	PRINTPLAIN 傷
ELSEIF COOLTIME:(ARG:0):0
	SETCOLOR カラー_警告
	PRINTFORM %TOFULL(TOSTR(COOLTIME:(ARG:0):0))%
ELSE
	PRINTPLAIN   
ENDIF
RESETCOLOR

PRINTFORM  精:
CALL PRINT_GRADATIONBAR(BASE:(ARG:0):精神力, MAXBASE:(ARG:0):精神力, 10, 0x5E9AFF)
PRINTFORM {BASE:(ARG:0):精神力,5, RIGHT}/{MAXBASE:(ARG:0):精神力, 5, RIGHT} 
PRINTFORM 好:
IF CFLAG:(ARG:0):好感度 > 99999
	PRINTFORM %"多い", 6, RIGHT%
ELSE
	PRINTFORM {CFLAG:(ARG:0):好感度, 6, RIGHT} 
ENDIF
PRINTFORM 依:
IF CFLAG:(ARG:0):依存度 > 99999
	PRINTFORM %"多い", 6, RIGHT%
ELSE
	PRINTFORM {CFLAG:(ARG:0):依存度, 6, RIGHT} 
ENDIF
PRINTFORM 従:
IF CFLAG:(ARG:0):従属度 > 99999
	PRINTFORM %"多い", 6, RIGHT%
ELSE
	PRINTFORM {CFLAG:(ARG:0):従属度, 6, RIGHT} 
ENDIF

RETURN 1

;-------------------------------------------------
;「捕虜の調教」の右カラムキャラリストボタン（下）表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD_BOTTOM_PRISONER(ARG:0)
#DIM 表示能力 = GETNUM(ABL, "欲望"), GETNUM(ABL, "性知識"), GETNUM(ABL, "奉仕"), GETNUM(ABL, "性技"), GETNUM(ABL, "性交")
PRINTPLAIN  
FOR LOCAL, 0, VARSIZE("表示能力")
	PRINTFORM %SUBSTRINGU(ABLNAME:(表示能力:LOCAL), 0, 2)%:
	IF 表示能力:LOCAL == GETNUM(ABL, "性知識")
		CALL PRINT_ALPHABET_RANK(ランク_性知識, ABL:(ARG:0):(表示能力:LOCAL))
	ELSE
		CALL PRINT_ALPHABET_RANK(ランク_その他, ABL:(ARG:0):(表示能力:LOCAL))
	ENDIF
	PRINTFORM {ABL:(ARG:0):(表示能力:LOCAL), 3}
	PRINT  
NEXT

RESETCOLOR
RETURN 1

