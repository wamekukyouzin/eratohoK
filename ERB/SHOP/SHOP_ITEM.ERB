;ショップ画面のアイテム関連の処理

;-------------------------------------------------
;アイテム
;-------------------------------------------------
@SHOP_ITEM
#DIM IS_GISHI

;[技師]持ちが主人公勢力に存在するかどうかを判定
IS_GISHI = 0
FOR LOCAL:0, 0, CHARANUM
	IF TALENT:(LOCAL:0):技師 && CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属
		IS_GISHI = 1
		BREAK
	ENDIF
NEXT

DRAWLINE
PRINTL ★★アイテムの購入・開発を行います★★
DRAWLINE

PRINTFORML 所持金:{MONEY}

;所持アイテム表示
PRINT 【所持アイテム】
LOCAL:1 = 999
FOR LOCAL:0, 100, 300
	IF ITEM:(LOCAL:0) >= 1
		LOCALS:0 = %ITEMNAME:(LOCAL:0)%
		IF ITEM:(LOCAL:0) >= 2
			LOCALS:0 = %LOCALS:0%×{ITEM:(LOCAL:0)}
		ENDIF
		LOCAL:1 += STRLENS(LOCALS:0) + 2
		IF LOCAL:1 > 70
			LOCAL:1 = 0
			PRINTL 
			PRINT 　　
		ENDIF
		PRINTFORM %LOCALS:0%　
	ENDIF
NEXT
PRINTL 
IF LOCAL:1 == 999
	PRINTL 　　なし
ENDIF

PRINTL 【購入】
PRINT 　　
LOCAL:1 = 0
FOR LOCAL:0, 100, 300
	IF ITEMNAME:(LOCAL:0) != "" && ITEMSALES:(LOCAL:0)
		IF LOCAL:1 > 0 && LOCAL:1 % 3 == 0
			PRINTL 
			PRINT 　　
		ENDIF
		LOCAL:1 ++
		CALL GET_PRICE(LOCAL:0)
		;所持金不足 or 所持数オーバーで購入できない時は色を変える
		IF MONEY < RESULT || ITEM:(LOCAL:0) >= 99
			SETCOLOR COLOR("選択不可")
			PRINTFORM [{LOCAL:0}] %ITEMNAME:(LOCAL:0), 20, LEFT%{RESULT, 7}  
			RESETCOLOR
		ELSE
			PRINTFORM [{LOCAL:0}] %ITEMNAME:(LOCAL:0), 20, LEFT%{RESULT, 7}  
		ENDIF
	ENDIF
NEXT
PRINTL 
DRAWLINE
PRINT 【開発】
IF IS_GISHI
	SETCOLOR COLOR("緑")
	PRINT ※[技師]効果あり
	RESETCOLOR
ENDIF
PRINTL 

FOR LOCAL:0, 200, 300
	IF ITEMNAME:(LOCAL:0) != "" && ITEM:(LOCAL:0) == 0 && !ITEMSALES:(LOCAL:0) && ITEMTIME:(LOCAL:0) == 0
		RESULT = 1
		TRYCALLFORM ITEM_ABLE{LOCAL:0}
		IF RESULT
			PRINTFORM 　　[{LOCAL:0}] %ITEMNAME:(LOCAL:0), 14, LEFT%…… 開発費:{ITEMPRICE:(LOCAL:0), 7}
			IF IS_GISHI
				SETCOLOR COLOR("緑")
				PRINTFORM →{ITEMPRICE:(LOCAL:0) * 75 / 100, 7}
				RESETCOLOR
			ENDIF
			PRINTFORM  開発期間:{ITEMTERM:(LOCAL:0), 2}
			IF IS_GISHI
				SETCOLOR COLOR("緑")
				PRINTFORM →{ITEMTERM:(LOCAL:0) / 2 + 1, 2}
				RESETCOLOR
			ENDIF
			PRINTL 
		ENDIF
	ENDIF
NEXT
DRAWLINE
PRINTL 【破棄】
PRINT 　　
LOCAL:1 = 0
FOR LOCAL:0, 500, 900
	LOCAL:2 = LOCAL:0 - 500
	IF ITEMNAME:(LOCAL:2) != "" && ITEM:(LOCAL:2) > 0
		LOCALS:0 = %ITEMNAME:(LOCAL:2)%
		IF ITEM:(LOCAL:2) >= 2
			LOCALS:0 = %LOCALS:0%×{ITEM:(LOCAL:2)}
		ENDIF
		IF LOCAL:1 > 0 && LOCAL:1 % 3 == 0
			PRINTL 
			PRINT 　　
		ENDIF
		LOCAL:1 ++
		CALL GET_PRICE(LOCAL:2)
		PRINTFORM [{LOCAL:0}] %LOCALS:0, 20, LEFT%{RESULT, 7}  
	ENDIF
NEXT
PRINTL
DRAWLINE

PRINT 　　[999] 戻る

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 999
	REDRAW 1
	RETURN
ELSEIF RESULT >= 100 && RESULT < 300 && ITEMNAME:RESULT != ""
	LOCAL:2 = RESULT

	IF ITEMSALES:(LOCAL:2)
		CALL GET_PRICE(LOCAL:2)
		LOCAL:3 = RESULT

		;アイテムの種別を判定
		IF (LOCAL:2 >= 150 && LOCAL:2 < 200) || (LOCAL:2 >= 250 && LOCAL:2 < 280)
			LOCAL:5 = 1
		ELSE
			LOCAL:5 = 0
		ENDIF

		;アイテムの説明を表示し、購入の入力処理を行う
		CALL ITEM_EXPLANATION(LOCAL:2, LOCAL:3, LOCAL:5)
		LOCAL:4 = RESULT

		;購入個数が0なら戻る
		IF LOCAL:4 == 0
			RESTART
		ENDIF

		;購入アイテム
		IF LOCAL:2 < 150
			IF ITEMNAME:(LOCAL:2) == "太平要術の書"
				CALL SELECT_ITEM_USER(LOCAL:2)
				IF !RESULT
					RESTART
				ENDIF
			ELSEIF ITEMNAME:(LOCAL:2) == "淫技大全"
				CALL SELECT_ITEM_USER(LOCAL:2)
				IF !RESULT
					RESTART
				ENDIF
			ELSE
				PRINTFORMW %ITEMNAME:(LOCAL:2)%を購入しました
				ITEM:(LOCAL:2) ++
				ITEMSALES:(LOCAL:2) = 0
			ENDIF
			MONEY -= LOCAL:3
			RESTART
		;購入アイテム(消耗品)
		ELSEIF LOCAL:2 < 200 || (LOCAL:2 >= 250 && LOCAL:2 < 280)
			PRINTFORMW %ITEMNAME:(LOCAL:2)%を{LOCAL:4}個購入しました
			MONEY -= LOCAL:3 * LOCAL:4
			ITEM:(LOCAL:2) += LOCAL:4
			RESTART
		;購入アイテム(その場使用)
		ELSEIF LOCAL:2 >= 280 && LOCAL:2 < 300
			IF ITEMNAME:(LOCAL:2) == "犬" || ITEMNAME:(LOCAL:2) == "豚"
				PRINTFORMW %ITEMNAME:(LOCAL:2)%を購入しました
				MONEY -= LOCAL:3
				CALL ADD_ANIMAL(ITEMNAME:(LOCAL:2), CFLAG:MASTER:所属)
				RESTART
			ELSE
				CALL SELECT_ITEM_USER(LOCAL:2)
				IF RESULT
					MONEY -= LOCAL:3
				ENDIF
				RESTART
			ENDIF
		ENDIF

	;開発アイテム
	ELSEIF ITEM:(LOCAL:2) == 0 && ITEMTIME:(LOCAL:2) == 0
		RESULT = 1
		TRYCALLFORM ITEM_ABLE{LOCAL:2}
		IF RESULT
			LOCAL:3 = ITEMPRICE:(LOCAL:2)
			IF IS_GISHI
				TIMES LOCAL:3, 0.75
			ENDIF

			;アイテムの説明を表示し、購入の入力処理を行う
			CALL ITEM_EXPLANATION(LOCAL:2, LOCAL:3, 2)
			LOCAL:4 = RESULT

			;購入個数が0なら戻る
			IF LOCAL:4 == 0
				RESTART
			ENDIF

			PRINTFORMW %ITEMNAME:(LOCAL:2)%の開発を開始しました
			IF IS_GISHI
				ITEMTIME:(LOCAL:2) = ITEMTERM:(LOCAL:2) / 2 + 1
			ELSE
				ITEMTIME:(LOCAL:2) = 1
			ENDIF
			MONEY -= LOCAL:3
			RESTART
		ENDIF
	ENDIF
ELSEIF 500 <= RESULT && RESULT <= 900 && ITEMNAME:(RESULT - 500) != ""
	LOCAL:1 = RESULT - 500
	IF ITEM:(LOCAL:1) > 0
		CALL GET_PRICE(LOCAL:1)
		LOCAL:2 = RESULT
		PRINTFORML %ITEMNAME:(LOCAL:1)%を売却します
		PRINTFORML 売却する個数を入力して下さい（0または所持数以上の入力でキャンセル)
		INPUT
		LOCAL:3 = RESULT
		;購入個数が0なら戻る
		IF LOCAL:3 == 0 || LOCAL:3 > ITEM:(LOCAL:1)
			RESTART
		ENDIF
		MONEY += RESULT * LOCAL:2
		PRINTFORMW %ITEMNAME:(LOCAL:1)%を売却し、金{RESULT * LOCAL:2}を得ました
		ITEM:(LOCAL:1) -= RESULT
		IF LOCAL:1 < 150
			ITEMSALES:(LOCAL:1) = 1
		ELSEIF ITEMTERM:(LOCAL:1) > 0
			ITEMTIME:(LOCAL:1) = 0
		ENDIF
		RESTART
	ENDIF
ENDIF

CLEARLINE 1
REUSELASTLINE 
REDRAW 2
GOTO INPUT_LOOP

;-------------------------------------------------
;アイテムの説明を表示し、購入の入力処理を行う
;ARG:0=アイテム番号 ARG:1=アイテムの値段 ARG:2=アイテムのタイプ(0=単品 1=消耗品 2=開発)
;戻り値として購入個数を返す
;-------------------------------------------------
@ITEM_EXPLANATION(ARG:0, ARG:1, ARG:2)
DRAWLINE
PRINTFORM 【%ITEMNAME:(ARG:0)%】  
IF ARG:2 == 2
	PRINTFORML 開発費:{ARG:1}
ELSE
	PRINTFORML 価格:{ARG:1}
ENDIF
TRYCALLFORM ITEM_EXPLANATION{ARG:0}
DRAWLINE

SELECTCASE ARG:2
	CASE 0
		CALL ASK_MULTI_JUDGE("このアイテムを購入する", MONEY >= ARG:1, "戻る", 1)
		LOCAL:0 = (RESULT == 0)
	CASE 1
		IF MONEY >= ARG:1
			PRINTL 購入する数を入力して下さい(0以下でキャンセル)
			LOCAL:2 = 0
			FOR LOCAL:0, 1, 10
				IF MONEY < LOCAL:0 * ARG:1 || ITEM:(ARG:0) + LOCAL:0 > 99
					BREAK
				ENDIF
				IF !LOCAL:2
					LOCAL:2 = 1
					PRINT  
				ENDIF
				PRINTFORM [{LOCAL:0, 2}]
			NEXT
			IF LOCAL:2
				PRINTL 
			ENDIF
			LOCAL:2 = 0
			FOR LOCAL:0, 1, 10
				IF MONEY < LOCAL:0 * 10 * ARG:1 || ITEM:(ARG:0) + LOCAL:0 * 10 > 99
					BREAK
				ENDIF
				IF !LOCAL:2
					LOCAL:2 = 1
					PRINT  
				ENDIF
				PRINTFORM [{LOCAL:0 * 10, 2}]
			NEXT
			IF LOCAL:2
				PRINTL 
			ENDIF
			;限界まで購入ボタン
			;有り金全部で買えるだけ買うといくつになるか計算
			LOCAL:3 = MONEY / ARG:1
			;元あった分と足したら所持限界をオーバーする場合、オーバー分を引く
			IF ITEM:(ARG:0) + LOCAL:3 > 99
				LOCAL:3 = LOCAL:3 - ( LOCAL:3 + ITEM:(ARG:0) - 99 )
			ENDIF
			;0でなければボタン表示
			IF LOCAL:3 != 0
				PRINTFORM  [{LOCAL:3, 2}]限界まで購入
				PRINTL 
			ENDIF
		;所持金が値段より小さい場合
		ELSE
			SETCOLOR COLOR("選択不可")
			PRINTL 所持金不足により購入不可
			RESETCOLOR
		ENDIF
		PRINTBUTTON "0[戻る]", 0
		PRINTL 
		$INPUT_LOOP_NUM
		INPUT
		IF ITEM:(ARG:0) + RESULT > 99
			CLEARLINE 1
			REUSELASTLINE アイテムの数が99個を超えています
			GOTO INPUT_LOOP_NUM
		ELSEIF RESULT * ARG:1 > MONEY
			CLEARLINE 1
			REUSELASTLINE 所持金が不足しています
			GOTO INPUT_LOOP_NUM
		ENDIF
		LOCAL:0 = MAX(RESULT, 0)
	CASE 2
		CALL ASK_MULTI_JUDGE("このアイテムを開発する", MONEY >= ARG:1, "戻る", 1)
		LOCAL:0 = (RESULT == 0)
ENDSELECT

RETURN LOCAL:0

;-------------------------------------------------
;その場使用アイテムの対象キャラを選択 ARG:0=アイテム番号
;-------------------------------------------------
@SELECT_ITEM_USER(ARG:0)
#DIM FIRSTLINE
;全員の調教参加フラグをクリア
CVARSET CFLAG, 6, 0

;アイテムを使用可能なキャラの数を数える
LOCAL:5 = 0
FOR LOCAL:0, 0, CHARANUM
	RESULT = 0
	TRYCALLFORM IS_ITEM_USE{ARG:0}(LOCAL:0)
	IF RESULT
		LOCAL:5 ++
	ENDIF
NEXT

;ページ数の計算
LOCAL:10 = (LOCAL:5 - 1) / 44 + 1
LOCAL:11 = 1
DRAWLINE
PRINTFORML %ITEMNAME:(ARG:0)%を誰に使用しますか？
DRAWLINE
FIRSTLINE = LINECOUNT

$SHOW_LOOP

;キャラリストの表示
LOCAL:12 = (LOCAL:11 - 1) * 44
LOCAL:13 = LOCAL:11* 44
LOCAL:14 = 0
LOCAL:15 = 0
LOCAL:16 = 4
FOR LOCAL:0, 0, CHARANUM
	RESULT = 0
	TRYCALLFORM IS_ITEM_USE{ARG:0}(LOCAL:0)
	IF RESULT
		IF LOCAL:14 >= LOCAL:12 && LOCAL:14 < LOCAL:13
			IF LOCAL:15 % 2 != 0
				PRINTPLAIN 　
			ELSEIF LOCAL:15 >= 1
				PRINTL 
				LOCAL:16 ++
			ENDIF
			CALL PRINT_PARTNER_DATA(LOCAL:0)
			LOCAL:15 ++
		ENDIF
		LOCAL:14 ++
	ENDIF
NEXT
PRINTL 
DRAWLINE

IF LOCAL:10 >= 2
	LOCAL:16 += 2
	IF LOCAL:11 > 1
		PRINT [  8] 前のページ            
	ELSE
		PRINT                             
	ENDIF
	LOCALS:0 = page{LOCAL:11}/{LOCAL:10}
	PRINTPLAINFORM %LOCALS:0, 28, LEFT%
	IF LOCAL:11 < LOCAL:10
		PRINT [  9] 後のページ
	ENDIF
	PRINTL 
	DRAWLINE
ENDIF

PRINTL [  0] 戻る

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 8
	LOCAL:11 --
	IF LOCAL:11 < 0
		LOCAL:11 = LOCAL:10
	ENDIF
	CLEARLINE LINECOUNT - FIRSTLINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 9
	LOCAL:11 ++
	IF LOCAL:11 > LOCAL:10
		LOCAL:11 = 0
	ENDIF
	CLEARLINE LINECOUNT - FIRSTLINE
	GOTO SHOW_LOOP
ELSE
	LOCAL:20 = RESULT - 100
	LOCAL:21 = NO_TO_CHARA(LOCAL:20)
	IF LOCAL:21 >= 0
		RESULT = 0
		TRYCALLFORM IS_ITEM_USE{ARG:0}(LOCAL:21)
		IF RESULT
			CALLFORM ITEM_EFFECT{ARG:0}(LOCAL:21)
			REDRAW 1
			RETURN 1
		ENDIF
	ENDIF
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
;アイテム開発イベント
;-------------------------------------------------
@ITEM_DEVELOPMENT
FOR LOCAL:0, 200, 300
	IF ITEMTIME:(LOCAL:0) >= 1
		IF ITEMTIME:(LOCAL:0) >= ITEMTERM:(LOCAL:0)
			ITEMTIME:(LOCAL:0) = 0
			IF LOCAL:0 < 280
				ITEM:(LOCAL:0) ++
			ENDIF
			IF LOCAL:0 >= 250
				ITEMSALES:(LOCAL:0) = 1
			ENDIF
			PRINTL 
			SETCOLOR COLOR("注意")
			PRINTFORMW 『%ITEMNAME:(LOCAL:0)%』が完成しました！
			RESETCOLOR
			PRINTL 
		ELSE
			ITEMTIME:(LOCAL:0) ++
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------
;アイテムの開発期間を設定(ニューゲーム・ロード時に呼び出される)
;-------------------------------------------------
@SET_ITEMTERM
ITEMTERM:200 = 3
ITEMTERM:201 = 5
ITEMTERM:202 = 6
ITEMTERM:203 = 3
ITEMTERM:204 = 4
ITEMTERM:206 = 5
ITEMTERM:207 = 5
ITEMTERM:208 = 2
ITEMTERM:209 = 4
ITEMTERM:210 = 6
ITEMTERM:211 = 8
ITEMTERM:212 = 3
ITEMTERM:213 = 5
ITEMTERM:214 = 4
ITEMTERM:240 = 3
ITEMTERM:241 = 3
ITEMTERM:242 = 3
ITEMTERM:243 = 1
ITEMTERM:244 = 1
ITEMTERM:245 = 1
ITEMTERM:250 = 6
ITEMTERM:251 = 5
ITEMTERM:252 = 16
ITEMTERM:253 = 25
ITEMTERM:260 = 3
ITEMTERM:261 = 3
ITEMTERM:262 = 4
ITEMTERM:263 = 5
ITEMTERM:264 = 5
ITEMTERM:265 = 5
ITEMTERM:280 = 3
ITEMTERM:281 = 4
ITEMTERM:282 = 3
ITEMTERM:283 = 4
ITEMTERM:284 = 8
ITEMTERM:285 = 6
ITEMTERM:286 = 6
ITEMTERM:287 = 6
ITEMTERM:288 = 10
ITEMTERM:289 = 4
ITEMTERM:290 = 4
ITEMTERM:291 = 4
ITEMTERM:292 = 4
ITEMTERM:293 = 10
;-------------------------------------------------
;ARG:0番のアイテムの真の購入価格を取得
;-------------------------------------------------
@GET_PRICE(ARG:0)
RESULT = ITEMPRICE:(ARG:0)
TRYCALLFORM GET_PRICE{ARG:0}
RETURN RESULT

;-------------------------------------------------
;アイテムの購入価格
;-------------------------------------------------

;●醸造酒
@GET_PRICE250
RETURN 6000

;●果実酒
@GET_PRICE251
RETURN 8000

;●日本酒
@GET_PRICE252
RETURN 10000

;●蒸留酒
@GET_PRICE253
RETURN 12000

;●媚薬
@GET_PRICE260
RETURN 2000

;●排卵誘発剤
@GET_PRICE261
RETURN 3000

;●絶倫丸
@GET_PRICE262
RETURN 3000

;●麻薬
@GET_PRICE263
RETURN 10000

;●ピアス
@GET_PRICE264
RETURN 7000

;●桃源香
@GET_PRICE265
RETURN 3500

;●ふたなり薬
@GET_PRICE280
RETURN 30000

;●性転換薬
@GET_PRICE281
RETURN 30000

;●岩神ノ秘薬
@GET_PRICE282
RETURN 20000

;●獣変化の秘薬
@GET_PRICE283
RETURN 40000

;●再生の秘薬
@GET_PRICE284
RETURN 100000

;永久脱毛剤
@GET_PRICE287
RETURN 50000

;タトゥー除去手術
@GET_PRICE288
RETURN 100000

;穴トレ
@GET_PRICE289
RETURN 50000

;豊胸手術
@GET_PRICE290
RETURN 40000

;増茎手術
@GET_PRICE291
RETURN 40000

;避妊薬
@GET_PRICE292
RETURN 20000

;堕胎薬
@GET_PRICE293
RETURN 50000

;-------------------------------------------------
;アイテムの開発条件
;-------------------------------------------------
;●アナルバイブ
@ITEM_ABLE202
;バイブを開発済み
IF ITEM:バイブ >= 1
	RETURN 1
ENDIF
RETURN 0

;●クリキャップ
@ITEM_ABLE206
;ローターを開発済み
IF ITEM:ローター >= 1
	RETURN 1
ENDIF
RETURN 0

;●ニプルキャップ
@ITEM_ABLE207
;ローターを開発済み
IF ITEM:ローター >= 1
	RETURN 1
ENDIF
RETURN 0

;●双頭バイブ
@ITEM_ABLE208
;ペニスバンドを開発済み
IF ITEM:ペニスバンド >= 1
	RETURN 1
ENDIF
RETURN 0

;●触手部屋
@ITEM_ABLE210
;触手部屋の管理が可能なキャラが必要
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:0):捕虜先 == 0 && TALENT:(LOCAL:0):妖術知識 && (ABL:(LOCAL:0):触手 >= 5 || LOCAL:0 == MASTER)
		RETURN 1
	ENDIF
NEXT
RETURN 0

;●遠隔通信用スキマ
@ITEM_ABLE211
;太平淫術の書を持っている
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;●首輪
@ITEM_ABLE212
;太平淫術の書を持っている
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;●ニードルアンドインク
@ITEM_ABLE213
;太平淫術の書を持っている
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;●果実酒
@ITEM_ABLE251
;醸造酒を開発済み
IF ITEMSALES:醸造酒
	RETURN 1
ENDIF
RETURN 0

;●蒸留酒
@ITEM_ABLE253
;醸造酒を開発済み
IF ITEMSALES:醸造酒
	RETURN 1
ENDIF
RETURN 0

;●絶倫丸
@ITEM_ABLE262
;媚薬を開発済み
IF ITEMSALES:媚薬
	RETURN 1
ENDIF
RETURN 0

;●麻薬
@ITEM_ABLE263
;太平淫術の書を持っている
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;●ピアス
@ITEM_ABLE264
;太平淫術の書を持っている
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;●桃源香
@ITEM_ABLE265
;媚薬を開発済み、且つ太平淫術の書を持っている
IF ITEMSALES:媚薬 && ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;●ふたなり薬
@ITEM_ABLE280
;太平淫術の書を持っている
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;●性転換薬
@ITEM_ABLE281
;太平淫術の書を持っている
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;●岩神ノ秘薬
@ITEM_ABLE282
;太平淫術の書を持っている
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;●獣変化の秘薬
@ITEM_ABLE283
;太平淫術の書を持っている
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;●再生の秘薬
@ITEM_ABLE284
;ふたなり、性転換、獣変化のうち２つ以上を開発済み
IF (ITEMSALES:ふたなり薬 != 0) + (ITEMSALES:性転換薬 != 0) + (ITEMSALES:獣変化の秘薬 != 0) >= 2
	RETURN 1
ENDIF

;犬と豚
@ITEM_ABLE285
;太平淫術の書を持っている
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

@ITEM_ABLE286
;太平淫術の書を持っている
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;永久脱毛剤
@ITEM_ABLE287
;ふたなり、性転換、獣変化のうち２つ以上を開発済み
IF (ITEMSALES:ふたなり薬 != 0) + (ITEMSALES:性転換薬 != 0) + (ITEMSALES:獣変化の秘薬 != 0) >= 2
	RETURN 1
ENDIF
RETURN 0

;タトゥー除去手術
@ITEM_ABLE288
;ニードル＆インクを所持
IF ITEM:ニードル＆インク
	RETURN 1
ENDIF
RETURN 0

;穴トレ
@ITEM_ABLE289
;太平淫術の書を持っている
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;豊胸
@ITEM_ABLE290
;太平淫術の書を持っている
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;増茎
@ITEM_ABLE291
;太平淫術の書を持っている
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;避妊薬
@ITEM_ABLE292
RETURN 1

;堕胎薬
@ITEM_ABLE293
IF ITEM:太平淫術の書
	RETURN 1
ENDIF
RETURN 0

;-------------------------------------------------
;アイテムの使用条件
;-------------------------------------------------
;●ふたなり薬
@IS_ITEM_USE280(ARG:0)
IF (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
	RETURN 1
ENDIF
RETURN 0

;●性転換薬
@IS_ITEM_USE281(ARG:0)
IF !IS_ANIMAL(ARG:0) && ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属)
	RETURN 1
ENDIF
RETURN 0

;●岩神ノ秘薬
@IS_ITEM_USE282(ARG:0)
IF (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
	IF GROUPMATCH(TALENT:(ARG:0):性別, 0, 4) && !TALENT:(ARG:0):孕み可能
		RETURN 1
	ENDIF
ENDIF
RETURN 0

;●獣変化の秘薬
@IS_ITEM_USE283(ARG:0)
IF (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
	IF CFLAG:(ARG:0):獣変化フラグ || (!TALENT:(ARG:0):動物耳 && !TALENT:(ARG:0):しっぽ)
		RETURN 1
	ENDIF
ENDIF
RETURN 0

;●再生の秘薬
@IS_ITEM_USE284(ARG:0)
IF ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属) && !GETBIT(TALENT:(ARG:0):デイリー系, 素質_デイリー_借り物) && ARG:0 != NAME_TO_CHARA("あなた")
	RETURN 1
ENDIF
RETURN 0

;●太平要術の書
@IS_ITEM_USE141(ARG:0)
IF (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
	IF !TALENT:(ARG:0):妖術知識
		RETURN 1
	ENDIF
ENDIF
RETURN 0

;●淫技大全
@IS_ITEM_USE142(ARG:0)
IF (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
	IF ABL:(ARG:0):性知識 == 4 && ABL:(ARG:0):性技 >= 6
		RETURN 1
	ENDIF
ENDIF
RETURN 0

;●永久脱毛剤
@IS_ITEM_USE287(ARG:0)
SIF TALENT:(ARG:0):陰毛現在値 != 陰毛_パイパン
	RETURN (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
RETURN 0

;●入れ墨除去手術
@IS_ITEM_USE288(ARG:0)
FOR LOCAL, 0, VARSIZE("TATOO")
	SIF TATOO:(ARG:0):LOCAL != ""
		RETURN (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
NEXT
RETURN 0

;●穴トレ
@IS_ITEM_USE289(ARG:0)
RETURN (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属

;●豊胸
@IS_ITEM_USE290(ARG:0)
RETURN IS_FEMALE(ARG:0) && ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属)

;●ぞうけい
@IS_ITEM_USE291(ARG:0)
RETURN PENIS(ARG:0) && ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属)

;避妊薬
@IS_ITEM_USE292(ARG:0)
RETURN !CFLAG:(ARG:0):妊娠期間 && IS_FEMALE(ARG:0) && CFLAG:(ARG:0):避妊薬残ターン == 0 && ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属)

;堕胎薬
@IS_ITEM_USE293(ARG:0)
RETURN TALENT:(ARG:0):妊娠  && IS_FEMALE(ARG:0) && ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属)

;-------------------------------------------------
;その場使用アイテムの効果
;-------------------------------------------------
;●ふたなり薬
@ITEM_EFFECT280(ARG:0)
SELECTCASE TALENT:(ARG:0):性別
	CASE 0
		TALENT:(ARG:0):性別 = 3
		PRINTFORMW %ANAME(ARG:0)%をふたなりにしました
	CASE 1
		TALENT:(ARG:0):性別 = 2
		PRINTFORMW %ANAME(ARG:0)%をふたなりにしました
	CASE 2
		TALENT:(ARG:0):性別 = 1
		PRINTFORMW %ANAME(ARG:0)%を女性にしました
	CASE 3
		TALENT:(ARG:0):性別 = 0
		PRINTFORMW %ANAME(ARG:0)%を男性にしました
	CASE 4
		TALENT:(ARG:0):性別 = 5
		PRINTFORMW %ANAME(ARG:0)%をふたなりにしました
	CASE 5
		TALENT:(ARG:0):性別 = 4
		PRINTFORMW %ANAME(ARG:0)%を男の娘にしました
ENDSELECT
;ピアス穴を塞ぐ
CALL CLEAR_PIERCE_HOLE_SEX_CHANGE(ARG:0)
IF GROUPMATCH(TALENT:(ARG:0):性別, 2, 3, 5)
	PRINTFORML %ANAME(ARG:0)%を童貞に戻しておきますか？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORMW %ANAME(ARG:0)%を童貞に戻しました
		TALENT:(ARG:0):童貞 = 1
		SEXUAL_EXPERIENCE:(ARG:0):初体験_童貞 = ----
	ENDIF
ENDIF

;●性転換薬
@ITEM_EFFECT281(ARG:0)
SELECTCASE TALENT:(ARG:0):性別
	CASE 0
		TALENT:(ARG:0):性別 = 1
		PRINTFORMW %ANAME(ARG:0)%を女性にしました
	CASE 1
		IF CFLAG:(ARG:0):元男の娘フラグ
			TALENT:(ARG:0):性別 = 4
			PRINTFORMW %ANAME(ARG:0)%を男の娘にしました
			CFLAG:(ARG:0):元男の娘フラグ = 0
		ELSE
			TALENT:(ARG:0):性別 = 0
			PRINTFORMW %ANAME(ARG:0)%を男性にしました
		ENDIF
	CASE 2
		IF CFLAG:(ARG:0):元男の娘フラグ
			TALENT:(ARG:0):性別 = 5
			PRINTFORMW %ANAME(ARG:0)%を男の娘ふたなりにしました
			CFLAG:(ARG:0):元男の娘フラグ = 0
		ELSE
			TALENT:(ARG:0):性別 = 3
			PRINTFORMW %ANAME(ARG:0)%を男ふたなりにしました
		ENDIF
	CASE 3
		TALENT:(ARG:0):性別 = 2
		PRINTFORMW %ANAME(ARG:0)%を女ふたなりにしました
	CASE 4
		TALENT:(ARG:0):性別 = 1
		PRINTFORMW %ANAME(ARG:0)%を女性にしました
		;元男の娘フラグ
		CFLAG:(ARG:0):元男の娘フラグ = 1
	CASE 5
		TALENT:(ARG:0):性別 = 2
		PRINTFORMW %ANAME(ARG:0)%を女ふたなりにしました
		;元男の娘フラグ
		CFLAG:(ARG:0):元男の娘フラグ = 1
ENDSELECT
;ピアス穴を塞ぐ
CALL CLEAR_PIERCE_HOLE_SEX_CHANGE(ARG:0)

;●岩神ノ秘薬
@ITEM_EFFECT282(ARG:0)
TALENT:(ARG:0):孕み可能 = 1
PRINTFORMW %ANAME(ARG:0)%はお尻の穴で孕めるようになりました

;●獣変化の秘薬
@ITEM_EFFECT283(ARG:0)
IF CFLAG:(ARG:0):獣変化フラグ
	TALENT:(ARG:0):動物耳 = 0
	TALENT:(ARG:0):しっぽ = 0
	CFLAG:(ARG:0):獣変化フラグ = 0
	PRINTFORMW %ANAME(ARG:0)%は<動物耳><しっぽ>を失いました
ELSE
	TALENT:(ARG:0):動物耳 = 1
	TALENT:(ARG:0):しっぽ = 1
	CFLAG:(ARG:0):獣変化フラグ = 1
	PRINTFORMW %ANAME(ARG:0)%は<動物耳><しっぽ>を得ました
ENDIF

;●再生の秘薬
@ITEM_EFFECT284(ARG:0)
#DIM FIRST_LINE
IF !EXISTCSV(ARG:0)
	CALL OLD_ITEM_EFFECT_284(ARG:0)
	RETURN
ENDIF
PRINTFORML %ANAME(ARG:0)%が再生の秘薬を飲むが……
PRINTFORML
FIRST_LINE = LINECOUNT
$SHOW_LOOP_SET
LOCAL:1 = CFLAG:(ARG:0):所属
LOCAL:2 = CFLAG:(ARG:0):捕虜先
CALL CLEAR_ALL_PASS_SETTINGS()
CALL PASS_CHARA_UI(ARG)
CALL PASS_CHARA(ARG)
CFLAG:(ARG:0):所属 = LOCAL:1
CFLAG:(ARG:0):捕虜先 = LOCAL:2

;デフォルトで処女でないキャラは、再生させるか確認する
IF CSVTALENT(NO:(ARG:0), GETNUM(TALENT, "処女"))
	PRINTFORML %ANAME(ARG:0)%はデフォルトで処女でないので、先ほどの選択で処女を初期化しても非処女のままです
	PRINTFORML %ANAME(ARG:0)%の処女を再生しますか？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML %ANAME(ARG:0)%の処女を再生しました
		TALENT:(ARG:0):処女 = 1
		SEXUAL_EXPERIENCE:(ARG:0):初体験_処女 = ----
	PRINTFORML %ANAME(ARG:0)%の<処女>が再生されました
ENDIF
IF CSVTALENT(NO:(ARG:0), GETNUM(TALENT, "アナル処女"))
	PRINTFORML %ANAME(ARG:0)%はデフォルトでアナル処女でないので、先ほどの選択でアナル処女を初期化しても非処女のままです
	PRINTFORML %ANAME(ARG:0)%のアナル処女を再生しますか？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML %ANAME(ARG:0)%のアナル処女を再生しました
		TALENT:(ARG:0):アナル処女 = 1
		SEXUAL_EXPERIENCE:(ARG:0):初体験_アナル処女 = ----
	PRINTFORML %ANAME(ARG:0)%の<アナル処女>が再生されました
ENDIF
IF CSVTALENT(NO:(ARG:0), GETNUM(TALENT, "童貞"))
	PRINTFORML %ANAME(ARG:0)%はデフォルトで童貞でないので、先ほどの選択で童貞を初期化しても童貞のままです
	PRINTFORML %ANAME(ARG:0)%の童貞を再生しますか？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML %ANAME(ARG:0)%の童貞を再生しました
		TALENT:(ARG:0):童貞 = 1
		SEXUAL_EXPERIENCE:(ARG:0):初体験_童貞 = ----
	PRINTFORML %ANAME(ARG:0)%の<童貞>が再生されました
ENDIF


PRINTFORML デイリーイベントで得る素質を残しますか？
CALL ASK_YN("残す", "消去")
IF RESULT == 1
	FOR LOCAL:0, 0, CHARANUM
		TALENT:(ARG:0):デイリー系 = 0
	NEXT
ENDIF

PRINTFORML %ANAME(ARG:0)%の肉体が再生されました
WAIT
RETURN

;固定キャラ以外に再生の秘薬を飲ませた場合
@OLD_ITEM_EFFECT_284(ARG:0)
PRINTFORML %ANAME(ARG:0)%の肉体が再生されました
FOR LOCAL:0, 0, 5
	ABL:(ARG:0):(LOCAL:0) = 0
	EXP:(ARG:0):(LOCAL:0) = 0
NEXT
IF TALENT:(ARG:0):処女 != 1
	TALENT:(ARG:0):処女 = 1
	SEXUAL_EXPERIENCE:(ARG:0):初体験_処女 = ----
	PRINTFORML %ANAME(ARG:0)%の<処女>が再生されました
ENDIF
IF TALENT:(ARG:0):キス未経験 != 1
	TALENT:(ARG:0):キス未経験 = 1
	SEXUAL_EXPERIENCE:(ARG:0):初体験_ファーストキス = ----
	PRINTFORML %ANAME(ARG:0)%の<キス未経験>が再生されました
ENDIF
IF TALENT:(ARG:0):童貞 != 1
	TALENT:(ARG:0):童貞 = 1
	SEXUAL_EXPERIENCE:(ARG:0):初体験_童貞 = ----
	PRINTFORML %ANAME(ARG:0)%の<童貞>が再生されました
ENDIF
IF TALENT:(ARG:0):アナル処女 != 1
	TALENT:(ARG:0):アナル処女 = 1
	SEXUAL_EXPERIENCE:(ARG:0):初体験_アナル処女 = ----
	PRINTFORML %ANAME(ARG:0)%の<アナル処女>が再生されました
ENDIF

IF TALENT:(ARG:0):烙印
	TALENT:(ARG:0):烙印 = 0
	PRINTFORML %ANAME(ARG:0)%の体から<烙印>が消えました
ENDIF
TALENT:(ARG:0):特殊勢力陥落系 = 0
TALENT:(ARG:0):デイリー系 = 0
WAIT


;永久脱毛剤
@ITEM_EFFECT287(ARG:0)
PRINTFORML %ANAME(ARG:0)%の股間に永久脱毛剤を塗り込んだ
PRINTFORML %ANAME(ARG:0)%は<パイパン>になった
TALENT:(ARG:0):陰毛目標値 = 陰毛_パイパン
TALENT:(ARG:0):陰毛現在値 = 陰毛_パイパン

;入れ墨除去手術
@ITEM_EFFECT288(ARG:0)
PRINTFORML %ANAME(ARG:0)%のどの入れ墨を除去しますか？
FOR LOCAL, 0, VARSIZE("TATOO")
	SIF TATOO:(ARG:0):(LOCAL) != ""
		PRINTFORML [{LOCAL}] %GET_TATOO_NAME(LOCAL)% - 「%TATOO:(ARG:0):(LOCAL)%」
NEXT
PRINTFORML [99] 戻る
$INPUT_LOOP
INPUT
IF RESULT == 99
	CALL GET_PRICE288
	MONEY += RESULT
	RETURN
ELSEIF 0 <= RESULT && RESULT < VARSIZE("TATOO") && TATOO:(ARG:0):RESULT != ""
	PRINTFORMW %ANAME(ARG:0)%の%GET_TATOO_NAME(RESULT)%のタトゥーを除去しました……
	TATOO:(ARG:0):RESULT = 
ELSE
	GOTO INPUT_LOOP
ENDIF

;穴トレ
@ITEM_EFFECT289(ARG:0)
#DIMS 変化前
#DIMS 変化後
PRINTFORML %ANAME(ARG:0)%のどちらの穴を鍛えますか？
CALL ASK_MULTI_JUDGE("膣", VAGINA(ARG:0), "尻", 1, "戻る", 1)
SELECTCASE RESULT
	CASE 0
		変化前 = %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ｖ締まり)%
		TALENT:(ARG:0):Ｖ締まり = LIMIT(TALENT:(ARG:0):Ｖ締まり + 100, 締まり_最低値, 締まり_最高値)
		変化後 = %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ｖ締まり)%
		CALL COLORPRINTFORM(@"%ANAME(ARG:0)%の膣穴をトレーニングしました！", COLOR("注意"), "W")
		CALL COLORPRINTFORM(@"変化前:%変化前%　→　変化後:%変化後%", COLOR("注意"), "W")
	CASE 1
		変化前 = %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ａ締まり)%
		TALENT:(ARG:0):Ａ締まり = LIMIT(TALENT:(ARG:0):Ａ締まり + 100, 締まり_最低値, 締まり_最高値)
		変化後 = %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ａ締まり)%
		CALL COLORPRINTFORM(@"%ANAME(ARG:0)%の尻穴をトレーニングしました！", COLOR("注意"), "W")
		CALL COLORPRINTFORM(@"変化前:%変化前%　→　変化後:%変化後%", COLOR("注意"), "W")
	CASE 2
		CALL GET_PRICE289
		MONEY += RESULT
		RETURN
ENDSELECT
RETURN

;豊胸
@ITEM_EFFECT290(ARG:0)
PRINTFORML %ANAME(ARG:0)%のバストをどうしますか？
LOCAL = (TALENT:(ARG:0):絶壁 || TALENT:(ARG:0):貧乳 || TALENT:(ARG:0):巨乳 || TALENT:(ARG:0):爆乳)
CALL ASK_MULTI_JUDGE("絶壁", !TALENT:(ARG:0):絶壁, "貧乳", !TALENT:(ARG:0):貧乳, "普通", LOCAL, "巨乳", !TALENT:(ARG:0):巨乳, "爆乳", !TALENT:(ARG:0):爆乳, "戻る", 1)
SELECTCASE RESULT
	CASE 5
		CALL GET_PRICE290
		MONEY += RESULT
		RETURN
	CASEELSE
		TALENT:(ARG:0):絶壁 = 0
		TALENT:(ARG:0):貧乳 = 0
		TALENT:(ARG:0):巨乳 = 0
		TALENT:(ARG:0):爆乳 = 0
		SELECTCASE RESULT
			CASE 0
				LOCAL = GETNUM(TALENT, "絶壁")
				TALENT:(ARG:0):絶壁 = 1
			CASE 1
				LOCAL = GETNUM(TALENT, "貧乳")
				TALENT:(ARG:0):貧乳 = 1
			CASE 2
				LOCAL = -1
			CASE 3
				LOCAL = GETNUM(TALENT, "巨乳")
				TALENT:(ARG:0):巨乳 = 1
			CASE 4
				LOCAL = GETNUM(TALENT, "爆乳")
				TALENT:(ARG:0):爆乳 = 1
		ENDSELECT
		CALL COLORPRINTFORM(@"%ANAME(ARG:0)%の胸が\@ LOCAL == -1 ? 普通 # %TALENTNAME:LOCAL% \@になりました！", COLOR("注意"), "W")
ENDSELECT

;増茎
@ITEM_EFFECT291(ARG:0)
PRINTFORML %ANAME(ARG:0)%のペニスをどうしますか？
LOCAL = (TALENT:(ARG:0):超短小 || TALENT:(ARG:0):短小 || TALENT:(ARG:0):巨根 || TALENT:(ARG:0):超巨根)
CALL ASK_MULTI_JUDGE("超短小", !TALENT:(ARG:0):超短小, "短小", !TALENT:(ARG:0):短小, "普通", LOCAL, "巨根", !TALENT:(ARG:0):巨根, "超巨根", !TALENT:(ARG:0):超巨根, "戻る", 1)
SELECTCASE RESULT
	CASE 5
		CALL GET_PRICE290
		MONEY += RESULT
		RETURN
	CASEELSE
		TALENT:(ARG:0):超短小 = 0
		TALENT:(ARG:0):短小 = 0
		TALENT:(ARG:0):巨根 = 0
		TALENT:(ARG:0):超巨根 = 0
		SELECTCASE RESULT
			CASE 0
				LOCAL = GETNUM(TALENT, "超短小")
				TALENT:(ARG:0):超短小 = 1
			CASE 1
				LOCAL = GETNUM(TALENT, "短小")
				TALENT:(ARG:0):短小 = 1
			CASE 2
				LOCAL = -1
			CASE 3
				LOCAL = GETNUM(TALENT, "巨根")
				TALENT:(ARG:0):巨根 = 1
			CASE 4
				LOCAL = GETNUM(TALENT, "超巨根")
				TALENT:(ARG:0):超巨根 = 1
		ENDSELECT
		CALL COLORPRINTFORM(@"%ANAME(ARG:0)%のペニスが\@ LOCAL == -1 ? 普通 # %TALENTNAME:LOCAL% \@になりました！", COLOR("注意"), "W")
ENDSELECT

;避妊薬
@ITEM_EFFECT292(ARG:0)
PRINTFORMW %ANAME(ARG:0)%はしばらく妊娠しなくなった……
CFLAG:(ARG:0):避妊薬残ターン += 5
CALL CLEAR_CREAMPIE_HISTORY(LOCAL:0)

;堕胎手術
@ITEM_EFFECT293(ARG:0)
PRINTFORMW %ANAME(ARG:0)%に堕胎手術を受けさせた……
PRINTFORMW %ANAME(ARG:0)%は＜妊娠＞ではなくなった
;状況に応じて崩壊値が増えたりする処理をいれたい

;妊娠関係のフラグをクリア
SIF CFLAG:(ARG:0):行動不能状態 == 1
	CFLAG:(ARG:0):行動不能状態 = 0
CFLAG:(ARG:0):妊娠期間 = 0
CFLAG:(ARG:0):子の遺伝上の父親 = 0
CFLAG:(ARG:0):子の父親 = 0
TALENT:(ARG:0):妊娠 = 0

;中出し履歴をクリア
CALL CLEAR_CREAMPIE_HISTORY(ARG:0)
IF CFLAG:(ARG:0):育児期間 == 0
	CALL RESET_MOTHER_STATE(ARG:0, 0)
ENDIF

;●太平要術の書
@ITEM_EFFECT141(ARG:0)
TALENT:(ARG:0):妖術知識 = 1
PRINTFORMW %ANAME(ARG:0)%が<妖術知識>を身につけました

;●淫技大全
@ITEM_EFFECT142(ARG:0)
ABL:(ARG:0):性知識 = 5
PRINTFORMW %ANAME(ARG:0)%の性知識が上昇しました(Lv5)

;-------------------------------------------------
;犬と豚を追加する
;-------------------------------------------------
@ADD_ANIMAL(ARGS:0, ARG:0)
CALL ADD_VOID_CHARA()
LOCAL = RESULT
CALL NEWCHARA_INIT(LOCAL)
NAME:LOCAL = %ARGS:0%
CALLNAME:LOCAL = %ARGS:0%
MAXBASE:LOCAL:体力 = 3000
MAXBASE:LOCAL:気力 = 3000
MAXBASE:LOCAL:精神力 = 1500

BASE:LOCAL:体力 = MAXBASE:LOCAL:体力
BASE:LOCAL:気力 = MAXBASE:LOCAL:気力
BASE:LOCAL:精神力 = MAXBASE:LOCAL:精神力

ABL:LOCAL:Ｃ感 = 5
ABL:LOCAL:欲望 = 5
ABL:LOCAL:性技 = 5
ABL:LOCAL:性知識 = 5
ABL:LOCAL:奉仕 = 3
ABL:LOCAL:性交 = 10
ABL:LOCAL:射精 = 5

EXP:LOCAL:絶頂経験 = 300

ABL:LOCAL:武闘 = 5
ABL:LOCAL:知略 = 5
ABL:LOCAL:政治 = 1
ABL:LOCAL:歌唱 = 1
ABL:LOCAL:料理 = 1
TALENT:LOCAL:貞操無頓着 = 1
TALENT:LOCAL:動物耳 = 1
TALENT:LOCAL:しっぽ = 1
TALENT:LOCAL:服従 = 1
TALENT:LOCAL:隷属 = 1
TALENT:LOCAL:絶倫 = 1
TALENT:(LOCAL:0):合意 = 1
TALENT:LOCAL:陰毛現在値 = 陰毛_標準
TALENT:LOCAL:陰毛目標値 = 陰毛_標準
CFLAG:(LOCAL):好感度 = 3000
CFLAG:(LOCAL):従属度 = 10000
CFLAG:(LOCAL):依存度 = 5000

CFLAG:(LOCAL:0):面識 = 1
CALL CHANGE_COUNTRY(LOCAL, ARG:0)

SELECTCASE ARGS:0
	CASE "犬"
		TALENT:LOCAL:犬 = 1
	CASE "豚"
		TALENT:LOCAL:豚 = 1
ENDSELECT
