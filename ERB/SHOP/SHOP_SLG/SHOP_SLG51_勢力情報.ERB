;-------------------------------------------------
;「勢力情報」の名称
;-------------------------------------------------
@SHOP_SLG_NAME51
RESULTS:0 '= "勢力情報"

;-------------------------------------------------
;「勢力情報」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK51
RETURN 1

;-------------------------------------------------
;「勢力情報」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY51
CALL SHOW_COUNTRY_DATA
RETURN 1


;軍事画面の勢力情報表示
;※大半の関数で高速化用の変数を使用しているため安易な流用は避けること！
;-------------------------------------------------
;「勢力情報」本体
;-------------------------------------------------
@SHOW_COUNTRY_DATA
#DIM LIST_COUNTRY, MAX_COUNTRY
#DIM LIST_ECONOMY, MAX_COUNTRY
#DIM LIST_STARS, MAX_COUNTRY
#DIM LIST_MAX
#DIM SELECTED
#DIM FIRST_LINE
VARSET SELECTED

LIST_MAX = 0
REDRAW 0

;必要な高速化用マップの作成
CALL TMP_CREATE_RELATION_MAP
CALL TMP_CREATE_COUNTRY_BOSS_MAP
CALL TMP_CREATE_UNION_TARGET_MAP
CALL TMP_PREPARE_CHARA_STARS
CALL TMP_PREPARE_COUNTRY_STARS
;勢力を経済規模順に並び替えてリストに記録
FOR LOCAL:0, 1, MAX_COUNTRY
	;勢力の存在判定 ただし都市のないときの野盗は除外する
	IF IS_COUNTRY(LOCAL:0)
		LOCAL:5 = GET_SUM_ECONOMY(LOCAL:0)
		LIST_COUNTRY:(LIST_MAX) = LOCAL:0
		LIST_ECONOMY:(LIST_MAX) = LOCAL:5
		FOR LOCAL:1, 0, LIST_MAX
			IF LOCAL:5 > LIST_ECONOMY:(LOCAL:1)
				FOR LOCAL:2, 0, LIST_MAX - LOCAL:1
					SWAP LIST_COUNTRY:(LIST_MAX - LOCAL:2 - 1), LIST_COUNTRY:(LIST_MAX - LOCAL:2)
					SWAP LIST_ECONOMY:(LIST_MAX - LOCAL:2 - 1), LIST_ECONOMY:(LIST_MAX - LOCAL:2)
				NEXT
				BREAK
			ENDIF
		NEXT
		LIST_MAX ++
	ENDIF
NEXT

FOR LOCAL:0, 0, LIST_MAX
	LOCAL:2 = 0
	LOCAL:5 = LIST_COUNTRY:(LOCAL:0)
	FOR LOCAL:1, 0, CHARANUM
		SIF CFLAG:(LOCAL:1):所属 == LOCAL:5 && TMP_CHARA_STARS:(LOCAL:1) >= 7
			LOCAL:2 ++
	NEXT
	LIST_STARS:(LOCAL:0) = LOCAL:2
NEXT

;表示部分開始
FIRST_LINE = LINECOUNT

$SHOW_LOOP

CALL SINGLE_DRAWLINE

PRINTFORM %"君主",MAX_CHARANAME_LENGTH / 2 + 2,LEFT%
PRINTFORM %"経済", 6,RIGHT%
PRINTFORM %"国庫", 6,RIGHT%
PRINTFORM %"兵力", 7, RIGHT%
PRINTFORM %"傾向", 6, RIGHT%
PRINTFORM %"都市", 6, RIGHT%
PRINTFORM %"士官", 6, RIGHT%
PRINTFORM %"★７↑", 8, RIGHT%
PRINTFORM %"関係", 8, RIGHT%
PRINTFORML %"評価", 13, RIGHT%

FOR LOCAL:0, 0, LIST_MAX
	LOCAL:5 = LIST_COUNTRY:(LOCAL:0)

	; IF CFLAG:MASTER:所属 == LOCAL:5
		; SETCOLOR カラー_注意
	; ENDIF
	PRINT

	SETCOLOR COUNTRY_COLOR:(LOCAL:5)　

	PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(LOCAL:5)), MAX_CHARANAME_LENGTH / 2, LEFT%]", LOCAL:5
	RESETCOLOR

	PRINTFORM {LIST_ECONOMY:(LOCAL:0) / 100, 6, RIGHT}
	PRINTFORM {MONEY:(LOCAL:5), 6, RIGHT}
	PRINTFORM {GET_SUM_SOLDIER(LOCAL:5), 7, RIGHT}
	PRINTFORM %TOSTR_AI_TYPE(COUNTRY_AI_TYPE:(LOCAL:5)), 6, RIGHT%
	PRINTFORM {GET_OWN_CITY(LOCAL:5), 6, RIGHT}
	PRINTFORM {GET_COMMANDER_NUM(LOCAL:5), 6, RIGHT}
	PRINTFORM {LIST_STARS:(LOCAL:0), 8, RIGHT}
	IF LOCAL:5 != CFLAG:MASTER:所属
		CALL TMP_SETCOLOR_COUNTRY_RELATION(CFLAG:MASTER:所属, LOCAL:5)
		PRINTFORM %TMP_TOSTR_COUNTRY_RELATION(CFLAG:MASTER:所属, LOCAL:5, 1), 8, RIGHT%
	ELSE
		PRINTFORM %TOSTR_REPEAT(" ", 8)%
	ENDIF
	PRINT    
	CALL TMP_PRINT_COUNTRY_STARS(LOCAL:5)
	PRINTL 
	RESETCOLOR
NEXT
IF IS_COUNTRY(SELECTED)
	CALL SINGLE_DRAWLINE
	PRINTFORM %"★" + ANAME(GET_COUNTRY_BOSS(SELECTED))+ "★", 20, LEFT% 評価:
	CALL TMP_PRINT_COUNTRY_STARS(SELECTED)
	PRINTFORML  経済:{GET_SUM_ECONOMY(SELECTED)/100, 6, LEFT} 兵力:{GET_SUM_SOLDIER(SELECTED), 7, LEFT} 都市:{GET_OWN_CITY(SELECTED), 3, LEFT}
	CALL SINGLE_DRAWLINE
	PRINTFORM 所属武将:
	PRINTFORM %ANAME(GET_COUNTRY_BOSS(SELECTED))%  
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:(LOCAL):所属 == SELECTED && LOCAL != GET_COUNTRY_BOSS(SELECTED)
			PRINTFORM %ANAME(LOCAL)%  
	NEXT
	PRINTL
	PRINTFORM 捕虜:
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:捕虜先 == SELECTED
			PRINTFORM %ANAME(LOCAL)%  
	NEXT
	PRINTL
ENDIF
CALL SINGLE_DRAWLINE
PRINTBUTTON " 98[在野武将]", 98
PRINTL
PRINTBUTTON " 99[関係一覧]", 99
PRINTL 
PRINTBUTTON " 0[    戻る]", 0
PRINTL 

$INPUT_LOOP
INPUT

IF RESULT == 99
	CALL SINGLE_DRAWLINE
	CALL TMP_SHOW_COUNTRY_RELATION1
	WAIT
	CALL SINGLE_DRAWLINE
	CALL TMP_SHOW_COUNTRY_RELATION2
	CALL SINGLE_DRAWLINE
	WAIT
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 98
	LOCAL:1 = 0
	FOR LOCAL, 0, CHARANUM
		IF CFLAG:LOCAL:特殊状態 == 特殊状態_放浪
			IF LOCAL:1 == 0
				LOCAL:1 = 1
			ELSE
				PRINTFORM ,
			ENDIF
			PRINTFORM %ANAME(LOCAL)%
		ENDIF
	NEXT
	WAIT
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF IS_COUNTRY(RESULT)
	CALL SHOW_COUNTRY_INFO(RESULT)
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 0
	REDRAW 1
	RETURN
ELSE
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
;ARG:0勢力のARG:1勢力に対する関係を文字列として返す関数
;ARG:2が0なら全角２文字の略式、1なら全角４文字の文字列が返る
;-------------------------------------------------
@TMP_TOSTR_COUNTRY_RELATION(ARG:0, ARG:1, ARG:2)
#FUNCTIONS
;正式
IF ARG:2
	SELECTCASE TMP_COUNTRY_RELATION:(ARG:0):(ARG:1)
		CASE 4
			RETURNF "永久同盟"
		CASE 3
			RETURNF "同盟" + "(" + TOSTR(TMP_COUNTRY_RELATION_TERM:(ARG:0):(ARG:1)) + ")"
		CASE 2
			RETURNF "連合" + "(" + TOSTR(TMP_COUNTRY_RELATION_TERM:(ARG:0):(ARG:1)) + ")"
		CASE 1
			RETURNF "停戦" + "(" + TOSTR(TMP_COUNTRY_RELATION_TERM:(ARG:0):(ARG:1)) + ")"
		CASE 0
			IF TMP_COUNTRY_UNION_TARGET:(ARG:0) == ARG:1
				RETURNF "討伐対象"
			ENDIF
	ENDSELECT
	RETURNF "――"
;略式
;0-4  :２文字表記
;5-9  :１文字表記
;10-14:記号表記

ELSE
	SELECTCASE TMP_COUNTRY_RELATION:(ARG:0):(ARG:1) + CONFIG:319 * 5
		CASE 4
			RETURNF "永"
		CASE 9
			RETURNF "∞"
		CASE 3
			RETURNF "盟"
		CASE 8
			RETURNF "＋"
		CASE 2
			RETURNF "連"
		CASE 7
			RETURNF "‡"
		CASE 1
			RETURNF "停"
		CASE 6
			RETURNF "∴"
		CASE 0, 5, 10
			IF TMP_COUNTRY_UNION_TARGET:(ARG:0) == ARG:1
				SELECTCASE CONFIG:319
					CASE 0
						RETURNF "討"
					CASE 2
						RETURNF "×"
				ENDSELECT
			ENDIF
	ENDSELECT
	RETURNF "・"
ENDIF

;-------------------------------------------------
;ARG:0勢力のARG:1勢力に対する関係を調べ、それに応じた文字カラーを設定する関数
;-------------------------------------------------
@TMP_SETCOLOR_COUNTRY_RELATION(ARG:0, ARG:1)
SELECTCASE TMP_COUNTRY_RELATION:(ARG:0):(ARG:1)
	CASE 4
		SETCOLOR カラー_永久同盟
	CASE 3
		SETCOLOR カラー_同盟
	CASE 2
		SETCOLOR カラー_連合
	CASE 1
		SETCOLOR カラー_停戦
	CASE 0
		IF TMP_COUNTRY_UNION_TARGET:(ARG:0) == ARG:1
			SETCOLOR カラー_注意
		ELSE
			SETCOLOR カラー_敵
		ENDIF
ENDSELECT

;-------------------------------------------------
;勢力関係の表示
;※TMP_CREATE_BOSS_NAME_MAP関数で君主略名マップを事前に作成しておく必要がある
;-------------------------------------------------
@TMP_SHOW_COUNTRY_RELATION1
PRINT     
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && GET_OWN_CITY(LOCAL:0) > 0
		SETCOLOR COUNTRY_COLOR:(LOCAL:0)　
		PRINTFORM %TMP_COUNTRY_BOSS_NAME_SHORT:(LOCAL:0), 3, LEFT%
		RESETCOLOR
	ENDIF
NEXT

;  
PRINTL 
PRINT    
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && GET_OWN_CITY(LOCAL:0) > 0
		PRINTFORM ---
	ENDIF
NEXT
PRINTL

FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && GET_OWN_CITY(LOCAL:0) > 0
		SETCOLOR COUNTRY_COLOR:(LOCAL:0)　
		PRINTFORM %TMP_COUNTRY_BOSS_NAME_SHORT:(LOCAL:0), 2, LEFT%
		RESETCOLOR
		PRINT |
		FOR LOCAL:1, 1, MAX_COUNTRY
			IF TMP_COUNTRY_BOSS_NUMBER:(LOCAL:1) >= 0 && GET_OWN_CITY(LOCAL:1) > 0
				PRINT  
				IF LOCAL:0 == LOCAL:1
					SETCOLOR カラー_選択不可
					PRINT ―
				ELSE
					CALL TMP_SETCOLOR_COUNTRY_RELATION(LOCAL:0, LOCAL:1)
					PRINTFORM %TMP_TOSTR_COUNTRY_RELATION(LOCAL:0, LOCAL:1, 0)%
				ENDIF
				RESETCOLOR
			ENDIF
		NEXT
		PRINTL 
	ENDIF
NEXT

IF CONFIG:319
	PRINT 凡例　

	SETCOLOR カラー_永久同盟
	PRINTFORM \@CONFIG:319 ? ∞ # 永 \@：永久同盟
	RESETCOLOR
	PRINT ｜

	SETCOLOR カラー_同盟
	;PRINT ∽：同盟
	PRINTFORM \@CONFIG:319 ? ＋ # 盟 \@：同盟
	RESETCOLOR
	PRINT ｜

	SETCOLOR カラー_オレンジ
	PRINTFORM \@CONFIG:319 ? ‡ # 連 \@：連合
	RESETCOLOR
	PRINT ｜

	SETCOLOR カラー_停戦
	PRINTFORM \@CONFIG:319 ? ∴ # 停 \@：停戦
	RESETCOLOR
	PRINT ｜

	SETCOLOR カラー_討伐
	PRINTFORM \@CONFIG:319 ? × # 討 \@：討伐
	RESETCOLOR
	PRINT ｜

	SETCOLOR カラー_敵
	PRINTL ・：通常
	RESETCOLOR
ENDIF
;-------------------------------------------------
;勢力間の共感値・評価値の表示
;※TMP_CREATE_BOSS_NAME_MAP関数で君主略名マップを事前に作成しておく必要がある
;-------------------------------------------------
@TMP_SHOW_COUNTRY_RELATION2
PRINTFORML 「（行)から見た（列)」への印象を示しています。
PRINTFORML
PRINT     
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && (COUNTRY_EVENT_ID:(LOCAL:0) != SP_COUNTRY_ID:(特殊勢力_野盗) || GET_OWN_CITY(LOCAL:0) > 0)
		SETCOLOR COUNTRY_COLOR:(LOCAL:0)　
		PRINTFORM %TMP_COUNTRY_BOSS_NAME_SHORT:(LOCAL:0), 3, LEFT%
		RESETCOLOR
	ENDIF
NEXT


;  
PRINTL 
PRINT    
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && (COUNTRY_EVENT_ID:(LOCAL:0) != SP_COUNTRY_ID:(特殊勢力_野盗) || GET_OWN_CITY(LOCAL:0) > 0)
		PRINTFORM ---
	ENDIF
NEXT
PRINTL

FOR LOCAL:0, 1, MAX_COUNTRY
	LOCAL:5 = TMP_COUNTRY_BOSS_NUMBER:(LOCAL:0)
	IF LOCAL:5 >= 0 && (COUNTRY_EVENT_ID:(LOCAL:0) != SP_COUNTRY_ID:(特殊勢力_野盗) || GET_OWN_CITY(LOCAL:0) > 0)
		SETCOLOR COUNTRY_COLOR:(LOCAL:0)　
		PRINTFORM %TMP_COUNTRY_BOSS_NAME_SHORT:(LOCAL:0), 2, LEFT%
		RESETCOLOR
		PRINT |
		FOR LOCAL:1, 1, MAX_COUNTRY
			LOCAL:6 = TMP_COUNTRY_BOSS_NUMBER:(LOCAL:1)
			IF LOCAL:6 >= 0 && GET_OWN_CITY(LOCAL:1) > 0
				CALL TMP_PRINT_COUNTRY_RELATION2(LOCAL:5, LOCAL:6)
			ENDIF
		NEXT
		PRINTL 
	ENDIF
NEXT

IF CONFIG:319
	PRINT 凡例　

	SETCOLOR 0x12eca3
	PRINTFORM \@CONFIG:319 ? ◎ # 友 \@：友好
	RESETCOLOR
	PRINT ｜

	SETCOLOR 0x23b1b1
	PRINTFORM \@CONFIG:319 ? ○ # 好 \@：好意
	RESETCOLOR
	PRINT ｜

	SETCOLOR 0x9528cc
	PRINTFORM \@CONFIG:319 ? ◇ # 注 \@：注視
	RESETCOLOR
	PRINT ｜

	SETCOLOR 0x482889
	PRINTFORM \@CONFIG:319 ? ⇔ # ラ \@：好敵
	RESETCOLOR
	PRINT ｜

	SETCOLOR 0x808080
	PRINT ・：中立
	RESETCOLOR
	PRINT ｜

	SETCOLOR 0x8600c8
	PRINTFORM \@CONFIG:319 ? ≫ # 天 \@：天敵
	RESETCOLOR
	PRINT ｜

	SETCOLOR 0x842c2c
	PRINTFORM \@CONFIG:319 ? † # 悪 \@：険悪
	RESETCOLOR
	PRINT ｜

	SETCOLOR 0xff0000
	PRINTFORM \@CONFIG:319 ? ≠ # 仇 \@：仇敵
	RESETCOLOR

	IF !RAND:500
		PRINT ｜

		SETCOLOR 0x333333
		PRINTFORM ⑨：バカ
		RESETCOLOR
	ENDIF
	PRINTL
ENDIF

;●デバッグ用の関係詳細表示
;FOR LOCAL:0, 1, MAX_COUNTRY
;	LOCAL:5 = TMP_COUNTRY_BOSS_NUMBER:(LOCAL:0)
;	IF LOCAL:5 >= 0
;		PRINTFORM %TMP_COUNTRY_BOSS_NAME_SHORT:(LOCAL:0), 5, LEFT%
;		FOR LOCAL:1, 1, MAX_COUNTRY
;			LOCAL:6 = TMP_COUNTRY_BOSS_NUMBER:(LOCAL:1)
;			IF LOCAL:6 >= 0
;				LOCAL:10 = REL_LIKE:(LOCAL:5):(LOCAL:6)
;				LOCAL:11 = REL_HATE:(LOCAL:5):(LOCAL:6)
;				LOCAL:12 = (LOCAL:10 >= 800) + (LOCAL:10 >= 300)
;				LOCAL:13 = (LOCAL:11 >= 800) + (LOCAL:11 >= 300)
;				PRINTFORM {LOCAL:10 / 10, 3}{LOCAL:11 / 10, 3}
;			ENDIF
;		NEXT
;		PRINTL 
;	ENDIF
;NEXT

;-------------------------------------------------
;君主ARG:0の君主ARG:1に対する印象を表示する関数
;勢力番号ではなく、君主のキャラ番号を渡す点に注意
;-------------------------------------------------
@TMP_PRINT_COUNTRY_RELATION2(ARG:0, ARG:1)
#DIMS REL_STR, 27
#DIM  REL_NUM
LOCAL:10 = REL_LIKE:(ARG:0):(ARG:1)
LOCAL:11 = REL_HATE:(ARG:0):(ARG:1)
LOCAL:12 = (LOCAL:10 >= 800) + (LOCAL:10 >= 300)
LOCAL:13 = (LOCAL:11 >= 800) + (LOCAL:11 >= 300)

REL_NUM = CONFIG:319 * 9 + LOCAL:12 * 3 + LOCAL:13

REL_STR:0  '= " ・", " 悪", " 仇", " 好", " 注", " 天", " 友", " ラ", " ラ"
REL_STR:9 '= " ・", " †", " ≠", " ○", " ◇", " ≫", " ◎", " ⇔", " ⇔"
IF ARG:0 == ARG:1
	SETCOLOR カラー_選択不可
	PRINT  ―
ELSEIF CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属
	SETCOLOR カラー_選択不可
	PRINT  ☆
ELSEIF LOCAL:12 == 2 && LOCAL:13 == 2
	SETCOLOR 0x482889
;	PRINT 好敵 
;	PRINT  ⇔  
	PRINTS REL_STR:REL_NUM
ELSEIF LOCAL:12 == 2 && LOCAL:13 == 1
	SETCOLOR 0x482889
;	PRINT 好敵 
;	PRINT  ⇔  
	PRINTS REL_STR:REL_NUM
ELSEIF LOCAL:12 == 2 && LOCAL:13 == 0
	SETCOLOR 0x12eca3
;	PRINT 友好 
;	PRINT  ◎  
	PRINTS REL_STR:REL_NUM
ELSEIF LOCAL:12 == 1 && LOCAL:13 == 2
	SETCOLOR 0x8600c8
;	PRINT 天敵 
;	PRINT  ≫  
	PRINTS REL_STR:REL_NUM
ELSEIF LOCAL:12 == 1 && LOCAL:13 == 1
	SETCOLOR 0x9528cc
;	PRINT 注視 
;	PRINT  ◇  
	PRINTS REL_STR:REL_NUM
ELSEIF LOCAL:12 == 1 && LOCAL:13 == 0
	SETCOLOR 0x23b1b1
;	PRINT 好意 
;	PRINT  ○  
	PRINTS REL_STR:REL_NUM
ELSEIF LOCAL:12 == 0 && LOCAL:13 == 2
	SETCOLOR 0xff0000
;	PRINT 仇敵 
;	PRINT  ≠  
	PRINTS REL_STR:REL_NUM
ELSEIF LOCAL:12 == 0 && LOCAL:13 == 1
	SETCOLOR 0x842c2c
;	PRINT 険悪 
;	PRINT  †  
	PRINTS REL_STR:REL_NUM
ELSE
	SETCOLOR 0x808080
;	PRINT 中立 
	PRINTS REL_STR:REL_NUM
ENDIF
RESETCOLOR
