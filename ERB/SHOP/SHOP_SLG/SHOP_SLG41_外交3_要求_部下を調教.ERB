;-------------------------------------------------
;部下を調教する
;-------------------------------------------------
@DIPLOMACY_REQUEST_TRAIN_BUKA(ARG:0)
#DIM FIRST_LINE
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

IF COUNTRY_EVENT_ID:(ARG:0) == SP_COUNTRY_ID:(特殊勢力_野盗)
	PRINTFORMW 野盗は突っぱねてきました
	RETURN 0
ENDIF

PRINTFORMW %ANAME(MASTER)%は%ANAME(LOCAL:5)%に対して、部下を調教させろと要求した

CALL DIPLOMACY_REQUEST_COMMON(ARG:0, 250 + 20 * CFLAG:(LOCAL:5):外交調教カウンタ, 1)
LOCAL:2 = RESULT
SIF LOCAL:2 <= 0
	RETURN 0

DONE_DIPLOMACY_REQUEST:(ARG:0) = 1
FIRST_LINE = LINECOUNT

$SHOW_LOOP

CALL SINGLE_DRAWLINE
PRINTL 調教するキャラを選んで下さい
CALL SINGLE_DRAWLINE

CALL SELECT_CHARA_LIST_ONLY_LOGIC_SLG("TRAIN_BUKA", "NONE")

SIF RESULT == -1
	RETURN 1
LOCAL:30 = RESULT
CALL SINGLE_DRAWLINE
CALL SHOW_INFO(LOCAL:30)
CALL SINGLE_DRAWLINE
PRINTL このキャラを調教しますか？
CALL ASK_YN()
IF RESULT == 1
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF

SETCOLOR カラー_注意
PRINTFORML %ANAME(LOCAL:30)%が「捕虜の調教」メニューから調教可能になりました
RESETCOLOR
PRINTFORMW (条件を満たしている間は常に調教可能になります)
CFLAG:(LOCAL:30):外交調教中 = 1
REDRAW 1
;快楽で言うことを聞かせた場合、ご褒美の処理
SIF LOCAL:2 == 2
	CALL DIPLOMACY_REQUEST_PRIZE(ARG:0)
;薬物で言うことを聞かせた場合、ドラッグの処理
SIF LOCAL:2 == 3
	CALL DIPLOMACY_REQUEST_DRUG(ARG:0)
;催眠で言うことを聞かせた場合、催眠の処理
SIF LOCAL:2 == 4
	CALL DIPLOMACY_REQUEST_HYPNOSIS(ARG:0)

RETURN 1


@SELECT_CHARA_LIST_SHOW_LOGIC_TRAIN_BUKA(対象)
#DIM 対象
RETURN CFLAG:対象:所属 == DIPLOMACY_DIPLOMAT && CFLAG:対象:捕虜先 == 0 && 対象 != GET_COUNTRY_BOSS(CFLAG:対象:所属)　&& !IS_SP_CHARA(対象)
