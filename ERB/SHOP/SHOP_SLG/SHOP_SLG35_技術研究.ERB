;-------------------------------------------------
;「国家方針・研究」の名称
;-------------------------------------------------
@SHOP_SLG_NAME35
RESULTS:0 '= "技術研究"

;-------------------------------------------------
;「国家方針・研究」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK35
SIF FLAG:クリアフラグ
	RETURN 0
SIF FLAG:観戦モード
	RETURN 0
SIF !IS_COUNTRY(CFLAG:MASTER:所属)
	RETURN 0
SIF CONFIG:302
	RETURN 0
RETURN 1

;-------------------------------------------------
;「国家方針・研究」の右カラムオン判定
;-------------------------------------------------
;@SHOP_SLG_EVENTBUY_COLUMN_RIGHT_ON_CHECK35

;-------------------------------------------------
;「国家方針・研究」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY35
CALL SHOW_TECHNOLOGY_RESEARCH
RETURN 1

;-------------------------------------------------
;「勢力方針・研究」の表示処理本体
;-------------------------------------------------
@SHOW_TECHNOLOGY_RESEARCH(勢力)
#DIM 勢力
#DIM 勢力方針
#DIM 君主
#DIM 研究予定
#DIMS 技術名
#DIMS 説明
#DIM ジャンル
#DIM ステージ
#DIM 表示中研究番号
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT

;ショップから飛んできた場合は引数勢力が0
SIF !IS_COUNTRY(勢力)
	勢力 = CFLAG:MASTER:所属

;UI部分
CALL SINGLE_DRAWLINE
君主 = GET_COUNTRY_BOSS(勢力)

ジャンル = TECHNOLOGY_RESEARCH_TARGET:勢力 / 100
ステージ = TECHNOLOGY_RESEARCH_TARGET:勢力 % 100

PRINTFORML 君主：%ANAME(君主), MAX_CHARANAME_LENGTH, LEFT% 勢力方針：%TOSTR_AI_TYPE(COUNTRY_AI_TYPE:勢力)%

IF IS_TECHNOLOGY_EXISTS(ジャンル, ステージ)
	技術名 = %GET_TECHNOLOGY_NAME(ジャンル, ステージ)%
	PRINTFORM 現在進行中の研究(進捗/総期間)：
	SETCOLOR カラー_選択中
	PRINTFORM %技術名%
	RESETCOLOR
	PRINTFORML ({TECHNOLOGY_RESEARCH_PROGRESS:勢力}/{GET_RESEARCH_PERIOD(勢力, ジャンル, ステージ)}期)
ELSE 
	PRINTL
ENDIF
CALL SINGLE_DRAWLINE
CALL SHOW_TECHNOLOGY_TREE(勢力)
CALL SINGLE_DRAWLINE

ジャンル = 表示中研究番号 / 100
ステージ = 表示中研究番号 % 100

IF !IS_TECHNOLOGY_EXISTS(ジャンル, ステージ)
	PRINTL
	PRINTL
	PRINTL
	PRINTL
ELSE
	技術名 = %GET_TECHNOLOGY_NAME(ジャンル, ステージ)%
	説明 = %GET_TECHNOLOGY_DESCRIPTION(ジャンル, ステージ)%
	PRINTFORML %技術名%
	PRINTFORML 効果：%説明%
	PRINTFORML 完了まで{GET_RESEARCH_PERIOD(勢力, ジャンル, ステージ)}期
	IF 勢力 == CFLAG:MASTER:所属 && IS_TECHNOLOGY_RESEARCHABLE(勢力, ジャンル, ステージ) && 表示中研究番号 != TECHNOLOGY_RESEARCH_TARGET:勢力
		PRINTBUTTON "[研究開始]", 1001
		PRINTL
	ELSE
		PRINTL
	ENDIF
ENDIF
CALL SINGLE_DRAWLINE

PRINTBUTTON "[戻る]", 1000

$TECHNOLOGY_RESEARCH_START
INPUT

IF RESULT == 1000
	RETURN
ELSEIF RESULT == 1001 && 勢力 == CFLAG:MASTER:所属 && IS_TECHNOLOGY_RESEARCHABLE(勢力, ジャンル, ステージ)
	PRINTFORML 完了まで{GET_RESEARCH_PERIOD(勢力, ジャンル, ステージ)}期
	PRINTL 
	PRINTFORML 研究を開始しますか？（現在研究中の進捗状況はリセットされます）
	CALL ASK_YN()
	IF RESULT == 0 && TECHNOLOGY_RESEARCH_TARGET:勢力 != 表示中研究番号
		TECHNOLOGY_RESEARCH_TARGET:勢力 = 表示中研究番号
		TECHNOLOGY_RESEARCH_PROGRESS:勢力 = 0
		PRINTFORMW 研究を開始しました
	ENDIF
ELSE
	ジャンル = RESULT / 100
	ステージ = RESULT % 100
	SIF IS_TECHNOLOGY_EXISTS(ジャンル, ステージ)
		表示中研究番号 = RESULT
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;------------------
; 技術ツリーの表示
;------------------
@SHOW_TECHNOLOGY_TREE(勢力)
#DIM 勢力
#DIM 技術進捗
#DIMS 技術名
#DIMS CONST 技術ジャンル = "戦闘", "防衛", "外交", "徴兵", "徴税", "雇用"
#DIM ジャンル
#DIM ステージ

FOR ジャンル, 0, TECHNOLOGY_MAX_GENRE
	CALL PRINTBUTTON_CENTER(@"%技術ジャンル:ジャンル%", 0, 9, 0)
NEXT
PRINTL

FOR ステージ, 1, TECHNOLOGY_MAX_STAGE
	FOR ジャンル, 0, TECHNOLOGY_MAX_GENRE
		IF !IS_TECHNOLOGY_EXISTS(ジャンル, ステージ)
			PRINTFORM %TOSTR_SPACE(18)%
			CONTINUE
		ENDIF
		技術進捗 = TECHNOLOGY_STATUS:勢力:ジャンル
		技術名 = %GET_TECHNOLOGY_NAME(ジャンル, ステージ)%
		IF 技術進捗 >= ステージ
			SETCOLOR カラー_緑
		ELSEIF (ジャンル * 100 + ステージ) == TECHNOLOGY_RESEARCH_TARGET:勢力
			SETCOLOR カラー_選択中
		ELSE
			SETCOLOR GETDEFCOLOR()
		ENDIF
		CALL PRINTBUTTON_CENTER(@"[%技術名%]", ジャンル * 100 + ステージ, 9)
	NEXT
	PRINTL

	FOR ジャンル, 0, TECHNOLOGY_MAX_GENRE
		IF !IS_TECHNOLOGY_EXISTS(ジャンル, ステージ + 1)
			PRINTFORM %TOSTR_SPACE(18)%
			CONTINUE
		ENDIF
		技術進捗 = TECHNOLOGY_STATUS:勢力:ジャンル
		IF 技術進捗 >= ステージ + 1
			SETCOLOR カラー_緑
		ELSEIF (ジャンル * 100 + ステージ + 1) == TECHNOLOGY_RESEARCH_TARGET:勢力
			SETCOLOR カラー_選択中
		ELSE
			SETCOLOR カラー_選択不可
		ENDIF
		PRINTFORM %TOSTR_SPACE(8)%↓%TOSTR_SPACE(8)%
		RESETCOLOR
	NEXT
	PRINTL
NEXT

;------------------
; 研究期間
;------------------
@GET_RESEARCH_PERIOD(勢力, ジャンル, ステージ)
#FUNCTION
#DIM 勢力
#DIM ジャンル
#DIM ステージ
#DIM ウェイト
#DIM CONST 基礎研究期間 = 10
#DIM 研究機関

SIF !IS_COUNTRY(勢力)
	RETURNF 0

研究機関 = GET_DEVELOPMENT_COUNT(建造物_研究所, 勢力) > 0 ? 1 # 0
IF GET_DEVELOPMENT_COUNT(建造物_忍びの里, 勢力)
	FOR LOCAL, 0, MAX_COUNTRY
		SIF !IS_COUNTRY(LOCAL)
			CONTINUE
		IF TECHNOLOGY_STATUS:LOCAL:ジャンル >= ステージ
			研究機関 ++
			BREAK
		ENDIF
	NEXT
ENDIF

SIF ジャンル == -1 
	RETURNF 基礎研究期間

ウェイト = MIN(AI_TECHNOLOGY_PREFERENCE:(COUNTRY_AI_TYPE:勢力):ジャンル / 10, 5)

RETURNF 基礎研究期間 - ウェイト + ステージ - 研究機関 - 1


;------------------
; 技術名の返却
;------------------
@GET_TECHNOLOGY_NAME(ジャンル, ステージ)
#FUNCTIONS
#DIM ジャンル
#DIM ステージ

;DEBUGPRINTFORML GET_TECHNOLOGY_NAME({ジャンル}, {ステージ})
;DEBUGPRINTFORML %TECHNOLOGY_NAME:ジャンル:ステージ%
SIF !IS_TECHNOLOGY_EXISTS(ジャンル,ステージ)
	RETURNF ""

RETURNF TECHNOLOGY_NAME:ジャンル:ステージ


;------------------
; 技術の説明
;------------------
@GET_TECHNOLOGY_DESCRIPTION(ジャンル, ステージ)
#FUNCTIONS
#DIM ジャンル
#DIM ステージ

SIF !IS_TECHNOLOGY_EXISTS(ジャンル,ステージ)
	RETURNF ""


RETURNF TECHNOLOGY_DESC:ジャンル:ステージ

;------------------
; 存在する技術か
;------------------
@IS_TECHNOLOGY_EXISTS(ジャンル,ステージ)
#FUNCTION
#DIM ジャンル
#DIM ステージ

;DEBUGPRINTFORML IS_TECHNOLOGY_EXISTS({ジャンル}, {ステージ})

SIF !INRANGE(ジャンル, 0, TECHNOLOGY_MAX_GENRE - 1) || !INRANGE(ステージ, 0, TECHNOLOGY_MAX_STAGE - 1)
	RETURNF 0

RETURNF TECHNOLOGY_NAME:ジャンル:ステージ != ""

;------------------
; 研究可能か
;------------------
@IS_TECHNOLOGY_RESEARCHABLE(勢力,ジャンル,ステージ)
#FUNCTION
#DIM 勢力
#DIM ジャンル
#DIM ステージ

SIF !IS_TECHNOLOGY_EXISTS(ジャンル, ステージ)
	RETURNF 0

RETURNF TECHNOLOGY_STATUS:勢力:ジャンル == ステージ - 1

;------------------
;技術の名前など設定する
;------------------
@INIT_TECHNOLOGY()

TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:1 '= "妖精の応援"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:2 '= "妖精参戦"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:3 '= "炎弾幕"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:4 '= "霊界トランス"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:5 '= "へにょりレーザー"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:6 '= "森羅結界"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_SKILL:7 '= "妖精応援団"

TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_SKILL:1 '= "戦闘中のスキル発動率が+5\%"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_SKILL:2 '= "戦闘中、妖精参戦が発動する。部隊の人員が+1000～2000"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_SKILL:3 '= "戦闘中、炎弾幕が発動する。部隊の攻撃力が+5\%"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_SKILL:4 '= "戦闘中、霊界トランスが発動する。部隊の防御力が+5\%"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_SKILL:5 '= "戦闘中、へにょりレーザーが発動する。部隊の攻撃力が+7\%"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_SKILL:6 '= "戦闘中、森羅結界が発動する。部隊の防御力が+7\%"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_SKILL:7 '= "戦闘中のスキル発動率がさらに+10\%"

TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_PASSIVE:1 '= "弾幕櫓"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_PASSIVE:2 '= "逆茂木"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_PASSIVE:3 '= "補給路攻撃"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_PASSIVE:4 '= "補給路整備"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_PASSIVE:5 '= "本拠地"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_PASSIVE:6 '= "弾幕銃眼"
TECHNOLOGY_NAME:TECHNOLOGY_BATTLE_PASSIVE:7 '= "稜堡城郭"

TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_PASSIVE:1 '= "都市の火力支援の威力が+20\%"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_PASSIVE:2 '= "防衛倍率を+0.10"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_PASSIVE:3 '= "防衛時、敵疲労度を追加で増加させる"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_PASSIVE:4 '= "攻撃時、疲労度増加を緩和させる"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_PASSIVE:5 '= "建造物『本拠地』が解放される"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_PASSIVE:6 '= "都市の火力支援の威力がさらに+20\%"
TECHNOLOGY_DESC:TECHNOLOGY_BATTLE_PASSIVE:7 '= "防衛倍率がさらに+0.10"

TECHNOLOGY_NAME:TECHNOLOGY_DIPLOMACY:1 '= "優れた交渉術"
TECHNOLOGY_NAME:TECHNOLOGY_DIPLOMACY:2 '= "柔らかな物腰"
TECHNOLOGY_NAME:TECHNOLOGY_DIPLOMACY:3 '= "外交官養成"
TECHNOLOGY_NAME:TECHNOLOGY_DIPLOMACY:4 '= "柔和な外見"
TECHNOLOGY_NAME:TECHNOLOGY_DIPLOMACY:5 '= "忍びの里"
TECHNOLOGY_NAME:TECHNOLOGY_DIPLOMACY:6 '= "停戦の輪"
TECHNOLOGY_NAME:TECHNOLOGY_DIPLOMACY:7 '= "同盟の輪"

TECHNOLOGY_DESC:TECHNOLOGY_DIPLOMACY:1 '= "停戦・同盟交渉時が有利になる"
TECHNOLOGY_DESC:TECHNOLOGY_DIPLOMACY:2 '= "外交警戒値の減少量に+1"
TECHNOLOGY_DESC:TECHNOLOGY_DIPLOMACY:3 '= "停戦・同盟交渉時がさらに有利になる"
TECHNOLOGY_DESC:TECHNOLOGY_DIPLOMACY:4 '= "外交警戒値の減少量にさらに+1"
;AI勢力時: 他のAIから好意的な対応をされやすくなる
TECHNOLOGY_DESC:TECHNOLOGY_DIPLOMACY:5 '= "建造物『忍びの里』が解放される"
TECHNOLOGY_DESC:TECHNOLOGY_DIPLOMACY:6 '= "停戦できる国家数が+1"
TECHNOLOGY_DESC:TECHNOLOGY_DIPLOMACY:7 '= "同盟できる国家数が+1"

TECHNOLOGY_NAME:TECHNOLOGY_CONSCRIPTION:1 '= "デカ兵舎"
TECHNOLOGY_NAME:TECHNOLOGY_CONSCRIPTION:2 '= "街頭演説"
TECHNOLOGY_NAME:TECHNOLOGY_CONSCRIPTION:3 '= "ドデカ兵舎"
TECHNOLOGY_NAME:TECHNOLOGY_CONSCRIPTION:4 '= "天狗のビラ配り"
TECHNOLOGY_NAME:TECHNOLOGY_CONSCRIPTION:5 '= "集積所"
TECHNOLOGY_NAME:TECHNOLOGY_CONSCRIPTION:6 '= "クソデカ兵舎"
TECHNOLOGY_NAME:TECHNOLOGY_CONSCRIPTION:7 '= "河童のラジオ"

TECHNOLOGY_DESC:TECHNOLOGY_CONSCRIPTION:1 '= "総兵数の上限が+10\%"
TECHNOLOGY_DESC:TECHNOLOGY_CONSCRIPTION:2 '= "各期末の徴兵効率が+10\%"
TECHNOLOGY_DESC:TECHNOLOGY_CONSCRIPTION:3 '= "総兵数の上限がさらに+15\%"
TECHNOLOGY_DESC:TECHNOLOGY_CONSCRIPTION:4 '= "各期末の徴兵効率がさらに+15\%"
TECHNOLOGY_DESC:TECHNOLOGY_CONSCRIPTION:5 '= "建造物『物資集積所』が解放される"
TECHNOLOGY_DESC:TECHNOLOGY_CONSCRIPTION:6 '= "総兵数の上限がさらに+20\%"
TECHNOLOGY_DESC:TECHNOLOGY_CONSCRIPTION:7 '= "各期末の徴兵効率がさらに+20\%"

TECHNOLOGY_NAME:TECHNOLOGY_TAX:1 '= "投資信託"
TECHNOLOGY_NAME:TECHNOLOGY_TAX:2 '= "灰吹法"
TECHNOLOGY_NAME:TECHNOLOGY_TAX:3 '= "株取引"
TECHNOLOGY_NAME:TECHNOLOGY_TAX:4 '= "二期作"
TECHNOLOGY_NAME:TECHNOLOGY_TAX:5 '= "税務署"
TECHNOLOGY_NAME:TECHNOLOGY_TAX:6 '= "不動産経営"
TECHNOLOGY_NAME:TECHNOLOGY_TAX:7 '= "品種改良"

TECHNOLOGY_DESC:TECHNOLOGY_TAX:1 '= "国庫収入に+500"
TECHNOLOGY_DESC:TECHNOLOGY_TAX:2 '= "徴税効率が+5\%"
TECHNOLOGY_DESC:TECHNOLOGY_TAX:3 '= "国庫収入にさらに+1000"
TECHNOLOGY_DESC:TECHNOLOGY_TAX:4 '= "徴税効率がさらに+10\%"
TECHNOLOGY_DESC:TECHNOLOGY_TAX:5 '= "建造物『税務署』が解放される"
TECHNOLOGY_DESC:TECHNOLOGY_TAX:6 '= "国庫収入にさらに+1500"
TECHNOLOGY_DESC:TECHNOLOGY_TAX:7 '= "徴税効率がさらに+10\%"

TECHNOLOGY_NAME:TECHNOLOGY_EMPLOYMENT:1 '= "地元スカウト"
TECHNOLOGY_NAME:TECHNOLOGY_EMPLOYMENT:2 '= "有名スカウト"
TECHNOLOGY_NAME:TECHNOLOGY_EMPLOYMENT:3 '= "プロスカウト"
TECHNOLOGY_NAME:TECHNOLOGY_EMPLOYMENT:4 '= "スカウトの神"
TECHNOLOGY_NAME:TECHNOLOGY_EMPLOYMENT:5 '= "大宴会場"
TECHNOLOGY_NAME:TECHNOLOGY_EMPLOYMENT:6 '= ""
TECHNOLOGY_NAME:TECHNOLOGY_EMPLOYMENT:7 '= ""

TECHNOLOGY_DESC:TECHNOLOGY_EMPLOYMENT:1 '= "スカウト費用が-5\%"
TECHNOLOGY_DESC:TECHNOLOGY_EMPLOYMENT:2 '= "スカウト費用がさらに-10\%"
TECHNOLOGY_DESC:TECHNOLOGY_EMPLOYMENT:3 '= "スカウト費用がさらに-15\%"
TECHNOLOGY_DESC:TECHNOLOGY_EMPLOYMENT:4 '= "スカウト費用がさらに-20\%"
TECHNOLOGY_DESC:TECHNOLOGY_EMPLOYMENT:5 '= "建造物『大宴会場』が解放される"
TECHNOLOGY_DESC:TECHNOLOGY_EMPLOYMENT:6 '= ""
TECHNOLOGY_DESC:TECHNOLOGY_EMPLOYMENT:7 '= ""
