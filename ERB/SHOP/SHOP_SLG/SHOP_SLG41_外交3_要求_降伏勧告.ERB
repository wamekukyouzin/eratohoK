;-------------------------------------------------
;降伏勧告を行う
;-------------------------------------------------
@DIPLOMACY_SURRENDER(ARG:0)
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
;勢力同士の隣接関係マップを作成
CALL TMP_CREATE_COUNTRY_NEIBORING_MAP
CALL TMP_CREATE_SUM_ECONOMY_MAP
CALL TMP_CREATE_SUM_SOLDIER_MAP
CALL TMP_CREATE_POLITICS_POWER_MAP

IF !TMP_COUNTRY_IS_NEIBORING:(ARG:0):(CFLAG:MASTER:所属)
	PRINTW 隣接していない勢力に降伏勧告を行うことはできません
	RETURN 0
ENDIF

IF IS_SP_COUNTRY(ARG:0)
	PRINTFORMW 勧告を突っぱねてきました
	RETURN 0
ENDIF

PRINTFORMW %ANAME(MASTER)%は%ANAME(LOCAL:5)%に対して降伏するように諭した
PRINTFORMW ………………
PRINTFORMW ……


;要求受け入れ判定
IF DIPLOMACY_REQUEST_CHECK(CFLAG:MASTER:所属, ARG:0, 350)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(LOCAL:5)%は我が国の勧告を受け入れ降伏しました
	RESETCOLOR

	;兵数の移動
	CALL CLEAR_ALL_UNIT(ARG:0)
	COUNTRY_SOLDIER:(CFLAG:MASTER:所属) += COUNTRY_SOLDIER:(ARG:0)
	COUNTRY_SOLDIER:(ARG:0) = 0

	;領地の接収
	PRINTL 
	SETCOLOR カラー_注意
	PRINTFORML %ANAME(LOCAL:5)%の所領だった都市は我が国の領土となりました
	RESETCOLOR
	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == ARG:0
			CITY_OWNER:(LOCAL:0) = CFLAG:MASTER:所属
			PRINTFORML %GET_CITYNAME(LOCAL:0)%を我が国の支配下に置きました
		ENDIF
	NEXT
	WAIT

	PRINTL 
	PRINTFORMW %ANAME(LOCAL:5)%軍の兵士が我が国の兵力として加わりました

	;自動で登用
	IF CONFIG:303 == 0
		;士官の組み込み
		PRINTL 
		SETCOLOR カラー_注意
		PRINTFORML %ANAME(LOCAL:5)%に仕えていた士官は我が国の士官として組み込まれました
		RESETCOLOR
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):所属 == ARG:0
				IF !CFLAG:(LOCAL:0):捕虜先 || CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
					CALL CHANGE_COUNTRY(LOCAL:0, CFLAG:MASTER:所属, 1)
					PRINTFORML %ANAME(LOCAL:0)%を登用しました
				ELSE
					CALL CHANGE_COUNTRY(LOCAL:0, CFLAG:MASTER:所属)
					SETCOLOR カラー_シアン
					PRINTFORML %ANAME(LOCAL:0)%は%ANAME(GET_COUNTRY_BOSS(CFLAG:(LOCAL:0):捕虜先))%に囚われています
					RESETCOLOR
				ENDIF
			ENDIF
		NEXT
		WAIT

	;自動で投獄
	ELSEIF CONFIG:303 == 1
		;士官の組み込み
		PRINTL 
		SETCOLOR カラー_注意
		PRINTFORML %ANAME(LOCAL:5)%に仕えていた士官を投獄しました
		RESETCOLOR
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):所属 == ARG:0
				IF !CFLAG:(LOCAL:0):捕虜先 || CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
					CALL CHANGE_COUNTRY(LOCAL:0, 0, 1)
					CALL CAPTURE(LOCAL:0, CFLAG:MASTER:所属)
					PRINTFORML %ANAME(LOCAL:0)%を投獄しました
				ELSE
					CALL CHANGE_COUNTRY(LOCAL:0, CFLAG:MASTER:所属)
					SETCOLOR カラー_シアン
					PRINTFORML %ANAME(LOCAL:0)%は%ANAME(GET_COUNTRY_BOSS(CFLAG:(LOCAL:0):捕虜先))%に囚われています
					RESETCOLOR
				ENDIF
			ENDIF
		NEXT
		WAIT

	;手動で選択
	ELSE
		CALL DIPLOMACY_SURRENDER_SET_TREATMENT(ARG:0)
	ENDIF

	;ARG:0勢力の捕虜となっていたキャラの処理
	;ARG:0勢力が捕虜持ってるか
	IF CMATCH(CFLAG:捕虜先, ARG:0)
		PRINTL
		PRINTFORMW %ANAME(LOCAL:5)%に囚われていた捕虜を見つけました
		LOCAL:2 = 0
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):捕虜先 == ARG:0
				;自陣営の士官なら、そのまま元に戻る
				IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属
					CALL CAPTURE(LOCAL:0, 0)
					SETCOLOR カラー_注意
					PRINTFORML %ANAME(LOCAL:5)%勢力に囚えられていた%ANAME(LOCAL:0)%は、我が国に復帰した
					RESETCOLOR
					LOCAL:2 = 1
				ENDIF
			ENDIF
		NEXT
		IF LOCAL:2
			WAIT
		ENDIF
		FOR LOCAL:0, 0, CHARANUM
			IF CFLAG:(LOCAL:0):捕虜先 == ARG:0
				;第三勢力の士官なら、TREAT_PRISONERで扱いを決定
				CALL TREAT_PRISONER(LOCAL:0, ARG:0, CFLAG:MASTER:所属)
			ENDIF
		NEXT
	ENDIF

	;旧勢力の後始末
	CALL DESTROY_COUNTRY(ARG:0)

	;外交補正の加算処理
	DIPLOMACY_HATE:(CFLAG:MASTER:所属) += 5
ELSE
	PRINTFORMW 残念ながら、%ANAME(LOCAL:5)%は我が国の降伏勧告に応じませんでした…
	;外交補正の加算処理（他国の警戒が上がったと考えていただければ）
	DIPLOMACY_HATE:(CFLAG:MASTER:所属) += 1
ENDIF

RETURN 1

;-------------------------------------------------
;降伏した勢力の士官の処遇を個別に設定 ARG:0=降伏した勢力
;-------------------------------------------------
@DIPLOMACY_SURRENDER_SET_TREATMENT(ARG:0)
;キャラの扱い設定用変数
#DIM COMMANDER_NUM

;●士官の人数+捕虜の人数をカウント
COMMANDER_NUM = GET_COMMANDER_NUM(ARG:0)

;士官が一人もいなければ戻る
IF COMMANDER_NUM == 0
	;DEBUGPRINTFORML {ARG:0}番号の%ANAME(GET_COUNTRY_BOSS(ARG:0))%勢力に士官が誰もいませんでした
	RETURN
ENDIF

CALL SINGLE_DRAWLINE
PRINTFORML %ANAME(GET_COUNTRY_BOSS(ARG:0))%に仕えていた士官のうち、登用するものを選択してください
CALL SINGLE_DRAWLINE

CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(-1, "SURRENDER_SET_TREATMENT", "SURRENDER_SET_TREATMENT")

FOR LOCAL, 0, SELECTED_CHARA_NUM
	LOCAL:1 = SELECTED_CHARA:(LOCAL)
	CALL CHANGE_COUNTRY(LOCAL:1, CFLAG:MASTER:所属)
	PRINTFORML %ANAME(LOCAL:1)%を登用しました
NEXT

CALL SINGLE_DRAWLINE
PRINTFORML %ANAME(GET_COUNTRY_BOSS(ARG:0))%に仕えていた士官のうち、投獄するものを選択してください
CALL SINGLE_DRAWLINE

CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(-1, "SURRENDER_SET_TREATMENT", "SURRENDER_SET_TREATMENT")

FOR LOCAL, 0, SELECTED_CHARA_NUM
	LOCAL:1 = SELECTED_CHARA:(LOCAL)
	CALL CAPTURE(LOCAL:1, CFLAG:MASTER:所属)
	PRINTFORML %ANAME(LOCAL:1)%を投獄しました
NEXT

@SELECT_CHARA_LIST_SHOW_LOGIC_SURRENDER_SET_TREATMENT(対象)
#DIM 対象
RETURN CFLAG:対象:所属 == DIPLOMACY_DIPLOMAT && !IS_SP_CHARA(対象)

@SELECT_CHARA_LIST_SELECT_LOGIC_SURRENDER_SET_TREATMENT(対象)
#DIM 対象
RETURN CFLAG:対象:捕虜先 == 0 || CFLAG:対象:捕虜先 == CFLAG:MASTER:所属
