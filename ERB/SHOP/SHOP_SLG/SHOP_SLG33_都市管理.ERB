;-------------------------------------------------
;「都市管理」の名称
;-------------------------------------------------
@SHOP_SLG_NAME33
RESULTS:0 '= "都市管理"

;-------------------------------------------------
;「都市管理」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK33
SIF FLAG:クリアフラグ
	RETURN 0
SIF FLAG:観戦モード
	RETURN 0
SIF CFLAG:MASTER:所属 == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;「都市管理」の右カラムオン判定
;-------------------------------------------------
;@SHOP_SLG_EVENTBUY_COLUMN_RIGHT_ON_CHECK33

;-------------------------------------------------
;「都市管理」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY33
CALL SHOW_CITY_ARMY
RETURN 1

;-------------------------------------------------
;「都市管理」の表示処理本体
;-------------------------------------------------
@SHOW_CITY_ARMY
#DIM LINE
#DIM WIDTH_CITYNAME = 30
#DIM CITY_TABLE, MAX_CITY
#DIM PTR
#DIM CONST CITY_PER_PAGE = 30
#DIM MAX_PAGE
#DIM NOW_PAGE
#DIM NOW_SORT
#DIM PREV_SORT
#DIM DESC
#DIM SORT_VALUE, MAX_CITY
#DIM FIRST_LINE

VARSET CITY_TABLE, -1
NOW_PAGE = 1
PTR = 0
DESC = 0
NOW_SORT = 0

FOR LOCAL, 0, CITY_NUM+1
	IF CITY_OWNER:(LOCAL) == CFLAG:MASTER:所属
		CITY_TABLE:PTR = LOCAL
		PTR++
	ENDIF
NEXT

MAX_PAGE = (PTR/CITY_PER_PAGE)

;剰余算に変更
SIF PTR%CITY_PER_PAGE != 0
	MAX_PAGE ++

FIRST_LINE = LINECOUNT
$SHOW_LOOP

PRINTFORM %TOSTR_SPACE( (WIDTH_CITYNAME-6)/2 )%
PRINTBUTTON "都市名", 0
PRINTFORM %TOSTR_SPACE( (WIDTH_CITYNAME-6+1)/2 )%┃  
PRINTBUTTON "経済", 1
PRINTFORM   ┃
PRINTBUTTON "成長余地", 2
PRINTFORM ┃
PRINTBUTTON "防衛兵数", 3
PRINTFORM ┃  
PRINTBUTTON "守将", 4
PRINTFORM   ┃
PRINTBUTTON "前期兵損", 5
PRINTFORM ┃ 
PRINTBUTTON "危険度", 6
PRINTFORM  ┃
PRINTL
PRINTL ━━━━━━━━━━━━━━━╋━━━━╋━━━━╋━━━━╋━━━━╋━━━━╋━━━━┫


FOR LINE, 0, CITY_PER_PAGE
	LOCAL = CITY_TABLE:(LINE + CITY_PER_PAGE * (NOW_PAGE - 1))
	IF LOCAL >= 0
		LOCALS =  %CITY_NAME:LOCAL%%TOSTR_SPACE(WIDTH_CITYNAME-STRLENS(CITY_NAME:LOCAL))%
		PRINTBUTTON LOCALS, 1000+LOCAL
		PRINT ┃
		PRINTPLAINFORM  {(CITY_ECONOMY:LOCAL)/100, 7, RIGHT}┃
		PRINTPLAINFORM  {(CITY_ECONOMY_LIMIT:LOCAL-CITY_ECONOMY:LOCAL)/100, 7, RIGHT}┃
		LOCALS = {CITY_SOLDIER:LOCAL, 8, RIGHT}
		IF IS_STAY_ENEMY_UNIT(LOCAL) == 1
			SETCOLOR カラー_警告
			PRINTFORM %LOCALS%
			RESETCOLOR
		ELSE
			PRINTBUTTON LOCALS, 200+LOCAL
		ENDIF
		PRINTPLAINFORM ┃
		LOCALS = \@ GET_CITY_COMMANDER(LOCAL, 0) != -1 ? %"有", 8, RIGHT% # %"無", 8, RIGHT% \@
		IF IS_STAY_ENEMY_UNIT(LOCAL) == 1
			SETCOLOR カラー_警告
			PRINTFORM %LOCALS%
			RESETCOLOR
		ELSE
			PRINTBUTTON LOCALS, 400 + LOCAL
		ENDIF
		PRINTPLAINFORM ┃
		PRINTPLAINFORM  {CITY_SOLDIER_PREV:LOCAL, 7, RIGHT}┃
		SELECTCASE GET_CITY_RISK(LOCAL)
			CASE 0
				LOCALS '= "  安全  "
			CASE 1
				LOCALS '= "  注意  "
			CASE 2
				LOCALS '= "  注意  "
			CASE 3
				LOCALS '= "  注意  "
			CASE 4
				LOCALS '= "  前線  "
				SETCOLOR カラー_オレンジ
			CASE 5
				LOCALS '= " 敵発見 "
				SETCOLOR カラー_警告
			CASE 6
				LOCALS '= " 交戦中 "
				SETCOLOR カラー_警告
		ENDSELECT
		
		PRINTPLAINFORM %LOCALS%
		RESETCOLOR
		PRINT ┃
	ELSE
	ENDIF
	PRINTL
NEXT

PRINTL
PRINTFORM                         

IF NOW_PAGE != 1
	PRINTBUTTON " <<< ", 100
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM  <<< 
	RESETCOLOR
ENDIF

PRINT   
IF MAX_PAGE > 1
	PRINTBUTTON " << ", 101
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM  << 
	RESETCOLOR
ENDIF

PRINT    
PRINTFORM {NOW_PAGE, 2, RIGHT} / {MAX_PAGE, 2, LEFT}

PRINT    
IF MAX_PAGE > 1
	PRINTBUTTON " >> ", 102
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM  >> 
	RESETCOLOR
ENDIF

PRINT   
IF NOW_PAGE != MAX_PAGE
	PRINTBUTTON " >>> ", 103
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM  >>> 
	RESETCOLOR
ENDIF
PRINTL 
CALL SINGLE_DRAWLINE
CALL SHOW_CITY_INFO(SHOWN_CITY, 1)
CALL SINGLE_DRAWLINE
PRINTBUTTON "[99] 戻る", 99
PRINTL

INPUT

SELECTCASE RESULT
	CASE 100
		NOW_PAGE = 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	CASE 101
		NOW_PAGE --
		SIF NOW_PAGE <= 0
			NOW_PAGE = MAX_PAGE
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	CASE 102
		NOW_PAGE ++
		SIF NOW_PAGE > MAX_PAGE
			NOW_PAGE = 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	CASE 103
		NOW_PAGE = MAX_PAGE
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	CASE 99
		RETURN 0
ENDSELECT

LOCAL = RESULT

;都市名クリックしたときSHOP_SLG側で無駄にCLEARLINEされることへの対策
LINES_SHOP = LINECOUNT
CALL USERSHOP_SLG(LOCAL)
IF RESULT
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF

PREV_SORT = NOW_SORT
NOW_SORT = LOCAL

IF NOW_SORT == PREV_SORT
	DESC = !DESC
ELSE
	DESC = 0
ENDIF

SELECTCASE NOW_SORT
	CASE 0
		FOR LOCAL, 0, PTR
			SORT_VALUE:LOCAL = CITY_TABLE:LOCAL
		NEXT
	CASE 1
		FOR LOCAL, 0, PTR
			SORT_VALUE:LOCAL = CITY_ECONOMY:(CITY_TABLE:LOCAL)
		NEXT
	CASE 2
		FOR LOCAL, 0, PTR
			SORT_VALUE:LOCAL = CITY_ECONOMY_LIMIT:(CITY_TABLE:LOCAL)-CITY_ECONOMY:(CITY_TABLE:LOCAL)
		NEXT
	CASE 3
		FOR LOCAL, 0, PTR
			SORT_VALUE:LOCAL = CITY_SOLDIER:(CITY_TABLE:LOCAL)
		NEXT
	CASE 4
		FOR LOCAL, 0, PTR
			SORT_VALUE:LOCAL = GET_CITY_COMMANDER_NUM(CITY_TABLE:LOCAL)
		NEXT
	CASE 5
		FOR LOCAL, 0, PTR
			SORT_VALUE:LOCAL = CITY_SOLDIER_PREV:(CITY_TABLE:LOCAL)
		NEXT
	CASE 6
		FOR LOCAL, 0, PTR
			SORT_VALUE:LOCAL = GET_CITY_RISK(CITY_TABLE:LOCAL)
		NEXT
ENDSELECT

FOR LOCAL,0, PTR
	SIF CITY_TABLE:LOCAL == -1
		BREAK
	FOR LOCAL:11,LOCAL,0,-1
		LOCAL:12 = LOCAL:11-1
		LOCAL:13 = LOCAL:11
		IF (DESC && SORT_VALUE:(LOCAL:12) < SORT_VALUE:(LOCAL:13)) || (!DESC && SORT_VALUE:(LOCAL:12) > SORT_VALUE:(LOCAL:13))
			SWAP CITY_TABLE:(LOCAL:12),CITY_TABLE:(LOCAL:13)
			SWAP SORT_VALUE:(LOCAL:12),SORT_VALUE:(LOCAL:13)
		ELSE
			BREAK
		ENDIF
	NEXT
NEXT

CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP


;-------------------------------------------------
;「都市管理」防衛兵数の設定
;-------------------------------------------------
@SET_CITY_SOLDIER(ARG)
#DIM FIRST_LINE
#DIM PLUS = 100, 500, 1000, 5000, 10000, 50000, 100000
#DIM ERRMSG = 0

ERRMSG = 0

SIF !INRANGE(ARG, 1, CITY_NUM) || CITY_OWNER:ARG != CFLAG:MASTER:所属
	RETURN 0
SIF IS_STAY_ENEMY_UNIT(ARG)
	RETURN 0


REDRAW 0
;CLEARLINE LINES_SHOP_SLG-27-3-1

LOCAL:11 = CITY_SOLDIER:ARG
FIRST_LINE = LINECOUNT 
$INPUT_LOOP
LOCAL:21 = COUNTRY_SOLDIER:(CFLAG:MASTER:所属) - (LOCAL:11 - CITY_SOLDIER:ARG)
CALL SINGLE_DRAWLINE
PRINTFORML %CITY_NAME:ARG%の防衛兵数を決定してください
PRINTFORML 直接入力も可能です
PRINTFORML 防衛兵数   {CITY_SOLDIER:ARG,8,RIGHT} → {LOCAL:11,8,RIGHT}
PRINTFORML 残存兵数   {COUNTRY_SOLDIER:(CFLAG:MASTER:所属),8,RIGHT} → {LOCAL:21,8,RIGHT}
SETCOLOR カラー_パ青
FOR LOCAL , 0, 7
	LOCALS = [+{PLUS:LOCAL}]
	PRINTBUTTON LOCALS, -1000-LOCAL
NEXT
PRINTL 
SETCOLOR カラー_パ赤
FOR LOCAL , 0, 7
	LOCALS = [-{PLUS:LOCAL}]
	PRINTBUTTON LOCALS, -10000-LOCAL
NEXT
RESETCOLOR
PRINTL 
PRINTL 
PRINTBUTTON "[確定]", -1
PRINTBUTTON "[キャンセル]", -2
PRINTL
CALL SINGLE_DRAWLINE
PRINTL 

SELECTCASE ERRMSG
	CASE 1
		PRINTFORM 最低でも500の兵数が必要です
	CASE 2
		PRINT 兵数が足りません
ENDSELECT
PRINTL 
REDRAW 1
INPUT

ERRMSG = 0
IF RESULT >= 0
	IF RESULT >= 500
		IF COUNTRY_SOLDIER:(CFLAG:MASTER:所属) + CITY_SOLDIER:ARG < RESULT
			ERRMSG = 2
		ELSE
			GOTO OUT_OF_INPUT_LOOP
		ENDIF
	ELSE
		ERRMSG = 1
	ENDIF
ELSE
	LOCAL:0 = -1
	
	SIF RESULT == -1
		GOTO OUT_OF_INPUT_LOOP
		
	SIF RESULT == -2
		RETURN
	
	IF ABS(RESULT) >= 1000 && ABS(RESULT) <= 1001 + CITY_NUM
		LOCAL:0 = ABS(RESULT)-1000
		LOCAL:1 = 1
	ELSEIF ABS(RESULT) >= 10000 && ABS(RESULT) < 10100
		LOCAL:0 = ABS(RESULT)-10000
		LOCAL:1 = -1
	ENDIF
	
	IF (LOCAL:0 >= 0 && LOCAL:0 < 7)
		LOCAL:11 = LOCAL:11 + (PLUS:(LOCAL:0))*LOCAL:1
		LOCAL:11 = MIN(LOCAL:11, COUNTRY_SOLDIER:(CFLAG:MASTER:所属) + CITY_SOLDIER:ARG)
		LOCAL:11 = MAX(LOCAL:11, 500)
	ENDIF
ENDIF

REDRAW 0

CLEARLINE LINECOUNT - FIRST_LINE
GOTO INPUT_LOOP

REDRAW 1

$OUT_OF_INPUT_LOOP
LOCAL:21 = COUNTRY_SOLDIER:(CFLAG:MASTER:所属) - (LOCAL:11 - CITY_SOLDIER:ARG)
CITY_SOLDIER:ARG = LOCAL:11
COUNTRY_SOLDIER:(CFLAG:MASTER:所属) = LOCAL:21

;-------------------------------------------------
;どこからも呼び出されていない
;-------------------------------------------------
;@MEMORY_PREVIOUS_ARMY
;
;FOR LOCAL:0, 0, MAX_COUNTRY
;	FOR LOCAL:1, 0, MAX_UNIT
;		UNIT_SOLDIER_PREV:(LOCAL:0):(LOCAL:1) = UNIT_SOLDIER_PREV:(LOCAL:0):(LOCAL:1)
;	NEXT
;NEXT
;
;FOR LOCAL, 0, MAX_CITY
;	CITY_SOLDIER_PREV = CITY_SOLDIER
;NEXT
