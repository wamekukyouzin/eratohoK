;-------------------------------------------------
;「徴兵解雇」の名称
;-------------------------------------------------
@SHOP_SLG_NAME62
RESULTS:0 '= "徴兵/解雇"

;-------------------------------------------------
;「徴兵解雇」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK62
SIF FLAG:クリアフラグ
	RETURN 0
SIF FLAG:観戦モード
	RETURN 0
SIF CFLAG:MASTER:所属 == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;「徴兵解雇」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY62
CALL SLG_EXTRA_DRAFT
RETURN 1

;-------------------------------------------------
;「徴兵する」の次行説明指定有無
;-------------------------------------------------
@SHOP_SLG_NEXT_LINE_ADD62

;-------------------------------------------------
;「徴兵する」の次行説明指定内容
;-------------------------------------------------
@SHOP_SLG_NEXT_LINE_ADD_TEXT62
PRINTFORM %"レート:" + NUM_FORMAT(SLG_EXTRA_DRAFT_CALC_RATE(CFLAG:MASTER:所属)), 12%

;------------------------------------------------
;「徴兵解雇」本体
;-------------------------------------------------
@SLG_EXTRA_DRAFT()
#DIM 雇用レート
#DIM 解雇レート
#DIM 雇用兵数限界
#DIM 入力値
#DIM 兵数
雇用レート = SLG_EXTRA_DRAFT_CALC_RATE(CFLAG:MASTER:所属)
解雇レート = MAX(雇用レート / 2, 1)
兵数 = COUNTRY_SOLDIER:(CFLAG:MASTER:所属)
$SHOW_LOOP
CALL SINGLE_DRAWLINE
PRINTFORML 国庫を消費して臨時に徴兵することができます
PRINTFORML 負数を入力すると解雇して資金を得ることもできます
PRINTFORML 何人雇う/解雇しますか？(0を入力すると戻る)

PRINTFORM 現在の国庫:
SETCOLOR カラー_選択中
PRINTFORM {MONEY:(CFLAG:MASTER:所属)}
RESETCOLOR
PRINT    
PRINTFORM 雇用レート:
SETCOLOR カラー_選択中
PRINTFORM {雇用レート}
RESETCOLOR
PRINTFORM /兵
PRINT    
PRINTFORM 解雇レート:
SETCOLOR カラー_選択中
PRINTFORM {解雇レート}
RESETCOLOR
PRINTFORM /兵
PRINT    
PRINTFORM 現在の兵数（防衛・部隊所属除く）:
SETCOLOR カラー_選択中
PRINTFORM {兵数}
RESETCOLOR
PRINTL 
CALL SINGLE_DRAWLINE
IF MONEY:(CFLAG:MASTER:所属) - 100 * 雇用レート > 0
	PRINTFORM [{100, 8}] {100, 8}徴兵
	PRINTFORML   兵数:{兵数, 10} + {100, 10} = {兵数 + 100, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {100 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 100 * 雇用レート, 10}
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM [{100, 8}] {100, 8}徴兵
	PRINTFORML   兵数:{兵数, 10} + {100, 10} = {兵数 + 100, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {100 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 100 * 雇用レート, 10}
	RESETCOLOR
ENDIF

IF MONEY:(CFLAG:MASTER:所属) - 1000 * 雇用レート > 0
	PRINTFORM [{1000, 8}] {1000, 8}徴兵
	PRINTFORML   兵数:{兵数, 10} + {1000, 10} = {兵数 + 1000, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {1000 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 1000 * 雇用レート, 10}
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM [{1000, 8}] {1000, 8}徴兵
	PRINTFORML   兵数:{兵数, 10} + {1000, 10} = {兵数 + 1000, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {1000 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 1000 * 雇用レート, 10}
	RESETCOLOR
ENDIF

IF MONEY:(CFLAG:MASTER:所属) - 10000 * 雇用レート > 0
	PRINTFORM [{10000, 8}] {10000, 8}徴兵
	PRINTFORML   兵数:{兵数, 10} + {10000, 10} = {兵数 + 10000, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {10000 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 10000 * 雇用レート, 10}
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM [{10000, 8}] {10000, 8}徴兵
	PRINTFORML   兵数:{兵数, 10} + {10000, 10} = {兵数 + 10000, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {10000 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 10000 * 雇用レート, 10}
	RESETCOLOR
ENDIF

雇用兵数限界 = MONEY:(CFLAG:MASTER:所属) / 雇用レート
LOCALS:0 = 限界まで
IF 雇用兵数限界 > 0
	PRINTFORM [{雇用兵数限界, 8}] %LOCALS:0, 8%徴兵
	PRINTFORML   兵数:{兵数, 10} + {雇用兵数限界, 10} = {兵数 + 雇用兵数限界, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {雇用兵数限界 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 雇用兵数限界 * 雇用レート, 10}
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM [{雇用兵数限界, 8}] %LOCALS:0, 8%徴兵
	PRINTFORML   兵数:{兵数, 10} + {雇用兵数限界, 10} = {兵数 + 雇用兵数限界, 10}  資金:{MONEY:(CFLAG:MASTER:所属), 10} - {雇用兵数限界 * 雇用レート, 10} = {MONEY:(CFLAG:MASTER:所属) - 雇用兵数限界 * 雇用レート, 10}
	RESETCOLOR
ENDIF
CALL SINGLE_DRAWLINE
PRINTFORML [{0, 3}] やっぱりやめる
CALL SINGLE_DRAWLINE
$INPUT_LOOP
INPUT 0
入力値 = RESULT
IF 入力値 == 0
	RETURN
ELSEIF 入力値 > 0 && MONEY:(CFLAG:MASTER:所属) >= 雇用レート * 入力値
	CALL ICPRINT(@"国庫から<{入力値 * 雇用レート}>を拠出し、{入力値}の兵を雇います。", "L", カラー_注意)
	CALL ICPRINT(@"雇用後の国庫資金は{MONEY:(CFLAG:MASTER:所属) - 入力値 * 雇用レート}です。よろしいですか？", "L", カラー_注意)
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML 兵を{入力値}人雇いました！
		PRINTFORML 兵数:{兵数} → {兵数 + 入力値}
		PRINTFORMW 資金:{MONEY:(CFLAG:MASTER:所属)} → {MONEY:(CFLAG:MASTER:所属) - 入力値 * 雇用レート}
		COUNTRY_SOLDIER:(CFLAG:MASTER:所属) += 入力値
		TMP_SUM_SOLDIER:(CFLAG:MASTER:所属) += 入力値
		MONEY:(CFLAG:MASTER:所属) -= 入力値 * 雇用レート
	ELSE
		GOTO SHOW_LOOP
	ENDIF
	RETURN
ELSEIF 入力値 < 0 && 入力値 * -1 <= 兵数
	入力値 *= -1
	PRINTFORM 兵
	CALL COLOR_PRINT(@"{入力値}", カラー_注意)
	PRINTFORM を解雇し、
	CALL COLOR_PRINT(@"{入力値 * 解雇レート}", カラー_注意)
	PRINTFORML の資金を得ます。
	PRINTFORM 解雇後の資金は
	CALL COLOR_PRINT(@"{MONEY:(CFLAG:MASTER:所属) + 入力値 * 解雇レート}", カラー_注意)
	PRINTFORML です。
	PRINTFORML よろしいですか？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML 兵を{入力値}人解雇しました！
		PRINTFORML 兵数:{兵数} → {兵数 - 入力値}
		PRINTFORMW 資金:{MONEY:(CFLAG:MASTER:所属)} → {MONEY:(CFLAG:MASTER:所属) + 入力値 * 解雇レート}
		COUNTRY_SOLDIER: (CFLAG:MASTER:所属) -= 入力値
		TMP_SUM_SOLDIER:(CFLAG:MASTER:所属) -= 入力値
		MONEY:(CFLAG:MASTER:所属) += 入力値 * 解雇レート
	ELSE
		GOTO SHOW_LOOP
	ENDIF
	RETURN
ELSEIF 入力値 > 0 && MONEY:(CFLAG:MASTER:所属) < 雇用レート * 入力値
	PRINTFORML 必要資金は{入力値 * 雇用レート}ですが、そんなに資金がありません
	GOTO SHOW_LOOP
ELSEIF 入力値 < 0 && 入力値 * -1 > 兵数
	入力値 *= -1
	PRINTFORML {入力値}解雇しようとしましたが、そんなに兵を持っていません
	GOTO SHOW_LOOP
ELSE
ENDIF

;-------------------------------------------------
;徴兵解雇の雇用レート計算
;-------------------------------------------------
@SLG_EXTRA_DRAFT_CALC_RATE(勢力)
#FUNCTION
#DIM 都市レート基準
#DIM 雇用レート
#DIM 勢力

;星評価で増減する
SELECTCASE TMP_COUNTRY_STARS:勢力
	CASE 5
		雇用レート = 3
	CASE 3, 4
		雇用レート = 2
	CASE 1, 2
		雇用レート = 1
ENDSELECT

;全都市数の1/10を獲得するごとに1増える
都市レート基準 = MAX(GET_CITY_NUM() / 10, 1)
雇用レート += MAX(TMP_OWN_CITY:勢力 / 都市レート基準, 0)

;徴兵限界を越えているなら+1
;徴兵限界をまたぐように徴兵するとちょっとお得になってしまうんだけど、
;考慮すると処理がめんどくさくなるんでパス
SIF TMP_SUM_SOLDIER:勢力 > GET_RECRUIT_LIMIT(勢力)
	雇用レート += 1


RETURNF 雇用レート
