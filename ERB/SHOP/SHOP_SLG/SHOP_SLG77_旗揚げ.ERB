;-------------------------------------------------
;「旗揚げする」の名称
;-------------------------------------------------
@SHOP_SLG_NAME77
RESULTS:0 '= "旗揚げする"

;-------------------------------------------------
;「旗揚げする」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK77
SIF CFLAG:MASTER:所属 != 0
	RETURN 0
SIF FLAG:クリアフラグ
	RETURN 0
SIF FLAG:観戦モード
	RETURN 0
;勢力に空きがなければ不可
SIF GET_NEW_COUNTRY() == -1
	RETURN 0
RETURN 1

;-------------------------------------------------
;「旗揚げする」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY77
CALL SHOP_CREATE_COUNTRY
RETURN 1

;-------------------------------------------------
;「旗揚げする」本体
;-------------------------------------------------
@SHOP_CREATE_COUNTRY
#DIM 都市
#DIM 都市所持勢力
#DIM 新勢力

CALL SINGLE_DRAWLINE
PRINTL 旗揚げする都市を選択して下さい
CALL SINGLE_DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL DRAWMAP
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE

PRINTBUTTON " 0[戻る]", 0
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

SIF RESULT == 0
	RETURN

IF !INRANGE(RESULT, 1000, 1000 + MAX_CITY - 1)
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

都市 = RESULT - 1000
都市所持勢力 = CITY_OWNER:都市

IF CITY_TYPE:都市 == 1 || GET_RELAYNAME(都市) == "無名"
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

IF IS_COUNTRY(都市所持勢力) && GET_OWN_CITY(都市所持勢力) == 1
	PRINTFORMW 残り都市数が1の勢力から都市を奪うことはできません
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF


PRINTFORML %GET_RELAYNAME(都市)%で旗揚げしますか？
CALL ASK_MULTI("旗揚げする", "キャンセル", "戻る")

IF RESULT == 1
	RETURN
ELSEIF RESULT == 2
	RESTART
ENDIF

新勢力 = GET_NEW_COUNTRY()

CITY_OWNER:都市 = 新勢力
COUNTRY_BOSS:新勢力 = GET_ID(MASTER)
COUNTRY_SOLDIER:新勢力 = CITY_ECONOMY:都市 / 20
CITY_SOLDIER:都市 = 2000

SIF IS_COUNTRY(都市所持勢力)
	CALL CHANGE_RELATION_C_TO_C(都市所持勢力, 新勢力, -500, 500)

CALL CHANGE_COUNTRY(MASTER, 新勢力)

CALL COUNTRY_SET_TECHNOLOGY(新勢力)

CALL SINGLE_DRAWLINE
PRINTL 新しい勢力の色を選んで下さい
CALL SINGLE_DRAWLINE
CALL CHANGE_COUNTRY_COLOR(新勢力, 1)

TIME = 0
RETURN
