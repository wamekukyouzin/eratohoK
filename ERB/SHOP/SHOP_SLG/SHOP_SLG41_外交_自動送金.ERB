;-------------------------------------------------
;自動送金設定画面
;-------------------------------------------------
@AUTO_SENDMONEY_SETTING
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTFORML 徴税し国庫が増えたタイミングで、自動で各勢力と会談し送金することができます
PRINTFORML 国庫が設定額に不足していた場合、あるだけ支払います
PRINTFORML 徴兵などでも必要になるので、余裕を持たせたほうがよいでしょう
CALL SINGLE_DRAWLINE

FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL) || LOCAL == CFLAG:MASTER:所属 || COUNTRY_IS_CLOSED:LOCAL
		CONTINUE
	PRINTFORML [{LOCAL, 2, LEFT}] %ANAME(GET_COUNTRY_BOSS(LOCAL)), MAX_CHARANAME_LENGTH, LEFT% 送金額:\@ AUTO_INVEST_PRICE:LOCAL > 0 ? {AUTO_INVEST_PRICE:LOCAL} # なし \@
NEXT
CALL SINGLE_DRAWLINE
PRINTFORML [98]一括設定
PRINTFORML [99]戻る

INPUT

IF RESULT == 99
	RETURN
ELSEIF RESULT == 98
	CALL AUTO_SENDMONEY_SETTING_SET_PRICE
	VARSET AUTO_INVEST_PRICE, RESULT
ELSEIF INRANGE(RESULT, 0, MAX_COUNTRY - 1) && IS_COUNTRY(RESULT) && RESULT != CFLAG:MASTER:所属 && !COUNTRY_IS_CLOSED:RESULT
	LOCAL = RESULT
	CALL AUTO_SENDMONEY_SETTING_SET_PRICE
	AUTO_INVEST_PRICE:LOCAL = RESULT
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;-------------------------------------------------
;自動送金価格設定
;-------------------------------------------------
@AUTO_SENDMONEY_SETTING_SET_PRICE
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
PRINTFORML いくらに設定しますか？(1～50000、0でキャンセル)
INPUT
IF RESULT < 0 || RESULT > 50000
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF
PRINTFORML {RESULT}に設定しました
RETURN RESULT

;-------------------------------------------------
;自動送金
;-------------------------------------------------
@AUTO_SENDMONEY
#DIM 送金額

SIF !IS_COUNTRY(CFLAG:MASTER:所属)
	RETURN

LOCAL:1 = 0

FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL) || LOCAL == CFLAG:MASTER:所属 || COUNTRY_IS_CLOSED:LOCAL || AUTO_INVEST_PRICE:LOCAL == 0
		CONTINUE
	SIF DONE_DIPLOMACY_FRIENDSHIP:LOCAL
		CONTINUE
	SIF MONEY:(CFLAG:MASTER:所属) <= 0
		BREAK
	IF LOCAL:1 == 0
		CALL SINGLE_DRAWLINE
		LOCAL:1 = 1
	ENDIF
	送金額 = MIN(AUTO_INVEST_PRICE:LOCAL, MONEY:(CFLAG:MASTER:所属))
	CALL SEND_MONEY(LOCAL, 送金額)
	DONE_DIPLOMACY_FRIENDSHIP:LOCAL = 1
	IF MONEY:(CFLAG:MASTER:所属) <= 0
		PRINTFORML (国庫が尽きたので、自動送金を終了します)
		BREAK
	ENDIF
NEXT

