;-------------------------------------------------
;人質交渉
;-------------------------------------------------
@DIPLOMACY_PRISONER_TRADE(ARG:0)
#DIM FIRST_LINE
VARSET LOCAL, 0
CVARSET CFLAG, 58, 0

LOCAL:12 = 1
LOCAL:22 = 1

LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;士官の部隊所属マップを作成
CALL TMP_CREATE_IS_FREE_MAP

;政治能力の差による補正点数の計算
LOCAL:32 = TMP_GET_POLITICS_POWER(CFLAG:MASTER:所属)
LOCAL:33 = TMP_GET_POLITICS_POWER(ARG:0)
LOCAL:34 = SQRT(ABS(LOCAL:32 - LOCAL:33) * 4)
IF LOCAL:32 >= LOCAL:33
	LOCAL:35 = 1000 + LOCAL:34
	LOCAL:36 = 1000000 / (1000 + LOCAL:34)
ELSE
	LOCAL:36 = 1000 + LOCAL:34
	LOCAL:35 = 1000000 / (1000 + LOCAL:34)
ENDIF

;両国の捕虜の数をそれぞれカウント
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):所属 == ARG:0 && CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
		LOCAL:10 ++
	ELSEIF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:0):捕虜先 == ARG:0
		LOCAL:20 ++
	ENDIF
NEXT

;各ページ数を計算
LOCAL:11 = MAX(LOCAL:10 - 1, 0) / 20 + 1
LOCAL:21 = MAX(LOCAL:20 - 1, 0) / 20 + 1

$SHOW_LOOP_INIT

CALL SINGLE_DRAWLINE
PRINTL ★★お互いの解放する捕虜を選択して下さい★★
CALL SINGLE_DRAWLINE
PRINTFORML 【囚えている捕虜】%TOSTR_SPACE(11)%価値   【囚えられている捕虜】%TOSTR_SPACE(7)%価値

FIRST_LINE = LINECOUNT
$SHOW_LOOP

;互いの捕虜をリストに表示
LOCAL:13 = 0
LOCAL:14 = (LOCAL:12 - 1) * 20
LOCAL:15 = LOCAL:12 * 20
LOCAL:16 = 0
LOCAL:23 = 0
LOCAL:24 = (LOCAL:22 - 1) * 20
LOCAL:25 = LOCAL:22 * 20
LOCAL:26 = 0
FOR LOCAL:1, 0, 20
	LOCAL:17 = 1
	FOR LOCAL:0, LOCAL:13, CHARANUM
		IF CFLAG:(LOCAL:0):所属 == ARG:0 && CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
			IF LOCAL:16 >= LOCAL:14 && LOCAL:16 < LOCAL:15
				PRINTPLAIN   
				IF CFLAG:(LOCAL:0):外交選択フラグ
					SETCOLOR カラー_選択中
				ENDIF
				PRINTBUTTON @"{NO:(LOCAL:0) + 99, 3}\{%ANAME(LOCAL:0), 18, LEFT%\}{GET_PRISONER_VALUE(LOCAL:0), 8, RIGHT}", NO:(LOCAL:0) + 99
				RESETCOLOR
				PRINT   ┃
				LOCAL:13 = LOCAL:0 + 1
				LOCAL:16 ++
				LOCAL:17 = 0
				BREAK
			ELSE
				LOCAL:16 ++
			ENDIF
		ENDIF
	NEXT
	IF LOCAL:17
		PRINTFORM %TOSTR_SPACE(35)%┃
	ENDIF
	LOCAL:27 = 1
	FOR LOCAL:0, LOCAL:23, CHARANUM
		IF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:0):捕虜先 == ARG:0
			IF LOCAL:26 >= LOCAL:24 && LOCAL:26 < LOCAL:25
				PRINTPLAIN  
				IF CFLAG:(LOCAL:0):外交選択フラグ
					SETCOLOR カラー_選択中
				ENDIF
				PRINTBUTTON @"{NO:(LOCAL:0) + 99, 3}\{%ANAME(LOCAL:0), 18, LEFT%\}{GET_PRISONER_VALUE(LOCAL:0), 8, RIGHT}", NO:(LOCAL:0) + 99
				RESETCOLOR
				LOCAL:23 = LOCAL:0 + 1
				LOCAL:26 ++
				LOCAL:27 = 0
				BREAK
			ELSE
				LOCAL:26 ++
			ENDIF
		ENDIF
	NEXT
	PRINTL 
NEXT

PRINTPLAIN    
IF LOCAL:12 <= 1
	SETCOLOR カラー_選択不可
	PRINTPLAIN 10[←前]
	RESETCOLOR
ELSE
	PRINTBUTTON "10[←前]", 10
ENDIF
PRINTPLAINFORM     {LOCAL:12, 2, RIGHT}/{LOCAL:11, 2, LEFT}    
IF LOCAL:12 >= LOCAL:11
	SETCOLOR カラー_選択不可
	PRINTPLAIN 11[次→]
	RESETCOLOR
ELSE
	PRINTBUTTON "11[次→]", 11
ENDIF
PRINTPLAIN    ┃  
IF LOCAL:22 <= 1
	SETCOLOR カラー_選択不可
	PRINTPLAIN 20[←前]
	RESETCOLOR
ELSE
	PRINTBUTTON "20[←前]", 20
ENDIF
PRINTPLAINFORM     {LOCAL:22, 2, RIGHT}/{LOCAL:21, 2, LEFT}    
IF LOCAL:22 >= LOCAL:21
	SETCOLOR カラー_選択不可
	PRINTPLAIN 21[次→]
	RESETCOLOR
ELSE
	PRINTBUTTON "21[次→]", 21
ENDIF
PRINTL 

;●交渉点数の計算
;選択した士官の点数を合計
LOCAL:30 = 0
LOCAL:31 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):外交選択フラグ
		IF CFLAG:(LOCAL:0):所属 == ARG:0 && CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
			LOCAL:30 += GET_PRISONER_VALUE(LOCAL:0)
		ELSEIF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:0):捕虜先 == ARG:0
			LOCAL:31 += GET_PRISONER_VALUE(LOCAL:0)
		ENDIF
	ENDIF
NEXT

LOCAL:37 = LOCAL:30 * LOCAL:35 / 1000
LOCAL:38 = LOCAL:31 * LOCAL:36 / 1000
LOCAL:39 = LOCAL:37 - LOCAL:38
LOCAL:45 = MAX(GET_DIGIT(LOCAL:30), GET_DIGIT(LOCAL:31), 4)

CALL SINGLE_DRAWLINE
PRINTFORM   解放点数: {ABS(LOCAL:30), LOCAL:45}(価値合計) ×
IF LOCAL:32 > LOCAL:33
	SETCOLOR カラー_シアン
ELSEIF LOCAL:32 < LOCAL:33
	SETCOLOR カラー_オレンジ
ENDIF
PRINTFORM %DECIMAL_STRING(LOCAL:35, 3)%
RESETCOLOR
PRINTFORML (政治能力差) = {LOCAL:37}

PRINTFORM   要求点数: {ABS(LOCAL:31), LOCAL:45}(価値合計) ×
IF LOCAL:32 > LOCAL:33
	SETCOLOR カラー_シアン
ELSEIF LOCAL:32 < LOCAL:33
	SETCOLOR カラー_オレンジ
ENDIF
PRINTFORM %DECIMAL_STRING(LOCAL:36, 3)%
RESETCOLOR
PRINTFORML (政治能力差) = {LOCAL:38}

PRINTFORM       合計:
IF LOCAL:39 == 0
	LOCALS:0 =  {ABS(LOCAL:39)}
ELSEIF LOCAL:39 > 0
	LOCALS:0 = +{ABS(LOCAL:39)}
	SETCOLOR カラー_シアン
ELSE
	LOCALS:0 = -{ABS(LOCAL:39)}
	SETCOLOR カラー_オレンジ
ENDIF
PRINTFORM %LOCALS:0, LOCAL:45 + 1%
RESETCOLOR
PRINTL 

LOCAL:40 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):外交選択フラグ
		IF CFLAG:(LOCAL:0):所属 == ARG:0 && CFLAG:(LOCAL:0):捕虜先 == CFLAG:MASTER:所属
			LOCAL:40 = 1
			BREAK
		ELSEIF CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:0):捕虜先 == ARG:0
			LOCAL:40 = 1
			BREAK
		ENDIF
	ENDIF
NEXT

IF LOCAL:40
	PRINTBUTTON "  0[差分を兵で補填して精算]", 0
	PRINT     
	PRINTBUTTON "  1[差分を経済力で補填して精算]", 1
	PRINT     
	IF LOCAL:39 >= 0
	PRINTBUTTON "  2[かまわず返還]", 2
	ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAIN   2[かまわず返還]
	RESETCOLOR
	ENDIF
	
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAIN   0[差分を兵で補填して精算]      1[差分を経済力で補填して精算]      2[かまわず返還]
	RESETCOLOR

ENDIF
PRINTL 
CALL SINGLE_DRAWLINE
PRINTBUTTON " 99[戻る]", 99
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 99
	REDRAW 1
	RETURN
ELSEIF RESULT == 0 && LOCAL:40
	;要求兵数は、両国の規模の平均値に比例
	LOCAL:50 = GET_SUM_ECONOMY(CFLAG:MASTER:所属) + GET_SUM_ECONOMY(ARG:0)
	LOCAL:52 = GET_SUM_SOLDIER(CFLAG:MASTER:所属)
	LOCAL:53 = GET_SUM_ARMY_DF(CFLAG:MASTER:所属)
	LOCAL:54 = MAX(GET_SUM_SOLDIER(ARG:0) - GET_OWN_CITY(ARG:0) * 500, 0)
	LOCAL:55 = MAX(GET_SUM_SOLDIER(CFLAG:MASTER:所属) - GET_OWN_CITY(CFLAG:MASTER:所属) * 500, 0)

	LOCAL:51 = ABS(LOCAL:39) * LOCAL:50 / 360000
	
	;ただし、自国が補填してもらう場合（解放点数が要求点数より大きい場合）、最大でも補填国の40%が限界
	;そして、自国が補填する場合（要求点数が解放点数以上の場合）、算出した要求兵数と合計点数のうち、大きい方を採用
	IF LOCAL:39 > 0
		LOCAL:51 = MIN(LOCAL:51, LOCAL:54 * 40 / 100)
	ELSE
		LOCAL:51 = MAX(LOCAL:51, ABS(LOCAL:39))
	ENDIF
	IF LOCAL:39 == 0
		PRINTFORML 選択した捕虜を解放します
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			WAIT
		ENDIF
	ELSEIF LOCAL:39 > 0
		PRINTFORML %ANAME(LOCAL:5)%に対して兵{LOCAL:51}を要求することができます
		PRINTFORML 現在の総兵数:{LOCAL:52}(うち未割当兵数:{COUNTRY_SOLDIER:(CFLAG:MASTER:所属)})
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			LOCAL:60 = 1
			IF LOCAL:54 < LOCAL:51
				PRINTFORML %ANAME(LOCAL:5)%は要求を満たすだけの兵を用意できないようだ…(準備可能:{LOCAL:54})
				CALL ASK_YN("あるだけよこせ！", "それなら交渉は決裂だ")
				IF RESULT == 0
					LOCAL:51 = LOCAL:54
				ELSE
					LOCAL:60 = 0
				ENDIF
			ENDIF
			IF LOCAL:60
				;解放処理＆解放メッセージの表示
				CALL PRISONER_TRADE_RELEASE(ARG:0)
				;相手勢力から自国に兵を移動
				CALL SHIFT_SOLDIER(ARG:0, CFLAG:MASTER:所属, LOCAL:51)
				PRINTFORMW %ANAME(LOCAL:5)%勢力から{LOCAL:51}の兵を受け取りました
			ENDIF
		ENDIF

	ELSE
		PRINTFORML %ANAME(LOCAL:5)%に兵{LOCAL:51}を渡す必要があります
		PRINTFORML 現在の総兵数:{LOCAL:52}(うち未割当兵数:{COUNTRY_SOLDIER:(CFLAG:MASTER:所属)})
		IF LOCAL:51 > LOCAL:52
			CALL ASK_MULTI_JUDGE("決定", 0, "キャンセル", 1)
		ELSE
			IF LOCAL:51 > COUNTRY_SOLDIER:(CFLAG:MASTER:所属)
				IF LOCAL:11 > COUNTRY_SOLDIER:(CFLAG:MASTER:所属) + LOCAL:13
					PRINTFORML ※要求兵数が現在の未割当兵数より多いため、全部隊を解散し、防衛に着いている兵も渡す必要があります
				ELSE
					PRINTFORML ※要求兵数が現在の未割当兵数より多いため、防衛に着いている兵も渡す必要があります
				ENDIF
			ENDIF
			CALL ASK_YN("決定", "キャンセル")
		ENDIF
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			;自国から相手勢力に兵を移動
			CALL SHIFT_SOLDIER(CFLAG:MASTER:所属, ARG:0, LOCAL:51)
			PRINTFORMW %ANAME(LOCAL:5)%勢力に{LOCAL:51}の兵を引き渡しました
		ENDIF
	ENDIF

	REDRAW 1
	RESTART

ELSEIF RESULT == 1 && LOCAL:40
	;要求経済力は、両国の規模の平均値に比例
	LOCAL:50 = GET_SUM_ECONOMY(CFLAG:MASTER:所属) / 100
	LOCAL:51 = GET_SUM_ECONOMY(ARG:0) / 100
	LOCAL:52 = ABS(LOCAL:39) * (LOCAL:50 + LOCAL:51) / 70000
	LOCAL:53 = MAX(LOCAL:50 - GET_OWN_CITY(CFLAG:MASTER:所属) * 50, 0)
	LOCAL:54 = MAX(LOCAL:51 - GET_OWN_CITY(ARG:0) * 50, 0)


	;ただし、自国が補填してもらう場合（解放点数が要求点数より大きい場合）、最大でも補填国の20%が限界
	;そして、自国が補填する場合（要求点数が解放点数以上の場合）、算出した要求経済力と合計点数の1/3のうち、大きい方を採用
	IF LOCAL:39 > 0
		LOCAL:52 = MIN(LOCAL:52, LOCAL:54 * 20 / 100)
	ELSE
		LOCAL:52 = MAX(LOCAL:52, ABS(LOCAL:39) / 3)
	ENDIF

	IF LOCAL:39 == 0
		PRINTFORML 選択した捕虜を解放します
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			WAIT
		ENDIF
	ELSEIF LOCAL:39 > 0
		PRINTFORML %ANAME(LOCAL:5)%に対して経済力{LOCAL:52}を要求することができます
		PRINTFORML 現在の経済力……%ANAME(LOCAL:4)%:{LOCAL:50}    %ANAME(LOCAL:5)%:{LOCAL:51}
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			LOCAL:60 = 1
			IF LOCAL:54 < LOCAL:52
				PRINTFORML %ANAME(LOCAL:5)%は要求を満たすだけの経済力を持っていないようだ…(準備可能:{LOCAL:54})
				CALL ASK_YN("あるだけよこせ！", "それなら交渉は決裂だ")
				IF RESULT == 0
					LOCAL:52 = LOCAL:54
				ELSE
					LOCAL:60 = 0
				ENDIF
			ENDIF
			IF LOCAL:60
				;解放処理＆解放メッセージの表示
				CALL PRISONER_TRADE_RELEASE(ARG:0)
				;相手勢力から自国に経済力を移動
				CALL SHIFT_ECONOMY(ARG:0, CFLAG:MASTER:所属, LOCAL:52 * 100)
				PRINTFORMW %ANAME(LOCAL:5)%勢力から経済力{LOCAL:52}を受け取り所有都市に配分しました
			ENDIF
		ENDIF
	ELSE
		PRINTFORML %ANAME(LOCAL:5)%に経済力{LOCAL:52}を渡す必要があります
		PRINTFORML 現在の経済力……%ANAME(LOCAL:4)%:{LOCAL:50}    %ANAME(LOCAL:5)%:{LOCAL:51}
		IF LOCAL:52 > LOCAL:53
			PRINTFORML ※受け渡せる経済力の限界は{LOCAL:53}まで
			CALL ASK_MULTI_JUDGE("決定", 0, "キャンセル", 1)
		ELSE
			CALL ASK_YN("決定", "キャンセル")
		ENDIF
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			;自国から相手勢力に兵を移動
			CALL SHIFT_ECONOMY(CFLAG:MASTER:所属, ARG:0, LOCAL:52 * 100)
			PRINTFORMW %ANAME(LOCAL:5)%勢力に{LOCAL:52}の経済力を受け渡しました
		ENDIF
	ENDIF

	REDRAW 1
	RESTART
	
;;;追加ここから　人質交渉　2返還
ELSEIF RESULT == 2 && LOCAL:40
	IF LOCAL:39 == 0
		PRINTFORML 選択した捕虜を解放します
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			;解放処理＆解放メッセージの表示
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			WAIT
		ENDIF
	ELSEIF LOCAL:39 > 0
		PRINTFORML %ANAME(LOCAL:5)%に捕虜を返還します
		CALL ASK_YN("決定", "キャンセル")
		IF RESULT == 0
			LOCAL:60 = 1  ;解放の実施
			LOCAL:25 = LOCAL:39 / 30		;好感度・敵対度の操作値は要調整

			IF LOCAL:60	;解放を実施==1
				;解放処理＆解放メッセージの表示
				CALL PRISONER_TRADE_RELEASE(ARG:0)
				;互いの国同士の好感度を上昇させ敵対値を低下させる
				CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:25)
			ENDIF
		ENDIF
	ELSE
		PRINTFORML 自軍が優位の場合のみ選べます	;出るはずのないメッセージ
	ENDIF
	REDRAW 1
	RESTART
;;追加ここまで　人質交渉　2返還

ELSEIF RESULT == 10
	LOCAL:12 = MAX(LOCAL:12 - 1, 1)
ELSEIF RESULT == 11
	LOCAL:12 = MIN(LOCAL:12 + 1, LOCAL:11)
ELSEIF RESULT == 20
	LOCAL:22 = MAX(LOCAL:22 - 1, 1)
ELSEIF RESULT == 21
	LOCAL:22 = MIN(LOCAL:12 - 1, LOCAL:11)
ELSE
	LOCAL:2 = NO_TO_CHARA(RESULT - 99)
	IF LOCAL:2 < 0
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	IF (CFLAG:(LOCAL:2):所属 == ARG:0 && CFLAG:(LOCAL:2):捕虜先 == CFLAG:MASTER:所属) || (CFLAG:(LOCAL:2):所属 == CFLAG:MASTER:所属 && CFLAG:(LOCAL:2):捕虜先 == ARG:0)
		CFLAG:(LOCAL:2):外交選択フラグ = !CFLAG:(LOCAL:2):外交選択フラグ
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

;-------------------------------------------------
;選択した解放し、解放メッセージを表示する
;-------------------------------------------------
@PRISONER_TRADE_RELEASE(ARG:0)
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):外交選択フラグ && CFLAG:(LOCAL:0):所属 == ARG:0
		CALL CAPTURE(LOCAL:0, 0)
		PRINTFORML %ANAME(LOCAL:0)%を解放しました
		IF TALENT:(LOCAL:0):妊娠
			SELECTCASE RAND:4
				CASE 0
					PRINTFORML 捕虜になっていた%ANAME(LOCAL:0)%のお腹にはお土産が持たされている
				CASE 1
					PRINTFORML %ANAME(LOCAL:0)%のお腹の子について様々な憶測が流れている
				CASE 2
					PRINTFORML 辱められた上、こんな姿で味方のもとに返されることに%ANAME(LOCAL:0)%は涙している
				CASE 3
					PRINTFORML %ANAME(LOCAL:0)%の膨らんだ腹は返還式についてきた兵士たちの好奇の視線に晒されている
			ENDSELECT
		ELSEIF IS_LOVER(LOCAL:0)
			PRINTFORML %ANAME(LOCAL:0)%は解放を喜びつつも%ANAME(MASTER)%のことを想い、何度も振り返っている
		ELSEIF TALENT:(LOCAL:0):人気
			PRINTFORML 人気のある%ANAME(LOCAL:0)%の帰還を皆が喜んでいる
		ENDIF
	ENDIF
NEXT
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):外交選択フラグ && CFLAG:(LOCAL:0):所属 == CFLAG:MASTER:所属
		CALL CAPTURE(LOCAL:0, 0)
		PRINTFORML %ANAME(LOCAL:0)%は解放されました
		IF IS_LOVER(LOCAL:0) || IS_SLAVED_BY(LOCAL:0)
			PRINTFORML %ANAME(LOCAL:0)%は%ANAME(MASTER)%が解放に尽力してくれたことを喜んでいる
		ELSEIF IS_SLAVE(LOCAL:0)
			PRINTFORML %ANAME(LOCAL:0)%は%ANAME(MASTER)%に捕虜になってしまったことを詫びている
		ELSEIF TALENT:(LOCAL:0):人気
			PRINTFORML 人気のある%ANAME(LOCAL:0)%の帰還を皆が喜んでいる
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------
;勢力ARG:0から勢力ARG:1にARG:2だけの兵力を移動させる処理
;-------------------------------------------------
@SHIFT_SOLDIER(ARG:0, ARG:1, ARG:2)
LOCAL:12 = GET_SUM_SOLDIER(ARG:0)
LOCAL:13 = GET_SUM_ARMY_DF(ARG:0)

IF ARG:2 > COUNTRY_SOLDIER:(ARG:0)
	IF ARG:2 > COUNTRY_SOLDIER:(ARG:0) + LOCAL:13
		CALL CLEAR_ALL_UNIT(ARG:0)
	ENDIF

	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == ARG:0
			COUNTRY_SOLDIER:(ARG:0) += CITY_SOLDIER:(LOCAL:0)
			CITY_SOLDIER:(LOCAL:0) = 0
		ENDIF
	NEXT

	COUNTRY_SOLDIER:(ARG:0) = MAX(COUNTRY_SOLDIER:(ARG:0) - ARG:2, 0)

	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == ARG:0
			LOCAL:15 = MIN(COUNTRY_SOLDIER:(ARG:0), 500)
			CITY_SOLDIER:(LOCAL:0) += LOCAL:15
			COUNTRY_SOLDIER:(ARG:0) -= LOCAL:15
		ENDIF
	NEXT

ELSE
	COUNTRY_SOLDIER:(ARG:0) = MAX(COUNTRY_SOLDIER:(ARG:0) - ARG:2, 0)
ENDIF

COUNTRY_SOLDIER:(ARG:1) += ARG:2

;-------------------------------------------------
;人質交渉におけるキャラARG:0の価値  能力が高いほど価値が高くなる
;-------------------------------------------------
@GET_PRISONER_VALUE(ARG:0)
#FUNCTION
LOCAL:0 = 0
LOCAL:0 += (ABL:(ARG:0):武闘 + ABL:(ARG:0):防衛 + ABL:(ARG:0):知略 + ABL:(ARG:0):政治) / 4
LOCAL:0 += (ABL:(ARG:0):歌唱 + ABL:(ARG:0):料理) / 2
;対象が特殊な勢力に捕らえられており、それが自国の捕らえている捕虜でない場合、価値は5倍に
SIF CFLAG:MASTER:所属 != CFLAG:(ARG:0):捕虜先 && IS_SP_COUNTRY(CFLAG:(ARG:0):捕虜先)
	LOCAL:0 *= 5
RETURNF POWER(MAX(LOCAL:0 - 40, 10), 2)

