;-------------------------------------------------
;醜聞を流す
;-------------------------------------------------
@DIPLOMACY_SABOTAGE(ARG:0)
#DIM 君主
#DIM 相手君主
#DIM FIRST_LINE
#DIM CONST 支払額リスト = 1, 5, 50, 100, 500, 1000, 2500, 5000, 10000, 15000, 20000, 30000, 40000, 50000
#DIM 支払額
#DIM 効果

FIRST_LINE = LINECOUNT
君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
相手君主 = GET_COUNTRY_BOSS(ARG:0)

CALL SINGLE_DRAWLINE
PRINTFORML %ANAME(相手君主)%所属の、%ANAME(MASTER)%に心酔している士官を利用し、内部工作を仕掛けさせます
CALL COLOR_PRINTL("一人参加させるごとに国庫", GETDEFCOLOR(), "1000", カラー_注意,  "を消費します。最大3人です")
PRINTFORML 誰を参加させますか？
CALL SINGLE_DRAWLINE
CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(MIN(MONEY:(CFLAG:MASTER:所属) / 1000, 3), "DIPLOMACY_SABOTAGE", "DIPLOMACY_SABOTAGE")

IF SELECTED_CHARA_NUM == -1
	PRINTFORML やめておきます
	RETURN 0
ENDIF

MONEY:(CFLAG:MASTER:所属) -= SELECTED_CHARA_NUM * 1000

PRINTFORML 工作活動を実施します……

FOR LOCAL, 0, SELECTED_CHARA_NUM
	LOCAL:1 = SELECTED_CHARA:LOCAL
	SELECTCASE RAND:3
		CASE 0
			PRINTFORML %ANAME(LOCAL:1)%は%ANAME(相手君主)%勢力において、%ANAME(君主)%の印象を良くするべくロビー活動を行っている……
			CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, SQRT(ABL_POWER(ABL:(LOCAL:1):政治, LOCAL:1)) / 2)
		CASE 1
			PRINTFORML %ANAME(LOCAL:1)%は%ANAME(相手君主)%の財力を%ANAME(君主)%に横流ししている……
			CALL COLOR_PRINTL("経済力を", GETDEFCOLOR(), TOSTR(GET_SUM_ECONOMY(ARG:0) * ABL:(LOCAL:1):政治 / 100 / 40), カラー_注意, "奪った", GETDEFCOLOR())
			CALL SHIFT_ECONOMY(ARG:0, CFLAG:MASTER:所属, GET_SUM_ECONOMY(ARG:0) * ABL:(LOCAL:1):政治 / 40)
		CASE 2
			PRINTFORML %ANAME(LOCAL:1)%は%ANAME(相手君主)%勢力内で工作活動を行っている……
			CALL SELECT_CHARA_RANDOM("DIPLOMACY_SABOTAGE")
			IF !GROUPMATCH(RESULT, LOCAL:1 , -1) || RAND:100 < 50
				CALL ADD_COOLTIME(RESULT, 2)
			ELSE
				PRINTFORML ……が、うまくいかなかったようだ……
			ENDIF
	ENDSELECT
NEXT
PRINTFORMW
RETURN 1

@SELECT_CHARA_LIST_SHOW_LOGIC_DIPLOMACY_SABOTAGE(対象)
#DIM 対象
RETURN CFLAG:対象:所属 == DIPLOMACY_DIPLOMAT && 対象 != GET_COUNTRY_BOSS(DIPLOMACY_DIPLOMAT) && (IS_SLAVE(対象) || GETBIT(TALENT:対象:淫乱系, 素質_淫乱_傀儡) || GETBIT(TALENT:対象:淫乱系, 素質_淫乱_薬物中毒))

@SELECT_CHARA_LIST_SELECT_LOGIC_DIPLOMACY_SABOTAGE(対象)
#DIM 対象
RETURN CFLAG:対象:捕虜先 == 0

@SELECT_CHARA_RANDOM_LOGIC_DIPLOMACY_SABOTAGE(対象)
#DIM 対象
RETURN CFLAG:対象:所属 == DIPLOMACY_DIPLOMAT && CFLAG:対象:捕虜先 == 0

