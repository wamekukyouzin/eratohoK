;-------------------------------------------------
;「特別訓練」の名称
;-------------------------------------------------
@SHOP_SLG_NAME65
RESULTS:0 '= "特別訓練"

;-------------------------------------------------
;「特別訓練」の次行説明指定有無
;-------------------------------------------------
@SHOP_SLG_NEXT_LINE_ADD65

;-------------------------------------------------
;「特別訓練」の次行説明指定内容
;-------------------------------------------------
@SHOP_SLG_NEXT_LINE_ADD_TEXT65
IF SPECIAL_TRAIN_IS_BARGAIN()
	SETCOLOR カラー_注意
	PRINTFORM %"現在割引料金", 12%
	RESETCOLOR
ELSE
	SIF MONEY < SPECIAL_TRAIN_PRICE
		SETCOLOR カラー_選択不可
	LOCALS = %NUM_FORMAT(SPECIAL_TRAIN_PRICE)%
	SIF STRLENS(LOCALS) > 7
		LOCALS = "多い"

	PRINTFORM %"料金:" + LOCALS, 12%
	RESETCOLOR
ENDIF

;-------------------------------------------------
;「特別訓練」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK65
SIF MONEY < SPECIAL_TRAIN_PRICE
	RETURN 0
SIF CFLAG:MASTER:所属 == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;「特別訓練」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY65
CALL SLG_SPECIAL_TRAIN
RETURN RESULT

;-------------------------------------------------
;「特別訓練」本体
;-------------------------------------------------
@SLG_SPECIAL_TRAIN()
#DIM 対象, 1
#DIM 対象項目, 1
#DIM 取得経験値, 1
$SHOW_LOOP
CALL SINGLE_DRAWLINE
PRINTFORM 金
SETCOLOR カラー_注意
PRINTFORM %NUM_FORMAT(SPECIAL_TRAIN_PRICE)%
RESETCOLOR
PRINTFORML を消費し、配下に訓練を受けさせることができます
SIF SPECIAL_TRAIN_IS_BARGAIN()
	CALL COLOR_PRINTL("ただいま大幅割引中！", カラー_注意)
PRINTFORML 誰を訓練しますか？
PRINTFORML 現在の金:%NUM_FORMAT(MONEY)%
CALL SINGLE_DRAWLINE
CALL SELECT_CHARA_LIST_SLG()
IF RESULT == -1
	RETURN 0
ELSE
	対象 = RESULT
	CALL SINGLE_DRAWLINE
	PRINTFORML %ANAME(対象)%のどの能力を訓練しますか？
	CALL SINGLE_DRAWLINE
	PRINTFORM [{GETNUM(ABL, "武闘")}] 武闘 
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(対象):武闘)
	PRINTFORML {ABL:(対象):武闘, 3, RIGHT}
	PRINTFORM [{GETNUM(ABL, "防衛")}] 防衛 
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(対象):防衛)
	PRINTFORML {ABL:(対象):防衛, 3, RIGHT}
	PRINTFORM [{GETNUM(ABL, "知略")}] 知略 
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(対象):知略)
	PRINTFORML {ABL:(対象):知略, 3, RIGHT}
	PRINTFORM [{GETNUM(ABL, "政治")}] 政治 
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(対象):政治)
	PRINTFORML {ABL:(対象):政治, 3, RIGHT}
	IF TALENT:対象:妖術知識
		PRINTFORM [{GETNUM(ABL, "妖術")}] 妖術 
		CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(対象):妖術)
		PRINTFORML {ABL:(対象):妖術, 3, RIGHT}
	ENDIF
	PRINTFORM [{GETNUM(ABL, "歌唱")}] 歌唱 
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(対象):歌唱)
	PRINTFORML {ABL:(対象):歌唱, 3, RIGHT}
	PRINTFORM [{GETNUM(ABL, "料理")}] 料理 
	CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(対象):料理)
	PRINTFORML {ABL:(対象):料理, 3, RIGHT}
	PRINTL 
	CALL SINGLE_DRAWLINE
	PRINTFORML [0] 戻る
	PRINTL 
	$INPUT_LOOP2
	INPUT
	IF RESULT == 0
		GOTO SHOW_LOOP
	ELSEIF !GROUPMATCH(RESULT, GETNUM(ABL, "武闘"), GETNUM(ABL, "防衛"), GETNUM(ABL, "知略"), GETNUM(ABL, "政治"), GETNUM(ABL, "料理"), GETNUM(ABL, "歌唱"), GETNUM(ABL, "妖術"))
		GOTO INPUT_LOOP2
	ELSEIF RESULT == GETNUM(ABL, "妖術") && !TALENT:(対象):妖術知識
		GOTO INPUT_LOOP2
	ELSE
		対象項目 = RESULT
		;バーゲン時は安い
		取得経験値 = SPECIAL_TRAIN_CALC_EFFECT(対象, 対象項目)
		PRINTFORML 訓練を行った！
		CALL PRINT_ADD_EXP(対象, EXPNAME:GET_EXP(対象項目), 取得経験値, 1)
		CALL TRAIN_AUTO_ABLUP(対象)
		PRINTFORMW %ANAME(対象)%はいい汗をかいたようだ……
		MONEY -= SPECIAL_TRAIN_PRICE
		SPECIAL_TRAIN_COUNT ++
		SPECIAL_TRAIN_COUNT_THIS_TURN ++
		;再計算
		CALL CALC_SPECIAL_TRAIN_PRICE()
		IF MONEY >= SPECIAL_TRAIN_PRICE
			RESTART
		ELSE
			PRINTFORML 特別訓練を受けるだけのお金がなくなりました
			PRINTFORMW メニュー画面に戻ります
		ENDIF
	ENDIF
	PRINTFORML
	RETURN 1
ENDIF


@CALC_SPECIAL_TRAIN_PRICE()

;経済値の3倍
LOCAL = MAX(GET_SUM_ECONOMY(CFLAG:MASTER:所属) / 100 * 3, 3000)

;その周回の実施回数およびそのターンの実施回数で補正
;SELECTCASE SPECIAL_TRAIN_COUNT
;	CASE IS < 5
;		LOCAL += SPECIAL_TRAIN_COUNT * 500
;	CASE IS < 15
;		LOCAL += SPECIAL_TRAIN_COUNT * 1000
;	CASE IS < 50
;		LOCAL += SPECIAL_TRAIN_COUNT * 1500
;	CASEELSE
;		LOCAL += SPECIAL_TRAIN_COUNT * 3000
;ENDSELECT

;そのターンに行った回数を乗せる
IF SPECIAL_TRAIN_COUNT_THIS_TURN > 5
	LOCAL += SPECIAL_TRAIN_COUNT_THIS_TURN * 5000
ELSE
	LOCAL += SPECIAL_TRAIN_COUNT_THIS_TURN * 3000
ENDIF

;勢力の都市数を乗せる(6都市以上から)
LOCAL += MAX((GET_OWN_CITY(CFLAG:MASTER:所属) - 6) * 700, 0)

;勢力の★数で補正 ★3を補正0として±20%まで
LOCAL = LOCAL * (100 + (TMP_COUNTRY_STARS:(CFLAG:MASTER:所属) - 3) * 10) / 100

;乱数を乗せる
LOCAL = LOCAL * RAND(90, 110) / 100

;日数で補正
;LOCAL = LOCAL * (100 + MIN(DAY - 20, 100)) / 100

;俸給の方が高いなら、俸給の1/2をよこせ
SIF LOCAL < CALC_SALARY()
	LOCAL += CALC_SALARY() / 2

;その周回で最初の特別訓練、または5期に1回、1/10で特訓できる
SIF SPECIAL_TRAIN_IS_BARGAIN()
	LOCAL /= 10
;1ターン目の最初の1回は500固定
SIF DAY == 1 && SPECIAL_TRAIN_COUNT == 0
	LOCAL = 500

SPECIAL_TRAIN_PRICE = LOCAL


@SPECIAL_TRAIN_IS_BARGAIN()
#FUNCTION
RETURNF (SPECIAL_TRAIN_COUNT == 0 || (DAY % 5 == 0 && SPECIAL_TRAIN_COUNT_THIS_TURN == 0)) && CFLAG:MASTER:所属



@SPECIAL_TRAIN_CALC_EFFECT(対象, 対象項目)
#FUNCTION
#DIM 対象
#DIM 対象項目
#DIM 対象能力値
#DIM 能力合計
#DIM 取得経験基礎値
#DIM 取得経験値
#DIM 成長必要値
#DIM 返却値
対象能力値 = ABL:対象:対象項目
成長必要値 = ABLUP_NEED_EXP_SLG(対象能力値) / 100
能力合計 = ABL:対象:武闘 + ABL:対象:知略 + ABL:対象:政治 + ABL:対象:防衛 + ABL:対象:歌唱 + ABL:対象:料理
取得経験基礎値 = (600 - 能力合計)

SIF 対象項目 != GETNUM(ABL, "妖術")
	取得経験基礎値 /= 6
;歌唱と料理はさらに半分
SIF GROUPMATCH(対象項目, GETNUM(ABL, "歌唱"), GETNUM(ABL, "料理"))
	取得経験基礎値 /= 2

;選択した項目が高いと取得経験値は下がる
SELECTCASE 対象能力値
	CASE IS < ランク閾値:ランク_ＳＬＧ:ランク_E
		取得経験値 = 取得経験基礎値 * 120 / 100
	CASE IS < ランク閾値:ランク_ＳＬＧ:ランク_A
		取得経験値 = 取得経験基礎値 * 75 / 100
	CASEELSE
		取得経験値 = 取得経験基礎値 * 50 / 100
ENDSELECT

;ランクによって成長する数値に制限あり
;上のほうのランクはそもそも制限に届くことがないはず
SELECTCASE 対象能力値
	CASE IS < ランク閾値:ランク_ＳＬＧ:ランク_E
		返却値 = LIMIT(取得経験値, 5, 成長必要値 * 20)
	CASE IS < ランク閾値:ランク_ＳＬＧ:ランク_D
		返却値 = LIMIT(取得経験値, 5, 成長必要値 * 15)
	CASE IS < ランク閾値:ランク_ＳＬＧ:ランク_B
		返却値 = LIMIT(取得経験値, 5, 成長必要値 * 10)
	CASE IS < ランク閾値:ランク_ＳＬＧ:ランク_S
		返却値 = LIMIT(取得経験値, 5, 成長必要値 * 5)
ENDSELECT

RETURNF 返却値
