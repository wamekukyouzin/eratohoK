;-------------------------------------------------
;「奴隷市場」の名称
;-------------------------------------------------
@SHOP_SLG_NAME64
RESULTS:0 '= "奴隷市場"

;-------------------------------------------------
;「奴隷市場」の選択可否
;-------------------------------------------------
@SHOP_SLG_CHECK64
SIF CFLAG:MASTER:所属 == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;「奴隷市場」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_SLG_EVENTBUY64
CALL SLAVEMARKET
RETURN 1

;-------------------------------------------------
;「奴隷市場」本体
;-------------------------------------------------
@SLAVEMARKET
#DIM 対象
#DIM 対象添え字
#DIM 売却可能
#DIM FIRST_LINE


;売却には特殊勢力な勢力が必要(主人公の所属していないところで)
売却可能 = 0
FOR LOCAL, 0, MAX_COUNTRY
	SIF IS_SP_COUNTRY(LOCAL) && CFLAG:MASTER:所属 != LOCAL
		売却可能 = 1
NEXT


FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTFORML 「よぉ～こそ！　イキのいいのを揃えてますよぉ？」
PRINTFORML %ANAME(MASTER)%は奴隷市場を訪れた
PRINTFORML ボロボロの服を着た女達が檻に入れられ、並べられている
PRINTFORML 係の者は、使えそうな奴隷のリストを渡してきた……
PRINTFORML 所持金:{MONEY}
CALL SINGLE_DRAWLINE

VARSET SHOP_CHARA_LIST, -1
FOR LOCAL, 0, VARSIZE("SLAVEMARKET_GOODS")
	SHOP_CHARA_LIST:LOCAL = ID_TO_CHARA(SLAVEMARKET_GOODS:LOCAL)
NEXT

FOR LOCAL, 0, VARSIZE("SLAVEMARKET_GOODS")
	LOCAL:1 = ID_TO_CHARA(SLAVEMARKET_GOODS:LOCAL)
	SIF LOCAL:1 == -1
		CONTINUE
	;人質解放要求などにより既に捕虜でなくなっている場合、パージ
	IF !TALENT:(LOCAL:1):奴隷市場モブ && !IS_SP_COUNTRY(CFLAG:(LOCAL:1):捕虜先) && !IS_SP_COUNTRY(CFLAG:(LOCAL:1):所属)
		SLAVEMARKET_GOODS:LOCAL = 0
		CONTINUE
	ENDIF
	CALL SLAVEMARKET_SHOW_INFO(LOCAL:1, LOCAL)
	CALL SINGLE_DRAWLINE
NEXT

PRINTL 
SIF 売却可能
	PRINTFORML [98] 売却
PRINTFORML [99] 戻る

$INPUT_LOOP
INPUT

IF RESULT == 99
	RETURN 0
ELSEIF RESULT == 98
	IF !売却可能
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	CALL SLAVEMARKET_SELL_CHARA
	CLEARLINE FIRST_LINE - LINECOUNT
	RESTART
ENDIF

IF RESULT < 0
	CLEARLINE 1
	GOTO INPUT_LOOP
ELSEIF 100 <= RESULT && RESULT < VARSIZE("SLAVEMARKET_GOODS") + 100
	対象添え字 = RESULT - 100
	対象 = ID_TO_CHARA(SLAVEMARKET_GOODS:対象添え字)
	IF 対象 == -1
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	CALL SINGLE_DRAWLINE
	CALL SHOW_INFO_WITH_UI(対象, 1)
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ELSEIF VARSIZE("SLAVEMARKET_GOODS") <= RESULT || MONEY < SLAVEMARKET_PRICE:RESULT
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

対象 = ID_TO_CHARA(SLAVEMARKET_GOODS:RESULT)
対象添え字 = RESULT


IF 対象 == -1
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

対象 = ID_TO_CHARA(SLAVEMARKET_GOODS:RESULT)
対象添え字 = RESULT
PRINTFORML 「へへ、そいつぁ%NAME_FORMAL(対象)%ってぇ奴隷でさ」
PRINTFORML 「今なら{SLAVEMARKET_PRICE:対象添え字}でお譲りいたしやすよ」
SIF 対象添え字 == SLAVEMARKET_SPECIALGOODS
	PRINTFORML 「ちょっと"訳あり"でしてね、特売ってことで値引きさせていただいてやす」
IF IS_LOVER(対象)
	PRINTFORML 「……ん？　高いって？」
	PRINTFORML 「そりゃもちろん、コイツがアンタの"コレ"だって知ってるからに決まってんでしょォ？」
	PRINTFORML 「高いってんなら、自分のオンナが他のオトコに売られるとこでも見てるんだなァ、ヒヒッ！」
ENDIF
PRINTFORML 「で？　どうされます？」
PRINTL
CALL ASK_MULTI_JUDGE(@"%ANAME(対象)%を買う", MONEY >= SLAVEMARKET_PRICE:対象添え字, "やめておく", 1)
IF RESULT == 1
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF

MONEY -= SLAVEMARKET_PRICE:対象添え字
PRINTFORMW 「ひひ、じゃあ商談成立だ」
PRINTFORMW 「じゃ、持ってってくんなせぇ」

;確率でエロイベント　特売なら確定
IF IS_TOHO_CHARA(対象) && IS_FEMALE(対象) && (20 > RAND:100 || 対象添え字 == SLAVEMARKET_SPECIALGOODS)
	PRINTFORML
	PRINTFORMW ……男が連れてきた%ANAME(対象)%の姿を見、%ANAME(MASTER)%は目を疑った
	PRINTFORML 丸裸の%ANAME(対象)%は目隠しをされ、手足には枷が嵌められている
	PRINTFORMW 剥き出しの身体には、痛々しいピアスが取り付けられている
	PRINTFORML 全身白濁にまみれており、穴からも粘つく汁がどろどろと零れている
	PRINTFORMW あちこちに書かれた正の字が、%ANAME(対象)%のされたことを物語っていた
	PRINTFORMW そんな悲惨な様だというのに、%ANAME(対象)%はえへらえへらと笑っている
	PRINTFORMW ……これは楽しくて笑っているのではない。何か怪しげな薬の作用だ……
	PRINTFORML
	PRINTFORML 「あ～、市場に並べてる間も、奴隷としてあっしらが使わせていただいてますんでね」
	PRINTFORML 「洗浄はセルフサービスですんで、すいやせんけどね」
	PRINTFORMW 「おっと、返品は受け付けませんよ、残念ですけどねぇ」
	SIF 対象 == SLAVEMARKET_SPECIALGOODS
		PRINTFORMW 「ま、特売になるにはそれなりの理由があるってことですよ、へへへ」
	IF IS_LOVER(対象)
		PRINTFORMW 「別に構わんでしょ～？　恋人とカンドーノサイカイができたんだから」
		PRINTFORMW 「これに懲りたら、もっと大事にしてやることですな、げひゃひゃひゃ！」
	ENDIF
	PRINTFORMW 男を斬り捨ててやりたいが、この市場には多くの衛兵がいる。暴れれば自分が危ない
	PRINTFORMW 歯を噛みしめながら、%ANAME(MASTER)%は%ANAME(対象)%を受け取った……
	CALL FUCK_GANGBANG(対象, GET_SPERM_ID("奴隷商人"), @"奴隷商人の\@RAND:2 ? ペニス # 唇\@", "奴隷商人")
	CFLAG:対象:薬物依存 += RAND(30, 50)
	CALL SET_PIERCE_RANDOM(対象, 0)
ENDIF

IF IS_LOVER(対象)
	PRINTFORMW 「ひひ、これに懲りたら、もっと大事にしてやることですな、ひゃひゃひゃ！」
ELSE
	PRINTFORML 「えっへへへ、毎度ォ」
	PRINTFORMW 「そのオモチャと仲良くやることですな！　げへへへ」
ENDIF



PRINTFORMW %ANAME(対象)%を受け取り、市場を後にした……
;別勢力に所属（特殊勢力以外）
IF IS_COUNTRY(CFLAG:対象:所属) && !IS_SP_COUNTRY(CFLAG:対象:所属) && CFLAG:対象:所属 != CFLAG:MASTER:所属
	PRINTFORMW ……%ANAME(対象)%は%ANAME(GET_COUNTRY_BOSS(CFLAG:対象:所属))%の所属だ
	PRINTFORMW 返してやるのが人の道であるように思えるが……
	PRINTFORML どうしますか？
	CALL SINGLE_DRAWLINE
	CALL ASK_MULTI("返してやる", "士官として働いてもらう", "奴隷として監禁する")
	IF RESULT == 0
		PRINTFORMW %ANAME(対象)%を%ANAME(GET_COUNTRY_BOSS(CFLAG:対象:所属))%に返してやることにした
		PRINTFORMW %ANAME(GET_COUNTRY_BOSS(CFLAG:対象:所属))%から深く感謝された……
		CALL CHANGE_RELATION_C_TO_C(CFLAG:対象:所属, CFLAG:MASTER:所属, 600, -600)
		CFLAG:対象:好感度 = MAX(500, CFLAG:対象:好感度 + 500)
		CFLAG:対象:従属度 = MAX(500, CFLAG:対象:従属度 + 500)
		CFLAG:対象:依存度 = MAX(100, CFLAG:対象:依存度 + 100)
		CALL CAPTURE(対象, 0)
	ELSEIF RESULT == 1
		PRINTFORMW %ANAME(GET_COUNTRY_BOSS(CFLAG:対象:所属))%にせっかく買ったものをただで返してやるほどの義理はない
		PRINTFORMW 士官としての待遇を条件に、%ANAME(対象)%に協力を約束させた……
		CALL CHANGE_COUNTRY(対象, CFLAG:MASTER:所属, 1)
		CFLAG:対象:好感度 = MAX(0, CFLAG:対象:好感度 + 500)
		CFLAG:対象:従属度 = MAX(0, CFLAG:対象:従属度 + 500)
		CFLAG:対象:依存度 = MAX(0, CFLAG:対象:依存度 + 100)
	ELSE
		PRINTFORMW いやいや、金を出して買ったのだから、もう%ANAME(対象)%は自分のものだ
		PRINTFORMW どのように使ってやろうか考えながら、%ANAME(MASTER)%は市場を後にした……
		CALL CHANGE_COUNTRY(LOCAL:0, 0, 1)
		CALL CAPTURE(対象, CFLAG:MASTER:所属)
	ENDIF
;どこにも所属していないか、特殊勢力所属
ELSEIF CFLAG:対象:所属 != CFLAG:MASTER:所属
	PRINTFORML ……%ANAME(対象)%の待遇はどうしようか？
	CALL SINGLE_DRAWLINE
	CALL ASK_YN("士官として迎え入れる", "監禁しておく")
	IF RESULT == 0
		PRINTFORMW %ANAME(対象)%は%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%の士官として働くことになった……
		CALL CHANGE_COUNTRY(対象, CFLAG:MASTER:所属, 1)
		CFLAG:対象:好感度 = MAX(500, CFLAG:対象:好感度 + 500)
		CFLAG:対象:従属度 = MAX(500, CFLAG:対象:従属度 + 500)
		CFLAG:対象:依存度 = MAX(100, CFLAG:対象:依存度 + 100)
	ELSE
		PRINTFORMW %ANAME(対象)%を逃げ出さないように監禁しておくことにした……
		CALL CHANGE_COUNTRY(対象, 0, 1)
		CALL CAPTURE(対象, CFLAG:MASTER:所属)
	ENDIF
;自国の士官
ELSE
	CFLAG:対象:好感度 = MAX(500, CFLAG:対象:好感度 + 500)
	CFLAG:対象:従属度 = MAX(500, CFLAG:対象:従属度 + 500)
	CFLAG:対象:依存度 = MAX(100, CFLAG:対象:依存度 + 100)
	CALL CHANGE_COUNTRY(対象, CFLAG:MASTER:所属, 1)
ENDIF

;捕虜状態から解放、部隊から除外、買ったキャラを配列から削除
;念のため放浪フラグを折る
CFLAG:対象:特殊状態 = 0
CALL FORCE_FREE(対象)
SLAVEMARKET_GOODS:対象添え字 = 0


;-------------------------------------------------
;「奴隷市場」売却部分
;-------------------------------------------------
@SLAVEMARKET_SELL_CHARA
#DIM 対象
#DIM 行き先
#DIM FIRST_LINE
#DIM 対象売却額
#DIM 合計額

FIRST_LINE = LINECOUNT
PRINTFORML 「ひひ、誰を売ってくれるんで？」
PRINTFORML ※%ANAME(MASTER)%）に陥落済みのキャラを売却できます
CALL SINGLE_DRAWLINE
CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SEX(-1, "SLAVEMARKET_SELL_CHARA", "SLAVEMARKET_SELL_CHARA")

IF SELECTED_CHARA_NUM == 0
	PRINTFORMW 「おや、そいつぁ残念、ひひっ」
	RETURN 0
ENDIF

;個々の売却額表示しようかな
;でもそれはそれでうっとうしいかな
合計額 = 0
FOR LOCAL, 0, SELECTED_CHARA_NUM
	対象 = SELECTED_CHARA:LOCAL
	対象売却額 = SLAVEMARKET_CALC_PRICE(対象) / 5
	合計額 += 対象売却額
NEXT
PRINTFORML 担当の男は%ANAME(対象, SELECTED_CHARA_NUM)%を、欲望をむき出しにした目で品定めする……
PRINTFORML 「あー……しめて{合計額}になりやす」

CALL ASK_YN("売却", "やめておく")
IF RESULT == 1
	PRINTFORMW 「へっ、今さら良心でも湧いたんです？」
	CLEARLINE FIRST_LINE - LINECOUNT
	RESTART
ENDIF

PRINTFORML 「ひひっ、毎度あり！」
PRINTFORML %ANAME(対象, SELECTED_CHARA_NUM)%たちは枷を嵌められ、どこかへと連れていかれた……

MONEY += 合計額

FOR LOCAL, 0, SELECTED_CHARA_NUM
	対象 = SELECTED_CHARA:LOCAL
	WHILE 1
		行き先 = GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:IFRAND(@"1TO{VARSIZE("SP_COUNTRY_ID") - 1}, D{SP_COUNTRY_ID:特殊勢力_自警団}", 1))
		SIF GROUPMATCH(行き先, -1, CFLAG:MASTER:所属)
			CONTINUE
		PRINTFORML %ANAME(対象)%は%ANAME(GET_COUNTRY_BOSS(行き先))%勢力に売られていった……
		CALL CHANGE_COUNTRY(対象, 0, 1)
		CALL CAPTURE(対象, 行き先)
		CFLAG:対象:好感度 = CFLAG:対象:好感度 / 2 - 1000
		CFLAG:対象:依存度 = CFLAG:対象:依存度 / 2 - 1000
		CFLAG:対象:従属度 = CFLAG:対象:従属度 / 2 - 1000
		CFLAG:対象:支配度 = CFLAG:対象:支配度 / 2 - 1000
		BREAK
	WEND
NEXT

WAIT

@SELECT_CHARA_LIST_SHOW_LOGIC_SLAVEMARKET_SELL_CHARA(対象)
#DIM 対象
RETURN CFLAG:対象:所属 == CFLAG:MASTER:所属 && GET_COUNTRY_BOSS(CFLAG:対象:所属) != 対象 && !IS_SP_CHARA(対象) && !IS_ANIMAL(対象) && IS_FALLEN(対象) && CFLAG:対象:行動不能状態 != 行動不能_子供

@SELECT_CHARA_LIST_SELECT_LOGIC_SLAVEMARKET_SELL_CHARA(対象)
#DIM 対象
RETURN CFLAG:対象:捕虜先 == 0


;-------------------------------------------------
;奴隷市場の商品の情報表示＋ボタン生成
;-------------------------------------------------
@SLAVEMARKET_SHOW_INFO(対象, 番号)
#DIM 対象
#DIM 番号
IF MONEY >= SLAVEMARKET_PRICE:番号
	PRINTBUTTON @"%"[" + ANAME(対象) + "]", MAX_CHARANAME_LENGTH + 2, LEFT%", 番号
	PRINTFORM  販売額:{SLAVEMARKET_PRICE:番号, 8, LEFT}
	PRINTBUTTON "[能力表示]", 番号 + 100
	SIF SLAVEMARKET_SPECIALGOODS == 番号
		CALL COLOR_PRINT("特売！", カラー_ピンク)
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM %"[" + ANAME(対象) + "]", MAX_CHARANAME_LENGTH + 2, LEFT%
	RESETCOLOR
	PRINTFORM  販売額:{SLAVEMARKET_PRICE:番号, 8, LEFT}
	SIF SLAVEMARKET_SPECIALGOODS == 番号
		CALL COLOR_PRINT("特売！  ", カラー_ピンク)
	CALL COLOR_PRINT("資金不足のため購入不可", カラー_警告)
ENDIF
PRINTL
PRINT 性知識:
PRINTFORM {ABL:対象:性知識, 2, RIGHT}
PRINT  欲望:
CALL PRINT_ALPHABET_RANK(ランク_その他, ABL:対象:欲望)
PRINTFORM {ABL:対象:欲望, 2, RIGHT}
PRINT  性技:
CALL PRINT_ALPHABET_RANK(ランク_その他, ABL:対象:性技)
PRINTFORM {ABL:対象:性技, 3, RIGHT}
PRINT  奉仕:
CALL PRINT_ALPHABET_RANK(ランク_その他, ABL:対象:奉仕)
PRINTFORM {ABL:対象:奉仕, 3, RIGHT}
PRINT  性交:
CALL PRINT_ALPHABET_RANK(ランク_その他, ABL:対象:性交)
PRINTFORM {ABL:対象:性交, 3, RIGHT}
PRINT  精愛:
CALL PRINT_ALPHABET_RANK(ランク_その他, ABL:対象:精愛)
PRINTFORM {ABL:対象:精愛, 3, RIGHT}
PRINTL
PRINTFORM 所持素質:
LOCAL:1 = 0
FOR LOCAL, 0, VARSIZE("SEX_TALENT_NAME")
	IF GETBIT(TALENT:対象:淫乱系, LOCAL)
		PRINTFORM %SEX_TALENT_NAME:LOCAL% 
		LOCAL:1 = 1
	ENDIF
NEXT
FOR LOCAL, 0, VARSIZE("SP_FALLEN_TALENT_NAME")
	IF GETBIT(TALENT:対象:特殊勢力陥落系, LOCAL)
		PRINTFORM %SP_FALLEN_TALENT_NAME:LOCAL% 
		LOCAL:1 = 1
	ENDIF
NEXT
SIF !LOCAL:1
	PRINT なし
PRINTL

;-------------------------------------------------
;奴隷市場が利用できるかを返す関数
;-------------------------------------------------
@IS_SLAVEMARKET_AVAILABLE
#FUNCTION

;特殊勢力な勢力が必要(主人公の所属していないところで)
FOR LOCAL, 0, MAX_COUNTRY
	SIF IS_SP_COUNTRY(LOCAL) && CFLAG:MASTER:所属 != LOCAL
		RETURNF 1
NEXT

;商品がいればok
;FOR LOCAL, 0, SLAVEMARKET_MAX_GOODS
;	SIF ID_TO_CHARA(SLAVEMARKET_GOODS:LOCAL) != -1
;		RETURNF 1
;NEXT

RETURNF 0

;-------------------------------------------------
;奴隷市場用、奴隷の金額計算関数
;-------------------------------------------------
@SLAVEMARKET_CALC_PRICE(対象)
#FUNCTION
#DIM 対象
#DIM 額

;基礎値に能力とスケベ能力を加算する
額 = MIN(ABL:対象:武闘 + ABL:対象:防衛 + ABL:対象:知略 + ABL:対象:政治 + ABL:対象:歌唱 + ABL:対象:料理, 1000) * 30
額 += ABL:対象:性知識 * 500
額 += MIN(ABL:対象:欲望 + ABL:対象:性技 + ABL:対象:奉仕 + ABL:対象:性交, 50) * 500
額 += MIN(ABL:対象:露出 + ABL:対象:精愛 + ABL:対象:自慰, 10) * 300
額 += MIN(ABL:対象:Ｃ感 + ABL:対象:Ｖ感 + ABL:対象:Ａ感 + ABL:対象:Ｂ感 + ABL:対象:Ｍ感, 40) * 300
;自国所属と特殊勢力陥落済みは高い
SIF CFLAG:対象:所属 == CFLAG:MASTER:所属
	額 += 3000
FOR LOCAL, 0, MAX_SP_COUNTRY
	SIF GETBIT(TALENT:対象:特殊勢力陥落系, LOCAL)
		額 += 3000
NEXT

;恋人だと足下見られる
SIF IS_LOVER(対象)
	額 += 10000

SIF TALENT:対象:奴隷市場モブ
	額 = LIMIT(額 / 2, 5000, 30000)

RETURNF 額

;-------------------------------------------------
;奴隷市場用、そのターンの商品の準備
;-------------------------------------------------
@SLAVEMARKET_PREPARE_GOODS
VARSET SLAVEMARKET_GOODS
VARSET SLAVEMARKET_SPECIALGOODS

CALL CLEARN_SLAVE_MARKET_MOB()
CALL CREATE_SLAVE_MARKET_MOB()

CALL SELECT_CHARA_RANDOM_MULTI(SLAVEMARKET_MAX_GOODS, "SLAVEMARKET_PREPARE_GOODS")
;候補がいなければなしで。
SIF SELECTED_CHARA_NUM < 1
	RETURN 0

FOR LOCAL, 0, MIN(SELECTED_CHARA_NUM, SLAVEMARKET_MAX_GOODS)
	SLAVEMARKET_GOODS:LOCAL = GET_ID(SELECTED_CHARA:LOCAL)
	SLAVEMARKET_PRICE:LOCAL = SLAVEMARKET_CALC_PRICE(SELECTED_CHARA:LOCAL)
NEXT

IF !RAND:3
	SLAVEMARKET_SPECIALGOODS = RAND:MIN(SELECTED_CHARA_NUM, SLAVEMARKET_MAX_GOODS)
	SLAVEMARKET_PRICE:SLAVEMARKET_SPECIALGOODS /= 2
ELSE
	SLAVEMARKET_SPECIALGOODS = SLAVEMARKET_MAX_GOODS
ENDIF
RETURN 1

@SELECT_CHARA_RANDOM_LOGIC_SLAVEMARKET_PREPARE_GOODS(対象)
#DIM 対象
;SPキャラは対象としない（野盗そのものがリストアップされるのを防ぐ
SIF IS_SP_CHARA(対象)
	RETURN 0
SIF CFLAG:対象:所属 == CFLAG:MASTER:所属
	RETURN 0
;未購入の奴隷市場用モブ、特殊勢力の捕虜、特殊勢力所属で捕虜でない
RETURN (TALENT:対象:奴隷市場モブ && !IS_APPEAR(対象)) || IS_SP_COUNTRY(CFLAG:対象:捕虜先) || (IS_SP_COUNTRY(CFLAG:対象:所属) && !CFLAG:対象:捕虜先)

;-------------------------------------------------
;DESC  :奴隷市場用のモブの削除
;-------------------------------------------------
@CLEARN_SLAVE_MARKET_MOB()
;主人公と同一勢力じゃなくなった、もしくは前のターンで買われなかったキャラは削除
FOR LOCAL:0, CHARANUM - 1, -1, -1
	SIF !TALENT:LOCAL:奴隷市場モブ
		CONTINUE
	SIF IS_APPEAR(LOCAL) && CFLAG:LOCAL:所属 == CFLAG:MASTER:所属
		CONTINUE
	CALL DELETE_CHARA(LOCAL)
NEXT

;-------------------------------------------------
;DESC  :奴隷市場用のモブの生成
;-------------------------------------------------
@CREATE_SLAVE_MARKET_MOB()
#DIM 対象
#DIMS 相手
SKIPDISP 1
FOR LOCAL, 0, 5
	CALL CREATE_RANDOM_CHARA(0, 0, RAND:2)
	対象 = RESULT
	IF IS_FEMALE(対象)
		FOR LOCAL:1, 1, RAND(10, 20)
			SELECTCASE RAND:100
				CASE 0
					相手 = ごろつき
				CASE 1
					相手 = 娼館の客
				CASE 2
					相手 = 触手
				CASE 3
					相手 = 野盗
				CASE 4
					相手 = AV男優
				CASE 5 
					相手 = 貴族
				CASE 6 
					相手 = 平民
				CASE 7 
					相手 = 行きずりの男
				CASE 8 
					相手 = 妖怪
				CASE 9 
					相手 = チャラ男
				CASE 10 
					相手 = 兵士
				CASE 11 
					相手 = 浮浪者
				CASE 12 
					相手 = やくざ
				CASE 13
					相手 = ホフゴブリン
				CASE 14
					相手 = 鬼
				CASE 15
					相手 = 子供
				CASE 16
					相手 = 犬
				CASE 17
					相手 = 馬
				CASE 18
					相手 = 豚
				CASE 19
					相手 = 天狗
				CASE 20
					相手 = 河童
				CASE 21
					相手 = 触手
				CASE 22
					相手 = 不明
				CASEELSE
					相手 = 調教師
			ENDSELECT
			SELECTCASE RAND:100
				CASE 0 TO 19
					CALL FUCK_GANGBANG(対象, GET_SPERM_ID(相手), @"%相手%の\@ RAND:2 ? ペニス # 唇\@", 相手)
				CASE 20 TO 59
					CALL FUCK_RAPE(対象, GET_SPERM_ID(相手), @"%相手%の\@ RAND:2 ? ペニス # 唇\@", 相手)
				CASE 60 TO 64
					CALL FUCK_BEAST(対象, GET_SPERM_ID("犬"), @"犬の\@ RAND:2 ? ペニス # 唇\@", "犬")
				CASE 65 TO 69
					CALL FUCK_BEAST(対象, GET_SPERM_ID("豚"), @"豚の\@ RAND:2 ? ペニス # 唇\@", "豚")
				CASE 70 TO 74
					CALL FUCK_BEAST(対象, GET_SPERM_ID("馬"), @"馬の\@ RAND:2 ? ペニス # 唇\@", "馬")
				CASE 80 TO 84
					CALL FUCK_TENTACLE(対象, GET_SPERM_ID("触手"), "触手", "触手")
				CASE 85 TO 89
					CALL FUCK(対象, "欲望, 奉仕, 性技, 性交, 精愛, Ｃ, Ｖ, Ａ, Ｂ, Ｍ, 売春, Ｖ性交, Ａ性交", "キス喪失, 処女喪失, Ａ処女喪失, 膣内射精, 腸内射精, 口内射精", GET_SPERM_ID("娼館の客"), "娼館の客のペニス", "娼館の客", "", "売春")
				CASE 90 TO 94
					CALL FUCK(対象, "欲望, 性技, Ｃ, Ｖ, Ｂ, Ｍ, 性交, 精愛, 口淫, Ｖ性交, 撮影", "キス喪失, 処女喪失, 膣内射精, 口内射精", GET_SPERM_ID("AV男優"), @"男優の\@RAND:2 ? ペニス # 唇\@", "AV男優", "", "和姦")
				CASEELSE
					CALL FUCK(対象, "欲望, 奉仕, 性技, 射精, レズ, Ｃ", "", GET_SPERM_ID("サキュバス"), "", "", "", "調教")
			ENDSELECT
			;確率で出産経験を追加
			IF RAND:100 < 3
				EXP:対象:妊娠経験 = RAND(1, 5)
				EXP:対象:出産経験 = !RAND:3
				CALL REFRESH_PREGNANT_RECORD(対象, GET_SPERM_ID(相手), 1)
				;確率で母乳体質を固定する
				SIF RAND:100 < 15
					TALENT:対象:母乳体質 = 1
			ENDIF

		NEXT
	ELSE
		相手 = 調教師
		FOR LOCAL:1, 1, RAND(10, 20)
			SELECTCASE RAND:2
				CASE 0 TO 19
					CALL FUCK_GANGBANG(対象, GET_SPERM_ID(相手), @"%相手%の\@ RAND:2 ? 膣 # 唇\@", 相手)
				CASE 20 TO 39
					CALL FUCK_RAPE(対象, GET_SPERM_ID(相手), @"%相手%の\@ RAND:2 ? 膣 # 唇\@", 相手)
				CASEELSE
					CALL FUCK(対象, "欲望, 技巧, 性技, 奉仕, 性交, 射精, レズ, Ｃ, Ｂ, Ｍ, Ｖ挿入, Ａ挿入", "キス喪失, 童貞喪失", 0, "調教師の秘貝", "調教師", "調教師の膣", "調教")
			ENDSELECT
		NEXT
	ENDIF
	TALENT:対象:奴隷市場モブ = 1
	TALENT:対象:チョロイン = 1
	TALENT:対象:隷属 = 1
	TALENT:対象:服従 = 1
	TALENT:対象:合意 = 1
	CFLAG:対象:従属度 = 10000
	CFLAG:対象:依存度 = 3000
	;戦闘で使い物にならなくしておく
	TALENT:対象:成長型 = 10
	ABL:対象:武闘 = RAND(1, 5)
	ABL:対象:防衛 = RAND(1, 5)
	ABL:対象:知略 = RAND(1, 5)
	ABL:対象:政治 = RAND(1, 5)
	ABL:対象:料理 = RAND(1, 5)
	ABL:対象:歌唱 = RAND(1, 5)
	;傾向的にサドが不足気味になるので追加
	ABL:対象:サド += RAND(0, 3)
	TALENT:対象:Ｖ締まり = GET_RANDOM_TIGHTNESS()
	TALENT:対象:Ａ締まり = GET_RANDOM_TIGHTNESS()
	;FUCKで主導度がガン下がりしているのでリセット
	CALL INIT_TENDENCY(対象)
	;淫乱素質をつける
	FOR LOCAL:1, 0, 5
		SIF RAND:100 < 5
			SETBIT TALENT:対象:淫乱系, RAND:VARSIZE("SEX_TALENT_NAME")
	NEXT
	CALL CLEAR_CREAMPIE_HISTORY(対象, 1)
NEXT
SKIPDISP 0
