;	-----------------------------------------------
;対象キャラを選択するアイテムの使用条件
;-------------------------------------------------
;●ふたなり薬
@IS_ITEM_USE280(ARG:0)
SIF IS_ANIMAL(ARG:0)
	RETURN 0
IF (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
	RETURN 1
ENDIF
RETURN 0

;●性転換薬
@IS_ITEM_USE281(ARG:0)
IF !IS_ANIMAL(ARG:0) && ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属)
	RETURN 1
ENDIF
RETURN 0

;●岩神ノ秘薬
@IS_ITEM_USE282(ARG:0)
IF (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
	IF GROUPMATCH(TALENT:(ARG:0):性別, 0, 4) && !TALENT:(ARG:0):孕み可能
		RETURN 1
	ENDIF
ENDIF
RETURN 0

;●獣変化の秘薬
@IS_ITEM_USE283(ARG:0)
IF (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
	IF CFLAG:(ARG:0):獣変化フラグ || (!TALENT:(ARG:0):動物耳 && !TALENT:(ARG:0):しっぽ)
		RETURN 1
	ENDIF
ENDIF
RETURN 0

;●再生の秘薬
@IS_ITEM_USE284(ARG:0)
IF ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属) && ARG:0 != NAME_TO_CHARA("あなた")
	RETURN 1
ENDIF
RETURN 0

;●太平要術の書
@IS_ITEM_USE141(ARG:0)
IF (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
	IF !TALENT:(ARG:0):妖術知識
		RETURN 1
	ENDIF
ENDIF
RETURN 0

;●淫技大全
@IS_ITEM_USE142(ARG:0)
IF (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
	IF ABL:(ARG:0):性知識 == 4 && ABL:(ARG:0):性技 >= 6
		RETURN 1
	ENDIF
ENDIF
RETURN 0

;●永久脱毛剤
@IS_ITEM_USE287(ARG:0)
SIF TALENT:(ARG:0):陰毛現在値 != 陰毛_パイパン
	RETURN (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
RETURN 0

;●入れ墨除去手術
@IS_ITEM_USE288(ARG:0)
FOR LOCAL, 0, VARSIZE("TATOO")
	SIF TATOO:(ARG:0):LOCAL != ""
		RETURN (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属
NEXT
RETURN 0

;●穴トレ
@IS_ITEM_USE289(ARG:0)
RETURN (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属

;●美容
@IS_ITEM_USE290(ARG:0)
RETURN ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属)

;●危険日ずらし
@IS_ITEM_USE291(ARG:0)
RETURN ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属) && HAS_VAGINA(ARG:0)


;避妊薬
@IS_ITEM_USE292(ARG:0)
RETURN !CFLAG:(ARG:0):妊娠期間 && IS_FEMALE(ARG:0) && CFLAG:(ARG:0):避妊薬残ターン == 0 && ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属)

;堕胎薬
@IS_ITEM_USE293(ARG:0)
RETURN TALENT:(ARG:0):妊娠  && IS_FEMALE(ARG:0) && ((CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属)

;●能動受動
@IS_ITEM_USE294(ARG:0)
RETURN (CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0) || CFLAG:(ARG:0):捕虜先 == CFLAG:MASTER:所属

;-------------------------------------------------
;アイテムの対象キャラを選択してその場で使用
;ARG:0=アイテム番号
;-------------------------------------------------
@SELECT_ITEM_USER(ARG:0)
#DIM FIRST_LINE

CLEARLINE 3

;全員の調教参加フラグをクリア
CVARSET CFLAG, 6, 0

;アイテムを使用可能なキャラの数を数える
LOCAL:5 = 0
FOR LOCAL:0, 0, CHARANUM
	RESULT = 0
	TRYCALLFORM IS_ITEM_USE{ARG:0}(LOCAL:0)
	IF RESULT
		LOCAL:5 ++
	ENDIF
NEXT

;ページ数の計算
LOCAL:10 = (LOCAL:5 - 1) / 44 + 1
LOCAL:11 = 1
SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR
PRINTFORM %TOSTR_SPACE(6)%
PRINTFORML %ITEMNAME:(ARG:0)%を誰に使用しますか？
SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR
FIRST_LINE = LINECOUNT

$SHOW_LOOP

;キャラリストの表示
LOCAL:12 = (LOCAL:11 - 1) * 44
LOCAL:13 = LOCAL:11* 44
LOCAL:14 = 0
LOCAL:15 = 0
LOCAL:16 = 4
PRINTFORM %TOSTR_SPACE(6)%
FOR LOCAL:0, 0, CHARANUM
	RESULT = 0
	TRYCALLFORM IS_ITEM_USE{ARG:0}(LOCAL:0)
	IF RESULT
		IF LOCAL:14 >= LOCAL:12 && LOCAL:14 < LOCAL:13
			IF LOCAL:15 % 2 != 0
				PRINTPLAINFORM %TOSTR_SPACE(2)%
			ELSEIF LOCAL:15 >= 1
				PRINTL 
				PRINTFORM %TOSTR_SPACE(6)%
				LOCAL:16 ++
			ENDIF
			;CALL PRINT_PARTNER_DATA(LOCAL:0)
			CALL COLUMN_RIGHT_CHARALIST_BUTTON(LOCAL:0, 0, , , 1)
			LOCAL:15 ++
		ENDIF
		LOCAL:14 ++
	ENDIF
NEXT
PRINTL 
SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR

IF LOCAL:10 >= 2
	LOCAL:16 += 2
	IF LOCAL:11 > 1
		PRINTBUTTON @"[%"前のページ", MAX_CHARANAME_LENGTH, LEFT%]", SHOP_LIFE_LIST1_PAGE_BACK
	ELSE
		SETCOLOR カラー_選択不可
		PRINTPLAINFORM [%"前のページ", MAX_CHARANAME_LENGTH, LEFT%]
		RESETCOLOR
	ENDIF
	LOCALS:0 = page{LOCAL:11}/{LOCAL:10}
	PRINTPLAINFORM %TOSTR_SPACE(10)%
	PRINTPLAINFORM %LOCALS:0, 16, LEFT%
	IF LOCAL:11 < LOCAL:10
		PRINTBUTTON @"[%"次のページ", MAX_CHARANAME_LENGTH, LEFT%]", SHOP_LIFE_LIST1_PAGE_NEXT
	ELSE
		SETCOLOR カラー_選択不可
		PRINTPLAINFORM [%"次のページ", MAX_CHARANAME_LENGTH, LEFT%]
		RESETCOLOR
	ENDIF
	PRINTL 
	SETCOLOR カラー_灰色
	CALL SINGLE_DRAWLINE
	RESETCOLOR
ENDIF

PRINTPLAIN  
PRINTBUTTON @"[%"戻る", MAX_CHARANAME_LENGTH, LEFT%]", 0
PRINTFORML 

REDRAW 0

$INPUT_LOOP
INPUT 0

CLEARLINE 2

IF RESULT == 0
	REDRAW 1
	RETURN 0
ELSEIF RESULT == SHOP_LIFE_LIST1_PAGE_BACK
	LOCAL:11 --
	IF LOCAL:11 < 0
		LOCAL:11 = LOCAL:10
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == SHOP_LIFE_LIST1_PAGE_NEXT
	LOCAL:11 ++
	IF LOCAL:11 > LOCAL:10
		LOCAL:11 = 0
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSE
	LOCAL:20 = RESULT - 100
	LOCAL:21 = NO_TO_CHARA(LOCAL:20)
	IF LOCAL:21 >= 0
		RESULT = 0
		TRYCALLFORM IS_ITEM_USE{ARG:0}(LOCAL:21)
		IF RESULT
			CALLFORM ITEM_EFFECT{ARG:0}(LOCAL:21)
			REDRAW 1
			RETURN 1
		ENDIF
	ENDIF
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
;対象キャラを選択してその場で使用するアイテムの効果
;-------------------------------------------------
;●ふたなり薬
@ITEM_EFFECT280(ARG:0)
SELECTCASE TALENT:(ARG:0):性別
	CASE 0
		TALENT:(ARG:0):性別 = 3
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%をふたなりにしました
	CASE 1
		TALENT:(ARG:0):性別 = 2
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%をふたなりにしました
	CASE 2
		TALENT:(ARG:0):性別 = 1
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%を女性にしました
	CASE 3
		TALENT:(ARG:0):性別 = 0
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%を男性にしました
	CASE 4
		TALENT:(ARG:0):性別 = 5
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%をふたなりにしました
	CASE 5
		TALENT:(ARG:0):性別 = 4
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%を男の娘にしました
ENDSELECT
;ピアス穴を塞ぐ
CALL CLEAR_PIERCE_HOLE_SEX_CHANGE(ARG:0)

;女ふたなりになった場合(あたらしくチンポがついた場合)
IF TALENT:(ARG:0):性別 == 2
	PRINTFORML %TOSTR_SPACE(6)%ペニスのサイズは……
	CALL ASK_MULTI("超短小", "短小", "普通", "巨根", "超巨根")
	CALL SET_PENISSIZE(ARG:0, RESULT -2)
ENDIF

IF GROUPMATCH(TALENT:(ARG:0):性別, 2, 3, 5)
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%を童貞に戻しておきますか？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%を童貞に戻しました
		CALL VIRGINIZE(ARG:0, 初体験_童貞)
	ENDIF
ENDIF

;●性転換薬
@ITEM_EFFECT281(ARG:0)
SELECTCASE TALENT:(ARG:0):性別
	CASE 0
		TALENT:(ARG:0):性別 = 1
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%を女性にしました
	CASE 1
		IF CFLAG:(ARG:0):元男の娘フラグ
			TALENT:(ARG:0):性別 = 4
			PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%を男の娘にしました
			CFLAG:(ARG:0):元男の娘フラグ = 0
		ELSE
			TALENT:(ARG:0):性別 = 0
			PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%を男性にしました
		ENDIF
	CASE 2
		IF CFLAG:(ARG:0):元男の娘フラグ
			TALENT:(ARG:0):性別 = 5
			PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%を男の娘ふたなりにしました
			CFLAG:(ARG:0):元男の娘フラグ = 0
		ELSE
			TALENT:(ARG:0):性別 = 3
			PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%を男ふたなりにしました
		ENDIF
	CASE 3
		TALENT:(ARG:0):性別 = 2
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%を女ふたなりにしました
	CASE 4
		TALENT:(ARG:0):性別 = 1
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%を女性にしました
		;元男の娘フラグ
		CFLAG:(ARG:0):元男の娘フラグ = 1
	CASE 5
		TALENT:(ARG:0):性別 = 2
		PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%を女ふたなりにしました
		;元男の娘フラグ
		CFLAG:(ARG:0):元男の娘フラグ = 1
ENDSELECT
;ピアス穴を塞ぐ
CALL CLEAR_PIERCE_HOLE_SEX_CHANGE(ARG:0)

;●岩神ノ秘薬
@ITEM_EFFECT282(ARG:0)
TALENT:(ARG:0):孕み可能 = 1
PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%はお尻の穴で孕めるようになりました

;●獣変化の秘薬
@ITEM_EFFECT283(ARG:0)
IF CFLAG:(ARG:0):獣変化フラグ
	TALENT:(ARG:0):動物耳 = 0
	TALENT:(ARG:0):しっぽ = 0
	CFLAG:(ARG:0):獣変化フラグ = 0
	PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%は<動物耳><しっぽ>を失いました
ELSE
	TALENT:(ARG:0):動物耳 = 1
	TALENT:(ARG:0):しっぽ = 1
	CFLAG:(ARG:0):獣変化フラグ = 1
	PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%は<動物耳><しっぽ>を得ました
ENDIF

;●再生の秘薬
@ITEM_EFFECT284(ARG:0)
#DIM FIRST_LINE
IF !EXISTCSV(NO:(ARG:0))
	CALL OLD_ITEM_EFFECT_284(ARG:0)
	RETURN
ENDIF
PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%が再生の秘薬を飲むが……
PRINTFORML
FIRST_LINE = LINECOUNT
$SHOW_LOOP_SET
LOCAL:1 = CFLAG:(ARG:0):所属
LOCAL:2 = CFLAG:(ARG:0):捕虜先
CALL CLEAR_ALL_PASS_SETTINGS()
CALL PASS_CHARA_UI(ARG)
CALL PASS_CHARA(ARG)
CFLAG:(ARG:0):所属 = LOCAL:1
CFLAG:(ARG:0):捕虜先 = LOCAL:2

;デフォルトで処女でないキャラは、再生させるか確認する
IF !CSVTALENT(NO:(ARG:0), GETNUM(TALENT, "処女"))
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%はデフォルトで処女でないので、先ほどの選択で処女を初期化しても非処女のままです
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の処女を再生しますか？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の処女を再生しました
		CALL VIRGINIZE(ARG:0, 初体験_処女)
	ENDIF
ENDIF
IF !CSVTALENT(NO:(ARG:0), GETNUM(TALENT, "アナル処女"))
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%はデフォルトでアナル処女でないので、先ほどの選択でアナル処女を初期化しても非処女のままです
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%のアナル処女を再生しますか？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%のアナル処女を再生しました
		CALL VIRGINIZE(ARG:0, 初体験_アナル処女)
	ENDIF
ENDIF
IF !CSVTALENT(NO:(ARG:0), GETNUM(TALENT, "童貞"))
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%はデフォルトで童貞でないので、先ほどの選択で童貞を初期化しても童貞のままです
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の童貞を再生しますか？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の童貞を再生しました
		CALL VIRGINIZE(ARG:0, 初体験_童貞)
	ENDIF
ENDIF
IF !CSVTALENT(NO:(ARG:0), GETNUM(TALENT, "キス未経験"))
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%はデフォルトでキス未経験でないので、先ほどの選択でキス未経験を初期化してもキス経験済みのままです
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%のキス未経験を再生しますか？
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%のキス未経験を再生しました
		CALL VIRGINIZE(ARG:0, 初体験_キス)
	ENDIF
ENDIF


PRINTFORML %TOSTR_SPACE(6)%デイリーイベントで得る素質を残しますか？
CALL ASK_YN("残す", "消去")
SIF RESULT == 1
	TALENT:(ARG:0):デイリー系 = 0

PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の肉体が再生されました
WAIT
RETURN

;固定キャラ以外に再生の秘薬を飲ませた場合
@OLD_ITEM_EFFECT_284(ARG:0)
PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の肉体が再生されました
FOR LOCAL:0, 0, 5
	ABL:(ARG:0):(LOCAL:0) = 0
	EXP:(ARG:0):(LOCAL:0) = 0
NEXT
IF TALENT:(ARG:0):処女 != 1
	CALL VIRGINIZE(ARG:0, 初体験_処女)
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の<処女>が再生されました
ENDIF
IF TALENT:(ARG:0):キス未経験 != 1
		CALL VIRGINIZE(ARG:0, 初体験_キス)
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の<キス未経験>が再生されました
ENDIF
IF TALENT:(ARG:0):童貞 != 1
		CALL VIRGINIZE(ARG:0, 初体験_童貞)
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の<童貞>が再生されました
ENDIF
IF TALENT:(ARG:0):アナル処女 != 1
		CALL VIRGINIZE(ARG:0, 初体験_アナル処女)
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の<アナル処女>が再生されました
ENDIF

IF TALENT:(ARG:0):烙印
	TALENT:(ARG:0):烙印 = 0
	PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の体から<烙印>が消えました
ENDIF
TALENT:(ARG:0):特殊勢力陥落系 = 0
TALENT:(ARG:0):デイリー系 = 0
WAIT


;●永久脱毛剤
@ITEM_EFFECT287(ARG:0)
PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の股間に永久脱毛剤を塗り込んだ
PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%は<パイパン>になった
TALENT:(ARG:0):陰毛目標値 = 陰毛_パイパン
TALENT:(ARG:0):陰毛現在値 = 陰毛_パイパン

;●入れ墨除去手術
@ITEM_EFFECT288(ARG:0)
PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%のどの入れ墨を除去しますか？
FOR LOCAL, 0, VARSIZE("TATOO")
	SIF TATOO:(ARG:0):(LOCAL) != ""
		PRINTFORML %TOSTR_SPACE(6)%[{LOCAL}] %GET_TATOO_NAME(LOCAL)% - 「%TATOO:(ARG:0):(LOCAL)%」
NEXT
SETCOLOR カラー_灰色
CALL SINGLE_DRAWLINE
RESETCOLOR
PRINTPLAIN  
PRINTBUTTON @"[%"戻る", MAX_CHARANAME_LENGTH, LEFT%]", 99
PRINTL 
$INPUT_LOOP
INPUT 99
IF RESULT == 99
	CALL GET_PRICE288
	MONEY += RESULT
	RETURN
ELSEIF 0 <= RESULT && RESULT < VARSIZE("TATOO") && TATOO:(ARG:0):RESULT != ""
	PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%の%GET_TATOO_NAME(RESULT)%のタトゥーを除去しました……
	TATOO:(ARG:0):RESULT = 
ELSE
	GOTO INPUT_LOOP
ENDIF

;●穴トレ
@ITEM_EFFECT289(ARG:0)
#DIMS 変化前
#DIMS 変化後
PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%のどちらの穴を鍛えますか？
CALL ASK_MULTI_JUDGE("膣", HAS_VAGINA(ARG:0), "尻", 1, @"%"戻る", MAX_CHARANAME_LENGTH, LEFT%", 1)
SELECTCASE RESULT
	CASE 0
		変化前 = %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ｖ締まり)%
		TALENT:(ARG:0):Ｖ締まり = LIMIT(TALENT:(ARG:0):Ｖ締まり + 100, 締まり_最低値, 締まり_最高値)
		変化後 = %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ｖ締まり)%
		CALL COLORPRINT(@"%TOSTR_SPACE(6)%%ANAME(ARG:0)%の膣穴をトレーニングしました！", カラー_注意, "W")
		CALL COLORPRINT(@"変化前:%変化前%　→　変化後:%変化後%", カラー_注意, "W")
	CASE 1
		変化前 = %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ａ締まり)%
		TALENT:(ARG:0):Ａ締まり = LIMIT(TALENT:(ARG:0):Ａ締まり + 100, 締まり_最低値, 締まり_最高値)
		変化後 = %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ａ締まり)%
		CALL COLORPRINT(@"%TOSTR_SPACE(6)%%ANAME(ARG:0)%の尻穴をトレーニングしました！", カラー_注意, "W")
		CALL COLORPRINT(@"変化前:%変化前%　→　変化後:%変化後%", カラー_注意, "W")
	CASE 2
		CALL GET_PRICE289
		MONEY += RESULT
		RETURN
ENDSELECT
RETURN

;●美容手術
@ITEM_EFFECT290(ARG:0)
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
PRINTFORML %TOSTR_SPACE(6)%%ANAME(ARG:0)%の身体をどうしますか？
IF HAS_PENIS(ARG:0)
	PRINTFORM 竿:
	CALL PRINT_SELECT_BUTTON("{超短小}", 0, GET_PENISSIZE_NUM(ARG:0) == -2)
	CALL PRINT_SELECT_BUTTON("{短小}",   1, GET_PENISSIZE_NUM(ARG:0) == -1)
	CALL PRINT_SELECT_BUTTON("{並根}",   2, GET_PENISSIZE_NUM(ARG:0) ==  0)
	CALL PRINT_SELECT_BUTTON("{巨根}",   3, GET_PENISSIZE_NUM(ARG:0) ==  1)
	CALL PRINT_SELECT_BUTTON("{超巨根}", 4, GET_PENISSIZE_NUM(ARG:0) ==  2)
	PRINTL
ENDIF
IF IS_FEMALE(ARG:0)
	PRINTFORM 胸:
	CALL PRINT_SELECT_BUTTON("{絶壁}", 10, GET_BUSTSIZE_NUM(ARG:0) == -2)
	CALL PRINT_SELECT_BUTTON("{貧乳}", 11, GET_BUSTSIZE_NUM(ARG:0) == -1)
	CALL PRINT_SELECT_BUTTON("{並乳}", 12, GET_BUSTSIZE_NUM(ARG:0) == 0)
	CALL PRINT_SELECT_BUTTON("{巨乳}", 13, GET_BUSTSIZE_NUM(ARG:0) == 1)
	CALL PRINT_SELECT_BUTTON("{爆乳}", 14, GET_BUSTSIZE_NUM(ARG:0) == 2)
	PRINTL
ENDIF
IF HAS_VAGINA(ARG:0)
	PRINTFORM Ｖ:
	LOCALS = %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ｖ締まり)%
	CALL PRINT_SELECT_BUTTON("{崩壊}",     40, LOCALS == TOSTR_TIGHTNESS(締まり_最低値))
	CALL PRINT_SELECT_BUTTON("{がばがば}", 41, LOCALS == TOSTR_TIGHTNESS(締まり_がばがば))
	CALL PRINT_SELECT_BUTTON("{ぽっかり}", 42, LOCALS == TOSTR_TIGHTNESS(締まり_ぽっかり))
	CALL PRINT_SELECT_BUTTON("{ゆるゆる}", 43, LOCALS == TOSTR_TIGHTNESS(締まり_ゆるゆる))
	CALL PRINT_SELECT_BUTTON("{ゆるめ}",   44, LOCALS == TOSTR_TIGHTNESS(締まり_ゆるめ))
	CALL PRINT_SELECT_BUTTON("{普通}",     45, LOCALS == TOSTR_TIGHTNESS(締まり_普通))
	CALL PRINT_SELECT_BUTTON("{名器}",     46, LOCALS == TOSTR_TIGHTNESS(締まり_名器))
	CALL PRINT_SELECT_BUTTON("{きゅっきゅっ}", 47, LOCALS == TOSTR_TIGHTNESS(締まり_きゅっきゅっ))
	CALL PRINT_SELECT_BUTTON("{きつきつ}", 48, LOCALS == TOSTR_TIGHTNESS(締まり_きつきつ))
	CALL PRINT_SELECT_BUTTON("{ぎちぎち}", 49, LOCALS == TOSTR_TIGHTNESS(締まり_ぎちぎち))
	PRINTL
ENDIF

LOCALS =  %TOSTR_TIGHTNESS(TALENT:(ARG:0):Ａ締まり)%
PRINTFORM Ａ:
CALL PRINT_SELECT_BUTTON("{崩壊}",     50, LOCALS == TOSTR_TIGHTNESS(締まり_最低値))
CALL PRINT_SELECT_BUTTON("{がばがば}", 51, LOCALS == TOSTR_TIGHTNESS(締まり_がばがば))
CALL PRINT_SELECT_BUTTON("{ぽっかり}", 52, LOCALS == TOSTR_TIGHTNESS(締まり_ぽっかり))
CALL PRINT_SELECT_BUTTON("{ゆるゆる}", 53, LOCALS == TOSTR_TIGHTNESS(締まり_ゆるゆる))
CALL PRINT_SELECT_BUTTON("{ゆるめ}",   54, LOCALS == TOSTR_TIGHTNESS(締まり_ゆるめ))
CALL PRINT_SELECT_BUTTON("{普通}",     55, LOCALS == TOSTR_TIGHTNESS(締まり_普通))
CALL PRINT_SELECT_BUTTON("{名器}",     56, LOCALS == TOSTR_TIGHTNESS(締まり_名器))
CALL PRINT_SELECT_BUTTON("{きゅっきゅっ}", 57, LOCALS == TOSTR_TIGHTNESS(締まり_きゅっきゅっ))
CALL PRINT_SELECT_BUTTON("{きつきつ}", 58, LOCALS == TOSTR_TIGHTNESS(締まり_きつきつ))
CALL PRINT_SELECT_BUTTON("{ぎちぎち}", 59, LOCALS == TOSTR_TIGHTNESS(締まり_ぎちぎち))

PRINTL
CALL PRINT_TOGGLE_BUTTON("{美脚}", 20, TALENT:(ARG:0):美脚)
PRINTL
CALL PRINT_SELECT_BUTTON("{普尻}", 30, !TALENT:(ARG:0):美尻 && !TALENT:(ARG:0):豊尻)
CALL PRINT_SELECT_BUTTON("{美尻}", 31, TALENT:(ARG:0):美尻)
CALL PRINT_SELECT_BUTTON("{豊尻}", 32, TALENT:(ARG:0):豊尻)
PRINTL

PRINTBUTTON "[99] 完了", 99

INPUT
IF INRANGE(RESULT, 0, 4) && HAS_PENIS(ARG:0)
	CALL SET_PENISSIZE(ARG:0, RESULT - 2)
ELSEIF INRANGE(RESULT, 10, 14) && IS_FEMALE(ARG:0)
	CALL SET_BUSTSIZE(ARG:0, RESULT - 12)
	CFLAG:(ARG:0):妊娠時バストサイズ増加 = 0
ELSEIF RESULT == 20
	TALENT:(ARG:0):美脚 = !TALENT:(ARG:0):美脚
ELSEIF RESULT == 30
	TALENT:(ARG:0):美尻 = 0
	TALENT:(ARG:0):美脚 = 0
ELSEIF RESULT == 31
	TALENT:(ARG:0):豊尻 = 0
	TALENT:(ARG:0):美尻 = 1
ELSEIF RESULT == 32
	TALENT:(ARG:0):美尻 = 0
	TALENT:(ARG:0):豊尻 = 1
ELSEIF INRANGE(RESULT, 40, 49) && HAS_VAGINA(ARG:0)
	TALENT:(ARG:0):Ｖ締まり = (RESULT - 40) * 100 + 50
ELSEIF INRANGE(RESULT, 50, 59)
	TALENT:(ARG:0):Ａ締まり = (RESULT - 50) * 100 + 50
ELSEIF RESULT == 99
	CALL COLORPRINT(@"%ANAME(ARG:0)%の美容手術が終了した！", カラー_注意, "W")
	RETURN
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;●危険日ずらし
@ITEM_EFFECT291(ARG:0)
PRINTFORML %ANAME(ARG:0)%の危険日をいつにしようか……
PRINTFORML 現在の危険日:{TALENT:(ARG:0):危険日}
PRINTFORML （0-4で入力、5期ごとに来ます）
$INPUT_LOOP
INPUT

IF !INRANGE(RESULT, 0, 4)
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

LOCAL = RESULT
TALENT:(ARG:0):危険日 = LOCAL

CALL COLORPRINT(@"%ANAME(ARG:0)%の危険日が{LOCAL}になった！", カラー_注意, "W")


;●避妊薬
@ITEM_EFFECT292(ARG:0)
PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%はしばらく妊娠しなくなった……
CFLAG:(ARG:0):避妊薬残ターン += 5
CALL CLEAR_CREAMPIE_HISTORY(LOCAL:0)

;●堕胎手術
@ITEM_EFFECT293(ARG:0)
PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%に堕胎手術を受けさせた……
PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%は＜妊娠＞ではなくなった
;状況に応じて崩壊値が増えたりする処理をいれたい

;妊娠関係のフラグをクリア
SIF CFLAG:(ARG:0):行動不能状態 == 行動不能_臨月
	CFLAG:(ARG:0):行動不能状態 = 0
CFLAG:(ARG:0):妊娠期間 = 0
CFLAG:(ARG:0):子の父親 = 0
TALENT:(ARG:0):妊娠 = 0

;中出し履歴をクリア
CALL CLEAR_CREAMPIE_HISTORY(ARG:0)
IF CFLAG:(ARG:0):育児期間 == 0
	CALL RESET_MOTHER_STATE(ARG:0, 0)
ENDIF

;●主導操作の法
@ITEM_EFFECT294(ARG:0)
#DIM FIRST_LINE
#DIM 操作項目
#DIM 操作後値
FIRST_LINE = LINECOUNT
PRINTFORML %ANAME(ARG:0)%に主導操作を行う……
PRINTFORML [0] 主導度Ｕ
PRINTFORML [1] 主導度Ｎ
PRINTFORML [2] 倒錯度

INPUT

IF !INRANGE(RESULT, 0, 2)
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF

操作項目 = RESULT

PRINTFORML いくらにしようか……

IF GROUPMATCH(操作項目, 0, 1)
	PRINTFORML (-1000 ～ 1000で入力、範囲外でキャンセル)
ELSE
	PRINTFORML (0 ～ 1000で入力、範囲外でキャンセル)
ENDIF

INPUT

IF (GROUPMATCH(操作項目, 0, 1) && !INRANGE(RESULT, -1000, 1000)) || (操作項目 == 2 && !INRANGE(RESULT, 0, 1000))
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF

操作後値 = RESULT

SELECTCASE 操作項目
	CASE 0
		PRINTFORMW %ANAME(ARG:0)%の主導度Ｕが{操作後値}になりました
		ABL:(ARG:0):主導度Ｕ = 操作後値
	CASE 1
		PRINTFORMW %ANAME(ARG:0)%の主導度Ｎが{操作後値}になりました
		ABL:(ARG:0):主導度Ｎ = 操作後値
	CASE 2
		PRINTFORMW %ANAME(ARG:0)%の倒錯度が{操作後値}になりました
		ABL:(ARG:0):倒錯度 = 操作後値
ENDSELECT

;●太平要術の書
@ITEM_EFFECT141(ARG:0)
TALENT:(ARG:0):妖術知識 = 1
PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%が<妖術知識>を身につけました

;●淫技大全
@ITEM_EFFECT142(ARG:0)
ABL:(ARG:0):性知識 = 5
PRINTFORMW %TOSTR_SPACE(6)%%ANAME(ARG:0)%の性知識が上昇しました(Lv5)

