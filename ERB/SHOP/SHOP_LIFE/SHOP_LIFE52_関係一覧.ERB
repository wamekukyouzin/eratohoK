;-------------------------------------------------
;「関係一覧」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME52
RESULTS:0 '= "関係一覧"

;-------------------------------------------------
;「関係一覧」の選択可否
;-------------------------------------------------
@SHOP_LIFE_CHECK52
SIF CFLAG:MASTER:捕虜先
	RETURN 0
RETURN 1

;-------------------------------------------------
;「関係一覧」の選択可能キャラ存在判定
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA52(ARG:0)
;能力表示
CALL SHOP_LIFE_CHECKCHARA_NOURYOKU(ARG:0)
RETURN RESULT

;-------------------------------------------------
;「関係一覧」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY52
FLAG:拠点フェイズページ = 1
FLAG:関係一覧表示対象 = 0
FLAG:関係一覧表示モード = 0
FLAG:夜這い = 0
RETURN 0

;-------------------------------------------------
;「関係一覧」の右カラムキャラリストボタン表示に追尾させる情報
;　この追記後デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD52(ARG:0)
PRINTPLAIN  
IF FLAG:関係一覧表示対象 >= 0
	IF FLAG:関係一覧表示モード == 0
		SETCOLOR 0xE98FB0
		PRINTFORM 好
		SETCOLOR カラー_注釈
		PRINTFORM :
		RESETCOLOR
		PRINTFORM  %NUM_FORMAT(REL_LIKE:(FLAG:関係一覧表示対象):(ARG:0)), 5%
		PRINTFORM   
		SETCOLOR 0x8F99E0
		PRINTFORM 嫌
		SETCOLOR カラー_注釈
		PRINTFORM :
		RESETCOLOR
		PRINTFORM  %NUM_FORMAT(REL_HATE:(FLAG:関係一覧表示対象):(ARG:0)), 5% 
	ELSE
		SETCOLOR 0xE98FB0
		PRINTFORM 好
		SETCOLOR カラー_注釈
		PRINTFORM :
		RESETCOLOR
		PRINTFORM  %NUM_FORMAT(REL_LIKE:(ARG:0):(FLAG:関係一覧表示対象)), 5%
		PRINTFORM   
		SETCOLOR 0x8F99E0
		PRINTFORM 嫌
		SETCOLOR カラー_注釈
		PRINTFORM :
		RESETCOLOR
		PRINTFORM  %NUM_FORMAT(REL_HATE:(ARG:0):(FLAG:関係一覧表示対象)), 5% 
	ENDIF
ENDIF
RETURN 1

;-------------------------------------------------
;「関係一覧」の右カラムキャラリスト１のボタン（関数未指定なら選択中色なし表示）
;-------------------------------------------------
@SHOP_LIFE_LIST1_BUTTON52(ARG:0, ARG:1)
;キャラ, 選択中カラー表示フラグ, ボタン番号に追加する数値, CHECKCHARAの戻り値, 行動済みマークをオフにするフラグ
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, ARG:0 == FLAG:関係一覧表示対象, SHOP_LIFE_LIST1_ADD_INPUT, ARG:1, 1)
RETURN 0

;-------------------------------------------------
;「関係一覧」の右カラム表示処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW52
#DIM 選択人数
#DIM PRINTOPTION

IF FLAG:関係一覧表示対象 >= 0
	選択人数 = 1
ELSE
	選択人数 = 0
ENDIF

;タイトル
CALL COLUMN_RIGHT_TITLE("対象者選択", TOSTR(選択人数), "1", "0", "0")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

PRINTBUTTON @"[%CHARALIST_MODE(), MAX_CHARANAME_LENGTH, LEFT%]", 2
PRINTPLAIN   
IF FLAG:関係一覧表示モード == 0
	IF FLAG:関係一覧表示対象 >= 0
		LOCALS:0 '= ANAME(FLAG:関係一覧表示対象) + "➡他"
	ELSE
		LOCALS:0 '= "選択対象からの感情"
	ENDIF
ELSE
	IF FLAG:関係一覧表示対象 >= 0
		LOCALS:0 '= "他➡" + ANAME(FLAG:関係一覧表示対象)
	ELSE
		LOCALS:0 '= "選択対象への感情"
	ENDIF
ENDIF
PRINTBUTTON @"[%LOCALS:0, MAX_CHARANAME_LENGTH + 4, LEFT%]", 1
PRINTPLAIN   

;表示対象がMASTERでなく、所属があり、主人公と同一所属で、君主でない場合
IF FLAG:関係一覧表示対象 != MASTER && IS_COUNTRY(CFLAG:(FLAG:関係一覧表示対象):所属) && CFLAG:(FLAG:関係一覧表示対象):所属 == CFLAG:MASTER:所属 && GET_COUNTRY_BOSS(CFLAG:(FLAG:関係一覧表示対象):所属) != FLAG:関係一覧表示対象
	PRINTBUTTON "[報償]", 3
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAIN [報償]
	RESETCOLOR
ENDIF

CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;標準的なキャラリストとページ移動
CALL COLUMN_RIGHT_CHARALIST

RETURN 0

;-------------------------------------------------
;「関係一覧」の右カラムボタンの入力処理補佐
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SUB52(ARG:0)
;「への」「からの」切り替え
IF ARG:0 == 1
	FLAG:関係一覧表示モード = !FLAG:関係一覧表示モード
	RETURN 0
;能力表示フィルタ
ELSEIF ARG:0 == 2
	FLAG:能力表示フィルタ = ROUND_INCREMENT(FLAG:能力表示フィルタ, 0, 3)
	RETURN 0
ELSEIF ARG:0 == 3
	CALL SHOP_LIFE_PAYMENT(FLAG:関係一覧表示対象)
ENDIF
RETURN 0

;-------------------------------------------------
;「関係一覧」のリスト１入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP52(ARG:0)
FLAG:関係一覧表示対象 = ARG:0
RETURN 0

@SHOP_LIFE_PAYMENT(対象)
#DIM 対象
#DIM 君主
#DIM 効果量
#DIM 政治比
#DIM FIRST_LINE
#DIM CONST リスト = 1000, 3000, 5000, 10000, 15000, 30000, 50000, 75000

FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE

君主 = GET_COUNTRY_BOSS(CFLAG:MASTER:所属)
PRINTFORML 個人の財布から金を出して、労をねぎらい勢力への忠誠を高めます
PRINTFORML 効果は支払った額、%ANAME(MASTER)%と%ANAME(対象)%の政治により決まります
PRINTFORML いくら支払いますか？(リストから選択、または手入力)
CALL SINGLE_DRAWLINE
PRINTFORML %ANAME(対象)%の%ANAME(君主)%への友好度:{REL_LIKE:対象:君主} 嫌悪度:{REL_HATE:対象:君主}
PRINTFORML %ANAME(MASTER)%の政治:{ABL:MASTER:政治} %ANAME(対象)%の政治:{ABL:対象:政治}
PRINTFORML 所持金:{MONEY}
CALL SINGLE_DRAWLINE

FOR LOCAL, 0, VARSIZE("リスト")
	SIF リスト:LOCAL > MONEY
		BREAK
	PRINTFORML [{リスト:LOCAL}] 金{リスト:LOCAL}
NEXT

PRINTBUTTON "[戻る]", 0

INPUT

IF RESULT == 0
	RETURN 0
ELSEIF INRANGE(RESULT, 0, MONEY)
	PRINTFORMW %ANAME(対象)%に{RESULT}支払いました
	効果量 = RESULT / 50
	政治比 = MIN(ABL:MASTER:政治 * 100 / ABL:対象:政治, 300)
	効果量 = 効果量 * 政治比 / 100
	CALL CHANGE_RELATION_O_TO_C(対象, CFLAG:対象:所属, 効果量, 効果量 * -1)
	RETURN 0
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
RESTART