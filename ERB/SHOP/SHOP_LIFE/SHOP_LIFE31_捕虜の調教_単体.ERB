;-------------------------------------------------
;捕虜の調教でターゲットが一人だけのときの処理
;-------------------------------------------------
@PRISONER_TRAIT(ARG:0)
#DIM FIRST_LINE
#DIM CONST 選択肢_調教 = 0
#DIM CONST 選択肢_許す = 1
#DIM CONST 選択肢_見せしめ = 2
#DIM CONST 選択肢_軟禁 = 3
#DIM CONST 選択肢_釈放 = 4
#DIM CONST 選択肢_解放 = 5
#DIM CONST 選択肢_追放 = 6
#DIM CONST 選択肢_処刑 = 7
#DIM CONST 選択肢_触手処刑 = 8
#DIM CONST 選択肢_キャンセル = 99

FIRST_LINE = LINECOUNT

;SHOW_INFOの初期画面を基本情報にする
FLAG:能力表示モード = 0

;対象キャラの情報を表示
CALL SINGLE_DRAWLINE
CALL SHOW_INFO(ARG:0)
CALL SINGLE_DRAWLINE

PRINTFORML %ANAME(ARG:0)%をどうしますか？
CALL SINGLE_DRAWLINE
CALL PRISONER_TRAIT_PRINTBUTTONS(ARG:0)

INPUT

SELECTCASE RESULT
	CASE 選択肢_調教
		CALL PRISONER_TRAIT_START_TRAIN(ARG:0)
	CASE 選択肢_許す
		CALL PRISONER_TRAIT_FORGIVE(ARG:0)
	CASE 選択肢_見せしめ
		CALL PRISONER_TRAIT_EXAMPLE(ARG:0)
	CASE 選択肢_軟禁
		CALL PRISONER_TRAIT_LENIENT(ARG:0)
	CASE 選択肢_釈放
		CALL PRISONER_TRAIT_RELEASE(ARG:0)
	CASE 選択肢_解放
		CALL PRISONER_TRAIT_FREE(ARG:0)
	CASE 選択肢_追放
		CALL PRISONER_TRAIT_BANISH(ARG:0)
	CASE 選択肢_処刑
		CALL PRISONER_TRAIT_EXECUTE(ARG:0)
	CASE 選択肢_触手処刑
		CALL PRISONER_TRAIT_EXECUTE_TENTACLE(ARG:0)
	CASE 選択肢_キャンセル
		RETURN 1
ENDSELECT

SIF RESULT == 1
	RETURN 

CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;-------------------------------------------------
;捕虜の調教でターゲットが一人だけのときの処理中のボタン描画処理
;-------------------------------------------------
@PRISONER_TRAIT_PRINTBUTTONS(ARG:0)
#DIM CONST 選択肢_調教 = 0
#DIM CONST 選択肢_許す = 1
#DIM CONST 選択肢_見せしめ = 2
#DIM CONST 選択肢_軟禁 = 3
#DIM CONST 選択肢_釈放 = 4
#DIM CONST 選択肢_解放 = 5
#DIM CONST 選択肢_追放 = 6
#DIM CONST 選択肢_処刑 = 7
#DIM CONST 選択肢_触手処刑 = 8
#DIM CONST 選択肢_キャンセル = 99

PRINTBUTTON "[調教]", 選択肢_調教
PRINTPLAIN   

IF CFLAG:(ARG:0):外交調教中
	PRINTBUTTON "[許す]", 選択肢_許す
	PRINTPLAIN   
	PRINTBUTTON "[見せしめ]", 選択肢_見せしめ
	PRINTPLAIN   
	PRINTBUTTON "[キャンセル]", 選択肢_キャンセル
	PRINTL
	RETURN
ENDIF

IF CFLAG:(ARG:0):所属 != CFLAG:MASTER:所属
	PRINTBUTTON "[軟禁]", 選択肢_軟禁
	PRINTPLAIN   
	IF !FLAG:クリアフラグ
		PRINTBUTTON "[解放]", 選択肢_解放
		PRINTPLAIN   
	ENDIF
ELSE
	PRINTBUTTON "[釈放]", 選択肢_釈放
	PRINTPLAIN   
	IF !FLAG:クリアフラグ
		PRINTBUTTON "[追放]", 選択肢_追放
		PRINTPLAIN   
	ENDIF
ENDIF
PRINTBUTTON "[処刑]", 選択肢_処刑
PRINTPLAIN   
IF ITEM:触手部屋 && ID_TO_CHARA(FLAG:触手部屋管理者) >= 0
	PRINTBUTTON "[触手処刑]", 選択肢_触手処刑
	PRINTPLAIN   
ENDIF
PRINTBUTTON "[キャンセル]", 選択肢_キャンセル
PRINTL

;-------------------------------------------------
;捕虜の調教 調教開始
;-------------------------------------------------
@PRISONER_TRAIT_START_TRAIN(ARG:0)
CALL PRISONER_SCAPEGOAT(ARG:0)
CALL DECIDE_PRISONER_MEMBER()
RETURN 1

;-------------------------------------------------
;外交　許す
;-------------------------------------------------
@PRISONER_TRAIT_FORGIVE(ARG:0)

SIF !CFLAG:(ARG:0):外交調教中
	RETURN 0

PRINTFORML %ANAME(ARG:0)%の体を自由にする権利を手放します
PRINTFORML よろしいですか？
CALL ASK_YN

SIF RESULT == 1
	RETURN 0

PRINTFORMW %ANAME(ARG:0)%を許してやりました

CFLAG:(ARG:0):外交調教中 = 0
SIF FINDELEMENT(PRISONER_TARGET, ARG:0) != -1
	PRISONER_TARGET:(FINDELEMENT(PRISONER_TARGET, ARG:0)) = -1

RETURN 1

;-------------------------------------------------
;見せしめ
;-------------------------------------------------
@PRISONER_TRAIT_EXAMPLE(ARG:0)

SIF !CFLAG:(ARG:0):外交調教中
	RETURN 0

CVARSET CFLAG, GETNUM(CFLAG, "閨に呼ぶで選択中"), 0
CFLAG:(ARG:0):閨に呼ぶで選択中 = 1
CFLAG:(ARG:0):調教参加フラグ = 1
CFLAG:(ARG:0):強制友好化 = 1

;全員の慰安参加者フラグをクリア（あてがうメニューで操作されるため）
CVARSET CFLAG, GETNUM(CFLAG, "慰安参加者") , 0

;慰安モブの生成
LOCAL:1 = RAND(3, 6) - LOCAL:2
FOR LOCAL, 0, LOCAL:1
	CALL CREATE_IAN_MOB(慰安_行き先_一般住宅街, LOCAL + 1)
	SIF RESULT == -1
		BREAK
NEXT

FLAG:慰安モード = 3
FLAG:慰安場所 = 慰安_行き先_一般住宅街
ITEM:A_ローター = 1
ITEM:A_バイブ = 1
ITEM:A_アナルバイブ = 1
ITEM:A_ペニスバンド = 1
ITEM:A_オナホール = 1
ITEM:A_クリキャップ = 1
ITEM:A_ニプルキャップ = 1
ITEM:A_麻薬 = 99
ITEM:A_縄 = 1
ITEM:A_鞭 = 1
ITEM:A_目隠し = 1
ITEM:A_口枷 = 1
ITEM:A_鼻フック = 1
ITEM:A_マジック = 1
ITEM:A_ローション = 99
ITEM:A_コンドーム = 99
ITEM:A_媚薬 = 99
ITEM:A_排卵誘発剤 = 99
ITEM:A_桃源香 = 99
FLAG:調教モード = 7
;ウフフフラグをONに
FLAG:ウフフフラグ = 1

PRINTFORMW 裸にした%ANAME(ARG:0)%を町の広場まで連れてきた
PRINTFORMW 領民を慰めるのに、せいぜい役立ってもらうとしよう……

;行動開始時の共通処理
CALL START_TRAIN_COMMON

RETURN 1

;-------------------------------------------------
;軟禁
;-------------------------------------------------
@PRISONER_TRAIT_LENIENT(ARG:0)

SIF CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属
	RETURN 0

SIF CFLAG:(ARG:0):外交調教中
	RETURN 0

PRINTFORML %ANAME(ARG:0)%を牢屋から出して、部屋を与え軟禁します
PRINTFORML よろしいですか？
CALL ASK_YN
SIF RESULT == 1
	RETURN 0

PRINTFORMW %ANAME(ARG:0)%を軟禁しました

CFLAG:(ARG:0):軟禁中 = 1
SIF FINDELEMENT(PRISONER_TARGET, ARG:0) != -1
	PRISONER_TARGET:(FINDELEMENT(PRISONER_TARGET, ARG:0)) = -1

RETURN 1

;-------------------------------------------------
;釈放
;-------------------------------------------------
@PRISONER_TRAIT_RELEASE(ARG:0)

SIF CFLAG:(ARG:0):所属 != CFLAG:MASTER:所属
	RETURN 0

SIF CFLAG:(ARG:0):外交調教中
	RETURN 0

PRINTFORML %ANAME(ARG:0)%を牢屋から出します
PRINTFORML よろしいですか？
CALL ASK_YN

SIF RESULT == 1
	RETURN 0

PRINTFORMW %ANAME(ARG:0)%を牢屋から出しました
CALL CAPTURE(ARG:0, 0, TALENT:(ARG:0):崩壊)

SIF FINDELEMENT(PRISONER_TARGET, ARG:0) != -1
	PRISONER_TARGET:(FINDELEMENT(PRISONER_TARGET, ARG:0)) = -1

RETURN 1

;-------------------------------------------------
;解放
;-------------------------------------------------
@PRISONER_TRAIT_FREE(ARG:0)

SIF CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属
	RETURN 0

SIF CFLAG:(ARG:0):外交調教中
	RETURN 0

SIF FLAG:クリアフラグ
	RETURN 0

PRINTFORML 本当に%ANAME(ARG:0)%を解放しますか？
CALL ASK_YN

SIF RESULT == 1
	RETURN 0

CALL KOJO_EVENT(ARG:0, 340)
PRINTFORMW %ANAME(ARG:0)%を解放しました
CALL CAPTURE(ARG:0, 0, TALENT:(ARG:0):崩壊)

SIF FINDELEMENT(PRISONER_TARGET, ARG:0) != -1
	PRISONER_TARGET:(FINDELEMENT(PRISONER_TARGET, ARG:0)) = -1

RETURN 1

;-------------------------------------------------
;追放
;-------------------------------------------------
@PRISONER_TRAIT_BANISH(ARG:0)

SIF CFLAG:(ARG:0):所属 != CFLAG:MASTER:所属
	RETURN 0

SIF FLAG:クリアフラグ
	RETURN 0

PRINTFORML 本当に%ANAME(ARG:0)%を国から追放しますか？

SIF CALC_SHOP_TIME(!IS_ANIMAL(ARG:0)) == SHOP_TIME
	CALL COLOR_PRINTL("追放した場合、最大行動回数減少により直ちに今期の拠点フェイズが終了します！", カラー_警告)

CALL ASK_YN
SIF RESULT == 1
	RETURN 0

CALL KOJO_EVENT(ARG:0, 340)
PRINTFORMW %ANAME(ARG:0)%を追放しました

CALL CHANGE_COUNTRY(ARG:0, 0, 1)
CALL CAPTURE(ARG:0, 0, TALENT:(ARG:0):崩壊)

SIF FINDELEMENT(PRISONER_TARGET, ARG:0) != -1
	PRISONER_TARGET:(FINDELEMENT(PRISONER_TARGET, ARG:0)) = -1

IF CALC_SHOP_TIME() == SHOP_TIME
	PRINTFORMW 人数不足による仕事のしわ寄せにより自由時間がなくなったので拠点フェイズを終了します
	LINES_SHOP = LINECOUNT
	BEGIN TURNEND
ENDIF

RETURN 1

;-------------------------------------------------
;処刑
;-------------------------------------------------
@PRISONER_TRAIT_EXECUTE(ARG:0)

SIF CFLAG:(ARG:0):外交調教中
	RETURN 0

IF TALENT:(ARG:0):妊娠 && !TALENT:(ARG:0):子供人質
	CALL PRISONER_TRAIT_EXECUTE_PREGNANT(ARG:0)
	RETURN RESULT
ENDIF

PRINTFORML 本当に%ANAME(ARG:0)%を処刑しますか？
SIF CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CALC_SHOP_TIME(!IS_ANIMAL(ARG:0)) == SHOP_TIME
	CALL COLOR_PRINTL("追放した場合、最大行動回数減少により直ちに今期の拠点フェイズが終了します！", カラー_警告)

CALL ASK_YN

SIF RESULT == 1
	RETURN 0

CALL KOJO_EVENT(ARG:0, 341)
PRINTFORMW ……………………
PRINTFORMW ……
PRINTFORMW %ANAME(ARG:0)%を処刑しました
PRINTL 
CALL EXECUTION(ARG:0)
WAIT

CALL CHANGE_COUNTRY(ARG:0, 0, 1, 1)

SIF FINDELEMENT(PRISONER_TARGET, ARG:0) != -1
	PRISONER_TARGET:(FINDELEMENT(PRISONER_TARGET, ARG:0)) = -1

IF CALC_SHOP_TIME() == SHOP_TIME
	PRINTL 
	PRINTFORMW 人数不足による仕事のしわ寄せにより自由時間がなくなったので拠点フェイズを終了します
	LINES_SHOP = LINECOUNT
	BEGIN TURNEND
ENDIF

RETURN 1

;-------------------------------------------------
;触手処刑
;-------------------------------------------------
@PRISONER_TRAIT_EXECUTE_TENTACLE(ARG:0)
#DIM 管理人
#DIM 効果量

管理人 = ID_TO_CHARA(FLAG:触手部屋管理者)

SIF CFLAG:(ARG:0):外交調教中 
	RETURN 0

SIF !ITEM:触手部屋 || 管理人 < 0
	RETURN 0

IF !IS_SYOKUSYU_MANAGE(ID_TO_CHARA(FLAG:触手部屋管理者))
	PRINTFORML 設定された触手部屋の管理者（%ANAME(管理人)%）が、現在管理可能な状況にありません
	PRINTW 触手部屋の管理者を設定し直して下さい
	RETURN 0
ENDIF

IF TALENT:(ARG:0):妊娠 && !TALENT:(ARG:0):子供人質
	CALL PRISONER_TRAIT_EXECUTE_PREGNANT_TENTACLE(ARG:0)
	RETURN RESULT
ENDIF

PRINTFORMW 触手部屋の管理者である%ANAME(管理人)%が処刑を実行します
PRINTFORML 本当に%ANAME(ARG:0)%を処刑しますか？

SIF CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CALC_SHOP_TIME(!IS_ANIMAL(ARG:0)) == SHOP_TIME
	CALL COLOR_PRINTL("追放した場合、最大行動回数減少により直ちに今期の拠点フェイズが終了します！", カラー_警告)

CALL ASK_YN

SIF RESULT == 1
	RETURN 0

CALL KOJO_EVENT(ARG:0, 343)
PRINTFORMW ……………………
PRINTFORMW ……
PRINTFORMW %ANAME(ARG:0)%を処刑しました
PRINTL

CALL CHANGE_COUNTRY(ARG:0, 0, 1, 1)
SIF FINDELEMENT(PRISONER_TARGET, ARG:0) != -1
	PRISONER_TARGET:(FINDELEMENT(PRISONER_TARGET, ARG:0)) = -1

;手に入る妖術経験値を計算して取得＆表示
PRINTFORML <%ANAME(管理人)%>

効果量= 30 + BASE:(ARG:0):精神力 / 20 + ABL:(ARG:0):触手 * 10

CALL PRINT_ADD_EXP(管理人, "妖術経験値", 効果量)
CALL TRAIN_AUTO_ABLUP(管理人)
WAIT

IF CALC_SHOP_TIME() == SHOP_TIME
	PRINTFORMW 人数不足による仕事のしわ寄せにより自由時間がなくなったので拠点フェイズを終了します
	LINES_SHOP = LINECOUNT
	BEGIN TURNEND
ENDIF
RETURN 1

;-------------------------------------------------
;妊婦処刑
;-------------------------------------------------
@PRISONER_TRAIT_EXECUTE_PREGNANT(ARG:0)

PRINTFORML 処刑場につれて来られた%ANAME(ARG:0)%は、泣きながら命乞いを始めた
PRINTFORML 『二度と逆らわない、どんな事でもするから、お腹の子だけは助けて』と、必死の形相で懇願している
PRINTL 
PRINTFORML 本当に%ANAME(ARG:0)%を処刑しますか？

SIF CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CALC_SHOP_TIME(!IS_ANIMAL(ARG:0)) == SHOP_TIME
	CALL COLOR_PRINTL("追放した場合、最大行動回数減少により直ちに今期の拠点フェイズが終了します！", カラー_警告)

CALL ASK_YN("処刑する", "慈悲をかける")

IF RESULT == 0
	CALL KOJO_EVENT(ARG:0, 341)
	PRINTFORMW ……………………
	PRINTFORMW ……
	PRINTFORMW %ANAME(ARG:0)%をお腹の子共々処刑しました
	PRINTL 
	CALL EXECUTION(ARG:0)
	WAIT
	;他国からの評価を下げる
	PRINTL 
	PRINTFORMW この所業に各勢力から非難の声が殺到しています

	FOR LOCAL, 1, MAX_COUNTRY
		SIF !IS_COUNTRY(LOCAL) || LOCAL == CFLAG:MASTER:所属
			CONTINUE

		IF LOCAL == CFLAG:(ARG:0):所属
			CALL CHANGE_RELATION_C_TO_C(LOCAL, CFLAG:MASTER:所属, -200, 300)
		ELSE
			CALL CHANGE_RELATION_C_TO_C(LOCAL, CFLAG:MASTER:所属, -100, 150)
		ENDIF
	NEXT

	CALL CHANGE_COUNTRY(ARG:0, 0, 1, 1)
	SIF FINDELEMENT(PRISONER_TARGET, ARG:0) != -1
		PRISONER_TARGET:(FINDELEMENT(PRISONER_TARGET, ARG:0)) = -1

	IF CALC_SHOP_TIME() == SHOP_TIME
		PRINTFORMW 人数不足による仕事のしわ寄せにより自由時間がなくなったので拠点フェイズを終了します
		LINES_SHOP = LINECOUNT
		BEGIN TURNEND
	ENDIF
ELSE
	CALL KOJO_EVENT(ARG:0, 344)
	PRINTFORMW ……………………
	PRINTFORMW ……
	PRINTFORMW 執行人に処刑の中止を命じました
	PRINTFORMW %ANAME(ARG:0)%はその場に蹲り、尊い命の宿ったお腹を両腕で守るように抱え、震える声ですすり泣いた
	PRINTFORMW %ANAME(ARG:0)%は【子供を人質】にとられた…
	CFLAG:(ARG:0):従属度 += 500
	CFLAG:(ARG:0):好感度 -= 1000
	EXP:(ARG:0):トラウマ経験 += 16
	TALENT:(ARG:0):子供人質 = 1
ENDIF

RETURN 1

;-------------------------------------------------
;妊婦触手処刑
;-------------------------------------------------
@PRISONER_TRAIT_EXECUTE_PREGNANT_TENTACLE(ARG:0)
#DIM 管理人
#DIM 効果量

PRINTL 
PRINTFORML 触手に形作られた処刑部屋につれて来られた%ANAME(ARG:0)%は、泣きながら命乞いを始めた
PRINTFORMW 『二度と逆らわない、どんな事でもするから、お腹の子だけは助けて』と、必死の形相で懇願している
PRINTL 
PRINTFORML 本当に%ANAME(ARG:0)%を処刑しますか？

SIF CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CALC_SHOP_TIME(!IS_ANIMAL(ARG:0)) == SHOP_TIME
	CALL COLOR_PRINTL("追放した場合、最大行動回数減少により直ちに今期の拠点フェイズが終了します！", カラー_警告)

CALL ASK_YN("処刑する", "慈悲をかける")


IF RESULT == 0
	CALL KOJO_EVENT(ARG:0, 343)
	PRINTFORMW ……………………
	PRINTFORMW ……
	PRINTFORMW %ANAME(ARG:0)%をお腹の子共々処刑しました
	PRINTL 
	WAIT
	;他国からの評価を下げる
	PRINTL 
	PRINTFORMW この所業に各勢力から非難の声が殺到しています

	FOR LOCAL, 1, MAX_COUNTRY
		SIF !IS_COUNTRY(LOCAL) || LOCAL == CFLAG:MASTER:所属
			CONTINUE

		IF LOCAL == CFLAG:(ARG:0):所属
			CALL CHANGE_RELATION_C_TO_C(LOCAL, CFLAG:MASTER:所属, -200, 300)
		ELSE
			CALL CHANGE_RELATION_C_TO_C(LOCAL, CFLAG:MASTER:所属, -100, 150)
		ENDIF
	NEXT


	CALL CHANGE_COUNTRY(ARG:0, 0, 1, 1)
	SIF FINDELEMENT(PRISONER_TARGET, ARG:0) != -1
		PRISONER_TARGET:(FINDELEMENT(PRISONER_TARGET, ARG:0)) = -1


	;手に入る妖術経験値を計算して取得＆表示
	PRINTFORML <%ANAME(管理人)%>

	効果量= 30 + BASE:(ARG:0):精神力 / 20 + ABL:(ARG:0):触手 * 10

	CALL PRINT_ADD_EXP(管理人, "妖術経験値", 効果量)
	CALL TRAIN_AUTO_ABLUP(管理人)
	WAIT
	IF CALC_SHOP_TIME() == SHOP_TIME
		PRINTFORMW 人数不足による仕事のしわ寄せにより自由時間がなくなったので拠点フェイズを終了します
		LINES_SHOP = LINECOUNT
		BEGIN TURNEND
	ENDIF
ELSE
	CALL KOJO_EVENT(ARG:0, 345)
	PRINTFORMW ……………………
	PRINTFORMW ……
	PRINTFORMW 飢えた触手達に処刑の中止を命じました
	PRINTFORMW %ANAME(ARG:0)%はその場に蹲り、尊い命の宿ったお腹を両腕で守るように抱え、震える声ですすり泣いた
	PRINTFORMW %ANAME(ARG:0)%は【子供を人質】にとられた…
	CFLAG:(ARG:0):従属度 += 500
	CFLAG:(ARG:0):好感度 -= 1000
	EXP:(ARG:0):トラウマ経験 += 16
	TALENT:(ARG:0):子供人質 = 1
ENDIF

RETURN 1
