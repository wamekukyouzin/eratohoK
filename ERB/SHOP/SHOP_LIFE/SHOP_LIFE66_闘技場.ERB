;-------------------------------------------------
;「闘技場」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME66
RESULTS:0 '= "闘技場"

;-------------------------------------------------
;「闘技場」の選択可否
;-------------------------------------------------
@SHOP_LIFE_CHECK66
SIF CFLAG:MASTER:捕虜先
	RETURN 0
SIF DAY % 3 != 0
	RETURN 0
SIF MONEY < 5000
	RETURN 0
SIF ARENA_DONE
	RETURN 0
RETURN 1
;-------------------------------------------------
;「闘技場」の選択可能キャラ存在判定
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA66(ARG:0)
;捕虜でない、同一勢力、子供でない
RETURN CHECK91(ARG:0, 2, CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && (ARG:0 == MASTER || CFLAG:(ARG:0):所属) && !CFLAG:(ARG:0):捕虜先 && !IS_ANIMAL(ARG:0) && CFLAG:(ARG:0):行動不能状態 != 行動不能_子供)

;-------------------------------------------------
;「闘技場」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY66
FLAG:拠点フェイズページ = 1
FLAG:夜這い = 0
RETURN 0

;-------------------------------------------------
;「闘技場」の右カラム表示処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW66
CALL COLUMN_RIGHT_TITLE("出場者選択(武/防/知が影響)", "0", "1", "1", "3000", "")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL
;標準的なキャラリストとページ移動
CALL COLUMN_RIGHT_CHARALIST(1)
RETURN

;-------------------------------------------------
;「闘技場」の右カラムキャラリスト１のボタン（関数未指定なら選択中色なし表示）
;-------------------------------------------------
@SHOP_LIFE_LIST1_BUTTON66(ARG:0, ARG:1)
;キャラ, 選択中カラー表示フラグ, ボタン番号に追加する数値, CHECKCHARAの戻り値
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, 0, SHOP_LIFE_LIST1_ADD_INPUT, ARG:1, 0, "TOP")
RETURN


;-------------------------------------------------
;「闘技場」の右カラムキャラリストボタン表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD66(ARG:0)
PRINTPLAIN  
PRINTFORM 武:
CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ARG:0):武闘)
PRINTFORM  {ABL:(ARG:0):武闘, 3, RIGHT} 
PRINTFORM 
PRINTFORM 防:
CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ARG:0):防衛)
PRINTFORM  {ABL:(ARG:0):防衛, 3, RIGHT} 
PRINTFORM 
PRINTFORM 知:
CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ARG:0):知略)
PRINTFORM  {ABL:(ARG:0):知略, 3, RIGHT} 

RETURN 1
;-------------------------------------------------
;「闘技場」
;-------------------------------------------------
@SHOP_LIFE_USERSHOP66(対象)
#DIM 対象
CALL SHOP_LIFE_ARENA(対象)
IF RESULT
	ARENA_DONE = 1
	SHOP_TIME ++
	BEGIN TURNEND
ENDIF

