;-------------------------------------------------
;「婚姻の儀」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME65
RESULTS:0 '= "婚姻の儀"

;-------------------------------------------------
;「婚姻の儀」の選択可否
;-------------------------------------------------
@SHOP_LIFE_CHECK65
SIF MONEY < 100000
	RETURN 0
SIF CFLAG:MASTER:捕虜先
	RETURN 0
SIF !CFLAG:MASTER:所属
	RETURN 0
;既に婚姻している場合婚姻は不可能
;HAS_WIFE = 0
;FOR LOCAL:0, 0, CHARANUM
;	IF TALENT:(LOCAL:0):正妻
;		HAS_WIFE ++
;		BREAK
;	ENDIF
;NEXT
;SIF HAS_WIFE
;	RETURN 0
RETURN 1

;-------------------------------------------------
;「婚姻の儀」の選択可能キャラ存在判定
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA65(ARG:0)
;非主人公、同一勢力、捕虜でない、好感度10000以上、[恋人]or[烙印]、特殊状態が立っていない
RETURN ARG:0 != MASTER && CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && CFLAG:(ARG:0):捕虜先 == 0 && CFLAG:(ARG:0):好感度 >= 10000 && (TALENT:(ARG:0):恋人 || TALENT:(ARG:0):烙印) && CFLAG:(ARG:0):特殊状態 == 0 && !IS_ANIMAL(ARG:0) && !TALENT:(ARG:0):正妻 && !TALENT:(ARG:0):妾

;-------------------------------------------------
;「婚姻の儀」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY65
FLAG:拠点フェイズページ = 1
FLAG:夜這い = 0
RETURN 0

;-------------------------------------------------
;「婚姻の儀」の右カラム表示処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW65
CALL COLUMN_RIGHT_TITLE("配偶者選択", "0", "1", "0", "100,000")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL
IF HAS_WIFE()
	CALL COLOR_PRINT(" 高好感度で恋人か烙印所持の士官を妾(夫)にできます", カラー_注釈)
ELSE
	CALL COLOR_PRINT(" 高好感度で恋人か烙印所持の士官を正妻（夫）にできます", カラー_注釈)
ENDIF
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL
;標準的なキャラリストとページ移動
CALL COLUMN_RIGHT_CHARALIST
RETURN 0

;-------------------------------------------------
;「婚姻の儀」のリスト１入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP65(ARG:0)
REDRAW 1

IF MONEY < 100000
	PRINTFORMW 所持金が不足しています
	RETURN
ENDIF

IF IS_HUSBAND(ARG:0)
	LOCALS:0 = 夫
ELSE
	IF HAS_WIFE()
		LOCALS:0 = 妾
	ELSE
		LOCALS:0 = 正妻
	ENDIF
ENDIF

PRINTFORML %ANAME(ARG:0)%を%LOCALS:0%として迎えます
PRINTL よろしいですか？
CALL ASK_YN
IF RESULT == 1
	RETURN 0
ENDIF

MONEY -= 100000

;口上の表示
CALL KOJO_EVENT(ARG:0, 90)

PRINTL 
CUSTOMDRAWLINE *-
PRINTL 婚姻の儀
CUSTOMDRAWLINE *-
WAIT
PRINTFORMW 式の当日、多くの観衆に祝福されながら、%ANAME(MASTER)%は%ANAME(ARG:0)%を%LOCALS:0%として迎えることを宣言した

IF !HAS_WIFE()
	;口上の表示
	CALL KOJO_EVENT(ARG:0, 91)
ENDIF

IF HAS_WIFE()
	TALENT:(ARG:0):妾 = 1
ELSE
	TALENT:(ARG:0):正妻 = 1
ENDIF

PRINTL 
SETCOLOR カラー_注意
PRINTFORMW %ANAME(ARG:0)%が%ANAME(MASTER)%の%LOCALS:0%となりました
RESETCOLOR
PRINTL 
PRINTL 
RETURN 1

;-------------------------------------------------
;キャラARG:0について、素質<正妻>が<夫>と表示される条件を満たしているかどうか判定
;-------------------------------------------------
@IS_HUSBAND(ARG:0)
#FUNCTION
IF GROUPMATCH(TALENT:MASTER:性別, 1, 2) && GROUPMATCH(TALENT:(ARG:0):性別, 0, 3, 4, 5)
	RETURNF 1
ELSEIF GROUPMATCH(TALENT:MASTER:性別, 4, 5) && GROUPMATCH(TALENT:(ARG:0):性別, 0, 3)
	RETURNF 1
ENDIF
RETURNF 0

;-------------------------------------------------
;desc  :正妻が存在するか
;return:戻り値:正妻が存在するか
;-------------------------------------------------
@HAS_WIFE()
#FUNCTION 
FOR LOCAL:0, 0, CHARANUM
	IF TALENT:(LOCAL:0):正妻
		RETURNF 1
	ENDIF
NEXT
RETURNF 0
