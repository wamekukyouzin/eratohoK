;-------------------------------------------------
;「手引させる」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME22
RESULTS:0 '= "手引させる"

;-------------------------------------------------
;「手引させる」の選択可否
;-------------------------------------------------
@SHOP_LIFE_CHECK22
FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL) || LOCAL == CFLAG:MASTER:所属
		CONTINUE
	SIF !CFLAG:MASTER:所属 || TMP_COUNTRY_IS_NEIBORING:(CFLAG:MASTER:所属):LOCAL
		GOTO FOUND
NEXT

RETURN 0

$FOUND

SIF CFLAG:MASTER:捕虜先
	RETURN 0
SIF CFLAG:MASTER:行動不能状態 == 行動不能_臨月 || COOLTIME:MASTER:0 > 1
	RETURN 0

;通常はCHECK真CHECKCHARA真なら通すが
;手引きはCHECKCHARA_SUBが必須のためここでチェック
;内通者の選択可能キャラ存在チェック
LOCAL:1 = 0
FOR LOCAL:0, 1, CHARANUM
	CALL SHOP_LIFE_CHECKCHARA_SUB_NAITSUU(LOCAL:0)
	IF RESULT
		LOCAL:1 = 1
		BREAK
	ENDIF
NEXT

;内通者になれるキャラが存在しない
SIF LOCAL:1 == 0
	RETURN 0

RETURN 1

;-------------------------------------------------
;「手引させる」の選択可能キャラ存在判定
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA22(ARG:0)
;隣接している都市がない
SIF CFLAG:MASTER:所属 && !TMP_COUNTRY_IS_NEIBORING:(CFLAG:MASTER:所属):(CFLAG:(ARG:0):所属)
	RETURN 0

;非主人公、捕虜ではない、別勢力、どこかの勢力に所属している（放浪、未登場、死亡済みではない）
IF ARG:0 != MASTER && !CFLAG:(ARG:0):捕虜先 && CFLAG:MASTER:所属 != CFLAG:(ARG:0):所属 && CFLAG:(ARG:0):所属 && !IS_SP_CHARA(ARG:0) && CFLAG:(ARG:0):行動不能状態 != 行動不能_子供
	;内通者として選択したキャラ自身は不可
	SIF CFLAG:(ARG:0):夜這いの助手
		RETURN 2
	IF TEBIKI_ABLE(ARG:0)
		RETURN CHECK91(ARG:0, 2, 1)
	ENDIF
	RETURN 2
ENDIF
RETURN 0

;-------------------------------------------------
;「手引させる」の内通者の選択可能キャラ存在判定
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA_SUB22(ARG:0)
CALL SHOP_LIFE_CHECKCHARA_SUB_NAITSUU(ARG:0)
RETURN RESULT

;-------------------------------------------------
;「夜這いする」の追加チェック
;-------------------------------------------------
@SHOP_LIFE_CHECK_ADDITIONAL_CHECK22()
RETURN !FLAG:クリアフラグ

;-------------------------------------------------
;「手引させる」の陥落/能力判定
;-------------------------------------------------
@TEBIKI_ABLE(ARG:0)
#FUNCTION
LOCAL:1 = TEBIKI_ZYOSYU_CALC_POINT(MASTER)
LOCAL:2 = 0
FOR LOCAL,1, CHARANUM
	IF CFLAG:LOCAL:夜這いの助手
		LOCAL:1 += TEBIKI_ZYOSYU_CALC_POINT(LOCAL)
		LOCAL:2 = 1
		SIF CFLAG:LOCAL:所属 != CFLAG:ARG:所属
			RETURNF 0
	ENDIF
NEXT

SIF LOCAL:2 == 0
	RETURNF 0

RETURNF LOCAL:1 >= TEBIKI_TARGET_CALC_POINT(ARG)

;-------------------------------------------------
;「夜這いする」の能力判定用関数
;-------------------------------------------------
@TEBIKI_ZYOSYU_CALC_POINT(ARG)
#FUNCTION
RETURNF ABL:ARG:武闘 + ABL:ARG:知略 + FIND_PASSIVE_SKILL(ARG, "工作員") * 30

;-------------------------------------------------
;「夜這いする」の能力判定用関数
;-------------------------------------------------
@TEBIKI_TARGET_CALC_POINT(ARG)
#FUNCTION
SIF GROUPMATCH(1, TALENT:ARG:親愛, TALENT:ARG:恋慕, TALENT:ARG:隷属, TALENT:ARG:服従, TALENT:ARG:主人, TALENT:ARG:所有者)
	RETURNF 0

LOCAL = (ABL:ARG:武闘 + ABL:ARG:知略) + FIND_PASSIVE_SKILL(ARG, "工作員") * 15

SIF TALENT:(ARG:0):烙印
	RETURNF LOCAL
SIF GET_COUNTRY_BOSS(CFLAG:(ARG:0):所属) == ARG:0
	RETURNF LOCAL * 5 / 2
RETURNF LOCAL * 2

;-------------------------------------------------
;「手引させるの左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY22
FLAG:拠点フェイズページ = 1
FLAG:夜這い = 1
FOR LOCAL:0, 1, CHARANUM
	CFLAG:(LOCAL:0):夜這いの助手 = 0
NEXT
RETURN 0

;-------------------------------------------------
;「手引させる」の右カラムキャラリスト１のボタン（関数未指定なら選択中色なし表示）
;-------------------------------------------------
@SHOP_LIFE_LIST1_BUTTON22(ARG:0, ARG:1)
;内通者の存在チェック
LOCAL:1 = 0
FOR LOCAL:0, 1, CHARANUM
	IF CFLAG:(LOCAL:0):夜這いの助手
		LOCAL:1 = 1
		BREAK
	ENDIF
NEXT
;内通者が指定されていない
IF LOCAL:1 == 0
	;キャラ, 選択中カラー表示フラグなし, ボタン番号に追加する数値, 強制的に選択不可色表示
	CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, 0, SHOP_LIFE_LIST1_ADD_INPUT, 2, 0, "TOP")
ELSE
	;キャラ, 選択中カラー表示フラグなし, ボタン番号に追加する数値, CHECKCHARAの戻り値
	CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, 0, SHOP_LIFE_LIST1_ADD_INPUT, ARG:1, 0, "TOP")
ENDIF
RETURN

;-------------------------------------------------
;「手引させる」の右カラムキャラリスト２のボタン（関数未指定なら選択中色なし表示）
;-------------------------------------------------
@SHOP_LIFE_LIST2_BUTTON22(ARG:0, ARG:1)
;キャラ, 選択中カラー表示フラグ, ボタン番号に追加する数値, CHECKCHARAの戻り値
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, CFLAG:(ARG:0):夜這いの助手, SHOP_LIFE_LIST2_ADD_INPUT, ARG:1, 0, "BOTTOM")
RETURN 0

;-------------------------------------------------
;「夜這い」の右カラムキャラリストボタン（上）表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD_TOP22(ARG:0)
PRINTFORM P:{TEBIKI_TARGET_CALC_POINT(ARG:0), 3, RIGHT}
CALL SHOP_LIFE_BUTTON_ADD_TOP_PRISONER(ARG:0)
RETURN RESULT

;-------------------------------------------------
;「夜這い」の右カラムキャラリストボタン（上）表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD_BOTTOM22(ARG:0)
PRINTFORM P:{TEBIKI_ZYOSYU_CALC_POINT(ARG:0), 3, RIGHT}
CALL SHOP_LIFE_BUTTON_ADD_BOTTOM_PRISONER(ARG:0)
RETURN RESULT

;-------------------------------------------------
;「手引させる」の右カラム表示処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW22
CALL COLUMN_RIGHT_TITLE("対象者選択", "0", "1", "1", "0", "他勢力に内通者が必要")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;２段組キャラリスト上とページ移動
CALL COLUMN_RIGHT_CHARALIST_TOP(1)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;助手を数える
LOCAL:1 = 0
LOCAL:2 = TEBIKI_ZYOSYU_CALC_POINT(MASTER)
FOR LOCAL:0, 0, CHARANUM
	;選択可能な条件を満たしているかどうかを判定
	TRYCALLFORM SHOP_LIFE_CHECKCHARA_SUB{FLAG:拠点フェイズ選択コマンド}(LOCAL:0)
	IF RESULT == 1
		;選択中の人数を数える
		IF CFLAG:(LOCAL:0):夜這いの助手
			LOCAL:1 ++
			LOCAL:2 += TEBIKI_ZYOSYU_CALC_POINT(LOCAL:0)
		ENDIF
	ELSE
		;条件を満たしていなければ強制的にＯＦＦ
		CFLAG:(LOCAL:0):夜這いの助手 = 0
	ENDIF
NEXT

PRINT 誰に手引きさせますか？
PRINTFORM  ポイント:{LOCAL:2}
PRINTPLAIN  
PRINTBUTTON @"[%ZYOSYU_MODE()%]", 1
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;２段組キャラリスト下とページ移動
ソートキー保存 = ソートキー
ソート順保存 = 降順ソート
ソートキー = ソート_手引助手
降順ソート = 1
CALL CREATE_SELECTOR_SORT_LIST
CALL COLUMN_RIGHT_CHARALIST_BOTTOM(1)
ソートキー = ソートキー保存
降順ソート = ソート順保存
CALL CREATE_SELECTOR_SORT_LIST
RETURN 0

;-------------------------------------------------
;「夜這いする」の右カラムボタンの入力処理補佐
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SUB22(ARG:0)
SIF ARG:0 != 1
	RETURN 0

FLAG:助手フィルタ = ROUND_INCREMENT(FLAG:助手フィルタ, 0, 3)


;-------------------------------------------------
;「手引させる」のリスト１入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP22(ARG:0)
#DIM 好感度他減少

;SHOW_INFOの初期画面を基本情報にする
FLAG:能力表示モード = 0

$SHOW_LOOP_INFO_T

;対象キャラの情報を表示
CALL SINGLE_DRAWLINE
CALL SHOW_INFO(ARG:0)
CALL SINGLE_DRAWLINE

PRINTFORML 内通者の手引きで%ANAME(ARG:0)%を罠にかけます
PRINTFORML よろしいですか？
CALL ASK_YN
IF RESULT == 0
	CVARSET CFLAG, 6, 0
	FOR LOCAL:0, 0, CHARANUM
		IF LOCAL:0 != MASTER && CFLAG:(LOCAL:0):夜這いの助手
			CFLAG:(LOCAL:0):調教参加フラグ = 1
		ENDIF
	NEXT
	;夜這い経験
	EXP:ARG:59 ++
	;調教初期設定
	CALL TRAIN_TARGET_SETTING(ARG:0)
	;メインターゲットを調教対象に設定
	VARSET PRISONER_TARGET, -1
	PRISONER_TARGET:0 = ARG:0
	;初期のウフフフラグをＯＮにする
	FLAG:ウフフフラグ = 1
	;捕虜調教モード
	FLAG:調教モード = 2
	;夜這いフラグ
	FLAG:夜這い = 1

	;行動開始時の共通設定
	CALL START_TRAIN_COMMON
	IF !IS_LOVER(ARG:0) && !IS_SLAVE(ARG:0) && !IS_SLAVED_BY(ARG:0)
		好感度他減少 = LIMIT(RAND(100, 200) - ABL:ARG:欲望 * 20 - ABL:ARG:奉仕 * 20 ,0, 100)
		SIF TALENT:ARG:烙印
			好感度他減少 /= 2
		SIF CFLAG:(ARG:0):好感度 > 0
			CFLAG:(ARG:0):好感度 -= 好感度他減少
		SIF CFLAG:(ARG:0):従属度 > 0
			CFLAG:(ARG:0):従属度 -= 好感度他減少
		SIF CFLAG:(ARG:0):依存度 > 0
			CFLAG:(ARG:0):依存度 -= 好感度他減少
		SIF CFLAG:(ARG:0):支配度 > 0
			CFLAG:(ARG:0):支配度 -= 好感度他減少 / 2

		;相手との関係悪化（適当）
		CALL CHANGE_RELATION_O_TO_C(CFLAG:ARG:所属, CFLAG:MASTER:所属, RAND(50, 100) * -1, RAND(50, 100))
		CALL CHANGE_RELATION_O_TO_C(GET_COUNTRY_BOSS(CFLAG:(ARG:0):所属), CFLAG:MASTER:所属, RAND(50, 100) * -1, RAND(50, 100))
			;対象が君主ならおかわり
		SIF GET_COUNTRY_BOSS(CFLAG:(ARG:0):所属) == ARG:0
			CALL CHANGE_RELATION_C_TO_C(CFLAG:ARG:所属, CFLAG:MASTER:所属, RAND(100, 150) * -1, RAND(100, 150))
	ENDIF
	REDRAW 1
	RETURN 1
ENDIF
REDRAW 1
RETURN 0

;-------------------------------------------------
;「手引させる」のリスト２入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP_SUB22(ARG:0)
;参加キャラの数を数える
LOCAL:5 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):夜這いの助手
		LOCAL:5 ++
	ENDIF
NEXT
;1人まで
IF CFLAG:(ARG:0):夜這いの助手 || LOCAL:5 < 1
	;内通参加フラグを反転
	CFLAG:(ARG:0):夜這いの助手 = !CFLAG:(ARG:0):夜這いの助手
ENDIF

