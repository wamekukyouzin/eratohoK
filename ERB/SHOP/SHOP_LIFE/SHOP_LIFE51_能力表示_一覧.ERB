;-------------------------------------------------
;能力の一覧表示
;LOCAL:1=表示ページ 2=ページ最大 3=表示チャンネル 4=表示行数 0,10,11=カウンタ
;-------------------------------------------------
@SHOP_LIFE_CHARA_TABLE
#DIM CHARA_TABLE,MAX_CHARA_NUM
#DIM SORT_VALUE,MAX_CHARA_NUM
{
	#DIM HEAD_SLG =
		GETNUM(ABL, "武闘"), GETNUM(ABL, "防衛"), GETNUM(ABL, "知略"), GETNUM(ABL, "政治"),
		GETNUM(ABL, "妖術"), GETNUM(ABL, "歌唱"), GETNUM(ABL, "料理"), GETNUM(ABL, "野心")
}

{
	#DIM HEAD_ABL =
		GETNUM(ABL, "Ｃ感"), GETNUM(ABL, "Ｖ感"), GETNUM(ABL, "Ａ感"), GETNUM(ABL, "Ｂ感"), GETNUM(ABL, "Ｍ感"),
		GETNUM(ABL, "Ｕ感"), GETNUM(ABL, "欲望"), GETNUM(ABL, "性技"), GETNUM(ABL, "性知識")
}

#DIM HEAD_BASE = GETNUM(BASE, "体力"), GETNUM(BASE, "気力"), GETNUM(BASE, "精神力")

{
	#DIM HEAD_ABL_2 =
		GETNUM(ABL, "奉仕"), GETNUM(ABL, "性交"), GETNUM(ABL, "レズ"), GETNUM(ABL, "ＢＬ"), GETNUM(ABL, "露出"),
		GETNUM(ABL, "自慰"), GETNUM(ABL, "精愛"), GETNUM(ABL, "射精"), GETNUM(ABL, "噴乳"), GETNUM(ABL, "排泄"),
		GETNUM(ABL, "触手"), GETNUM(ABL, "出産")
}

{
	#DIM HEAD_CFLAG = 
		GETNUM(CFLAG, "好感度"), GETNUM(CFLAG, "依存度"), GETNUM(CFLAG, "従属度"), GETNUM(CFLAG, "支配度")
}

#DIM FIRST_LINE
#DIM DESC
#DIM NOW_SORT
#DIM PREV_SORT
#DIM ページ
#DIM 最大ページ
#DIM 項目
#DIM キャラ数
#DIM 最大キャラ数

VARSET CHARA_TABLE, -1
ページ = 1
項目 = 1
最大キャラ数 = 23
キャラ数 = 0
FOR LOCAL, 0, CHARANUM
	SIF SHOP_CHARA_LIST:LOCAL == -1
		CONTINUE
	CHARA_TABLE:LOCAL = SHOP_CHARA_LIST:LOCAL
NEXT
FOR LOCAL:0, 0, CHARANUM
	SIF CHARA_TABLE:(LOCAL:0) == -1
		BREAK
	TRYCALLFORM SHOP_LIFE_CHECKCHARA_NOURYOKU(CHARA_TABLE:(LOCAL:0))
	SIF RESULT
		キャラ数 ++
NEXT

最大ページ = キャラ数 / 最大キャラ数 + 1
FIRST_LINE = LINECOUNT
$INPUT_LOOP

;ヘッダ
CALL SINGLE_DRAWLINE
PRINTFORM ★能力を表示するキャラを選択して下さい★項目名クリックでソートできます★   
PRINTBUTTON @"[%CHARALIST_MODE(), 14, LEFT%]", 95
PRINTL
PRINTBUTTON "|  NO", 50
PRINTFORM |
PRINTFORM %TOSTR_REPEAT("-",MAX_CHARANAME_LENGTH/2)%
SELECTCASE 項目
	CASE 1
		PRINTFORM |
		PRINTBUTTON "  ★", 30
		PRINTFORM |
		PRINTBUTTON "4合計", 31
		PRINTFORM |
		PRINTBUTTON "7合計", 32
		FOR LOCAL, 0, VARSIZE("HEAD_SLG")
			PRINTFORM |
			PRINTBUTTON " " + ABLNAME:(HEAD_SLG:LOCAL), LOCAL
		NEXT
	CASE 2
		FOR LOCAL, 0, VARSIZE("HEAD_ABL")
			PRINTFORM |
			PRINTBUTTON " " + SUBSTRING(ABLNAME:(HEAD_ABL:LOCAL),0,4), LOCAL
		NEXT
		FOR LOCAL, 0, VARSIZE("HEAD_BASE")
			PRINTFORM |
			PRINTBUTTON " " + SUBSTRING(BASENAME:(HEAD_BASE:LOCAL), 0, 4), LOCAL + 70
		NEXT
	CASE 3
		FOR LOCAL, 0, VARSIZE("HEAD_ABL_2")
			PRINTFORM |
			PRINTBUTTON " " + SUBSTRING(ABLNAME:(HEAD_ABL_2:LOCAL),0,4), LOCAL
		NEXT
	CASE 4
		FOR LOCAL, 0, VARSIZE("HEAD_CFLAG")
			PRINTFORM |
			PRINTBUTTON "  " + CFLAGNAME:(HEAD_CFLAG:LOCAL), LOCAL
		NEXT
	CASE 5
		FOR LOCAL, 1, MAX_SP_COUNTRY
			PRINTFORM |
			PRINTBUTTON SUBSTRINGU(SP_COUNTRY_NAME:LOCAL, 0, 2), LOCAL + 10
		NEXT
	CASEELSE
		PRINTFORML %TOSTR_REPEAT("-",76)%
ENDSELECT
PRINTFORML |

;メイン
キャラ数 = 0
FOR LOCAL:0, 0, CHARANUM
	LOCAL:5 = CHARA_TABLE:(LOCAL:0)
	SIF LOCAL:5 == -1
		BREAK
	TRYCALLFORM SHOP_LIFE_CHECKCHARA_NOURYOKU(LOCAL:5)
	IF RESULT
		キャラ数 ++
		IF (ページ-1)*最大キャラ数 <= キャラ数 -1 && ページ*最大キャラ数 > キャラ数 -1
			CALL PRINT_PARTNER_DATA_TABLE(LOCAL:5,項目)
			PRINTL
		ENDIF
	ENDIF
NEXT


;フッタ
CALL SINGLE_DRAWLINE
PRINTFORM %TOSTR_SPACE(4)%
PRINTBUTTON "[表示切替  "+TOSTR(項目)+"/ 5]", 94
PRINTFORM %TOSTR_SPACE(4)%
IF 最大ページ >= 2
	PRINTBUTTON "[前のページ    ]", SHOP_LIFE_LIST1_PAGE_BACK
	PRINTFORM %TOSTR_SPACE(3)%page{ページ,2}/{最大ページ,2}%TOSTR_SPACE(3)%
	PRINTBUTTON "[次のページ    ]", SHOP_LIFE_LIST1_PAGE_NEXT
	PRINTFORM %TOSTR_SPACE(4)%
ELSE
	PRINTFORM %TOSTR_SPACE(51)%
ENDIF
PRINTBUTTON "[戻る          ]", 93
PRINTL

INPUT 0
CLEARLINE 1

SELECTCASE RESULT
	CASE 1
		FOR LOCAL, 0, CHARANUM
			SORT_VALUE:LOCAL = NO:LOCAL
		NEXT
	CASE 95
		;キャラ数の再計算が必要になるのでリスタート
		FLAG:能力表示フィルタ = ROUND_INCREMENT(FLAG:能力表示フィルタ, 0, 3)
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	CASE 93
		RETURN
	CASE SHOP_LIFE_LIST1_PAGE_BACK
		ページ --
		SIF ページ < 1
			ページ = 最大ページ
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO INPUT_LOOP
	CASE SHOP_LIFE_LIST1_PAGE_NEXT
		ページ ++
		SIF ページ > 最大ページ
			ページ = 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO INPUT_LOOP

	CASE 94
		項目 ++
		SIF 項目 > 5
			項目 = 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO INPUT_LOOP
	CASE IS >= 100
		SIF NO_TO_CHARA(RESULT - 100) != -1
			CALL SHOW_INFO_WITH_UI(NO_TO_CHARA(RESULT - 100), 1)
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO INPUT_LOOP
ENDSELECT

NOW_SORT = RESULT

IF NOW_SORT == PREV_SORT
	DESC = !DESC
ELSE
	DESC = 0
ENDIF

PREV_SORT = RESULT

;NOソート
IF RESULT == 1

ENDIF

;配列渡しがもうちょっとマトモな代物ならこんなゴミみたいな実装しなくていいのにね
SELECTCASE 項目
	CASE 1
		SELECTCASE NOW_SORT
			CASE 0 TO VARSIZE("HEAD_SLG")
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = ABL:LOCAL:(HEAD_SLG:NOW_SORT)
				NEXT
			CASE 30
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = TMP_CHARA_STARS:LOCAL
				NEXT
			CASE 31
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:(LOCAL:0) = ABL:(LOCAL:0):武闘 + ABL:(LOCAL:0):防衛 + ABL:(LOCAL:0):政治 + ABL:(LOCAL:0):知略
				NEXT
			CASE 32
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:(LOCAL:0) = ABL:(LOCAL:0):武闘 + ABL:(LOCAL:0):防衛 + ABL:(LOCAL:0):政治 + ABL:(LOCAL:0):知略 + ABL:(LOCAL):妖術 + ABL:LOCAL:歌唱 + ABL:LOCAL:料理
				NEXT
			CASE 50
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = NO:LOCAL
				NEXT
		ENDSELECT
	CASE 2
		SELECTCASE NOW_SORT
			CASE 0 TO VARSIZE("HEAD_ABL")
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = ABL:LOCAL:(HEAD_ABL:NOW_SORT)
				NEXT
			CASE 70 TO 70 + VARSIZE("HEAD_BASE")
				NOW_SORT -= 70
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = BASE:LOCAL:(HEAD_BASE:NOW_SORT)
				NEXT
		ENDSELECT
	CASE 3
		SELECTCASE NOW_SORT
			CASE 0 TO VARSIZE("HEAD_ABL_2")
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = ABL:LOCAL:(HEAD_ABL_2:NOW_SORT)
				NEXT
		ENDSELECT
	CASE 4
		SELECTCASE NOW_SORT
			CASE 0 TO VARSIZE("HEAD_CFLAG")
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = CFLAG:LOCAL:(HEAD_CFLAG:NOW_SORT)
				NEXT
		ENDSELECT
	CASE 5
		NOW_SORT -= 10
		SELECTCASE NOW_SORT
			CASE 1 TO MAX_SP_COUNTRY - 1
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = GETBIT(TALENT:LOCAL:特殊勢力陥落系, NOW_SORT)
				NEXT
		ENDSELECT
ENDSELECT

FOR LOCAL,1,CHARANUM
	SIF CHARA_TABLE:LOCAL == -1
		BREAK
	FOR LOCAL:11,LOCAL,0,-1
		LOCAL:12 = CHARA_TABLE:(LOCAL:11-1)
		LOCAL:13 = CHARA_TABLE:(LOCAL:11)
		IF (DESC && SORT_VALUE:(LOCAL:12) < SORT_VALUE:(LOCAL:13)) || (!DESC && SORT_VALUE:(LOCAL:12) > SORT_VALUE:(LOCAL:13))
			SWAP CHARA_TABLE:(LOCAL:11),CHARA_TABLE:(LOCAL:11 -1)
		ELSE
			BREAK
		ENDIF
	NEXT
NEXT

;ソート順に従ってSHOP_CHARA_LISTを更新
VARSET SHOP_CHARA_LIST, -1
FOR LOCAL, 0, VARSIZE("SHOP_CHARA_LIST")
	SHOP_CHARA_LIST:LOCAL = CHARA_TABLE:LOCAL
NEXT

CLEARLINE LINECOUNT - FIRST_LINE
GOTO INPUT_LOOP

;-------------------------------------------------
;ARG:0番のキャラの情報を表示する関数
;ARG:1は表示チャンネル
;ボタンの選択番号は NO + ARG:2 になる
;-------------------------------------------------
@PRINT_PARTNER_DATA_TABLE(ARG:0, ARG:1 = 0, ARG:2 = 100)
{
	#DIM HEAD_SLG =
		GETNUM(ABL, "武闘"), GETNUM(ABL, "防衛"), GETNUM(ABL, "知略"), GETNUM(ABL, "政治"),
		GETNUM(ABL, "妖術"), GETNUM(ABL, "歌唱"), GETNUM(ABL, "料理"), GETNUM(ABL, "野心")
}

{
	#DIM HEAD_ABL =
		GETNUM(ABL, "Ｃ感"), GETNUM(ABL, "Ｖ感"), GETNUM(ABL, "Ａ感"), GETNUM(ABL, "Ｂ感"), GETNUM(ABL, "Ｍ感"),
		GETNUM(ABL, "Ｕ感"), GETNUM(ABL, "欲望"), GETNUM(ABL, "性技"), GETNUM(ABL, "性知識")
}

#DIM HEAD_BASE = GETNUM(BASE, "体力"), GETNUM(BASE, "気力"), GETNUM(BASE, "精神力")

{
	#DIM HEAD_ABL_2 =
		GETNUM(ABL, "奉仕"), GETNUM(ABL, "性交"), GETNUM(ABL, "レズ"), GETNUM(ABL, "ＢＬ"), GETNUM(ABL, "露出"),
		GETNUM(ABL, "自慰"), GETNUM(ABL, "精愛"), GETNUM(ABL, "射精"), GETNUM(ABL, "噴乳"), GETNUM(ABL, "排泄"),
		GETNUM(ABL, "触手"), GETNUM(ABL, "出産")
}

{
	#DIM HEAD_CFLAG = 
		GETNUM(CFLAG, "好感度"), GETNUM(CFLAG, "依存度"), GETNUM(CFLAG, "従属度"), GETNUM(CFLAG, "支配度")
}

PRINTFORM [{NO:(ARG:0) + ARG:2, 4}] %SNAME(ARG:0), MAX_CHARANAME_LENGTH/2, LEFT%
SELECTCASE ARG:1
	CASE 1
		CALL TMP_PRINT_CHARA_STARS_NUM(ARG:0)
		PRINT |
		PRINTFORM {ABL:(ARG:0):武闘 + ABL:(ARG:0):防衛 + ABL:(ARG:0):知略 + ABL:(ARG:0):政治,5}|
		PRINTFORM {ABL:(ARG:0):武闘 + ABL:(ARG:0):防衛 + ABL:(ARG:0):知略 + ABL:(ARG:0):政治 + ABL:(ARG:0):妖術 + ABL:(ARG:0):歌唱+ABL:(ARG:0):料理,5}
		FOR LOCAL, 0, VARSIZE("HEAD_SLG")
			PRINT |
			CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ARG:0):(HEAD_SLG:LOCAL))
			PRINTFORM {ABL:(ARG:0):(HEAD_SLG:LOCAL), 4}
		NEXT
		PRINT |
	CASE 2
		FOR LOCAL, 0, VARSIZE("HEAD_ABL")
			IF HEAD_ABL:LOCAL == GETNUM(ABL, "性知識")
				CALL PRINT_ALPHABET_RANK(ランク_性知識, ABL:(ARG:0):(HEAD_ABL:LOCAL))
			ELSE
				CALL PRINT_ALPHABET_RANK(ランク_その他, ABL:(ARG:0):(HEAD_ABL:LOCAL))
			ENDIF
			PRINTFORM %TOSTR_R(ABL:(ARG:0):(HEAD_ABL:LOCAL), 4)%|
		NEXT
		FOR LOCAL, 0, VARSIZE("HEAD_BASE")
			PRINTFORM %TOSTR_R(BASE:(ARG:0):(HEAD_BASE:LOCAL), 5)%|
		NEXT
	CASE 3
		FOR LOCAL, 0, VARSIZE("HEAD_ABL_2")
			CALL PRINT_ALPHABET_RANK(ランク_その他, ABL:(ARG:0):(HEAD_ABL_2:LOCAL))
			PRINTFORM %TOSTR_R(ABL:(ARG:0):(HEAD_ABL_2:LOCAL), 4)%|
		NEXT
	CASE 4
		FOR LOCAL, 0, VARSIZE("HEAD_CFLAG")
			PRINTFORM {CFLAG:(ARG:0):(HEAD_CFLAG:LOCAL), 8}|
		NEXT
	CASE 5
		FOR LOCAL, 1, MAX_SP_COUNTRY
			PRINTFORM  \@ GETBIT(TALENT:(ARG:0):特殊勢力陥落系, LOCAL) ? 〇 # -- \@ |
		NEXT
ENDSELECT
RETURN
;-------------------------------------------------
; ARG:0を文字列に変換 ARG:1の幅で右寄せ
;-------------------------------------------------
@TOSTR_R(ARG:0,ARG:1)
#FUNCTIONS
VARSET LOCALS
LOCALS '= TOSTR(ARG:0)
LOCAL = STRLENS(LOCALS)
IF LOCAL < ARG:1
	FOR LOCAL:1,0,ARG:1-LOCAL
		LOCALS:1 += " "
	NEXT
ENDIF
LOCALS:1 += LOCALS
RETURNF LOCALS:1
