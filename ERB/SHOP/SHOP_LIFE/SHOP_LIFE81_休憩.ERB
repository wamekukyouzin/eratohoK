;※休憩は特殊なメニューでメニューボタンの表示位置を固定している
;　番号を変える場合は@COLUMN_LEFT_MENU_SHOWを確認する必要あり
;-------------------------------------------------
;「休憩」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME81
RESULTS:0 '= "休憩"

;-------------------------------------------------
;「休憩」の選択可否
;-------------------------------------------------
@SHOP_LIFE_CHECK81
SIF CFLAG:MASTER:捕虜先
	RETURN 0
RETURN 1

;-------------------------------------------------
;「休憩」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY81
RESULT = 1
LINES_SHOP = LINECOUNT
CALL SHOP_REST
SHOP_TIME = CALC_SHOP_TIME()
BEGIN TURNEND

;-------------------------------------------------
;「休憩」の右カラムオフ判定
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_COLUMN_RIGHT_OFF_CHECK81

;-------------------------------------------------
;「休憩」の処理
;-------------------------------------------------
;休憩を選択したときの余剰行動回数消費
@SHOP_REST
#DIM 余剰

FLAG:夜這い = 0

;クリアフラグ立ってたらなし
SIF FLAG:クリアフラグ
	RETURN 0

余剰 = CALC_SHOP_TIME() - SHOP_TIME
CALL SINGLE_DRAWLINE
SELECTCASE RAND:100
	CASE IS < 40
		PRINTFORMW 余った行動回数で訓練を行った……
		SELECTCASE RAND:6
			CASE 0
				LOCAL = GETNUM(EXP, "武闘経験値")
			CASE 1
				LOCAL = GETNUM(EXP, "知略経験値")
			CASE 2
				LOCAL = GETNUM(EXP, "防衛経験値")
			CASE 3
				LOCAL = GETNUM(EXP, "政治経験値")
			CASE 4
				LOCAL = GETNUM(EXP, "歌唱経験値")
			CASE 5
				LOCAL = GETNUM(EXP, "料理経験値")
		ENDSELECT

		CALL PRINT_ADD_EXP(MASTER, EXPNAME:LOCAL, LIMIT(余剰, 1, 5), 1)

		CALL TRAIN_AUTO_ABLUP(MASTER)
		WAIT
	CASE IS < 70
		PRINTFORMW 余った行動回数で仲間と交友を深めた……
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:所属 == CFLAG:MASTER:所属 && !CFLAG:LOCAL:捕虜先
				CALL CHANGE_RELATION_O_TO_O(LOCAL, MASTER, MIN(余剰, 5), MAX(-余剰, -5))
				CALL CHANGE_RELATION_O_TO_O(MASTER, LOCAL, MIN(余剰, 5), MAX(-余剰, -5))
			ENDIF
		NEXT
	CASEELSE
		LOCAL = LIMIT(余剰 / 2, 1, 5) * 500
		PRINTFORMW 余った行動回数で小遣いを稼いだ……
		PRINTFORM 金
		CALL COLOR_PRINT(@"{LOCAL}", カラー_注意)
		PRINTFORMW を手に入れた
		MONEY += LOCAL
ENDSELECT

LINES_SHOP = LINECOUNT

