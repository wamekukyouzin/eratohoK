;-------------------------------------------------
;「投獄する」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME25
RESULTS:0 '= "投獄する"

;-------------------------------------------------
;「投獄する」の選択可否
;-------------------------------------------------
@SHOP_LIFE_CHECK25
SIF CFLAG:MASTER:捕虜先
	RETURN 0
SIF !CFLAG:MASTER:所属
	RETURN 0
RETURN 1

;-------------------------------------------------
;「投獄する」の選択可能キャラ存在判定
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA25(ARG:0)
;非主人公、主人公より政治が低い、君主でない、捕虜でない、動物でない、育児されている子供でない、特殊キャラでない
{
RETURN ARG:0 != MASTER && CFLAG:(ARG:0):所属 == CFLAG:MASTER:所属 && ABL:(ARG:0):政治 < ABL:(MASTER):政治 && CFLAG:ARG:捕虜先 == 0 && ARG:0 != GET_COUNTRY_BOSS(CFLAG:MASTER:所属) && !IS_ANIMAL(ARG:0) &&
 CFLAG:(ARG:0):行動不能状態 != 行動不能_子供 && !IS_SP_CHARA(ARG:0)
}

;-------------------------------------------------
;「投獄するの左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY25
FLAG:拠点フェイズページ = 1
FLAG:夜這い = 0
RETURN 0

;-------------------------------------------------
;「投獄する」の右カラムキャラリストボタン表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD25(ARG:0)
PRINTPLAIN  
PRINTFORM 政:
CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ARG:0):政治)
PRINTFORM  {ABL:(ARG:0):政治, 2, RIGHT} 
RETURN 1

;-------------------------------------------------
;「投獄する」の右カラムキャラリスト１のボタン（関数未指定なら選択中色なし表示）
;-------------------------------------------------
@SHOP_LIFE_LIST1_BUTTON25(ARG:0, ARG:1)
;キャラ, 選択中カラー表示フラグ, ボタン番号に追加する数値, CHECKCHARAの戻り値, 行動済みマークをオフにするフラグ
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, 0, SHOP_LIFE_LIST1_ADD_INPUT, ARG:1, 1)
RETURN 0

;-------------------------------------------------
;「投獄する」の右カラム表示処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW25
CALL COLUMN_RIGHT_TITLE("対象者選択", "0", "1", "0", "0")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

CALL COLORPRINT(@" 政治の能力値が自身より低い自勢力対象に可  相手に嫌われます  %ANAME(MASTER)%の政治:", カラー_注釈)
CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(MASTER):政治)
CALL COLORPRINT(@" {ABL:(MASTER):政治, 2, RIGHT}", カラー_注釈)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;標準的なリスト
;ARG:0 = 3　ボタンを一行に3つ並べる
ソートキー保存 = ソートキー
ソートキー = ソート_政治
CALL COLUMN_RIGHT_CHARALIST(3)
ソートキー = ソートキー保存
RETURN 1

;-------------------------------------------------
;「投獄する」のリスト１入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP25(ARG:0)
#DIM SAVE_LINE_SHOP_LIFE

FLAG:能力表示モード = 0
SAVE_LINE_SHOP_LIFE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTFORML %ANAME(ARG:0)%に適当な罪を被せ、投獄します
PRINTFORML 当然、本人からは嫌われます
PRINTFORML それでも実行しますか？
CALL ASK_YN
IF RESULT == 0
	SETCOLOR カラー_注意
	PRINTFORMW <%ANAME(ARG:0)%>を投獄しました
	RESETCOLOR
	CALL CAPTURE(ARG:0, CFLAG:MASTER:所属)
	CFLAG:(ARG:0):2 -= MAX(500, CFLAG:(ARG:0):2 * 3 / 10)
	CFLAG:(ARG:0):3 -= MAX(500, CFLAG:(ARG:0):3 * 3 / 10)
	CFLAG:(ARG:0):4 -= MAX(500, CFLAG:(ARG:0):4 * 3 / 10)
	RETURN 1
ELSE
	LINES_SHOP = SAVE_LINE_SHOP_LIFE
	RETURN 0
ENDIF


