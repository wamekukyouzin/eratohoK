;-------------------
;存在判定
;なにもさせないこと
;-------------------
@TERRITORY_EVENT_REST_UNDER_A_TREE_EXISTS()
#DIM 対象
RETURN 1

;-------------------
;発生判定
;-------------------
@TERRITORY_EVENT_REST_UNDER_A_TREE_DECISION(対象)
#DIM 対象
RETURN 1

;-------------------
;本体
;-------------------
@TERRITORY_EVENT_REST_UNDER_A_TREE(対象)
#DIM 対象
PRINTFORML 森の中を歩いていると、丁度良い木陰を見つけた
PRINTFORML 寝転がれば気持ち良さそうだ
PRINTFORMW どうしよう？
PRINTL
CALL ASK_YN("寝転がる", "立ち去る")
IF RESULT == 1
	PRINTFORML 今は先を急いでいる
	PRINTFORMW %ANAME(対象)%はその場を後にした
    RETURN 1
ENDIF

PRINTFORML 折角だから休んで行こう
PRINTFORMW %ANAME(対象)%は木陰に寝転んだ
IF RAND:3 == 0
    PRINTFORML すると腰の辺りに何か硬いものが当たるのを感じた
    PRINTFORML 何かが埋まっているのだろうか？
    PRINTFORML 掘り返してみると小判の山が出てきた！
    PRINTFORML かつて誰かが埋めた物のようだ
    PRINTFORMW 当たりに人の気配はない、貰っていくことにした
    MONEY += 5000
    CALL ICPRINT("金<5000>を手に入れた", "W", カラー_注意)
ELSE
    PRINTFORML ゴロンと寝転がり目を閉じる
    PRINTFORML 頬に当たる風が心地よい
    PRINTFORMW 次第に眠気に襲われ、うつらうつらとしだした
    PRINTFORML ・
    PRINTFORML ・
    PRINTFORMW ・
    IF RAND:2 == 0
        PRINTFORML 烏の鳴き声で目が覚めた
        PRINTFORML どうやらすっかり眠ってしまっていたらしい、空は夕焼けに染まっている
        PRINTFORML とても心地の良い眠りだった
        PRINTFORMW %ANAME(対象)%は起き上がりグッと一伸びをすると、軽やかな足取りで帰路についた
    ELSE
        PRINTFORML 烏の鳴き声で目が覚めた
        PRINTFORML どうやらすっかり眠ってしまっていたらしい、空は夕焼けに染まっている
        PRINTFORML 起き上がり帰ろうとした%ANAME(対象)%は違和感を覚えた
        PRINTFORMW 財布がない！
        PRINTFORML どうやら寝ている間に何者かにスられてしまったらしい
        PRINTFORMW 不用心な自分を責めながらとぼとぼと帰路についた
        LOCAL = MIN(MONEY, 3000)
        MONEY -= LOCAL
        CALL ICPRINT(@"金<{LOCAL}>を失った", "W", カラー_注意)
    ENDIF
ENDIF
RETURN 1

