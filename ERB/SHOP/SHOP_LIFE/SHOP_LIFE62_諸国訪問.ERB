;-------------------------------------------------
;「諸国訪問」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME62
RESULTS:0 '= "諸国訪問"

;-------------------------------------------------
;「諸国訪問」の選択可否判定
;-------------------------------------------------
@SHOP_LIFE_CHECK62
SIF CFLAG:MASTER:行動不能状態 == 行動不能_臨月 || COOLTIME:MASTER:0 > 1
	RETURN 0
SIF CFLAG:MASTER:捕虜先
	RETURN 0
RETURN 1

;-------------------------------------------------
;「諸国訪問」の選択可能キャラ存在判定（行ける先）
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA62(ARG:0)
;非主人公、どこかの勢力のえらいひと(自勢力以外)
RETURN ARG:0 != MASTER && GET_COUNTRY_BOSS(CFLAG:(ARG:0):所属) == ARG:0 && CFLAG:(ARG:0):所属 != CFLAG:MASTER:所属

;-------------------------------------------------
;「諸国訪問の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY62
FLAG:拠点フェイズページ = 1
FLAG:夜這い = 0
RETURN 0

;-------------------------------------------------
;「諸国訪問」の右カラム表示処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW62
;タイトル
CALL COLUMN_RIGHT_TITLE("対象勢力選択", "", "", "1", "0")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL
;標準的なキャラリストとページ移動
CALL COLUMN_RIGHT_CHARALIST
RETURN 0

;-------------------------------------------------
;「諸国訪問」のリスト１入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP62(訪問先君主)
#DIM 訪問先君主
#DIM 訪問先
#DIM 候補, 3000
#DIM 人数
#DIM 対象
#DIM CONST VISIT_EVENT_MAX = 10
#DIM 好感度効果量
訪問先 = CFLAG:(訪問先君主):所属

FLAG:能力表示モード = 0

VARSET 候補, -1
VARSET 人数
FOR LOCAL, 0, CHARANUM
	IF 訪問先 == CFLAG:LOCAL:所属 && CFLAG:LOCAL:捕虜先 == 0
		候補:人数 = LOCAL
		人数 ++
	ENDIF
NEXT

対象 = 候補:(RAND:人数)

CALL SINGLE_DRAWLINE
PRINTFORMW %ANAME(訪問先君主)%の勢力を訪ねた……
PRINTFORMW ……%ANAME(対象)%に出会った
PRINTFORMW 少しの間、世間話をした……

IF !CFLAG:対象:面識
	CALL COLORPRINT(@"%ANAME(対象)%の面識を得ました", カラー_注意, "L")
	CFLAG:対象:面識 = 1
ENDIF

;1500に対する現在の好感度の割合。ただし最低でも100は入る
好感度効果量 = MAX(500 * MAX((1500 - CFLAG:対象:好感度) / 15, 0) / 100, 100)
CFLAG:対象:好感度 += 好感度効果量

CALL COLORPRINT(@"%ANAME(対象)%の好感度が{好感度効果量}上がりました", カラー_注意, "L")

PRINTFORML

SHOP_TIME += 1


FOR LOCAL, 0, VISIT_EVENT_MAX
	TRYCCALLFORM VISIT_EVENT_{LOCAL}_RATE(訪問先君主, 訪問先, 対象)
		IF RAND:1000 < RESULT
			CALLFORM VISIT_EVENT_{LOCAL}_DECISION(訪問先君主, 訪問先, 対象)
			IF RESULT == 1
				CALLFORM VISIT_EVENT_{LOCAL}(訪問先君主, 訪問先, 対象)
				BREAK
			ENDIF
		ENDIF
	CATCH
		BREAK
	ENDCATCH
NEXT

RESULT = 1
LINES_SHOP = LINECOUNT
BEGIN TURNEND


;-------------------------------------------------
;移籍の申し出
;-------------------------------------------------
@VISIT_EVENT_0_RATE(訪問先君主, 訪問先, 対象)
#DIM 訪問先君主
#DIM 訪問先
#DIM 対象
RETURN 250

@VISIT_EVENT_0_DECISION(訪問先君主, 訪問先, 対象)
#DIM 訪問先君主
#DIM 訪問先
#DIM 対象
SIF !CFLAG:MASTER:所属
	RETURN 0
SIF !IS_SLAVE(対象) && !IS_LOVER(対象)
	RETURN 0
SIF 対象 == 訪問先君主
	RETURN 0
SIF IS_SP_COUNTRY(CFLAG:MASTER:所属)
	RETURN 0
RETURN 1

@VISIT_EVENT_0(訪問先君主, 訪問先, 対象)
#DIM 訪問先君主
#DIM 訪問先
#DIM 対象
PRINTFORMW ……別れ際、%ANAME(対象)%に呼び止められた
PRINTFORMW どうか側に置いてくれないか、とのことだ
PRINTFORMW %ANAME(MASTER)%への想いから、%ANAME(訪問先君主)%を捨て、%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%につこうということらしい
PRINTFORMW 移籍してくれれば助かりはするが、%ANAME(訪問先君主)%からすれば裏切るようそそのかしたように見えるだろう
PRINTFORMW 関係が悪くなることは必至だが……
CALL ASK_YN("来てもらう", "断る")
IF RESULT == 0
	PRINTFORML %ANAME(対象)%の力は、外交関係の悪化をおいても是非欲しい
	PRINTFORML %ANAME(MASTER)%は%ANAME(対象)%の手を引き、拠点へと二人で戻った……
	CALL COLORPRINT(@"%ANAME(対象)%が仲間になりました", カラー_注意, "L")
	CALL COLORPRINT(@"%ANAME(訪問先君主)%との関係が悪化しました", カラー_警告, "L")
	CALL CHANGE_COUNTRY(対象, CFLAG:MASTER:所属, 1)
	CALL CHANGE_RELATION_C_TO_O(訪問先, MASTER, -100, 100)
ELSE
	PRINTFORML %ANAME(訪問先君主)%との関係をこじらせるわけにはいかない
	PRINTFORML 丁重に断ると、%ANAME(対象)%はさみしそうな表情を浮かべたが、納得してくれたようだ……
ENDIF


;-------------------------------------------------
;移籍の勧誘
;-------------------------------------------------
@VISIT_EVENT_1_RATE(訪問先君主, 訪問先, 対象)
#DIM 訪問先君主
#DIM 訪問先
#DIM 対象
RETURN (ABL:MASTER:武闘 + ABL:MASTER:防衛 + ABL:MASTER:知略 + ABL:MASTER:政治) / 3 + MIN(CFLAG:対象:好感度 / 10, 150)

@VISIT_EVENT_1_DECISION(訪問先君主, 訪問先, 対象)
#DIM 訪問先君主
#DIM 訪問先
#DIM 対象
SIF !CFLAG:MASTER:所属
	RETURN 0
SIF GET_COUNTRY_BOSS(CFLAG:MASTER:所属) == MASTER
	RETURN 0
RETURN 1

@VISIT_EVENT_1(訪問先君主, 訪問先, 対象)
#DIM 訪問先君主
#DIM 訪問先
#DIM 対象
#DIM 支度金

支度金 = (ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治) * 50 * RAND(80, 120) / 100

PRINTFORMW ……別れ際、%ANAME(対象)%に呼び止められた
PRINTFORMW %ANAME(訪問先君主)%の下に来ないか、とのことだ
PRINTFORM 支度金として、金
CALL COLORPRINT(@"{支度金}", カラー_注意)
PRINTFORM を用意しているという
PRINTFORMW どうやら%ANAME(MASTER)%の能力を見込んで、引き抜こうとしているようだ
PRINTFORMW 応じれば、%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%との関係は悪くなってしまうだろうが……？

CALL ASK_YN("応じる", "断る")
IF RESULT == 0
	PRINTFORML こんな時代だ。見込みのあるものについていくのが正解というものだろう
	PRINTFORML %ANAME(MASTER)%が頷くと、%ANAME(対象)%は満足げな笑みを浮かべた……
	CALL COLORPRINT(@"%ANAME(訪問先君主)%の下に移籍しました", カラー_注意, "W")
	CALL COLORPRINT(@"支度金{支度金}を受け取りました", カラー_注意, "W")
	CALL COLORPRINT(@"%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%との関係が悪化しました", カラー_警告, "W")
	CALL CHANGE_RELATION_C_TO_O(CFLAG:MASTER:所属, MASTER, -300, 300)
	CALL CHANGE_COUNTRY(MASTER, 訪問先, 1)
	MONEY += 支度金
ELSE
	PRINTFORML %ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%との関係をこじらせるわけにはいかない
	PRINTFORML 丁重に断ると、%ANAME(対象)%は残念そうな表情を浮かべたが、納得してくれたようだ……
ENDIF

;-------------------------------------------------
;士官の勧誘
;-------------------------------------------------
@VISIT_EVENT_2_RATE(訪問先君主, 訪問先, 対象)
#DIM 訪問先君主
#DIM 訪問先
#DIM 対象
RETURN (ABL:MASTER:武闘 + ABL:MASTER:防衛 + ABL:MASTER:知略 + ABL:MASTER:政治) / 3 + MIN(CFLAG:対象:好感度 / 10, 150)

@VISIT_EVENT_2_DECISION(訪問先君主, 訪問先, 対象)
#DIM 訪問先君主
#DIM 訪問先
#DIM 対象
SIF CFLAG:MASTER:所属
	RETURN 0
RETURN 1

@VISIT_EVENT_2(訪問先君主, 訪問先, 対象)
#DIM 訪問先君主
#DIM 訪問先
#DIM 対象
#DIM 支度金

支度金 = (ABL:MASTER:武闘 + ABL:MASTER:知略 + ABL:MASTER:政治) * 50 * RAND(80, 120) / 100

PRINTFORMW ……別れ際、%ANAME(対象)%に呼び止められた
PRINTFORMW %ANAME(訪問先君主)%の下に来ないか、とのことだ
PRINTFORM 支度金として、金
CALL COLORPRINT(@"{支度金}", カラー_注意)
PRINTFORM を用意しているという
PRINTFORMW どうやら%ANAME(MASTER)%の能力を見込んで、登用しようとしているようだ

CALL ASK_YN("応じる", "断る")
IF RESULT == 0
	PRINTFORML こんな時代だ。見込みのあるものについていくのが正解というものだろう
	PRINTFORML %ANAME(MASTER)%が頷くと、%ANAME(対象)%は満足げな笑みを浮かべた……
	CALL COLORPRINT(@"%ANAME(訪問先君主)%の下に移籍しました", カラー_注意, "W")
	CALL COLORPRINT(@"支度金{支度金}を受け取りました", カラー_注意, "W")
	CALL CHANGE_COUNTRY(MASTER, 訪問先, 1)
	MONEY += 支度金
ELSE
	PRINTFORML 丁重に断ると、%ANAME(対象)%は残念そうな表情を浮かべたが、納得してくれたようだ……
ENDIF