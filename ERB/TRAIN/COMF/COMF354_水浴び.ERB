;水浴び

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME354
RESULTS:0 = 水浴びする
RESULTS:1 = 水浴びに付き合う

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE354
;共通部分
CALL COM_ABLE_COMMON(354)
SIF RESULT == 0
	RETURN 0
;主人公がプレイヤーのとき限定
SIF MPLY:0 != MASTER
	RETURN 0
;川辺でのデート中限定
SIF GET_PLACENAME(TFLAG:54) != "川辺"
	RETURN 0
;怪我なら不可
SIF CFLAG:(MTAR:0):行動不能状態 == 3
	RETURN 0
;キス中は不可
SIF IS_EQUIP(MPLY:0, 342)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM354
;実行判定
CALL COM_ORDER_COMMON
IF RESULT == 0
	RETURN 0
ENDIF

IF MPLY:0 == MASTER
	;コマンドの成否をTFLAG:18にセット
	CALL JUDGE_COM_RESULT(MTAR:0, 5, 10)
	LOCAL:1 = MTAR:0
ELSE
	;コマンドの成否をTFLAG:18にセット
	CALL JUDGE_COM_RESULT(MPLY:0, 5, 10)
	LOCAL:1 = MPLY:0
ENDIF

;●プレイヤー側の処理
;固定で獲得するソース
SOURCE:(MPLY:0):歓楽 = 20
SOURCE:(MPLY:0):接触 = 10

;親密に応じた歓楽のソース追加
CALL ADD_SOURCE_KANRAKU(MPLY:0, 190)
SOURCE:(MPLY:0):歓楽 = MAX(SOURCE:(MPLY:0):歓楽, 0)

IF TALENT:(MPLY:0):臆病
	TIMES SOURCE:(MPLY:0):歓楽, 0.85
ENDIF
IF TALENT:(MPLY:0):大人しい
	TIMES SOURCE:(MPLY:0):歓楽, 0.85
ENDIF
IF TALENT:(MPLY:0):恥じらい
	TIMES SOURCE:(MPLY:0):歓楽, 0.85
ENDIF

;主導権に応じたソースの追加
CALL ADD_SOURCE_INITIATIVE_N(MPLY:0, 100, 100)

;●ターゲット側の処理
;固定で獲得するソース
SOURCE:(MTAR:0):歓楽 = 20
SOURCE:(MTAR:0):接触 = 10

;親密に応じた歓楽のソース追加
CALL ADD_SOURCE_KANRAKU(MTAR:0, 190)
SOURCE:(MTAR:0):歓楽 = MAX(SOURCE:(MTAR:0):歓楽, 0)

IF TALENT:(MTAR:0):臆病
	TIMES SOURCE:(MTAR:0):歓楽, 0.85
ENDIF
IF TALENT:(MTAR:0):大人しい
	TIMES SOURCE:(MTAR:0):歓楽, 0.85
ENDIF
IF TALENT:(MTAR:0):恥じらい
	TIMES SOURCE:(MTAR:0):歓楽, 0.85
ENDIF

;同性
IF IS_SAMESEX(MPLY:0, MTAR:0)
	SOURCE:(MPLY:0):露出 = 5
	SOURCE:(MTAR:0):露出 = 5
;異性
ELSE
	SOURCE:(MPLY:0):露出 = 20
	SOURCE:(MTAR:0):露出 = 20
ENDIF

;主導権に応じたソースの追加
CALL ADD_SOURCE_INITIATIVE_N(MTAR:0, 100, 100)

;失敗
IF TFLAG:18 == -1
	TIMES SOURCE:(MPLY:0):歓楽, 0.10
	TIMES SOURCE:(MTAR:0):歓楽, 0.10
	SOURCE:(MPLY:0):不満 += 500
	SOURCE:(MTAR:0):不満 += 500
	TFLAG:37 -= 5
;成功
ELSEIF TFLAG:18 == 0

;大成功
ELSE
	TIMES SOURCE:(MPLY:0):歓楽, 2.00
	TIMES SOURCE:(MTAR:0):歓楽, 2.00
	TFLAG:37 += 5
ENDIF

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = -1

RETURN 1

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER354(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 30

;共通部分
CALL COM_ORDER(ARG:0)

;好感度
IF CFLAG:(ARG:0):2 < 300
	IF CFLAG:(ARG:0):2 < 50
		LOCAL:0 = -30
	ELSEIF CFLAG:(ARG:0):2 < 150
		LOCAL:0 = -15
	ELSE
		LOCAL:0 = -5
	ENDIF
	CALL COM_ORDER_ELEMENT(ARG:0, @"好感度不足", LOCAL:0)
ELSE
	LOCAL:0 = CFLAG:(ARG:0):2 / 75
	CALL COM_ORDER_ELEMENT(ARG:0, @"好感度", LOCAL:0)
ENDIF

IF TALENT:(ARG:0):臆病
	CALL COM_ORDER_ELEMENT(ARG:0, "臆病", -3)
ENDIF
IF TALENT:(ARG:0):大人しい
	CALL COM_ORDER_ELEMENT(ARG:0, "大人しい", -2)
ENDIF
IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -3)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE354
;主導権が主人公側
IF IS_INITIATIVE(MASTER)
	PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%と一緒に川で水浴びをした
;主導権が相手側
ELSE
	PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に誘われ、一緒に川で水浴びをした
ENDIF

IF TFLAG:18 == 1
	PRINTFORML %ANAME(MTAR:0)%は心から楽しそうに笑っている…
ELSEIF TFLAG:18 == -1
	PRINTFORML %ANAME(MTAR:0)%はあまり楽しくなさそうだ…
ENDIF

PRINTFORM %ANAME(MTAR:0)%の服は濡れて肌に張り付き、その
	SIF TALENT:(MTAR:0):処女 == 1 && IS_FEMALE(MTAR:0)
		PRINT 男を知らない
	SIF TALENT:(MTAR:0):体格 == 体格_子供
		PRINT 幼い
	SIF GET_HIPSIZE(MTAR:0) >= 1 || IS_BOIN(MTAR:0)
		PRINT 豊満な
	PRINTFORML 身体の線を浮き彫りにしている。


;-------------------------------------------------
;地の文(パラメータ・刻印変動後)
;-------------------------------------------------
@COM_TEXT_LAST354
;日常コマンドに共通の地の文
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN354
RETURN コマンド_日常
