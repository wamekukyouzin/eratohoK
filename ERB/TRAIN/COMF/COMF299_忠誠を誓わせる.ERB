;忠誠を誓わせる

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME299
RESULTS:0 = 忠誠を誓わせる
RESULTS:1 = 忠誠を誓わせる
RESULTS:2 = 忠誠を誓わせる
RESULTS:3 = 忠誠を誓わせる
RESULTS:4 = 忠誠を誓わせる
RESULTS:5 = 忠誠を誓わせる

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE299
;共通部分
CALL COM_ABLE_COMMON(299)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーとターゲットのうちいずれかが行動不能なら不可(ターゲットは体力切れなら可)
SIF !IS_PLAYABLE(MPLY:0) || TCVAR:(MTAR:0):52 || TCVAR:(MTAR:0):53
	RETURN 0
;主人公がプレイヤーのとき限定
SIF MPLY:0 != MASTER
	RETURN 0
;主人公に主導権があるとき限定
SIF FLAG:主導権所有者 != MASTER && FLAG:主導権所有者 != -1
	RETURN 0
;既に烙印orIS_LOVERの場合、「自勢力所属のキャラ」と「自分の勢力以外の捕虜」は不可
SIF (TALENT:(MTAR:0):烙印 || IS_LOVER(MTAR:0) || IS_SLAVED_BY(MTAR:0)) && (CFLAG:MASTER:所属 == CFLAG:(MTAR:0):所属 || CFLAG:(MTAR:0):捕虜先 != CFLAG:MASTER:所属)
	RETURN 0
;プレイヤーの口が塞がっているなら不可
SIF IS_M_HOLD(MPLY:0)
	RETURN 0
;ターゲットの口が塞がっているなら不可
SIF IS_M_HOLD(MTAR:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM299
;●実行値の計算
CVARSET TCVAR, 24, -10000
CVARSET TCVAR, 25, 10000

TCVAR:(MTAR:0):25 = 90

CALL COM_ORDER(MTAR:0)

CALL COM_ORDER_ELEMENT(MTAR:0, "依存度", CFLAG:(MTAR:0):3 / 50)

IF TALENT:(MTAR:0):服従
	CALL COM_ORDER_ELEMENT(MTAR:0, "服従", 50)
ENDIF

IF TALENT:(MTAR:0):隷属
	CALL COM_ORDER_ELEMENT(MTAR:0, "隷属", 50)
ENDIF

IF TALENT:(MTAR:0):一線越えない
	CALL COM_ORDER_ELEMENT(MTAR:0, "一線越えない", -15)
ENDIF

IF FLAG:夜這い
	CALL COM_ORDER_ELEMENT(MTAR:0, "夜這い/手引き", -50)
ENDIF

LOCAL = LIMIT(REL_LIKE:(MTAR:0):(MPLY:0) / 75 - REL_HATE:(MTAR:0):(MPLY:0) / 50,  -20, 20)

IF LOCAL != 0
	CALL COM_ORDER_ELEMENT(MTAR:0, "友好度/嫌悪度", LOCAL)
ENDIF

SELECTCASE CONFIG:314
	CASE 2
		CALL COM_ORDER_ELEMENT(MTAR:0, "難易度補正", -5)
	CASE 3
		CALL COM_ORDER_ELEMENT(MTAR:0, "難易度補正", -10)
	CASE 4
		CALL COM_ORDER_ELEMENT(MTAR:0, "難易度補正", -20)
ENDSELECT


;判定結果を表示
PRINTFORMW  = {TCVAR:(MTAR:0):24} %TOSTR_COMPARE(TCVAR:(MTAR:0):24, TCVAR:(MTAR:0):25)% 実行値{TCVAR:(MTAR:0):25}

;判定成功
IF TCVAR:(MTAR:0):24 >= TCVAR:(MTAR:0):25
	;口上表示
	CALL KOJO_EVENT(MTAR:0, 50)

	LOCALS:0 = 
	IF !IS_LOVER(MTAR:0)
		TALENT:(MTAR:0):烙印 = 1
		LOCALS:0 += "<烙印>"
	ENDIF
	IF !TALENT:(MTAR:0):合意
		TALENT:(MTAR:0):合意 = 1
		LOCALS:0 += "<合意>"
	ENDIF

	PRINTL 
	IF LOCALS:0 != ""
		SETCOLOR カラー_注意
		PRINTFORML %ANAME(MTAR:0)%は%LOCALS:0%を得た
		RESETCOLOR
	ENDIF

	IF !TALENT:(MTAR:0):特殊勢力素質 && IS_SP_COUNTRY(CFLAG:MASTER:所属) && !TALENT:(MTAR:0):特殊勢力陥落系 && !GETBIT(TALENT:(MTAR:0):特殊勢力陥落系, SP_COUNTRY_TO_CONST(CFLAG:MASTER:所属))
		CALL COLOR_PRINTW(@"%ANAME(MTAR:0)%は<%SP_FALLEN_TALENT_NAME:(SP_COUNTRY_TO_CONST(CFLAG:MASTER:所属))%>になった", カラー_注意)
		SETBIT TALENT:(MTAR:0):特殊勢力陥落系, SP_COUNTRY_TO_CONST(CFLAG:MASTER:所属)
	ENDIF

	IF TALENT:(MTAR:0):プライド高い && CFLAG:(MTAR:0):好感度 < -500 && !TALENT:(MTAR:0):服従 && STRFIND(LOCALS:0, "烙印")
		PRINTL 
		SETCOLOR カラー_注意
		PRINTFORML 嫌悪している%ANAME(MASTER)%の奴隷になると認めたことで、%ANAME(MTAR:0)%のプライドは完全に折れてしまった
		CALL TALENT_LOSE_PRIDE(MTAR:0, 1, 1)
		RESETCOLOR
	ENDIF

	IF IS_COUNTRY(CFLAG:MASTER:所属) && CFLAG:(MTAR:0):捕虜先 == CFLAG:MASTER:所属
		CALL CHANGE_RELATION_C_TO_C(CFLAG:(MTAR:0):所属, CFLAG:MASTER:所属, -200, 200)
		PRINTFORMW %ANAME(MTAR:0)%が我が国の士官になりました
		PRINTFORML %ANAME(MTAR:0)%を牢獄から出してやりますか？
		CALL ASK_YN("出す", "牢獄での調教を続ける(後から解放可)")
		CALL CHANGE_COUNTRY(MTAR:0, CFLAG:MASTER:所属, !RESULT)
	ENDIF

	IF IS_COUNTRY(CFLAG:(MTAR:0):所属) && CFLAG:(MTAR:0):所属 != CFLAG:MASTER:所属
		IF GET_COUNTRY_BOSS(CFLAG:(MTAR:0):所属) == MTAR:0
			CALL CHANGE_RELATION_C_TO_C(CFLAG:(MTAR:0):所属, CFLAG:MASTER:所属, 500, -500)
		ELSE
			CALL CHANGE_RELATION_C_TO_C(CFLAG:(MTAR:0):所属, CFLAG:MASTER:所属, -200, 200)
		ENDIF
	ENDIF

;判定失敗
ELSE
	;口上表示
	CALL KOJO_EVENT(MTAR:0, 51)

	IF CFLAG:(MTAR:0):捕虜先 == CFLAG:MASTER:所属
		SOURCE:(MTAR:0):反感 = 300
	ELSE
		SOURCE:(MTAR:0):反感 = 3000
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN299
RETURN コマンド_ウフフ
