;電気按摩

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME103
LOCALS:0 = 電気按摩

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE103
;共通部分
CALL COM_ABLE_COMMON(103)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
;プレイヤーが挿入中なら不可
SIF IS_INSERT_SINGLE(MPLY:0, "全")
	RETURN 0
;プレイヤーが顔面騎乗されているなら不可
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;プレイヤーが土下座しているなら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 110)
	RETURN 0
;ターゲットが土下座しているなら不可
SIF IS_EQUIP_PLAYER(MTAR:0, 110)
	RETURN 0
;ターゲットが挿入中なら不可
SIF IS_INSERT_SINGLE(MTAR:0, "全")
	RETURN 0
;ターゲットが顔面騎乗中なら不可
SIF IS_RIDE(MTAR:0)
	RETURN 0
;プレイヤーがキス中なら不可
SIF IS_EQUIP(MPLY:0, 20)
	RETURN 0
;ターゲットがキス中なら不可
SIF IS_EQUIP(MTAR:0, 20)
	RETURN 0
;プレイヤーが胸愛撫中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 1)
	RETURN 0
;ターゲットが胸愛撫中なら不可
SIF IS_EQUIP_PLAYER(MTAR:0, 1)
	RETURN 0
;プレイヤーが足舐めされているなら不可
SIF IS_EQUIP_TARGET(MPLY:0, 104)
	RETURN 0
;ターゲットが触手オナホされているなら不可
SIF IS_EQUIP_TARGET(MTAR:0, 205)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM103
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;対Ｐ奉仕系コマンドの継続フラグを解除
CALL RELEASE_SERVE_P

;●プレイヤーについて判定
DOWNBASE:(MPLY:0):体力 += 120

EXP:(MPLY:0):性技経験値 += 1

SOURCE:(MPLY:0):奉仕 = SERVE_HOUSHI(MPLY:0, 150)
SOURCE:(MPLY:0):接触 = 60
SOURCE:(MPLY:0):逸脱 = 40
SOURCE:(MPLY:0):性行動 = 150

;プレイヤー主導の場合
IF GET_COM_INITIATIVE() == 0
	SOURCE:(MPLY:0):嗜虐 = 100
ENDIF

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 190, 80)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●ターゲットについて判定
DOWNBASE:(MTAR:0):体力 += 80

SOURCE:(MTAR:0):接触 = 60
SOURCE:(MTAR:0):逸脱 = 40
SOURCE:(MTAR:0):苦痛 = 60
SOURCE:(MTAR:0):露出 = 80
SOURCE:(MTAR:0):性行動 = 150
IF PENIS(MTAR:0)
	SOURCE:(MTAR:0):快Ｐ = SENSE_HOUSHI_P(MPLY:0, MTAR:0, 1200 + TALENT:(MPLY:0):美脚 * 300)
ELSE
	SOURCE:(MTAR:0):快Ｃ = SENSE_HOUSHI(MPLY:0, MTAR:0, 1200 + TALENT:(MPLY:0):美脚 * 300)
ENDIF

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 80, 140)

;射精箇所と対象をセット
CALL STACK_SPERM(MTAR:0, MPLY:0, 射精部位_足)

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 3

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP103
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP103(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;●プレイヤーの処理
DOWNBASE:(LOCAL:2):体力 += 20

EXP:(LOCAL:2):性技経験値 += 1

SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 50)
SOURCE:(LOCAL:2):接触 += 30
SOURCE:(LOCAL:2):逸脱 += 20
SOURCE:(LOCAL:2):性行動 += 50

;プレイヤーに主導権がある場合
IF IS_INITIATIVE(LOCAL:2)
	SOURCE:(MPLY:0):嗜虐 += 50
ENDIF

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

;倒錯度変化基準値
TCVAR:(LOCAL:2):50 += 3

;●ターゲットの処理
DOWNBASE:(LOCAL:3):体力 += 10

SOURCE:(LOCAL:3):接触 += 30
SOURCE:(LOCAL:3):逸脱 += 20
SOURCE:(LOCAL:3):苦痛 += 30
SOURCE:(LOCAL:3):露出 += 40
SOURCE:(LOCAL:3):性行動 += 50
IF PENIS(LOCAL:3)
	SOURCE:(MTAR:0):快Ｐ = SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 400 + TALENT:(LOCAL:2):美脚 * 100)
ELSE
	SOURCE:(MTAR:0):快Ｃ = SENSE_HOUSHI(LOCAL:2, LOCAL:3, 400 + TALENT:(LOCAL:2):美脚 * 100)
ENDIF

;射精箇所と対象をセット
CALL STACK_SPERM(LOCAL:3, LOCAL:2, 射精部位_足)

;倒錯度変化基準値
TCVAR:(LOCAL:3):50 += 3

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE103(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%に電気按摩中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP103(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%の股間を踏みつけ振動を与えている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP103(ARG:0)
PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の股間から足を離した…

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER103(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 85

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 4)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF

;合意
IF !TALENT:(ARG:0):合意
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE103
IF MPLY:0 != MASTER && (GETBIT(TALENT:(MPLY:0):淫乱系, 素質_淫乱_サド) || ABL:(MPLY:0):主導度Ｕ >= 500)
	LOCALS:0 = サディスティックな笑みを浮かべながら
ELSE
	LOCALS:0 = 
ENDIF
IF TALENT:(MPLY:0):美脚
	LOCALS:3 = すらりとした美しい
ELSE
	LOCALS:3 =
ENDIF
SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		IF PENIS(MTAR:0)
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の足首を掴んで股を押し開くと、%LOCALS:0%%ANAME(MTAR:0)%のペニスを%LOCALS:3%足で踏みつけて激しく振動させ%TARGET_STR("た", "てきた")%…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の足首を掴んで股を押し開くと、%LOCALS:0%%ANAME(MTAR:0)%のワレメを%LOCALS:3%足で踏み付けクリトリスを押し潰すように激しく振動させ%TARGET_STR("た", "てきた")%…
		ENDIF
	;ターゲットに主導権
	CASE 1
		IF PENIS(MTAR:0)
			PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の前で股を開き、電気按摩で気持ち良くするように命令した
			PRINTFORMW %ANAME(MPLY:0)%は言われた通り%ANAME(MTAR:0)%のペニスの上に%LOCALS:3%足を置くと、激しく振動させて男性器に快感を与えていった…
		ELSE
			PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の前で股を開き、電気按摩で気持ち良くするように命令した
			PRINTFORMW %ANAME(MPLY:0)%は言われた通り%ANAME(MTAR:0)%のワレメに%LOCALS:3%足を当てると、クリトリスを中心に激しく振動させて女性器に快感を与えていった…
		ENDIF
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に対し、%ANAME(MTAR:0)%を電気按摩で責めるように命令した
		IF PENIS(MTAR:0)
			PRINTFORMW %ANAME(MPLY:0)%は言われた通りに%ANAME(MTAR:0)%の足首を掴んで股を押し開くと、%ANAME(MTAR:0)%のペニスを%LOCALS:3%足で踏みつけて激しく振動させ%TARGET_STR("た", "てきた")%…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%は言われた通りに%ANAME(MTAR:0)%の足首を掴んで股を押し開くと、%ANAME(MTAR:0)%のワレメを%LOCALS:3%足で踏み付けクリトリスを押し潰すように激しく振動させ%TARGET_STR("た", "てきた")%…
		ENDIF
ENDSELECT
