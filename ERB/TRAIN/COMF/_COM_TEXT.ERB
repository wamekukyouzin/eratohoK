;地の文に関する処理

;---------------------------------------------------------
;コマンド実行前に表示される地の文の呼び出し
;主に行動内容や状況等を記述する
;---------------------------------------------------------
@COM_TEXT_BEFORE
;地の文が非表示なら戻る
;SIF FLAG:0 == 0
;	RETURN

;口上表示
CALL KOJO_COM_BEFORE

;口上側の戻り値が1なら地の文はカットされる
IF !RESULT
	TRYCALLFORM COM_TEXT_BEFORE{SELECTCOM}
ENDIF

;---------------------------------------------------------
;コマンド実行直後に表示される地の文の呼び出し
;主に行動の結果を記述する
;射精や絶頂に関連したメッセージもここで表示
;何かメッセージを表示したら1を返す
;---------------------------------------------------------
@COM_TEXT_AFTER
;地の文が非表示なら戻る
;SIF FLAG:0 == 0
;	RETURN

RESULT = 0
TRYCALLFORM COM_TEXT_AFTER{SELECTCOM}
IF RESULT
	;コマンド専用の地の文が定義されていれば戻る
	RETURN 0
ENDIF

LOCAL:10 = 0

;今ターンに絶頂したキャラの人数を数え、名前と最低強度を記録
LOCAL:5 = 0
LOCAL:6 = 0
LOCAL:7 = 999
LOCALS:1 = 
LOCALS:2 = 
FOR LOCAL:0, 0, CHARANUM
	IF LOCAL:0 == MASTER || CFLAG:(LOCAL:0):調教参加フラグ
		;今回のＣＶＡＢＭの絶頂回数(NOWEX)の合計を取得
		LOCAL:2 = SUM_NOWEX(LOCAL:0, 0, 0)

		IF LOCAL:2 > 0
			IF LOCAL:2 < LOCAL:7
				LOCAL:7 = LOCAL:2
			ENDIF

			IF LOCAL:0 == MASTER
				LOCAL:6 = 1
			ELSE
				IF LOCAL:5 == 1
					LOCALS:2 = と%ANAME(LOCAL:0)%
				ELSE
					LOCALS:1 = %ANAME(LOCAL:0)%
				ENDIF
				LOCAL:5 ++
			ENDIF
		ENDIF
	ENDIF
NEXT

;一人以上絶頂した場合
IF LOCAL:5 >= 1
	IF LOCAL:5 >= 3
		LOCALS:1 = %LOCALS:1%たち
	ELSE
		LOCALS:1 = %LOCALS:1%%LOCALS:2%
	ENDIF
	;主人公が同時に絶頂した場合
	IF LOCAL:6
		LOCALS:0 = %ANAME(MASTER)%と
		LOCALS:3 = 同時に
	ELSE
		LOCALS:0 = 
		LOCALS:3 = 
	ENDIF

	IF LOCAL:7 >= 8
		LOCAL:10 = 1
		PRINTFORML %LOCALS:0%%LOCALS:1%は強烈な絶頂感に何度も背を反らせ、打ち上げられた魚のように体を跳ねさせた…
	ELSEIF LOCAL:7 >= 3
		LOCAL:10 = 1
		PRINTFORML %LOCALS:0%%LOCALS:1%は%LOCALS:3%激しく絶頂し、口から涎を垂らしてガクガクと痙攣した…
	ELSEIF LOCAL:7 >= 1
		LOCAL:10 = 1
		PRINTFORML %LOCALS:0%%LOCALS:1%は%LOCALS:3%絶頂に達しビクビクと体を震わせた…
	ENDIF

;主人公だけが絶頂した場合
ELSEIF LOCAL:6
	;今回のＣＶＡＢＭの絶頂回数(NOWEX)の合計を取得
	LOCAL:2 = SUM_NOWEX(MASTER, 0, 0)

	IF LOCAL:2 >= 8
		LOCAL:10 = 1
		PRINTFORML %ANAME(MASTER)%は強烈な絶頂感に何度も背を反らせ、打ち上げられた魚のように体を跳ねさせた…
	ELSEIF LOCAL:2 >= 3
		LOCAL:10 = 1
		PRINTFORML %ANAME(MASTER)%は激しく絶頂し、口から涎を垂らしてガクガクと痙攣した…
	ELSEIF LOCAL:2 >= 1
		LOCAL:10 = 1
		PRINTFORML %ANAME(MASTER)%は絶頂に達しビクビクと体を震わせた…
	ENDIF
ENDIF

FOR LOCAL:0, 0, CHARANUM
	;番号LOCAL:0のキャラが射精した場合
	IF (LOCAL:0 == MASTER || CFLAG:(LOCAL:0):調教参加フラグ) && NOWEX:(LOCAL:0):射精 > 0
		LOCAL:10 = 1

		;射精対象の人数を数え、必要な文字列を作成
		LOCAL:2 = 0
		LOCAL:3 = 2
		LOCAL:4 = -1
		LOCALS:0 = 
		FOR LOCAL:1, 400, 420
			IF TCVAR:(LOCAL:0):(LOCAL:1) >= 0
				LOCAL:4 = TCVAR:(LOCAL:0):(LOCAL:1)
				IF LOCAL:2 == 0
					LOCALS:0 = %ANAME(TCVAR:(LOCAL:0):(LOCAL:1))%
				ELSE
					LOCALS:0 = %LOCALS:0%と%ANAME(TCVAR:(LOCAL:0):(LOCAL:1))%
				ENDIF
				LOCAL:2 ++
				;対象の精愛Lvを判定(主人公が含まれているor逆調教なら-1に固定)
				IF TCVAR:(LOCAL:0):(LOCAL:1) == MASTER || FLAG:調教モード == 4
					LOCAL:3 = -1
				ELSEIF LOCAL:3 >= 1 && ABL:(TCVAR:(LOCAL:0):(LOCAL:1)):精愛 < 2
					LOCAL:3 = 0
				ELSEIF LOCAL:3 >= 2 && ABL:(TCVAR:(LOCAL:0):(LOCAL:1)):精愛 < 5
					LOCAL:3 = 1
				ENDIF
			ENDIF
		NEXT
		;コンドーム装着中
		IF IS_EQUIP_TARGET(LOCAL:0, 75)
			PRINTFORML %ANAME(LOCAL:0)%の精液が、コンドームへと放たれていった……
		;射精対象が未設定
		ELSEIF LOCAL:2 <= 0
			;捕虜調教 or 捕虜逆調教
			IF GROUPMATCH(FLAG:調教モード, 2, 4, 6, 7)
				PRINTFORML %ANAME(LOCAL:0)%の精液が床を汚していった…
			ELSE
				PRINTFORML %ANAME(LOCAL:0)%の精液が%GET_GROUND_NAME(TFLAG:54)%を汚していった…
			ENDIF
		ELSE
			;射精対象が４人以上
			IF LOCAL:2 >= 4
				LOCALS:1 = %ANAME(TCVAR:(LOCAL:0):400)%達
			;射精対象が３人以下
			ELSE
				LOCALS:1 = %LOCALS:0%
			ENDIF
			PRINTFORM %ANAME(LOCAL:0)%の精液が%LOCALS:1%

			SELECTCASE TCVAR:(LOCAL:0):420
				CASE 1
					PRINTL の膣内へと注ぎ込まれた…
					;射精したキャラが獣変化中
					IF CFLAG:(LOCAL:0):獣変化フラグ
						IF LOCAL:0 == MASTER
							PRINTFORML 獣の本能がもたらす膣内射精の悦びが、%ANAME(LOCAL:0)%の脳天を甘く痺れさせた…
						;好感度または従属度が1500以上
						ELSEIF CFLAG:(LOCAL:0):2 >= 1500 || CFLAG:(LOCAL:0):4 >= 1500
							PRINTFORML %ANAME(LOCAL:0)%は獣の本能に流されるまま、膣内射精の悦びに顔を緩ませている…
						ELSE
							PRINTFORML %ANAME(LOCAL:0)%は獣の本能に逆らえず、膣内射精の悦びに打ち震えている…
						ENDIF
					ENDIF
					;射精されたキャラが獣変化中
					IF LOCAL:4 >= 0 && CFLAG:(LOCAL:4):獣変化フラグ
						IF LOCAL:4 == MASTER
							PRINTFORML 獣の本能がもたらす膣内射精の悦びが、%ANAME(LOCAL:4)%の脳天を甘く痺れさせた…
						;好感度または従属度が1500以上
						ELSEIF CFLAG:(LOCAL:4):2 >= 1500 || CFLAG:(LOCAL:4):4 >= 1500
							PRINTFORML %ANAME(LOCAL:4)%は獣の本能に流されるまま、膣内射精の悦びに顔を緩ませている…
						ELSE
							PRINTFORML %ANAME(LOCAL:4)%は獣の本能に逆らえず、膣内射精の悦びに打ち震えている…
						ENDIF
					ENDIF
					;捕虜調教のメインターゲット
					IF FLAG:調教モード == 2 && FINDELEMENT(PRISONER_TARGET, LOCAL:4) != -1 && !((TCVAR:(LOCAL:4):52) || (TCVAR:(LOCAL:4):53 == 1))
						IF RAND:3 == 0
							IF TALENT:(LOCAL:4):ＮＴＲ && (LOCAL:0 == MASTER)
								PRINTFORML 既に%ANAME(LOCAL:0)%から心の離れた%ANAME(LOCAL:4)%は膣内射精の嫌悪感に肩を震わせている
							ELSEIF TALENT:(LOCAL:4):隷属 && (LOCAL:0 == MASTER)
								PRINTFORML %ANAME(LOCAL:0)%の奴隷として調教が行き届いた%ANAME(LOCAL:4)%は膣内射精を受け入れ、お礼を言ってきた
							ELSEIF TALENT:(LOCAL:4):服従 && (LOCAL:0 == MASTER)
								PRINTFORML %ANAME(LOCAL:0)%の奴隷として調教された%ANAME(LOCAL:4)%は震え声ではあるが、お礼を言うことは忘れなかった
							ELSEIF TALENT:(LOCAL:4):正妻 && (LOCAL:0 == MASTER)
								PRINTFORML 伴侶である%ANAME(LOCAL:0)%に捕虜として犯され膣内射精されることに%ANAME(LOCAL:4)%は慣れることができない
							ELSEIF TALENT:(LOCAL:4):親愛 && (LOCAL:0 == MASTER)
								PRINTFORML %ANAME(LOCAL:0)%を愛しているのに捕虜として犯され膣内射精されることに%ANAME(LOCAL:4)%は涙を浮かべている
							ELSEIF TALENT:(LOCAL:4):恋慕 && (LOCAL:0 == MASTER)
								PRINTFORML %ANAME(LOCAL:0)%に惚れているのに捕虜として犯され膣内射精されることに%ANAME(LOCAL:4)%は涙を浮かべている
							ELSEIF TALENT:(LOCAL:4):親友 && (LOCAL:0 == MASTER)
								PRINTFORML 親友だと思っていた%ANAME(LOCAL:0)%に捕まり、犯されて膣内射精されることに%ANAME(LOCAL:4)%は今だ慣れない
							ELSEIF TALENT:(LOCAL:4):恋人 && (LOCAL:0 == MASTER)
								PRINTFORML 想いを打ち明けた%ANAME(LOCAL:0)%に捕虜として犯され膣内射精されることに%ANAME(LOCAL:4)%は耐えられない
							ELSEIF TALENT:(LOCAL:4):合意 && (LOCAL:0 == MASTER)
								PRINTFORML 以前に体を許した%ANAME(LOCAL:0)%に捕虜として犯され膣内射精されることに%ANAME(LOCAL:4)%は過去の選択を悔やんでいる
							ELSEIF TALENT:(LOCAL:4):子供人質
								PRINTFORML 子供を人質に取られた%ANAME(LOCAL:4)%に膣内射精を拒める余地などない
							ELSEIF TALENT:(LOCAL:4):妊娠
								PRINTFORML %ANAME(LOCAL:4)%は虜囚の身ゆえ、身重であっても膣内射精を避ける術はない
							ELSEIF TALENT:(LOCAL:4):キス未経験
								PRINTFORML %ANAME(LOCAL:4)%はキスすらしたことがないのに捕虜として膣内射精を強いられる現実を受け入れられない
							ELSEIF GETBIT(TALENT:(LOCAL:4):デイリー系, 素質_デイリー_借り物)
								PRINTFORML %ANAME(LOCAL:4)%は他勢力の将であるのに、牢獄に繋がれ容赦なく膣内射精されることに戸惑いを隠しきれない
							ELSEIF TALENT:(LOCAL:4):男嫌い && ((TALENT:(LOCAL:0):性別 == 0) || (TALENT:(LOCAL:0):性別 >= 3))
								PRINTFORML 男に犯されることに頑なに抵抗していた%ANAME(LOCAL:4)%だが、捕虜の身では膣内射精を防ぐ術などない
							ELSEIF TALENT:(LOCAL:4):人気
								PRINTFORML 外での人気など牢の中では何の役にも立たないと、%ANAME(LOCAL:4)%は膣内射精の熱い感触を感じながら悟った
							ELSEIF FIND_PASSIVE_SKILL(LOCAL:4, "努力家")
								PRINTFORML 努力ではどうにもならないことがあるのだと、%ANAME(LOCAL:4)%は膣内射精の熱い感触を感じながら悟った
							ENDIF
						ELSEIF RAND:2 == 0
							IF !(IS_LOVER(LOCAL:4) || IS_SLAVE(LOCAL:4)) || (IS_LOVER(LOCAL:4) && !(LOCAL:0 == MASTER))
								PRINTFORML %ANAME(LOCAL:4)%は必死に拒絶しようとしたが、捕虜に膣内射精を避ける権利などない
							ENDIF
						ELSE
							IF GETBIT(TALENT:(LOCAL:4):淫乱系, 素質_淫乱_淫乱)
								PRINTFORML %ANAME(LOCAL:4)%は捕虜であるにもかかわらず、その淫奔さから膣内射精の快感に酔い痴れている
							ELSEIF TALENT:(LOCAL:4):快感に素直
								PRINTFORML 子宮に精子を浴びせかけられる快感に%ANAME(LOCAL:4)%は喘ぎ声を抑えようともしない
							ELSEIF TALENT:(LOCAL:4):快感の否定
								PRINTFORML 子宮を精子が蹂躙する快感を否定するように%ANAME(LOCAL:4)%は喘ぎ声を抑えている
							ELSEIF TALENT:(LOCAL:4):プライド高い
								PRINTFORML 例え囚われの身であっても醜態を晒すまいと、%ANAME(LOCAL:4)%は膣内射精されても努めて平静を保とうとした
							ENDIF
						ENDIF
					ENDIF
					IF LOCAL:4 >= 0 && GETBIT(TALENT:(LOCAL:4):淫乱系, 素質_淫乱_孕みたがり)
						PRINTFORML %ANAME(LOCAL:4)%は子を宿すことの快楽に酔い痴れている…
					ENDIF
					IF LOCAL:4 >= 0 && GETBIT(TALENT:(LOCAL:4):淫乱系, 素質_淫乱_生ハメ依存)
						PRINTFORML %ANAME(LOCAL:4)%は生でないと味わえない快感に酔い痴れている…
					ENDIF
					IF LOCAL:4 >= 0 && TCVAR:(LOCAL:4):60
						PRINTFORML 媚薬の効果ゆえか、%ANAME(LOCAL:4)%はとろけた声を上げながら膣内射精の悦びに腰をくねらせる…
					ENDIF
					IF LOCAL:4 >= 0 && TCVAR:(LOCAL:4):67
						PRINTFORML 麻薬の力でトリップした%ANAME(LOCAL:4)%は、膣内射精の悦びにとろけた声をあげて腰をくねらせる…
					ENDIF
					IF LOCAL:4 >= 0 && TCVAR:(LOCAL:4):65
						PRINTFORML 薬の効果で孕みやすくなっている%ANAME(LOCAL:4)%の子宮では、大量の精子が卵子に群がっている
					ENDIF
					IF LOCAL:4 >= 0 && TCVAR:(LOCAL:4):68
						PRINTFORML 桃源香が漂う中、薄く霞みがかった%ANAME(LOCAL:4)%の頭と身体に膣内射精の悦びが駆け巡る…
					ENDIF
					IF LOCAL:4 >= 0 && IS_OVALUATION(LOCAL:4)
						PRINTFORML 危険日である%ANAME(LOCAL:4)%の子宮に、無数の精子が注ぎ込まれた…
					ENDIF
				CASE 2
					PRINTL の腸内に注ぎ込まれた…
					IF LOCAL:3 == 2
						PRINTFORML %LOCALS:1%の肛内は、打ちつけられる精液を貪り尽くそうと淫猥に収縮を繰り返している
					ENDIF
					IF GETBIT(TALENT:(LOCAL:4):淫乱系, 素質_淫乱_尻穴狂い)
						PRINTFORML 完全に性器と化した淫肛を駆け上り蹂躙していく熱い感触に、%LOCALS:1%は背徳的な充足感で満たされた…
					ENDIF
					IF GETBIT(TALENT:(LOCAL:4):淫乱系, 素質_淫乱_生ハメ依存)
						PRINTFORML %ANAME(LOCAL:4)%は、生でないと味わえない腸内射精の快感に酔い痴れている…
					ENDIF
					IF LOCAL:4 >= 0 && TCVAR:(LOCAL:4):60
						PRINTFORML 媚薬の効果ゆえか、%ANAME(LOCAL:4)%はとろけた声を上げながら肛内射精の悦びに腰をくねらせる…
					ENDIF
					IF LOCAL:4 >= 0 && TCVAR:(LOCAL:4):67
						PRINTFORML 麻薬の力でトリップした%ANAME(LOCAL:4)%は、肛内射精の悦びにとろけた声をあげて腰をくねらせる…
					ENDIF
					IF LOCAL:4 >= 0 && TCVAR:(LOCAL:4):65
						PRINTFORML 薬の効果で孕みやすくなっている%ANAME(LOCAL:4)%の子宮が、肛内射精に反応してひくひくと震えている…
					ENDIF
					IF LOCAL:4 >= 0 && TCVAR:(LOCAL:4):68
						PRINTFORML 桃源香が漂う中、薄く霞みがかった%ANAME(LOCAL:4)%の頭と身体に肛内射精の悦びが駆け巡る…
					ENDIF
				CASE 3
					PRINTL の手の中に迸った…
					IF LOCAL:3 == 2
						PRINTFORML %LOCALS:1%は恍惚とした表情で手に付いた精液を舐め取っている…
					ENDIF
				CASE 4
					PRINTL の口の中に迸った…
					IF TALENT:(LOCAL:4):プライド高い
						IF TALENT:(LOCAL:4):恋慕 && (LOCAL:0 == MASTER)
							PRINTFORML %LOCALS:1%は恍惚とペニスを咥えたまま、自負心に満ちた瞳で見つめてきた
						ELSEIF TALENT:(LOCAL:4):服従 && (LOCAL:0 == MASTER)
							PRINTFORML %LOCALS:1%は己を屈服させたペニスを睨みながらも、決して熱い視線を離そうとはしない
						ELSE
							PRINTFORML %LOCALS:1%はペニスを咥えたまま上目遣いで睨んできた
						ENDIF
					ENDIF
					IF LOCAL:3 == 2
						IF LOCAL:2 >= 2
							PRINTFORML %LOCALS:1%は吐き出された精液を争うようにして舐め取っていった…
						ELSE
							PRINTFORML %LOCALS:1%は吐き出された精液を一滴残らず吸い上げ、喉を鳴らして飲み込んだ…
						ENDIF
					ELSEIF LOCAL:3 == 1
						IF LOCAL:2 >= 2
							PRINTFORML %LOCALS:1%は吐き出された精液を協力して舐め取っていった…
						ELSE
							PRINTFORML %LOCALS:1%は吐き出された精液を飲み込んだ…
						ENDIF
					ELSEIF LOCAL:3 == 0 && LOCAL:2 == 1
						PRINTFORML %LOCALS:1%は精液を飲み込むことができず、吐き出してしまった…
					ENDIF
				CASE 5
					PRINTL の胸を白く染め上げた…
					IF LOCAL:3 == 2
						PRINTFORML %ANAME(LOCAL:4)%は胸に掛かった精液を指ですくい上げ、美味しそうに舐め取っている…
					ENDIF
				CASE 6
					PRINTL の足を汚した…
					IF LOCAL:3 == 2
						PRINTFORML %ANAME(LOCAL:4)%は足に掛かった精液をネチャネチャと気持ち良さそうに擦り付けている…
					ENDIF
				CASE 7
					PRINTL の尻尾を汚していった…
				CASE 8
					PRINTL の顔に降りかかった…
					IF TALENT:(LOCAL:4):プライド高い
						IF CFLAG:(LOCAL:4):2 >= 1500 || CFLAG:(LOCAL:4):4 >= 1500
							PRINTFORML %LOCALS:1%の顔射への屈辱は、既に充足感へと変わっている
						ELSE
							PRINTFORML %LOCALS:1%は顔に精液をかけられ、やや目を鋭くしている
						ENDIF
					ELSEIF TALENT:(LOCAL:4):プライド低い
						PRINTFORML %LOCALS:1%は目を閉じ、精液を顔に浴びることを受け入れている
					ENDIF
					IF LOCAL:3 == 2
						PRINTFORML %LOCALS:1%は顔に付いた精液を指ですくい上げ、美味しそうに舐め取っている…
					ENDIF
				CASE 9
					PRINTL の髪にべったりとこびり付いた…
				CASE 10
					PRINTL の腋を白く染め上げた…
				CASE 11
					IF TALENT:(LOCAL:4):美尻
						PRINTL の綺麗な桃尻を白く染め上げた…
					ELSEIF TALENT:(LOCAL:4):豊尻
						PRINTL の肉付きのいい見事なお尻を白く染め上げた…
					ELSE
						PRINTL のお尻を白く染め上げた…
					ENDIF
					IF LOCAL:3 == 2
						PRINTFORML %LOCALS:1%はお尻に精液が掛かる度に、その双丘をぷるぷると震わせている…
					ENDIF
				CASE 12
					PRINTL の持つオナホールの中に注がれていった…
				CASE 13
					PRINTL の操る触手に吸い出されていった…
				CASEELSE
					PRINTL へ降り掛かっていった…
			ENDSELECT
		ENDIF

		IF TCVAR:(LOCAL:0):51
			IF TCVAR:(LOCAL:0):52
				PRINTFORML %ANAME(LOCAL:0)%は限界を超えた射精に、意識を失ったまま小さく呻いた
			ELSE
				IF LOCAL:0 == MASTER
					PRINTFORML %ANAME(LOCAL:0)%は限界を超えた射精に思わず呻いた
				ELSE
					PRINTFORML %ANAME(LOCAL:0)%は限界を超えた射精に苦しそうに呻いた
				ENDIF
			ENDIF
		ENDIF
	ENDIF

	;番号LOCAL:0のキャラが噴乳した場合
	IF (LOCAL:0 == MASTER || CFLAG:(LOCAL:0):調教参加フラグ) && NOWEX:(LOCAL:0):噴乳 > 0
		;触手搾乳中
		IF (SELECTCOM == 207 && IS_MTAR(LOCAL:0)) || IS_EQUIP_TARGET(LOCAL:0, 207)
			IF NOWEX:(LOCAL:0):噴乳 >= 2
				PRINTFORML %ANAME(LOCAL:0)%の母乳が、乳首に吸い付く触手の中に大量に迸った…
			ELSE
				PRINTFORML %ANAME(LOCAL:0)%の母乳が、乳首に吸い付く触手の中に迸った…
			ENDIF
		ELSE
			IF NOWEX:(LOCAL:0):噴乳 >= 2
				PRINTFORML %ANAME(LOCAL:0)%の乳首から大量の母乳が吹き出した…
			ELSE
				PRINTFORML %ANAME(LOCAL:0)%の乳首から母乳が吹き出した…
			ENDIF
		ENDIF
	ENDIF

	;番号LOCAL:0のキャラが潮吹きした場合
	IF (LOCAL:0 == MASTER || CFLAG:(LOCAL:0):調教参加フラグ) && NOWEX:(LOCAL:0):潮吹き > 0
		;顔面騎乗中ならターゲットを取得
		IF GROUPMATCH(SELECTCOM, 101, 102, 105) && IS_MPLY(LOCAL:0)
			LOCAL:7 = MTAR:0
		ELSE
			LOCAL:7 = SEARCH_EQUIP(101, LOCAL:0, -1)
			IF LOCAL:7 < 0
				LOCAL:7 = SEARCH_EQUIP(102, LOCAL:0, -1)
				IF LOCAL:7 < 0
					LOCAL:7 = SEARCH_EQUIP(105, LOCAL:0, -1)
				ENDIF
			ENDIF
			IF LOCAL:7 >= 0
				LOCAL:7 = MEQUIP_TARGET:(LOCAL:5):0
			ENDIF
		ENDIF

		LOCAL:5 = PENIS(LOCAL:0) && NOWEX:(LOCAL:0):Ｃ絶頂 >= 1
		LOCAL:6 = VAGINA(LOCAL:0)
		IF LOCAL:5
			PRINTFORML %ANAME(LOCAL:0)%はペニスから潮を吹いてビクビクと体を震わせた…
		ELSE
			IF LOCAL:7 >= 0
				PRINTFORML %ANAME(LOCAL:0)%の膣から大量の潮が吹き出し、%ANAME(LOCAL:7)%の顔に降り掛かった…
			ELSE
				PRINTFORML %ANAME(LOCAL:0)%の膣からぷしゃっ、と潮が吹き出した…
			ENDIF
		ENDIF
	ENDIF

	;番号LOCAL:0のキャラがおもらしした場合
	IF (LOCAL:0 == MASTER || CFLAG:(LOCAL:0):調教参加フラグ) && TCVAR:(LOCAL:0):0
		IF PENIS(LOCAL:0)
			LOCALS:0 = ペニス
		ELSE
			LOCALS:0 = 股間
		ENDIF
;2015 08 21 ここのLOCAL:7は（上の表記を参照する限りでは)
		LOCAL:7 = -1
		IF !PENIS(LOCAL:0)
			;顔面騎乗中ならターゲットを取得
			IF GROUPMATCH(SELECTCOM, 101, 102, 105) && IS_MPLY(LOCAL:0)
				LOCAL:7 = MTAR:0
			ELSE
				LOCAL:7 = SEARCH_EQUIP(101, LOCAL:0, -1)
				IF LOCAL:7 < 0
					LOCAL:7 = SEARCH_EQUIP(102, LOCAL:0, -1)
					IF LOCAL:7 < 0
						LOCAL:7 = SEARCH_EQUIP(105, LOCAL:0, -1)
					ENDIF
				ENDIF
				IF LOCAL:7 >= 0
					LOCAL:7 = MEQUIP_TARGET:(LOCAL:5):0
				ENDIF
			ENDIF
		ENDIF

		SELECTCASE TCVAR:(LOCAL:0):0
			;尿意がある状態で絶頂強度の合計が4以上
			CASE 1
				;大量
				IF TCVAR:(LOCAL:0):1 >= 2
					IF NOWEX:(LOCAL:0):射精 >= 1
						PRINTFORML 射精を終えた%ANAME(LOCAL:0)%の%LOCALS:0%からジョロジョロと大量の尿が溢れ出した…
					ELSEIF LOCAL:7 >= 0
						PRINTFORML 絶頂に震える%ANAME(LOCAL:0)%は%ANAME(LOCAL:7)%の顔の上にジョロジョロと大量の尿を漏らしてしまった…
					ELSE
						PRINTFORML 絶頂に震える%ANAME(LOCAL:0)%の%LOCALS:0%からジョロジョロと大量の尿が溢れ出した…
					ENDIF
				;通常
				ELSE
					IF NOWEX:(LOCAL:0):射精 >= 1
						PRINTFORML 射精を終えた%ANAME(LOCAL:0)%の%LOCALS:0%からチョロチョロと尿が漏れ出した…
					ELSEIF LOCAL:7 >= 0
						PRINTFORML 絶頂に震える%ANAME(LOCAL:0)%は%ANAME(LOCAL:7)%の顔の上にチョロチョロと尿を漏らしてしまった…
					ELSE
						PRINTFORML 絶頂に震える%ANAME(LOCAL:0)%の%LOCALS:0%からチョロチョロと尿が漏れ出した…
					ENDIF
				ENDIF

			;尿意が高い状態で絶頂
			CASE 2
				IF NOWEX:(LOCAL:0):射精 >= 1
					PRINTFORML 射精を終えた%ANAME(LOCAL:0)%の%LOCALS:0%からジョロジョロと大量の尿が溢れ出した…
				ELSEIF LOCAL:7 >= 0
					PRINTFORML 絶頂に達した%ANAME(LOCAL:0)%は%ANAME(LOCAL:7)%の顔の上にジョロジョロと大量の尿を漏らしてしまった…
				ELSE
					PRINTFORML %ANAME(LOCAL:0)%は絶頂と同時に%LOCALS:0%からジョロジョロと大量の尿を漏らしてしまった…
				ENDIF

			;尿意の限界
			CASE 3
				IF LOCAL:7 >= 0
					PRINTFORML %ANAME(LOCAL:0)%は尿意を我慢できず、%ANAME(LOCAL:7)%の顔の上にジョロジョロと大量の尿を漏らしてしまった…
				ELSE
					PRINTFORML %ANAME(LOCAL:0)%は尿意を我慢できずに%LOCALS:0%からジョロジョロと大量の尿を漏らしてしまった…
				ENDIF

			;恐怖による失禁
			CASE 4
				PRINTL 
				PRINTFORM %ANAME(LOCAL:0), 8%:
				SETCOLOR COLOR("注意")
				IF TCVAR:(LOCAL:0):1 >= 2
					PRINTL 大量失禁
				ELSE
					PRINTL 失禁
				ENDIF
				RESETCOLOR
				PRINTL 
				;大量
				IF TCVAR:(LOCAL:0):1 >= 2
					PRINTFORML 恐怖に震える%ANAME(LOCAL:0)%の%LOCALS:0%からジョロジョロと大量の尿が漏れだした…
				;通常
				ELSE
					PRINTFORML 恐怖に震える%ANAME(LOCAL:0)%の%LOCALS:0%からチョロチョロと尿が漏れだした…
				ENDIF
		ENDSELECT
	ENDIF
NEXT

FOR LOCAL:0, 0, CHARANUM
	;尿意が高い場合
	IF (LOCAL:0 == MASTER || CFLAG:(LOCAL:0):調教参加フラグ) && TCVAR:(LOCAL:0):54 >= 3000
		PRINTFORML %ANAME(LOCAL:0)%は尿意のあまり股をモジモジとさせている…
	ENDIF
NEXT

RETURN LOCAL:10

;---------------------------------------------------------
;コマンド実行直前に表示される地の文の呼び出し
;主にキャラの反応などを記述する
;---------------------------------------------------------
@COM_TEXT_LAST
;地の文が非表示なら戻る
;SIF FLAG:0 == 0
;	RETURN 0

TRYCCALLFORM COM_TEXT_LAST{SELECTCOM}
	PRINTL 
CATCH
	;コマンド個別の反応が定義されていなければTEQUIPの地の文を呼び出す
	;CALL COM_TEXT_LAST_EQUIP
ENDCATCH

;---------------------------------------------------------
;コマンド実行前に表示される装着物や継続状態に関わる地の文の呼び出し
;---------------------------------------------------------
@COM_TEXT_BEFORE_EQUIP
FOR LOCAL:0, 0, MAX_MEQUIP
	;地の文カットフラグが立っているなら表示しない
	IF MEQUIP:(LOCAL:0) >= 0 && !MEQUIP_CUT:(LOCAL:0)
		TRYCALLFORM COM_TEXT_BEFORE_EQUIP{MEQUIP:(LOCAL:0)}(LOCAL:0)
	ENDIF
NEXT
