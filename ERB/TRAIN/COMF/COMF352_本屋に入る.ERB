;本屋に入る

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME352
RESULTS:0 = 本屋に誘う
RESULTS:1 = 本屋に付き合う

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE352
;共通部分
CALL COM_ABLE_COMMON(352)
SIF RESULT == 0
	RETURN 0
;主人公がプレイヤーのとき限定
SIF MPLY:0 != MASTER
	RETURN 0
;城下町でのデート中限定
SIF GET_PLACENAME(TFLAG:54) != "城下町"
	RETURN 0
;怪我なら不可
SIF CFLAG:(MTAR:0):行動不能状態 == 3
	RETURN 0
;キス中は不可
SIF IS_EQUIP(MPLY:0, 342)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM352
;実行判定
CALL COM_ORDER_COMMON
IF RESULT == 0
	RETURN 0
ENDIF

;成否の判定なし
TFLAG:18 = 0

;1/3の確率で淫交絵巻を入荷
IF RAND:3 == 0
	LOCAL:10 = 1
ELSE
	LOCAL:10 = 0
ENDIF

PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MTAR:0)%は本屋に入った…

$SELECT_LOOP

CALL SINGLE_DRAWLINE
PRINTFORML ●本の購入(所持金:{MONEY})
PRINTL   [0] 物語      ( 500)  歓楽のソースが多く入る
PRINTL   [1] 武術指南書(1000)  今回と次回の拠点フェイズで「武術訓練」の効果が上昇
PRINTL   [2] 兵法書    (1000)  今回と次回の拠点フェイズで「兵法学習」の効果が上昇
PRINTL   [3] 学術書    (1000)  今回と次回の拠点フェイズで「政務」が「学問」に変化
PRINTL   [4] 歌謡集    (1000)  今回と次回の拠点フェイズで「歌唱訓練」の効果が上昇
PRINTL   [5] 料理本    (1000)  今回と次回の拠点フェイズで「料理」の効果が上昇
PRINTL   [6] 性技指南書(1000)  性知識Lvが上昇する(【Lv1→2】1冊 【Lv2→3】2冊)
PRINTL   [7] 淫交絵巻  (10000)  性知識Lvが上昇する(【Lv3→4】2冊)
CALL SINGLE_DRAWLINE
PRINTL   [9] 立ち読み

REDRAW 0

$INPUT_LOOP
INPUT

LOCAL:4 = RESULT

IF LOCAL:4 >= 1 && LOCAL:4 <= 5 && CFLAG:(MTAR:0):本の効果 != 0
	PRINTFORML 現在の本の効果は打ち消されます
	PRINTFORML 購入しますか？
	CALL ASK_YN()
	IF RESULT == 1
		REDRAW 1
		GOTO SELECT_LOOP
	ENDIF
ENDIF

SELECTCASE LOCAL:4
	;物語
	CASE 0
		IF MONEY < 500
			PRINTW お金が足りません
			CLEARLINE 2
			GOTO INPUT_LOOP
		ENDIF

		MONEY -= 500

		;素質によるボーナス
		LOCAL:5 = 100
		LOCAL:5 += (TALENT:(MTAR:0):好奇心 != 0) * 20
		LOCAL:5 += (TALENT:(MTAR:0):大人しい != 0) * 20
		LOCAL:5 += (TALENT:(MTAR:0):幼稚 != 0) * 50
		LOCAL:5 += (TALENT:(MTAR:0):プライド高い != 0) * -40
		LOCAL:5 += (TALENT:(MTAR:0):無関心 != 0) * -30
		LOCAL:5 += (TALENT:(MTAR:0):感情乏しい != 0) * -20
		LOCAL:5 += GROWTYPE_RATE(MTAR:0, "知略") + GROWTYPE_RATE(MTAR:0, "政治") - 200
		LOCAL:5 = LIMIT(LOCAL:5, 0, 200)

		SOURCE:(MTAR:0):歓楽 = 100 * LOCAL:5 / 100
		;親密に応じた歓楽のソース追加
		CALL ADD_SOURCE_KANRAKU(MTAR:0, 200 * LOCAL:5 / 100)
		SOURCE:(MTAR:0):歓楽 = MAX(SOURCE:(MTAR:0):歓楽, 0)

	;武術指南書
	CASE 1
		IF MONEY < 1000
			PRINTW お金が足りません
			CLEARLINE 2
			GOTO INPUT_LOOP
		ENDIF

		MONEY -= 1000

		;素質によるボーナス
		LOCAL:5 = 100
		LOCAL:5 += (TALENT:(MTAR:0):好奇心 != 0) * 20
		LOCAL:5 += (TALENT:(MTAR:0):臆病 != 0) * -20
		LOCAL:5 += (TALENT:(MTAR:0):大人しい != 0) * -20
		LOCAL:5 += (TALENT:(MTAR:0):無関心 != 0) * -30
		LOCAL:5 += (TALENT:(MTAR:0):感情乏しい != 0) * -20
		LOCAL:5 += (GROWTYPE_RATE(MTAR:0, "武闘") - 100) * 2
		LOCAL:5 = LIMIT(LOCAL:5, 0, 200)

		SOURCE:(MTAR:0):歓楽 = 80 * LOCAL:5 / 100
		;親密に応じた歓楽のソース追加
		CALL ADD_SOURCE_KANRAKU(MTAR:0, 160 * LOCAL:5 / 100)
		SOURCE:(MTAR:0):歓楽 = MAX(SOURCE:(MTAR:0):歓楽, 0)

		;本の効果をセット
		CFLAG:(MTAR:0):本の効果 = 1
		CFLAG:(MTAR:0):本の効果残ターン = 2

	;兵法書
	CASE 2
		IF MONEY < 1000
			PRINTW お金が足りません
			CLEARLINE 2
			GOTO INPUT_LOOP
		ENDIF

		MONEY -= 1000

		;素質によるボーナス
		LOCAL:5 = 100
		LOCAL:5 += (TALENT:(MTAR:0):好奇心 != 0) * 20
		LOCAL:5 += (TALENT:(MTAR:0):大人しい != 0) * 20
		LOCAL:5 += (TALENT:(MTAR:0):無関心 != 0) * -30
		LOCAL:5 += (TALENT:(MTAR:0):感情乏しい != 0) * -20
		LOCAL:5 += (GROWTYPE_RATE(MTAR:0, "知略") - 100) * 2
		LOCAL:5 = LIMIT(LOCAL:5, 0, 200)

		SOURCE:(MTAR:0):歓楽 = 80 * LOCAL:5 / 100
		;親密に応じた歓楽のソース追加
		CALL ADD_SOURCE_KANRAKU(MTAR:0, 160 * LOCAL:5 / 100)
		SOURCE:(MTAR:0):歓楽 = MAX(SOURCE:(MTAR:0):歓楽, 0)

		;本の効果をセット
		CFLAG:(MTAR:0):本の効果 = 2
		CFLAG:(MTAR:0):本の効果残ターン = 2

	;学術書
	CASE 3
		IF MONEY < 1000
			PRINTW お金が足りません
			CLEARLINE 2
			GOTO INPUT_LOOP
		ENDIF

		MONEY -= 1000

		;素質によるボーナス
		LOCAL:5 = 100
		LOCAL:5 += (TALENT:(MTAR:0):好奇心 != 0) * 20
		LOCAL:5 += (TALENT:(MTAR:0):大人しい != 0) * 20
		LOCAL:5 += (TALENT:(MTAR:0):無関心 != 0) * -30
		LOCAL:5 += (TALENT:(MTAR:0):感情乏しい != 0) * -20
		LOCAL:5 += (GROWTYPE_RATE(MTAR:0, "知略") - 100) * 2 / 3
		LOCAL:5 += (GROWTYPE_RATE(MTAR:0, "政治") - 100) * 3 / 2
		LOCAL:5 = LIMIT(LOCAL:5, 0, 200)

		SOURCE:(MTAR:0):歓楽 = 80 * LOCAL:5 / 100
		;親密に応じた歓楽のソース追加
		CALL ADD_SOURCE_KANRAKU(MTAR:0, 160 * LOCAL:5 / 100)
		SOURCE:(MTAR:0):歓楽 = MAX(SOURCE:(MTAR:0):歓楽, 0)

		;本の効果をセット
		CFLAG:(MTAR:0):本の効果 = 3
		CFLAG:(MTAR:0):本の効果残ターン = 2

	;歌謡集
	CASE 4
		IF MONEY < 1000
			PRINTW お金が足りません
			CLEARLINE 2
			GOTO INPUT_LOOP
		ENDIF

		MONEY -= 1000

		;素質によるボーナス
		LOCAL:5 = 100
		LOCAL:5 += (TALENT:(MTAR:0):好奇心 != 0) * 20
		LOCAL:5 += (TALENT:(MTAR:0):無関心 != 0) * -30
		LOCAL:5 += (TALENT:(MTAR:0):感情乏しい != 0) * -20
		LOCAL:5 += (GROWTYPE_RATE(MTAR:0, "芸能") - 100) * 2
		LOCAL:5 = LIMIT(LOCAL:5, 0, 230)

		SOURCE:(MTAR:0):歓楽 = 90 * LOCAL:5 / 100
		;親密に応じた歓楽のソース追加
		CALL ADD_SOURCE_KANRAKU(MTAR:0, 180 * LOCAL:5 / 100)
		SOURCE:(MTAR:0):歓楽 = MAX(SOURCE:(MTAR:0):歓楽, 0)

		;本の効果をセット
		CFLAG:(MTAR:0):本の効果 = 4
		CFLAG:(MTAR:0):本の効果残ターン = 2

	;料理本
	CASE 5
		IF MONEY < 1000
			PRINTW お金が足りません
			CLEARLINE 2
			GOTO INPUT_LOOP
		ENDIF

		MONEY -= 1000

		;素質によるボーナス
		LOCAL:5 = 100
		LOCAL:5 += (TALENT:(MTAR:0):好奇心 != 0) * 20
		LOCAL:5 += (TALENT:(MTAR:0):無関心 != 0) * -30
		LOCAL:5 += (TALENT:(MTAR:0):感情乏しい != 0) * -20
		LOCAL:5 += (GROWTYPE_RATE(MTAR:0, "料理") - 100) * 2
		LOCAL:5 = LIMIT(LOCAL:5, 0, 230)

		SOURCE:(MTAR:0):歓楽 = 90 * LOCAL:5 / 100
		;親密に応じた歓楽のソース追加
		CALL ADD_SOURCE_KANRAKU(MTAR:0, 180 * LOCAL:5 / 100)
		SOURCE:(MTAR:0):歓楽 = MAX(SOURCE:(MTAR:0):歓楽, 0)

		;本の効果をセット
		CFLAG:(MTAR:0):本の効果 = 5
		CFLAG:(MTAR:0):本の効果残ターン = 2

	;性技指南書
	CASE 6
		IF MONEY < 1000
			PRINTW お金が足りません
			CLEARLINE 2
			GOTO INPUT_LOOP
		ENDIF

		MONEY -= 1000

		;失敗(性知識なし)
		IF ABL:(MTAR:0):性知識 == 0
			TFLAG:16 = 0

		ELSE
			;実行値の計算
			CALL COM_ORDER_352_6(MTAR:0)
			LOCAL:5 = RESULT

			;成功
			IF LOCAL:5 >= 0
				TFLAG:16 = 1

				SOURCE:(MTAR:0):歓楽 = 20 + MIN(LOCAL:5, 130)
				;親密に応じた歓楽のソース追加
				CALL ADD_SOURCE_KANRAKU(MTAR:0, 100 + MIN(LOCAL:5 / 2, 100))
				SOURCE:(MTAR:0):感応 = 100 + MIN(LOCAL:5, 200)

				IF ABL:(MPLY:0):性知識 == 1
					EXP:(MPLY:0):性知識経験値 += 100
				ELSEIF ABL:(MPLY:0):性知識 == 2
					EXP:(MPLY:0):性知識経験値 += 50
				ENDIF

				IF ABL:(MTAR:0):性知識 == 1
					EXP:(MTAR:0):性知識経験値 += 100
				ELSEIF ABL:(MTAR:0):性知識 == 2
					EXP:(MTAR:0):性知識経験値 += 50
				ENDIF

				;ムードの増加
				TFLAG:37 += 4 + MIN(LOCAL:5 / 10, 16)

			;失敗
			ELSE
				TFLAG:16 = -1

				SOURCE:(MTAR:0):反感 = 200 + LIMIT(-LOCAL:5 * 5, 0, 800)
			ENDIF
		ENDIF

	;淫行絵巻
	CASE 7

		IF MONEY < 10000
			PRINTW お金が足りません
			CLEARLINE 2
			GOTO INPUT_LOOP
		ENDIF

		MONEY -= 10000

		;失敗(性知識なし)
		IF ABL:(MTAR:0):性知識 == 0
			TFLAG:16 = 0

		ELSE
			;実行値の計算
			CALL COM_ORDER_352_6(MTAR:0)
			LOCAL:5 = RESULT

			;成功
			IF LOCAL:5 >= 0
				TFLAG:16 = 1

				SOURCE:(MTAR:0):歓楽 = 20 + MIN(LOCAL:5, 130)
				;親密に応じた歓楽のソース追加
				CALL ADD_SOURCE_KANRAKU(MTAR:0, 100 + MIN(LOCAL:5 / 2, 100))
				SOURCE:(MTAR:0):感応 = 200 + MIN(LOCAL:5 * 2, 400)

				IF ABL:(MPLY:0):性知識 == 3
					EXP:(MPLY:0):性知識経験値 += 50
				ENDIF

				IF ABL:(MTAR:0):性知識 == 3
					EXP:(MTAR:0):性知識経験値 += 50
				ENDIF

				;ムードの増加
				TFLAG:37 += 4 + MIN(LOCAL:5 / 10, 16)

			;失敗
			ELSE
				TFLAG:16 = -1

				SOURCE:(MTAR:0):反感 = 400 + LIMIT(-LOCAL:5 * 10, 0, 1600)
			ENDIF
		ENDIF

	;立ち読み
	CASE 9
		;素質によるボーナス
		LOCAL:5 = 100
		LOCAL:5 += (TALENT:(MTAR:0):好奇心 != 0) * 20
		LOCAL:5 += (TALENT:(MTAR:0):大人しい != 0) * 20
		LOCAL:5 += (TALENT:(MTAR:0):無関心 != 0) * -40
		LOCAL:5 += (TALENT:(MTAR:0):感情乏しい != 0) * -40
		LOCAL:5 += GROWTYPE_RATE(MTAR:0, "知略") + GROWTYPE_RATE(MTAR:0, "政治") - 200
		LOCAL:5 = LIMIT(LOCAL:5, 0, 200)

		SOURCE:(MTAR:0):歓楽 = 60 * LOCAL:5 / 100
		;親密に応じた歓楽のソース追加
		CALL ADD_SOURCE_KANRAKU(MPLY:0, 140 * LOCAL:5 / 100)
		SOURCE:(MPLY:0):歓楽 = MAX(SOURCE:(MPLY:0):歓楽, 0)

	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
ENDSELECT

REDRAW 1

TFLAG:17 = LOCAL:4

;●プレイヤー側の処理
;主導権に応じたソースの追加
CALL ADD_SOURCE_INITIATIVE_N(MTAR:0, 80, 80)

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 0

RETURN 1

;-------------------------------------------------
;性技指南書・淫行絵巻を渡したときの実行判定
;-------------------------------------------------
@COM_ORDER_352_6(ARG:0)
CVARSET TCVAR, 24, -10000
CVARSET TCVAR, 25, 10000

TCVAR:(ARG:0):25 = 80

CALL COM_ORDER_BASE(ARG:0)

IF TALENT:(ARG:0):反抗的
	CALL COM_ORDER_ELEMENT(ARG:0, "反抗的", -5)
ENDIF
IF TALENT:(ARG:0):プライド高い
	CALL COM_ORDER_ELEMENT(ARG:0, "プライド高い", -15)
ENDIF
IF TALENT:(ARG:0):プライド低い
	CALL COM_ORDER_ELEMENT(ARG:0, "プライド低い", 5)
ENDIF
IF TALENT:(ARG:0):自制心
	CALL COM_ORDER_ELEMENT(ARG:0, "自制心", -8)
ENDIF
IF TALENT:(ARG:0):無関心
	CALL COM_ORDER_ELEMENT(ARG:0, "無関心", -6)
ENDIF
IF TALENT:(ARG:0):好奇心
	CALL COM_ORDER_ELEMENT(ARG:0, "好奇心", 15)
ENDIF
IF TALENT:(ARG:0):保守的
	CALL COM_ORDER_ELEMENT(ARG:0, "保守的", -10)
ENDIF
IF TALENT:(ARG:0):孤高
	CALL COM_ORDER_ELEMENT(ARG:0, "孤高", -(20 * KOKOU_RATE(ARG:0) / 100))
ENDIF
IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -10)
ENDIF
IF TALENT:(ARG:0):恥薄い
	CALL COM_ORDER_ELEMENT(ARG:0, "恥薄い", 8)
ENDIF
IF TALENT:(ARG:0):快感に素直
	CALL COM_ORDER_ELEMENT(ARG:0, "快感に素直", 10)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -35)
ENDIF
IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_淫乱)
	CALL COM_ORDER_ELEMENT(ARG:0, "淫乱", 20)
ENDIF

IF IS_SAMESEX(MPLY:0, ARG:0)
	CALL COM_ORDER_ELEMENT(ARG:0, "同性", 50)
ELSE
	IF TALENT:(ARG:0):一線越えない
		CALL COM_ORDER_ELEMENT(ARG:0, "一線越えない", -15)
	ENDIF
ENDIF

IF TALENT:(ARG:0):親愛
	CALL COM_ORDER_ELEMENT(ARG:0, "親愛", 25)
ELSEIF TALENT:(ARG:0):恋慕
	CALL COM_ORDER_ELEMENT(ARG:0, "恋慕", 10)
ENDIF

IF TALENT:(ARG:0):恋人
	CALL COM_ORDER_ELEMENT(ARG:0, "恋人", 20)
ELSEIF TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "親友", 20)
ENDIF

IF TALENT:(ARG:0):隷属
	CALL COM_ORDER_ELEMENT(ARG:0, "隷属", 25)
ELSEIF TALENT:(ARG:0):服従
	CALL COM_ORDER_ELEMENT(ARG:0, "服従", 10)
ENDIF

IF TALENT:(ARG:0):烙印
	CALL COM_ORDER_ELEMENT(ARG:0, "烙印", 20)
ENDIF

CALL COM_ORDER_ELEMENT(ARG:0, "合意", 15)

IF TALENT:(ARG:0):プライド高い
	LOCAL:0 = 4
ELSEIF TALENT:(ARG:0):プライド低い
	LOCAL:0 = 1
ELSE
	LOCAL:0 = 2
ENDIF
IF MARK:(ARG:0):既成事実 >= 1
	CALL COM_ORDER_ELEMENT(ARG:0, @"既成事実Lv{MARK:(ARG:0):既成事実}", MARK:(ARG:0):既成事実 * 3 * LOCAL:0)
ENDIF

IF TALENT:MASTER:魅惑
	CALL COM_ORDER_ELEMENT(ARG:0, "主人公の魅惑", 6)
ENDIF
IF TALENT:MASTER:謎の魅力
	CALL COM_ORDER_ELEMENT(ARG:0, "主人公の謎の魅力", 6)
ENDIF

IF PALAM:(ARG:0):酔い >= 150
	CALL COM_ORDER_ELEMENT(ARG:0, "酔い", PALAM:(ARG:0):酔い / 150)
ENDIF

;判定結果を表示
PRINTFORMW  = {TCVAR:(ARG:0):24} %TOSTR_COMPARE(TCVAR:(ARG:0):24, TCVAR:(ARG:0):25)% 実行値{TCVAR:(ARG:0):25}

RETURN TCVAR:(ARG:0):24 - TCVAR:(ARG:0):25

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE352
SELECTCASE TFLAG:17
	;物語
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%は物語の本を購入し、%ANAME(MTAR:0)%と一緒に読むことにした
	;武術指南書
	CASE 1
		PRINTFORMW %ANAME(MPLY:0)%は武術指南書を購入し、%ANAME(MTAR:0)%に手渡した
	;兵法書
	CASE 2
		PRINTFORMW %ANAME(MPLY:0)%は兵法書を購入し、%ANAME(MTAR:0)%に手渡した
	;学術書
	CASE 3
		PRINTFORMW %ANAME(MPLY:0)%は学術書を購入し、%ANAME(MTAR:0)%に手渡した
	;歌謡集
	CASE 4
		PRINTFORMW %ANAME(MPLY:0)%は歌謡集を購入し、%ANAME(MTAR:0)%に手渡した
	;料理本
	CASE 5
		PRINTFORMW %ANAME(MPLY:0)%は料理本を購入し、%ANAME(MTAR:0)%に手渡した
	;性技指南書
	CASE 6
		PRINTFORML %ANAME(MPLY:0)%は性技指南書を購入し、%ANAME(MTAR:0)%に手渡した
		;成功
		IF TFLAG:16 == 1
			IF TALENT:(MTAR:0):反抗的
				PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に対して何を考えているのかと罵ったが、どうやら興味はある様子だ…
			ELSEIF TALENT:(MTAR:0):恥じらい
				PRINTFORMW %ANAME(MTAR:0)%は顔を真っ赤にして慌てている…
			ELSEIF TALENT:(MTAR:0):小悪魔
				PRINTFORMW %ANAME(MTAR:0)%はニヤニヤしながら%ANAME(MPLY:0)%の顔を見つめている…
			ELSE
				PRINTFORMW %ANAME(MTAR:0)%は刺激的なタイトルを見て顔を赤くした…
			ENDIF
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%と一緒にエッチの技術を学んだ
		;失敗(性知識なし)
		ELSEIF TFLAG:16 == 0
			PRINTFORMW だが%ANAME(MTAR:0)%には内容が理解できなかったようだ…
		;失敗
		ELSE
			PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%を軽蔑するような視線で見つめている…
			PRINTFORMW どうやらこの本を読む気はないようだ
		ENDIF

	;淫行絵巻
	CASE 7
		PRINTFORML %ANAME(MPLY:0)%は淫行絵巻を購入し、%ANAME(MTAR:0)%に手渡した
		;成功
		IF TFLAG:16
			IF TALENT:(MTAR:0):反抗的
				PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に対して何を考えているのかと罵ったが、どうやら興味はある様子だ…
			ELSEIF TALENT:(MTAR:0):恥じらい
				PRINTFORMW %ANAME(MTAR:0)%は顔を真っ赤にして今にも卒倒しそうだ…
			ELSEIF TALENT:(MTAR:0):小悪魔
				PRINTFORMW %ANAME(MTAR:0)%はニヤニヤしながら%ANAME(MPLY:0)%の顔を見つめている…
			ELSE
				PRINTFORMW %ANAME(MTAR:0)%は刺激的なタイトルを見て顔を赤くした…
			ENDIF
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%と一緒に淫猥な春画を眺めて過ごした
			IF ABL:(MTAR:0):性知識 <= 2
				PRINTFORMW だが%ANAME(MTAR:0)%にはあまりにも内容が高度すぎて、上手く理解できなかったようだ…
			ENDIF
		;失敗(性知識なし)
		ELSEIF TFLAG:16 == 0
			PRINTFORMW だが%ANAME(MTAR:0)%には内容が理解できなかったようだ…
		;失敗
		ELSE
			PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%を軽蔑するような視線で見つめている…
			PRINTFORMW どうやらこの本を読む気はないようだ
		ENDIF

	;立ち読み
	CASE 9
		PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MTAR:0)%は気になる本を立ち読みしながら時間を潰した
ENDSELECT

;-------------------------------------------------
;地の文(パラメータ・刻印変動後)
;-------------------------------------------------
@COM_TEXT_LAST352
