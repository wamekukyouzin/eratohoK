;代わる代わるＡ挿入する

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME58
LOCALS:0 = 代わる代わるＡ挿入

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE58
;共通部分
CALL COM_ABLE_COMMON(58)
SIF RESULT == 0
	RETURN 0
;プレイヤーは2人以上
SIF MPLY_NUM < 2
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM < 1
	RETURN 0

;全てのプレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	;プレイヤーとターゲットのうち両方に行動不能または拘束中のキャラがいるなら不可
	IF !IS_PLAYABLE(MPLY:(LOCAL:0)) || IS_BIND(MPLY:(LOCAL:0))
		FOR LOCAL:1, 0, MTAR_NUM
			IF !IS_PLAYABLE(MTAR:(LOCAL:1)) || IS_BIND(MTAR:(LOCAL:1))
				RETURN 0
			ENDIF
		NEXT
	ENDIF

	;プレイヤーに竿がなく、ペニスバンド装着中でもないと不可
	SIF !HAS_PENIS(MPLY:(LOCAL:0)) && !IS_EQUIP_PLAYER((MPLY:(LOCAL:0)), 50)
		RETURN 0
	;プレイヤーが顔面騎乗されているなら不可
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	;プレイヤーが顔面騎乗中なら不可
	SIF IS_RIDE(MPLY:(LOCAL:0))
		RETURN 0
	;プレイヤーが各種奉仕を受けている・各種奉仕を行っているなら不可
	SIF IS_EQUIP(MPLY:(LOCAL:0), 1, 2, 3, 4, 20, 21, 22) || IS_EQUIP(MPLY:(LOCAL:0), 10, 11, 12, 13, 14, 15, 16, 17) || IS_EQUIP(MPLY:(LOCAL:0), 87, 88, 90, 103, 104, 106, 107)
		RETURN 0
	;プレイヤーのチンポが埋まってるなら駄目
	SIF IS_P_HOLD(MPLY:0)
		RETURN 0
	;プレイヤーが土下座しているなら不可
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 110)
		RETURN 0
NEXT

;全てのターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	;Ａが埋まっているなら不可
	SIF IS_A_HOLD(MTAR:(LOCAL:0))
		RETURN 0
	;プレイヤーとターゲットの挿入関係を取得
	LOCAL:1 = IS_INSERT_MUTUAL(MPLY:0, MTAR:(LOCAL:0))
	;ターゲットからプレイヤーに挿入中なら不可
	SIF LOCAL:1 == 3 || LOCAL:1 == 4
		RETURN 0
	;顔面騎乗中なら不可
	SIF IS_RIDE(MTAR:(LOCAL:0))
		RETURN 0
	;クンニされているなら不可
	SIF IS_EQUIP_TARGET(MTAR:(LOCAL:0), 2)
		RETURN 0
	;貝合わせ中・双頭バイブ使用中なら不可
	SIF IS_EQUIP(MTAR:(LOCAL:0), 21, 22)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM58
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;挿入関係を一時リセット

FOR LOCAL:0, 0, MPLY_NUM
	CALL CLEAR_INSERT_FLAG(MPLY:(LOCAL:0), "Ｐ")

	;プレイヤーが行動不能または拘束中なら騎乗位挿入にする
	LOCAL:6 = !IS_PLAYABLE(MPLY:(LOCAL:0)) || IS_BIND(MPLY:(LOCAL:0))

	;●プレイヤー側の処理
	EXP:(MPLY:(LOCAL:0)):性技経験値 += 2
	EXP:(MPLY:(LOCAL:0)):性交経験値 += 3

	LOCAL:5 = 0
	FOR LOCAL:1, 0, MTAR_NUM
		IF LOCAL:6
			SOURCE:(MPLY:(LOCAL:0)):快Ｐ = SENSE_SEX_TARGET_P(MTAR:(LOCAL:1), MPLY:(LOCAL:0), 1500)
		ELSE
			SOURCE:(MPLY:(LOCAL:0)):快Ｐ = SENSE_SEX_PLAYER_P(MTAR:(LOCAL:1), MPLY:(LOCAL:0), 1500)
		ENDIF

		CALL DOUTEI_COMMON(MPLY:(LOCAL:0), @"%ANAME(MTAR:(LOCAL:1))%たちのアナル", "乱交")

		;性交系の共通処理
		CALL COM_COMMON_ASEX_P(MPLY:(LOCAL:0), MTAR:(LOCAL:1))

		LOCAL:5 += SOURCE:(MPLY:(LOCAL:0)):快Ｐ
		SOURCE:(MPLY:(LOCAL:0)):快Ｐ = 0
	NEXT
	SOURCE:(MPLY:(LOCAL:0)):快Ｐ = LOCAL:5 / MPLY_NUM
	SOURCE:(MPLY:(LOCAL:0)):露出 = 100
	SOURCE:(MPLY:(LOCAL:0)):性行動 = 450

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(MPLY:(LOCAL:0), 200, 100)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(MPLY:(LOCAL:0)):4 = 1

	;プレイヤーの射精先を強制リセット
	CALL RESET_STACK_SPERM(MPLY:(LOCAL:0))

	FOR LOCAL:1, 0, MTAR_NUM
		CALL STACK_SPERM(MPLY:(LOCAL:0), MTAR:(LOCAL:1), 2, 1)
	NEXT
NEXT

;●ターゲット側の処理
FOR LOCAL:1, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:1)
	LOCAL:3 = MPLY:(RAND:MPLY_NUM)

	EXP:(LOCAL:2):性技経験値 += 2
	EXP:(LOCAL:2):性交経験値 += 3
	EXP:(LOCAL:2):輪姦経験 += MPLY_NUM / 2
	IF LOCAL:6
		SOURCE:(LOCAL:2):快Ａ = SENSE_SEX_PLAYER(LOCAL:2, LOCAL:3, MAX(1200 - MTAR_NUM * 75, 600))
	ELSE
		SOURCE:(LOCAL:2):快Ａ = SENSE_SEX_TARGET(LOCAL:2, LOCAL:3, MAX(1200 - MTAR_NUM * 75, 600))
	ENDIF
	SOURCE:(LOCAL:2):露出 = 400
	SOURCE:(LOCAL:2):性行動 = 450

	CALL VIRGIN_COMMON_A(LOCAL:2, @"%ANAME(MPLY:0)%たち", (GET_SITUATION_BY_TRAIN_MODE() == "和姦" ? "乱交" # "輪姦"))

	;性交系の共通処理
	CALL COM_COMMON_ASEX_A(LOCAL:2, LOCAL:3)
	CALL TIGHTNESS_DECREASE_A(LOCAL:2, MPLY_NUM / 2)

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 150, 100)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;ターゲットのＶ⇔プレイヤーのＰの汚れが移動
	;STAIN:(LOCAL:2):3 |= STAIN:(MPLY:0):2
	;STAIN:(MPLY:0):2 |= STAIN:(LOCAL:2):3
NEXT


;体位フラグのセット
;TCVAR:(MPLY:0):31 = 1
;TCVAR:(MTAR:0):33 = 1

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP58(ARG:0)
;全プレイヤーに対して処理
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)
	DOWNBASE:(LOCAL:1):体力 += 50

	;プレイヤーが行動不能または拘束中なら騎乗位挿入にする
	LOCAL:6 = !IS_PLAYABLE(LOCAL:1) || IS_BIND(LOCAL:1)

	;●プレイヤー側の処理
	EXP:(LOCAL:1):性技経験値 += 2
	EXP:(LOCAL:1):性交経験値 += 2

	LOCAL:5 = 0
	FOR LOCAL:2, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:2)
		IF LOCAL:6
			SOURCE:(LOCAL:1):快Ｐ = SENSE_SEX_TARGET_P(LOCAL:3, LOCAL:1, 750)
		ELSE
			SOURCE:(LOCAL:1):快Ｐ = SENSE_SEX_PLAYER_P(LOCAL:3, LOCAL:1, 750)
		ENDIF

		;性交系の共通処理
		CALL COM_COMMON_ASEX_P(LOCAL:1, LOCAL:3)

		LOCAL:5 += SOURCE:(LOCAL:1):快Ｐ
		SOURCE:(LOCAL:1):快Ｐ = 0
	NEXT
	SOURCE:(LOCAL:1):快Ｐ = LOCAL:5 / MEQUIP_PLAYER_NUM:(ARG:0)
	SOURCE:(LOCAL:1):露出 += 100
	SOURCE:(LOCAL:1):性行動 += 450

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 200, 100)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:1):4 = 1


	;プレイヤーの射精先を強制リセット
	CALL RESET_STACK_SPERM(LOCAL:1)

	FOR LOCAL:2, 0, MEQUIP_TARGET_NUM:(ARG:0)
		CALL STACK_SPERM(LOCAL:1, MEQUIP_TARGET:(ARG:0):(LOCAL:2), 2, 1)
	NEXT
NEXT
;体位フラグのセット
;TCVAR:(MPLY:0):31 = 1
;TCVAR:(MTAR:0):33 = 1

;●ターゲット側の処理
FOR LOCAL:1, 0,  MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 =  MEQUIP_TARGET:(ARG:0):(LOCAL:1)
	LOCAL:3 = MEQUIP_PLAYER:(ARG:0):(RAND:(MEQUIP_PLAYER_NUM:(ARG:0)))

	EXP:(LOCAL:2):性技経験値 += 2
	EXP:(LOCAL:2):性交経験値 += 2
	EXP:(LOCAL:2):輪姦経験 += MEQUIP_PLAYER_NUM:(ARG:0) / 2

	IF LOCAL:6
		SOURCE:(LOCAL:2):快Ａ += SENSE_SEX_PLAYER(LOCAL:2, LOCAL:3, MAX(1200 - MTAR_NUM * 75, 600))
	ELSE
		SOURCE:(LOCAL:2):快Ａ += SENSE_SEX_TARGET(LOCAL:2, LOCAL:3, MAX(1200 - MTAR_NUM * 75, 600))
	ENDIF
	SOURCE:(LOCAL:2):露出 += 400
	SOURCE:(LOCAL:2):性行動 += 450

	;性交系の共通処理
	CALL COM_COMMON_ASEX_A(LOCAL:2, LOCAL:3)
	CALL TIGHTNESS_DECREASE_A(LOCAL:2, MEQUIP_PLAYER_NUM / 2)

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 150, 100)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;ターゲットのＶ⇔プレイヤーのＰの汚れが移動
	;STAIN:(LOCAL:2):3 |= STAIN:(MPLY:0):2
	;STAIN:(MPLY:0):2 |= STAIN:(LOCAL:2):3
NEXT

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP58
RETURN 1

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE58(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%に代わる代わるアナル挿入中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP58(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%のアナルを代わる代わる犯している……

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP58(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%から離れた

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER58(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 90

;膣性交共通
CALL COM_ORDER_ASEX_P(ARG:0)
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE58
IF MTAR_NUM >= 3
	LOCALS:0 = %ANAME(MTAR:0)%たち
	LOCALS:2 = 横一列に並べ
	LOCALS:3 = 順番に
ELSEIF MTAR_NUM == 2
	LOCALS:0 = %ANAME(MTAR:0)%と%ANAME(MTAR:1)%
	LOCALS:2 = 左右に並べ
	LOCALS:3 = 交互に
ELSE
	LOCALS:0 = %ANAME(MTAR:0)%
	LOCALS:2 = こちらに向け
	LOCALS:3 = 乱暴に
ENDIF

IF MPLY_NUM >= 3
	LOCALS:4 = %ANAME(MPLY:0)%たち
ELSE
	LOCALS:4 = %ANAME(MPLY:0)%と%ANAME(MPLY:1)%
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %LOCALS:4%は%LOCALS:0%を%LOCALS:2%ると、%LOCALS:3%自らの竿を突き入れていった…
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %LOCALS:0%は一斉に%LOCALS:4%を押し倒すと、%LOCALS:3%上に跨りそれぞれの尻穴で竿を咥え込ん%PLAYER_STR("でいった", "できた")%…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%LOCALS:4%に、%LOCALS:0%を%LOCALS:3%犯すように命令した
		PRINTFORMW %LOCALS:4%は%LOCALS:2%られた%LOCALS:0%の尻穴に、%LOCALS:3%竿を挿入していった…
ENDSELECT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN58
RETURN コマンド_ウフフ
