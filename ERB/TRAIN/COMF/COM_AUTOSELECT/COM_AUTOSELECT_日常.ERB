;-------------------------------------------------
;# コマンドの自動実行の処理
;	393「なりゆきまかせ」日常オート
;-------------------------------------------------
@COM_AUTOSELECT_N
#DIM MAIN_PLAYER		;選択権を持つキャラ
#DIM L_COUNTER			;FOR文用のカウンタ１
#DIM L_COUNTER_CHECK	;FOR文用のカウンタ２
#DIM TEMP_COM			;一時的な決定コマンド
#DIM FINAL_COM			;最終的な決定コマンド
#DIM RAND_NUM			;任意値代入用
#DIM CHECK_RANDOMIZED	;乱択用

[IF_DEBUG]
;DEBUGPRINTFORML 
;DEBUGPRINTFORML #### 自動行動_日常_開始
;DEBUGPRINTFORML      現在の調教モードは {FLAG:調教モード}.%COM_AUTO_MODE_NAME()%
[ENDIF]

;-------------------------------------------------
;## カテゴリ設定
;-------------------------------------------------
CALL COM_AUTOSELECT_N_SET_COM_CATEGORY

;-------------------------------------------------
;## 実行者（MAIN_PLAYER）を設定
;	対象者（MAIN_TARGET）はMASTER
;	ここでいう実行者対象者は実際のコマンド行動で入れ替わることがある
;	行動決定権を持つキャラを決める
;	日常の場合、候補はひとりなのでそのままとる
;-------------------------------------------------
FOR L_COUNTER, 0, CHARANUM
	IF L_COUNTER != MASTER && CFLAG:L_COUNTER:調教参加フラグ
		MAIN_PLAYER = L_COUNTER
		BREAK
	ENDIF
NEXT

;-------------------------------------------------
;## カテゴリ優先度設定
;-------------------------------------------------
CALL COM_AUTOSELECT_N_SET_CATEGORY_PRIORITY(MAIN_PLAYER, MASTER)

;-------------------------------------------------
;## コマンド優先度設定
;-------------------------------------------------
CALL COM_AUTOSELECT_N_SET_COM_PRIORITY(MAIN_PLAYER, MASTER)

;-------------------------------------------------
;## 実行するコマンドを決定 
;-------------------------------------------------
;リセット
CALL COM_AUTO_RANDOMIZED_WEIGHTED_START

VARSET CHECK_RANDOMIZED
FOR L_COUNTER, 0, ADD_NUM_対象が実行 + 1000
	SIF COM_AUTO_COM_PRIORITY:L_COUNTER < 1
		CONTINUE
	CALL COM_AUTO_RANDOMIZED_WEIGHTED_VALUE(L_COUNTER, COM_AUTO_COM_PRIORITY:L_COUNTER)
	CHECK_RANDOMIZED ++
NEXT

;ひとつも候補がないとき最低値確保
IF CHECK_RANDOMIZED < 1
	IF COM_AUTO_COM_PRIORITY:GET_TRAINNUM("なにもしない") < 10
		COM_AUTO_COM_PRIORITY:GET_TRAINNUM("なにもしない") = 10
	ENDIF
	CHECK_RANDOMIZED += COM_AUTO_COM_PRIORITY:GET_TRAINNUM("なにもしない")
ENDIF

;結果取得
TEMP_COM = COM_AUTO_RANDOMIZED_WEIGHTED_RESULT()
IF TEMP_COM >= ADD_NUM_対象が実行 + 1000
;	PRINTFORML ※可能な行動が見つかりませんでした。なにもしないを代入します
;	PRINTFORML ※閨は普通に制限されていて起きる可能性があるので、ここではエラーメッセージを表示していませんが
;	PRINTFORML ※日常おまかせのほうで起きる可能性はないはずなので、表示してもいいかもしれません
;	PRINTFORML ※@COM_AUTOSELECT_N
	TEMP_COM = GET_TRAINNUM("なにもしない")
ENDIF

[IF_DEBUG]
;DEBUGPRINTFORML 
;DEBUGPRINTFORML #### DEBUG 決定したコマンド
IF TEMP_COM >= ADD_NUM_対象が実行
;DEBUGPRINTFORML {TEMP_COM, 4}.%TRAINNAME:(TEMP_COM - ADD_NUM_対象が実行), 20, LEFT%される %NUM_FORMAT(COM_AUTO_COM_PRIORITY:TEMP_COM), 10, RIGHT%
ELSE
;DEBUGPRINTFORML {TEMP_COM, 4}.%TRAINNAME:(TEMP_COM), 20, LEFT%する %NUM_FORMAT(COM_AUTO_COM_PRIORITY:TEMP_COM), 10, RIGHT%
ENDIF
[ENDIF]

CALL CLEAR_MPLY()
CALL CLEAR_MTAR()

;メインの実行者・対象者の設定
IF TEMP_COM < ADD_NUM_対象が実行
	CALL ADD_MPLY(MAIN_PLAYER)
	CALL ADD_MTAR(MASTER)
ELSEIF TEMP_COM < ADD_NUM_対象が実行 + 1000
	CALL ADD_MPLY(MASTER)
	CALL ADD_MTAR(MAIN_PLAYER)
ELSE
ENDIF

FINAL_COM = TEMP_COM % ADD_NUM_対象が実行

;-------------------------------------------------
;## 継続状態の設定
;-------------------------------------------------
VARSET LOCAL, 0
RESULT = 0
TRYCALLFORM COM_IS_EQUIP{FINAL_COM}
;継続状態が不可能なコマンドの場合
IF RESULT == 0
	;非継続フラグON
	TFLAG:1 = 1
;常に継続状態で実行されるコマンドの場合
ELSEIF RESULT == 2
	;非継続フラグOFF
	TFLAG:1 = 0
;継続、非継続のどちらでも可能なコマンドの場合
ELSE
	LOCAL:20 = 15
	LOCAL:21 = 40
	LOCAL:22 = 80
	LOCAL:23 = 100
	IF RAND:100 < LOCAL:(COM_TENDENCY:MAIN_PLAYER:FINAL_COM + 20)
		;非継続フラグOFF
		TFLAG:1 = 0
	ELSE
		;非継続フラグON
		TFLAG:1 = 1
	ENDIF
ENDIF

;実行
DOTRAIN FINAL_COM

