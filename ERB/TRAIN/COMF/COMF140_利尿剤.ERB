;利尿剤

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME140
LOCALS:0 = 利尿剤

RESULTS:0 = %LOCALS:0%を飲ませる
RESULTS:1 = %LOCALS:0%を飲まさせられる
RESULTS:2 = %LOCALS:0%を飲まさせる
RESULTS:3 = %LOCALS:0%を飲まされる
RESULTS:4 = %LOCALS:0%を飲まさせる
RESULTS:5 = %LOCALS:0%使用見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE140
;共通部分
CALL COM_ABLE_COMMON(140)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは一人以上
SIF MTAR_NUM <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;利尿剤が必要
SIF ITEM:利尿剤 <= 0 && ITEM:A_利尿剤 <= 0
	RETURN 0
;既に利尿剤を使用済みなら不可
SIF TCVAR:(MTAR:0):62
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM140
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;利尿剤を１つ減少させる(追加アイテム使用時は減少させない)
IF ITEM:A_利尿剤 <= 0
	ITEM:利尿剤 = MAX(ITEM:利尿剤 - 1, 0)
ENDIF

;●プレイヤー側の処理
SOURCE:(MPLY:0):逸脱 = 50

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 30, 20)

;●ターゲット側の処理
LOCAL:4 = 80

FOR LOCAL, 0, MTAR_NUM
	LOCAL:2 = MTAR:LOCAL

	SELECTCASE ABL:(LOCAL:2):排泄
		CASE 0
			TIMES LOCAL:4, 1.00
		CASE 1
			TIMES LOCAL:4, 0.60
		CASE 2
			TIMES LOCAL:4, 0.25
		CASE 3
			TIMES LOCAL:4, 0.05
		CASEELSE
			LOCAL:4 = 0
	ENDSELECT

	SOURCE:(LOCAL:2):逸脱 = 100
	SOURCE:(LOCAL:2):反感 = LOCAL:4

	;利尿剤仕様フラグをON
	TCVAR:(LOCAL:2):62 = 1

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 30, 40)
NEXT

;主導度変化基準値
TFLAG:49 = 1

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER140(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 80

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"露出Lv{ABL:(ARG:0):露出}", ABL:(ARG:0):露出 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"排泄Lv{ABL:(ARG:0):排泄}", ABL:(ARG:0):排泄 * 6)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):倒錯的
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯的", 8)
ENDIF
IF TALENT:(ARG:0):保守的
	CALL COM_ORDER_ELEMENT(ARG:0, "保守的", -8)
ENDIF

IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE140
SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0, MTAR_NUM)%に利尿剤を飲ませ%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0, MTAR_NUM)%に利尿剤を飲ませるように命令した
		PRINTFORMW %ANAME(MPLY:0)%は言われるまま%ANAME(MTAR:0, MTAR_NUM)%の口に利尿剤を注ぎ込んだ…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0, MTAR_NUM)%に対して、%ANAME(MTAR:0, MTAR_NUM)%に利尿剤を飲ませるように命令した
		PRINTFORMW %ANAME(MPLY:0)%は言われるまま%ANAME(MTAR:0, MTAR_NUM)%の口に利尿剤を注ぎ、飲み込ませた…
ENDSELECT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN140
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_140()
RETURN POWER(2, 性的嗜好_道具を使いたい)

@COM_PREFERENCE_TARGET_140()
RETURN POWER(2, 性的嗜好_道具を使われたい)

