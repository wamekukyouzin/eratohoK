;-------------------------------------------------
;共通実行判定
;-------------------------------------------------
@COM_ABLE_COMMON(ARG)
IF !COM_ENABLE
	RETURN 0
ENDIF

;おまかせ中は除く
IF !TFLAG:0
	;なすがまま、おまかせは例外
	IF !GROUPMATCH(ARG, 290, 291)
		;行動不能のキャラが主導権を握っているとコマンド実行不可
		FOR LOCAL:0, 0, MPLY_NUM
			IF !IS_PLAYABLE(MPLY:(LOCAL:0)) && IS_INITIATIVE_SETTING(MPLY:(LOCAL:0)) == 2
				RETURN 0
			ENDIF
		NEXT
		FOR LOCAL:0, 0, MTAR_NUM
			IF !IS_PLAYABLE(MTAR:(LOCAL:0)) && IS_INITIATIVE_SETTING(MTAR:(LOCAL:0)) == 2
				RETURN 0
			ENDIF
		NEXT
	ENDIF

	;なすがままは例外
	IF !GROUPMATCH(ARG, 290)
		;主人公が行動不能だとコマンド実行不可
		IF !IS_PLAYABLE(MASTER)
			RETURN 0
		ENDIF
	ENDIF
ENDIF

;主導権を持っているキャラが、その行為の知識を持っている必要がある
;ペニバンは別
IF ARG:0 == 50
	LOCAL:2 = 1
ELSEIF FLAG:主導権所有者 == -1
	;「成り行き」の場合、プレイヤー・ターゲットのうち誰か一人が知識を持っていれば良い
	LOCAL:2 = 0
	FOR LOCAL:0, 0, MPLY_NUM
		CALL CHECK_COM_KNOWLEDGE(MPLY:(LOCAL:0), ARG:0)
		IF RESULT
			LOCAL:2 = 1
			BREAK
		ENDIF
	NEXT
	IF !LOCAL:2
		FOR LOCAL:0, 0, MTAR_NUM
			CALL CHECK_COM_KNOWLEDGE(MTAR:(LOCAL:0), ARG:0)
			IF RESULT
				LOCAL:2 = 1
				BREAK
			ENDIF
		NEXT
	ENDIF
ELSE
	CALL CHECK_COM_KNOWLEDGE(FLAG:主導権所有者, ARG:0)
	LOCAL:2 = RESULT
ENDIF
IF !LOCAL:2
	RETURN 0
ENDIF

FOR LOCAL:0, 0, MPLY_NUM
	IF IS_ANIMAL(MPLY:(LOCAL:0)) && !ANIMAL_COMABLE_MPLY(ARG:0)
		RETURN 0
	ENDIF
NEXT

FOR LOCAL:0, 0, MTAR_NUM
	IF IS_ANIMAL(MTAR:(LOCAL:0)) && !ANIMAL_COMABLE_MTAR(ARG:0)
		RETURN 0
	ENDIF
NEXT

;ホモフィルタ
IF CONFIG:201
	;尻尾Ａ、フェラ、尻コキ、キス、各種挿入系、イラマの場合、MTARとMPLY両方に男が含まれてると不可
	IF GROUPMATCH(ARG, 6, 7, 11, 15, 20, 40, 41, 42, 43, 44, 45, 46, 47, 48, 56, 58, 90)
		FOR LOCAL:0, 0, MPLY_NUM
			IF IS_MALE(MPLY:(LOCAL:0))
				FOR LOCAL:1, 0, MTAR_NUM
					IF IS_MALE(MTAR:(LOCAL:1))
						RETURN 0
					ENDIF
				NEXT
			ENDIF
		NEXT
	ENDIF
ENDIF


;共通コマンドは常に可能
IF ARG >= 550 && ARG <= 599
	RETURN 1
ENDIF

;子育てモードは子育てコマンドのみ
IF FLAG:調教モード == 3
	IF ARG >= 500 && ARG <= 549
		RETURN 1
	ENDIF

;ウフフ中はウフフコマンドのみ
ELSEIF FLAG:ウフフフラグ
	IF ARG >= 0 && ARG <= 299
		RETURN 1
	ENDIF

;非ウフフ中は日常コマンドのみ
ELSE
	IF ARG >= 300 && ARG <= 499
		RETURN 1
	ENDIF
ENDIF

RETURN 0

;-------------------------------------------------
;キャラARG:0にコマンドARG:1の知識を獲得させる(ウフフコマンドのみ 0～299)
;-------------------------------------------------
@SET_COM_KNOWLEDGE(ARG:0, ARG:1)
IF ARG:1 >= 0 && ARG:1 <= 299
	CALL CHECK_COM_KNOWLEDGE(ARG:0, ARG:1)
	IF !RESULT
		PRINTFORML %ANAME(ARG:0)%は「%TRAINNAME:(ARG:1)%」の知識を得ました
	ENDIF
	CFLAG:(ARG:0):(ARG:1 / 60 + 100) |= (1 << (ARG:1 % 60))
ENDIF

;-------------------------------------------------
;キャラARG:0がARG:1番のコマンドを主導実行できる知識があるかどうかを判定
;実行可能なら1、不可能なら0が返る
;-------------------------------------------------
@CHECK_COM_KNOWLEDGE(ARG:0, ARG:1)
IF ARG:1 < 0 || ARG:1 > 999
	RETURN 0
ELSEIF ARG:1 > 299
	RETURN 1
ENDIF

;そのコマンドの知識を持っているならOK
IF CFLAG:(ARG:0):(ARG:1 / 60 + 100) & (1 << (ARG:1 % 60))
	RETURN 1
ENDIF

;必要な性知識Lvを取得
RESULT = 0
TRYCALLFORM COM_KNOWLEDGE_LEVEL{ARG:1}

;性知識Lvが必要値に達していればOK
IF ABL:(ARG:0):性知識 >= RESULT
	RETURN 1
ENDIF
RETURN 0

;-------------------------------------------------
;動物キャラがプレイヤー側のときの実行可能コマンドをまるっと制限する関数
;実行可能なら1、不可能なら0
;-------------------------------------------------
@ANIMAL_COMABLE_MPLY(ARG:0)
#FUNCTION
;クンニ、アナル舐め、フェラチオ、キス
SIF GROUPMATCH(ARG:0, 2, 8, 11, 20)
	RETURNF 1
;正常位、後背位、騎乗位、背面騎乗位、二本挿入、順番挿入、代わる代わる挿入、それらのＡ版
SIF GROUPMATCH(ARG:0, 30, 31, 34, 35, 36, 40, 41, 44, 45, 46, 55, 56, 57, 58)
	RETURNF 1
;イラマ
SIF GROUPMATCH(ARG:0, 90)
	RETURNF 1
;脚を舐める
SIF GROUPMATCH(ARG:0, 104)
	RETURNF 1
;放尿する、尿をかける、口内放尿
SIF GROUPMATCH(ARG:0, 130, 131, 132)
	RETURNF 1
;おまかせする、なにもしない
SIF GROUPMATCH(ARG:0, 291, 292)
	RETURNF 1
RETURNF 0
;-------------------------------------------------
;動物キャラがターゲット側のときの実行可能コマンドをまるっと制限する関数
;実行可能なら1、不可能なら0
;-------------------------------------------------
@ANIMAL_COMABLE_MTAR(ARG:0)
#FUNCTION
;愛撫、アナル愛撫、尻尾アナル挿入、アナル舐め
SIF GROUPMATCH(ARG:0, 0, 4, 7, 8)
	RETURNF 1

;手コキ、フェラチオ、パイズリ、足コキ、オナホコキ、尻尾コキ
SIF GROUPMATCH(ARG:0, 10, 11, 12, 14, 17)
	RETURNF 1

;アナル正常位、後背位、順番にＡ、代わる代わるＡ
SIF GROUPMATCH(ARG:0, 40, 41, 56, 58)
	RETURNF 1
	
;アナルバイブ, Ａ拡張バルーン、ローション、媚薬、絶倫丸、麻薬、桃源香
SIF GROUPMATCH(ARG:0, 62, 66, 70, 71, 72, 74, 76)
	RETURNF 1
;スパンキング、鞭、針、目隠し、縄、口枷、アナルフィスト、平手打ち、イラマチオ、腹パン
SIF GROUPMATCH(ARG:0, 81, 82, 83, 84, 85, 86, 88, 89, 90, 97)
	RETURNF 1
;自慰、顔面騎乗、Ａ顔面騎乗、電気あんま、足舐め、Ｗ顔面騎乗、亀頭責め手ｺｷとフェラ、尻尾セルフ、尻尾セルフＡ、土下座、おねだり、セルフオナホ
SIF GROUPMATCH(ARG:0, 100, 101, 102, 103, 104, 105, 106, 107, 108, 109, 110, 111, 116)
	RETURNF 1
;放尿する、尿をかける、口内放尿、包皮、がんきほうひ
SIF GROUPMATCH(ARG:0, 130, 131, 132, 150, 151)
	RETURNF 1
;おまかせする、なにもしない
SIF GROUPMATCH(ARG:0, 291, 292)
	RETURNF 1
;PENIBAN
SIF GROUPMATCH(ARG:0, 50)
	RETURNF 1
RETURNF 0
