;口枷

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME93
LOCALS:0 = 鼻フック

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%装着見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE93
;共通部分
CALL COM_ABLE_COMMON(93)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;鼻フックが必要
SIF !ITEM:鼻フック && !ITEM:A_鼻フック
	RETURN 0
;プレイヤーがターゲット以外に挿入中なら不可
SIF IS_INSERT_SINGLE(MPLY:0, "Ｐ") && !GROUPMATCH(IS_INSERT_MUTUAL(MPLY:0, MTAR:0), 1, 2)
	RETURN 0
;プレイヤーがＶかＡに挿入されている場合
IF IS_INSERT_SINGLE(MPLY:0, "ＶＡ")
	;ターゲット以外に挿入されている場合は不可
	SIF !GROUPMATCH(IS_INSERT_MUTUAL(MTAR:0, MPLY:0), 1, 2)
		RETURN 0
	;体位が正常位・対面座位・騎乗位以外なら不可
	SIF !GROUPMATCH(GET_SEX_POSITION(MTAR:0, MPLY:0), 1, 3, 5)
		RETURN 0
ENDIF
;プレイヤーが顔面騎乗されているなら不可
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
;ターゲットが既に鼻フック装着中なら不可
SIF IS_EQUIP_TARGET(MTAR:0, 93)
	RETURN 0
;プレイヤーが奉仕中なら不可(尻尾系除く)
SIF IS_EQUIP_PLAYER(MPLY:0, 1, 2, 3, 4, 5) || IS_EQUIP_PLAYER(MPLY:0, 10, 11, 12, 13, 14, 15, 16) || IS_EQUIP_PLAYER(MPLY:0, 103, 104, 106, 107)
	RETURN 0
;ターゲットが奉仕中なら不可(尻尾系除く)
SIF IS_EQUIP_PLAYER(MTAR:0, 1, 2, 3, 4, 5) || IS_EQUIP_PLAYER(MTAR:0, 10, 11, 12, 13, 14, 15, 16) || IS_EQUIP_PLAYER(MTAR:0, 103, 104, 106, 107)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM93
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤーについて処理
SOURCE:(MPLY:0):嗜虐 = 200
SOURCE:(MPLY:0):逸脱 = 80
SOURCE:(MPLY:0):接触 = 20
EXP:(MPLY:0):嗜虐経験値 += 1

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 130, 40)

;●ターゲットについて処理
EXP:(MTAR:0):緊縛経験 += 1
EXP:(MTAR:0):被虐経験値 += 1

SOURCE:(MTAR:0):恐怖 = 40
SOURCE:(MTAR:0):不安 = 70
SOURCE:(MTAR:0):逸脱 = 150
SOURCE:(MTAR:0):接触 = 20

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 0, 110)

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 4

;レズ・ＢＬ経験基準値
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP93
;継続コマンドかつフィルタリング不可
RETURN 2

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP93(ARG:0)
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

EXP:(LOCAL:3):緊縛経験 += 1
EXP:(LOCAL:3):被虐経験値 += 1

SOURCE:(LOCAL:3):恐怖 += 15
SOURCE:(LOCAL:3):不安 += 30
SOURCE:(LOCAL:3):逸脱 += 80

;倒錯度変化
TCVAR:(LOCAL:3):50 += 2

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE93(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%が鼻フックを装着中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP93(ARG:0)
PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%の鼻はフックにより醜く歪められている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP93(ARG:0)
PRINTFORMW %EQUIP_TARGET_ANAME(ARG:0)%の鼻フックを外した

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER93(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 110

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"サドLv{ABL:(ARG:0):サド}", ABL:(ARG:0):サド * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):大人しい
	CALL COM_ORDER_ELEMENT(ARG:0, "大人しい", -3)
ENDIF
IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ気質
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ気質", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド)
	CALL COM_ORDER_ELEMENT(ARG:0, "サド", 40)
ELSE
	IF ABL:(ARG:0):主導度Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE93
SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に鼻フックを装着し%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に鼻フックを渡すと、それを%ANAME(MTAR:0)%に装着するように命令した…
		PRINTFORML %ANAME(MPLY:0)%は言われるまま、%ANAME(MTAR:0)%に鼻フックを取り付けた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%ANAME(MTAR:0)%に鼻フックを付けるように命令した
		PRINTFORML %ANAME(MPLY:0)%は言われるまま、%ANAME(MTAR:0)%に鼻フックを装着した…
ENDSELECT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN93
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_93()
RETURN POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_93()
RETURN POWER(2, 性的嗜好_虐げられたい) + POWER(2, 性的嗜好_縛られたい)

