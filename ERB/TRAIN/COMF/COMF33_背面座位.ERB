;背面座位

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME33

IF IS_INSERT_SINGLE(MTAR:0, "Ａ") && IS_INSERT_MUTUAL(MPLY, MTAR:0) != 2
	LOCALS:0 = 二穴背面座位
ELSE
	LOCALS:0 = 背面座位
ENDIF
RESULTS:0 = %LOCALS:0%で挿入
RESULTS:1 = %LOCALS:0%で挿入させられる
RESULTS:2 = %LOCALS:0%で挿入させる
RESULTS:3 = %LOCALS:0%で挿入される
RESULTS:4 = %LOCALS:0%で挿入させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE33
;共通部分
CALL COM_ABLE_COMMON(33)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;対象にＶが必要
SIF !HAS_VAGINA(MTAR:0)
	RETURN 0
SIF IS_V_HOLD(MTAR:0)
	RETURN 0
;being fucked in the ass
SIF IS_INSERT_SINGLE(MTAR:0, "Ａ")
	RETURN 0
SIF !CAN_FUCK(MPLY:0, MTAR:0, 33)
	RETURN 0
SIF REACHING_BODY(MTAR, MPLY)
	RETURN 0
SIF REACHING_GROIN(MTAR, MPLY)
	RETURN 0
SIF LICKING_BODY(MTAR, MPLY) || LICKING_BODY(MPLY, MTAR)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM33
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;既に挿入中なら現在の体位を取得
TFLAG:17 = 0
IF IS_INSERT_MUTUAL(MPLY:0, MTAR:0) == 1
	SELECTCASE GET_SEX_POSITION(MPLY:0, MTAR:0)
		CASE 1
			TFLAG:17 = 3
		CASE 2
			TFLAG:17 = 2
		CASE 3
			TFLAG:17 = 3
		CASE 4
			TFLAG:17 = 1
		CASE 5
			TFLAG:17 = 3
		CASE 6
			TFLAG:17 = 2
		CASE 7
			TFLAG:17 = 2
		CASE 8
			TFLAG:17 = 3
	ENDSELECT
ENDIF
IF TFLAG:17 == 0
	SELECTCASE PREVCOM
		CASE 30
			TFLAG:17 = 3
		CASE 31
			TFLAG:17 = 2
		CASE 32
			TFLAG:17 = 3
		CASE 33
			TFLAG:17 = 1
		CASE 34
			TFLAG:17 = 3
		CASE 35
			TFLAG:17 = 2
		CASE 37
			TFLAG:17 = 2
		CASE 38
			TFLAG:17 = 3
	ENDSELECT
ENDIF

;挿入関係を一時リセット
CALL CLEAR_INSERT_FLAG(MPLY:0, "Ｐ")

;●プレイヤー側の処理
DOWNBASE:(MPLY:0):体力 += 180

EXP:(MPLY:0):性技経験値 += 1
EXP:(MPLY:0):性交経験値 += 2

SOURCE:(MPLY:0):快Ｐ = SENSE_SEX_PLAYER_P(MTAR:0, MPLY:0, 900)
SOURCE:(MPLY:0):性行動 = 450

IF MTAR:0 == MASTER
	CALL ADD_SOURCE_AIZYOU(MPLY:0, 20)
ELSEIF MPLY:0 != MASTER
	SOURCE:(MPLY:0):露出 += 200
ENDIF

;性交系の共通処理
CALL COM_COMMON_SEX_P(MPLY:0, MTAR:0)

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 200, 80)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●ターゲット側の処理
DOWNBASE:(MTAR:0):体力 += 140

EXP:(MTAR:0):性技経験値 += 1
EXP:(MTAR:0):性交経験値 += 2

SOURCE:(MTAR:0):快Ｃ = SENSE_SEX_TARGET(MPLY:0, MTAR:0, 500)
SOURCE:(MTAR:0):快Ｂ = SENSE_SEX_TARGET(MPLY:0, MTAR:0, 500)
SOURCE:(MTAR:0):快Ｖ = SENSE_SEX_TARGET(MPLY:0, MTAR:0, 900)
SOURCE:(MTAR:0):性行動 = 450

IF MPLY:0 == MASTER
	CALL ADD_SOURCE_AIZYOU(MTAR:0, 20)
ELSEIF MTAR:0 != MASTER
	SOURCE:(MTAR:0):露出 += 200
ENDIF

;性交系の共通処理
CALL COM_COMMON_SEX_V(MTAR:0, MPLY:0)

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 120, 180)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MTAR:0):4 = 1

;体位フラグのセット
TCVAR:(MPLY:0):31 = 4
TCVAR:(MTAR:0):33 = 4

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 1

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP33
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP33(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;ソースを退避
CALL PUTOUT_SOURCE(LOCAL:2)
CALL PUTOUT_SOURCE(LOCAL:3)

DOWNBASE:(LOCAL:2):体力 += 20

EXP:(LOCAL:2):性技経験値 += 1
EXP:(LOCAL:2):性交経験値 += 1

SOURCE:(LOCAL:2):快Ｐ += SENSE_SEX_PLAYER_P(LOCAL:3, LOCAL:2, 450)
SOURCE:(LOCAL:2):性行動 += 150

;性交系の共通処理
CALL COM_COMMON_SEX_P(LOCAL:2, LOCAL:3)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

;倒錯度変化基準値
TCVAR:(LOCAL:2):50 += 1

DOWNBASE:(LOCAL:3):体力 += 20

EXP:(LOCAL:3):性技経験値 += 1
EXP:(LOCAL:3):性交経験値 += 1

SOURCE:(LOCAL:3):快Ｃ += SENSE_SEX_TARGET(LOCAL:2, LOCAL:3, 250)
SOURCE:(LOCAL:3):快Ｂ += SENSE_SEX_TARGET(LOCAL:2, LOCAL:3, 250)
SOURCE:(LOCAL:3):快Ｖ += SENSE_SEX_TARGET(LOCAL:2, LOCAL:3, 450)
SOURCE:(LOCAL:3):露出 += 200
SOURCE:(LOCAL:3):性行動 += 150

;性交系の共通処理
CALL COM_COMMON_SEX_V(LOCAL:3, LOCAL:2)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:3):4 = 1

;倒錯度変化基準値
TCVAR:(LOCAL:3):50 += 1

;退避したソースを加算
CALL SUM_SOURCE(LOCAL:2)
CALL SUM_SOURCE(LOCAL:3)

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE33(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%の膣に背面座位で挿入中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP33(ARG:0)
SELECTCASE RAND:2
	CASE 0
		PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%の膣が%EQUIP_PLAYER_ANAME(ARG:0)%の%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を咥え込んでいる…
	CASE 1
		LOCAL:0 = MEQUIP_TARGET:(ARG:0):0
			;ターゲットが行動不能
		IF !IS_PLAYABLE(LOCAL:0)
			PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は意識を失った%EQUIP_TARGET_ANAME(ARG:0)%の膣を下から突き上げている…
		ELSE
			SELECTCASE GET_COM_INITIATIVE()
					;プレイヤーに主導権
				CASE 0
					PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は背後から%EQUIP_TARGET_ANAME(ARG:0)%の体を愛撫しつつ、激しく腰を突き入れている…
					;ターゲットに主導権
				CASE 1
					PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%は%EQUIP_PLAYER_ANAME(ARG:0)%の愛撫に身を任せながら、自らも快感を貪るように腰を動かしている…
					;第三者に主導権
				CASEELSE
					PRINTFORML %ANAME(TFLAG:45)%は%EQUIP_TARGET_ANAME(ARG:0)%に背面座位で挿入している%EQUIP_PLAYER_ANAME(ARG:0)%に対し、もっと%EQUIP_TARGET_ANAME(ARG:0)%を責めるように命令し
					PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%への愛撫を激しくしながら、腰を突き上げて%EQUIP_TARGET_ANAME(ARG:0)%の膣壁を擦り上げている…
			ENDSELECT
		ENDIF
ENDSELECT

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP33(ARG:0)
PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の膣内から%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を引き抜いた…

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER33(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 110

;膣性交共通
CALL COM_ORDER_VSEX_P(ARG:0)
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE33
IF IS_PIERCED(MTAR:0, ピアス_ラビア)
	LOCALS:0 = ピアスの取り付けられた
ELSE
	LOCALS:0 = 
ENDIF
IF TALENT:(MTAR:0):処女 == 1
	LOCALS:0 = まだ乙女の
ELSEIF PALAM:(MTAR:0):潤滑 >= PALAMLV:7
	LOCALS:0 = 濡れそぼった
ELSE
	LOCALS:0 = 
ENDIF
IF IS_OVULATION(MTAR:0)
	LOCALS:0 = %LOCALS:0%危険日の
ENDIF

IF TALENT:(MTAR:0):体格 == 体格_小柄
	LOCALS:0 = %LOCALS:0%幼いワレメ
ELSE
	LOCALS:0 = %LOCALS:0%秘裂
ENDIF

;ペニスバンド装着中
IF IS_EQUIP_PLAYER(MPLY:0, 50)
	;既に挿入済みの場合
	IF TFLAG:17
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				;前回が背面座位
				IF TFLAG:17 == 1
					PRINTFORMW %ANAME(MPLY:0)%は背後から%ANAME(MTAR:0)%の体を愛撫しつつ、さらに激しく腰を突き入れ%TARGET_STR("た", "てきた")%…
				;後背位・背面騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を抱き上げると、背後から%ANAME(MTAR:0)%を抱き締めて再び腰を動かし始めた…
				;正常位・対面座位・騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%を後ろ向きにすると、背後から%ANAME(MTAR:0)%を抱き上げて再び腰を動かし始めた…
				ENDIF
			;ターゲットに主導権
			CASE 1
				;前回が背面座位
				IF TFLAG:17 == 1
					PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の愛撫に身を任せながら、自らも快感を貪るように腰を動かし%PLAYER_STR("た", "てきた")%…
				;後背位・背面騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に背中を預けて後ろから抱き締めさせると、背面座位の体勢で腰を振らせた…
				;正常位・対面座位・騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORMW %ANAME(MTAR:0)%は挿入したまま体をひねって%ANAME(MPLY:0)%に背中を向けると、後ろから抱き締めさせて腰を振らせた…
				ENDIF
			;第三者に主導権
			CASEELSE
				;前回が背面座位
				IF TFLAG:17 == 1
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に背面座位で挿入している%ANAME(MPLY:0)%に対し、もっと%ANAME(MTAR:0)%を責めるように命令した
					PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%への愛撫を激しくしながら、再び腰を突き上げて%ANAME(MTAR:0)%の膣壁を擦り上げた…
				;後背位・背面騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、体位を背面座位に変えて再び腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を抱き上げると、背後から%ANAME(MTAR:0)%を抱き締めて再び腰を動かし始めた…
				;正常位・対面座位・騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、体位を背面座位に変えて再び腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%を後ろ向きにすると、背後から%ANAME(MTAR:0)%を抱き上げて再び腰を動かし始めた…
				ENDIF
		ENDSELECT

	ELSE
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の身体を背後から抱き上げると、%LOCALS:0%を装着した張り型で貫き、愛撫しながら腰を突き上げ%TARGET_STR("た", "てきた")%…
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に背中を預けて後ろから抱き締めさせると、背後から%LOCALS:0%に張り型を挿入させて腰を突き上げさせた…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、背面座位で%ANAME(MTAR:0)%を犯すように命令した
				PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の身体を背後から抱き上げると、%LOCALS:0%を装着した張り型で貫き愛撫しながら腰を突き上げた…
		ENDSELECT
	ENDIF

;ペニスバンド不使用
ELSE
	;既に挿入済みの場合
	IF TFLAG:17
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				;前回が背面座位
				IF TFLAG:17 == 1
					PRINTFORMW %ANAME(MPLY:0)%は背後から%ANAME(MTAR:0)%の体を愛撫しつつ、さらに激しく腰を突き入れ%TARGET_STR("た", "てきた")%…
				;後背位・背面騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を抱き上げると、背後から%ANAME(MTAR:0)%を抱き締めて再び腰を動かし始めた…
				;正常位・対面座位・騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%を後ろ向きにすると、背後から%ANAME(MTAR:0)%を抱き上げて再び腰を動かし始めた…
				ENDIF
			;ターゲットに主導権
			CASE 1
				;前回が背面座位
				IF TFLAG:17 == 1
					PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の愛撫に身を任せながら、お返しするかのように腰をグリグリとひねってペニスを締め付け%PLAYER_STR("た", "てきた")%…
				;後背位・背面騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に背中を預けて後ろから抱き締めさせると、背面座位の体勢で腰を振らせた…
				;正常位・対面座位・騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORMW %ANAME(MTAR:0)%は挿入したまま体をひねって%ANAME(MPLY:0)%に背中を向けると、後ろから抱き締めさせて腰を振らせた…
				ENDIF
			;第三者に主導権
			CASEELSE
				;前回が背面座位
				IF TFLAG:17 == 1
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に背面座位で挿入している%ANAME(MPLY:0)%に対し、もっと%ANAME(MTAR:0)%を責めるように命令した
					PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%への愛撫を激しくしながら、再び腰を突き上げて%ANAME(MTAR:0)%の膣壁を擦り上げた…
				;後背位・背面騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、体位を背面座位に変えて再び腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を抱き上げると、背後から%ANAME(MTAR:0)%を抱き締めて再び腰を動かし始めた…
				;正常位・対面座位・騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、体位を背面座位に変えて再び腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%を後ろ向きにすると、背後から%ANAME(MTAR:0)%を抱き上げて再び腰を動かし始めた…
				ENDIF
		ENDSELECT

	ELSE
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の身体を背後から抱き上げると、%LOCALS:0%を貫いて愛撫しながら腰を突き上げ%TARGET_STR("た", "てきた")%…
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に背中を預けて後ろから抱き締めさせると、背後から%LOCALS:0%に挿入させて腰を突き上げさせた…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、背面座位で%ANAME(MTAR:0)%を犯すように命令した
				PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の身体を背後から抱き上げると、%LOCALS:0%を貫いて愛撫しながら腰を突き上げた…
		ENDSELECT
	ENDIF
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST33
SIF IS_PIERCED(MPLY:0, ピアス_ペニス)
	PRINTFORML %ANAME(MPLY:0)%のペニスに取り付けられたピアスが、%ANAME(MTAR:0)%の膣内をごりごりと抉っていく……

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN33
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_33()
RETURN POWER(2, 性的嗜好_性交したい)

@COM_PREFERENCE_TARGET_33()
RETURN POWER(2, 性的嗜好_性交されたい)

@COM_STACK_SPERM_MPLY_TO_MTAR_33()
RETURN 射精部位_膣内
