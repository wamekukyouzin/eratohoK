;尻コキ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME15
LOCALS:0 = 尻コキ

IF MPLY_NUM == 2 && MTAR_NUM == 1
	LOCALS:0 = Ｗ尻コキ
ELSE
	LOCALS:0 = 尻コキ
ENDIF

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE15
;共通部分
CALL COM_ABLE_COMMON(15)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で2人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 2
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
SIF !HAS_PENIS(MTAR:0)
	RETURN 0
SIF !P_STACKABLE(MPLY:0, MTAR:0, 15)
	RETURN 0
;grovelling
SIF IS_EQUIP_PLAYER(MTAR:0, 110)
	RETURN 0

;全プレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	;行動不能なら不可
	SIF !IS_PLAYABLE(MPLY:(LOCAL:0))
		RETURN 0
	;拘束中なら不可
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
	;プレイヤーが挿入中なら不可
	SIF IS_INSERT_SINGLE(MPLY:(LOCAL:0), "全")
		RETURN 0
	SIF IS_RIDE(MPLY:(LOCAL:0)) || IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	SIF IS_RIDE(MTAR:0) || IS_RIDDEN(MTAR:0)
		RETURN 0
    ;anal caress/ using Avibe
	SIF IS_EQUIP_TARGET(MPLY:(LOCAL:0), 4)
		RETURN 0
    ;grovelling
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL), 110)
		RETURN 0
    ;getting/receiving tribadism, double dildo, trampling, footlicking
	SIF (SEARCH_EQUIP_IC_M(MPLY:(LOCAL:0), MTAR:0, 21, 22, 103, 104) >= 0)
		RETURN 0
    ;receiving footjob, boobjob, buttjob from target
	SIF SEARCH_EQUIP_M(MTAR:0, MPLY:(LOCAL:0), 14, 12, 15) >= 0
		RETURN 0
	SIF REACHING_BODY(MPLY:(LOCAL:0), MTAR)
		RETURN 0
	SIF LICKING_GROIN(MPLY:(LOCAL:0), MTAR) || LICKING_GROIN(MTAR:0, MPLY:(LOCAL:0))
		RETURN 0
	SIF LICKING_BODY(MPLY:(LOCAL:0), MTAR) || LICKING_BODY(MTAR:0, MPLY:(LOCAL:0))
		RETURN 0
NEXT


RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM15
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0


;●全プレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	DOWNBASE:(MPLY:(LOCAL:0)):体力 += 120

	EXP:(MPLY:(LOCAL:0)):性技経験値 += MAX(MTAR_NUM / 2 + 1, 1)


	SOURCE:(MPLY:(LOCAL:0)):露出 = 50
	SOURCE:(MPLY:(LOCAL:0)):奉仕 = SERVE_HOUSHI(MPLY:(LOCAL:0), 300)
	SOURCE:(MPLY:(LOCAL:0)):接触 = 150
	SOURCE:(MPLY:(LOCAL:0)):逸脱 = 50
	SOURCE:(MPLY:(LOCAL:0)):性行動 = 240

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(MPLY:(LOCAL:0), 160, 90)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(MPLY:(LOCAL:0)):4 = 1

	SOURCE:(MTAR:0):快Ｐ += SENSE_HOUSHI_P(MPLY:(LOCAL:0), MTAR:0, 1350 + (TALENT:(MPLY:(LOCAL:0)):美尻 || GET_HIPSIZE(MPLY:(LOCAL:0)) >= 1) * 200)
NEXT


;●ターゲットについて判定
DOWNBASE:(MTAR:0):体力 += 60

SOURCE:(MTAR:0):接触 = 100
SOURCE:(MTAR:0):逸脱 = 50
SOURCE:(MTAR:0):性行動 = 180


;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 130, 100)

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 1

;レズ・ＢＬ経験基準値
TFLAG:51 = 5

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP15
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP15(ARG:0)
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0




;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):LOCAL
	;●プレイヤーについて処理
	DOWNBASE:(LOCAL:2):体力 += 20

	EXP:(LOCAL:2):性技経験値 += MAX(MEQUIP_TARGET_NUM:(ARG:0) / 2 + 1, 1)

	SOURCE:(LOCAL:2):露出 += 25
	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 100)
	SOURCE:(LOCAL:2):接触 += 75
	SOURCE:(LOCAL:2):逸脱 += 25
	SOURCE:(LOCAL:2):性行動 += 80

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1
	SOURCE:(LOCAL:3):快Ｐ += SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 450 + (TALENT:(LOCAL:2):美尻 || GET_HIPSIZE(LOCAL:2) >= 1) * 100)

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 += 1

NEXT

;●ターゲットについて処理
DOWNBASE:(LOCAL:3):体力 += 10

SOURCE:(LOCAL:3):接触 += 50
SOURCE:(LOCAL:3):逸脱 += 25
SOURCE:(LOCAL:3):性行動 += 60

;倒錯度変化基準値
TCVAR:(LOCAL:3):50 += 1

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE15(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%に尻コキ中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP15(ARG:0)
PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%が%EQUIP_PLAYER_ANAME(ARG:0)%のお尻でペニスを扱き上げている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP15(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER15(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 85

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"精愛Lv{ABL:(ARG:0):精愛}", ABL:(ARG:0):精愛 * 1)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF
IF TALENT:(ARG:0):男嫌い
	CALL COM_ORDER_ELEMENT(ARG:0, "男嫌い", -7)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE15
SIF (MPLY:0 != MASTER || CONFIG:441) && !TALENT:(MPLY:0):恥薄い && ABL:(MPLY:0):露出 <= 1 && ABL:(MPLY:0):主導度Ｕ < 500 && !GETBIT(TALENT:(MPLY:0):淫乱系, 素質_淫乱_サド)
	LOCALS:0 = 少し恥ずかしそうにしながらも、

IF TALENT:(MPLY:0):美尻
	LOCALS:1 = 美しく形の整った
ELSEIF GET_HIPSIZE(MPLY:0) >= 1
	LOCALS:1 = むっちりと肉の乗った
ENDIF



IF MPLY_NUM == 2
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は%LOCALS:0%%ANAME(MTAR:0)%のペニスを%STR_BODY("尻：尻肉：長", MPLY:0)%で押し潰すように擦り上げて責め立て%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%の%STR_BODY("尻：長", MPLY:0)%をペニスに押し付けさせると、尻肉の間で竿を擦らせて奉仕させた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%に、%ANAME(MTAR:0)%のペニスを尻の間でシゴくように命令した
			PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は%LOCALS:0%%ANAME(MTAR:0)%のペニスを%STR_BODY("尻：尻肉：長", MPLY:0)%で押し潰すようにして擦り上げた…
	ENDSELECT
ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%%ANAME(MTAR:0)%のペニスを%STR_BODY("尻：尻肉：長", MPLY:0)%で押し潰すように擦り上げて責め立て%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の%STR_BODY("尻：長", MPLY:0)%をペニスに押し付けさせると、尻肉の間で竿を擦らせて奉仕させた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%のペニスを尻の間でシゴくように命令した
			PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%%ANAME(MTAR:0)%のペニスを%STR_BODY("尻：尻肉：長", MPLY:0)%で押し潰すようにして擦り上げた…
	ENDSELECT
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN15
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_15()
RETURN POWER(2, 性的嗜好_奉仕したい)

@COM_PREFERENCE_TARGET_15()
RETURN POWER(2, 性的嗜好_奉仕されたい)

@COM_STACK_SPERM_MTAR_TO_MPLY_15()
RETURN 射精部位_尻
