;クンニ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME2
LOCAL:0 = IS_RIDE(MPLY:0, MTAR:0)

;男性プレイヤーがターゲットに顔面騎乗中
IF LOCAL:0 && !HAS_VAGINA(MPLY:0)
	LOCALS:0 = 顔騎クンニ
;非男性プレイヤーがターゲットに顔面騎乗中 or ターゲットからプレイヤーにフェラ・クンニ中
ELSEIF LOCAL:0 || SEARCH_EQUIP(11, MTAR:0, MPLY:0) >= 0 || SEARCH_EQUIP(2, MTAR:0, MPLY:0) >= 0
	IF HAS_PENIS(MTAR:0) && HAS_VAGINA(MTAR:0)
		LOCALS:0 = シックスナイン(Ｃ)
	ELSE
		LOCALS:0 = シックスナイン
	ENDIF
ELSE
	LOCALS:0 = クンニ
ENDIF

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE2
;共通部分
CALL COM_ABLE_COMMON(2)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;対象にＶが必要
SIF !HAS_VAGINA(MTAR:0)
	RETURN 0
SIF !CAN_LICK_GROIN(MPLY:0, MTAR:0)
	RETURN 0
;target is anal facesitting player
SIF SEARCH_EQUIP(102, MTAR:0, MPLY:0) >= 0
	RETURN 0
;Although this is technically a Ｃ action, players won't understand if cunnilingus is blocked by the clit cap or similar.
;SIF IS_EQUIP_TARGET(MTAR:0, 63)
;	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM2
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0



;顔面騎乗中は強制的に非継続にする
IF IS_RIDDEN(MPLY:0)
	TFLAG:1 = 1
ENDIF

;●プレイヤーについて処理
DOWNBASE:(MPLY:0):体力 += 120

EXP:(MPLY:0):性技経験値 += 1

SOURCE:(MPLY:0):奉仕 = SERVE_HOUSHI(MPLY:0, 300)
SOURCE:(MPLY:0):接触 = 50
SOURCE:(MPLY:0):性行動 = 300

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 100)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;膣口へのキス
CALL KISS_COMMON(MPLY:0, @"%ANAME(MTAR:0)%の秘貝", GET_SITUATION_BY_TRAIN_MODE())

;●ターゲットについて処理
DOWNBASE:(MTAR:0):体力 += 60

SOURCE:(MTAR:0):液体追加 = 300
SOURCE:(MTAR:0):露出 = 150
SOURCE:(MTAR:0):逸脱 = 50
SOURCE:(MTAR:0):接触 = 50
SOURCE:(MTAR:0):快Ｃ = SENSE_HOUSHI(MPLY:0, MTAR:0, 1500)
SOURCE:(MTAR:0):性行動 = 300

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 150, 0)

IF TALENT:(MPLY:0):舌使い
	TIMES SOURCE:(MTAR:0):快Ｃ, 1.50
ENDIF

;射精箇所と対象をセット
CALL STACK_SPERM(MTAR:0, MPLY:0, 8)

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 3

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP2
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP2(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;●プレイヤーの処理
DOWNBASE:(LOCAL:2):体力 += 20

EXP:(LOCAL:2):性技経験値 += 1

SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 100)
SOURCE:(LOCAL:2):接触 += 25
SOURCE:(LOCAL:2):性行動 += 100

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

;●ターゲットの処理
DOWNBASE:(LOCAL:3):体力 += 10

SOURCE:(LOCAL:3):液体追加 += 150
SOURCE:(LOCAL:3):露出 += 75
SOURCE:(LOCAL:3):逸脱 += 25
SOURCE:(LOCAL:3):接触 += 25
SOURCE:(LOCAL:3):性行動 += 100

IF TALENT:(LOCAL:2):舌使い
	SOURCE:(LOCAL:3):快Ｃ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 1125)
ELSE
	SOURCE:(LOCAL:3):快Ｃ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 750)
ENDIF

;射精箇所と対象をセット
CALL STACK_SPERM(LOCAL:3, LOCAL:2, 8, 0)

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE2(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%にクンニ中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP2(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%の秘唇を舌で舐めている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP2(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER2(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 75

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -1)
ENDIF
IF TALENT:(ARG:0):汚臭鈍感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭鈍感", 1)
ENDIF
IF TALENT:(ARG:0):汚臭敏感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭敏感", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF
IF TALENT:(ARG:0):女嫌い
	CALL COM_ORDER_ELEMENT(ARG:0, "女嫌い", -7)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE2
LOCAL:5 = SEARCH_EQUIP(101, MPLY:0, MTAR:0)
LOCAL:6 = SEARCH_EQUIP(102, MPLY:0, MTAR:0)
LOCAL:7 = SEARCH_EQUIP(11, MTAR:0, MPLY:0)
LOCAL:8 = SEARCH_EQUIP(2, MTAR:0, MPLY:0)
LOCAL:9 = SEARCH_EQUIP(101, MTAR:0, MPLY:0)
LOCALS:1 = 
IF HAS_PENIS(MTAR:0)
	LOCALS:1 = 竿の付け根にある
ELSEIF PALAM:(MTAR:0):潤滑 >= PALAMLV:5
	LOCALS:1 = 濡れそぼった
ELSEIF TALENT:(MTAR:0):体格 == 体格_子供
	LOCALS:1 = 幼い
ENDIF

IF IS_PIERCED(MTAR:0, ピアス_ラビア)
	LOCALS:2 = ピアスの取り付けられた
ELSE
	LOCALS:2 = 
ENDIF

SELECTCASE GET_INMOU_LEVEL(MTAR:0)
	CASE 1, 2
		LOCALS:3 = 陰毛の生い茂る
	CASEELSE
		LOCALS:3 = 
ENDSELECT

IF CUM_GET_AMOUNT:(MTAR:0):射精部位_膣内 > 0
	LOCALS:4 = 白濁をこぼしている
ELSE
	LOCALS:4 = 
ENDIF


;プレイヤーがターゲットに顔面騎乗中かつ男の場合
IF LOCAL:5 >= 0 && !HAS_VAGINA(MPLY:0)
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に自身の玉を舐めさせながら、%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:3%%LOCALS:1%%LOCALS:4%ワレメを舐めほぐし%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の玉を舐めながら、%ANAME(MPLY:0)%に自身の%LOCALS:2%%LOCALS:3%%LOCALS:1%%LOCALS:4%ワレメを舐めさせた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に%ANAME(MTAR:0)%の%LOCALS:2%秘唇を舐めるように命令した
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に自身の玉を舐めさせながら、%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:3%%LOCALS:1%%LOCALS:4%ワレメを舐めほぐした…
	ENDSELECT
	;顔面騎乗の地の文をカット
	IF LOCAL:5 >= 0
		MEQUIP_CUT:(LOCAL:5) = 1
	ENDIF

;非男性プレイヤーがターゲットに顔面騎乗中 or ターゲットからプレイヤーにフェラ・クンニ中
ELSEIF LOCAL:5 >= 0 || LOCAL:6 >= 0 || LOCAL:7 >= 0 || LOCAL:8 >= 0
	IF LOCAL:6 >= 0
		LOCALS:0 = アナル
	ELSEIF LOCAL:7 >= 0
		LOCALS:0 = ペニス
	ELSE
		LOCALS:0 = 秘唇
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に自身の%LOCALS:0%を舐めさせながら、%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:3%%LOCALS:1%%LOCALS:4%ワレメを舐めほぐし%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の%LOCALS:0%を舐めながら、%ANAME(MPLY:0)%に自身の%LOCALS:2%%LOCALS:3%%LOCALS:1%%LOCALS:4%ワレメを舐めさせた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%と%ANAME(MTAR:0)%にシックスナインの体勢で絡み合うように命令した
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に自身の%LOCALS:0%を舐めさせながら、%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:3%%LOCALS:1%%LOCALS:4%ワレメを舐めほぐした…
	ENDSELECT
	;顔面騎乗・フェラ・クンニの地の文をカット
	IF LOCAL:5 >= 0
		MEQUIP_CUT:(LOCAL:5) = 1
	ENDIF
	IF LOCAL:6 >= 0
		MEQUIP_CUT:(LOCAL:6) = 1
	ENDIF
	IF LOCAL:7 >= 0
		MEQUIP_CUT:(LOCAL:7) = 1
	ENDIF
	IF LOCAL:8 >= 0
		MEQUIP_CUT:(LOCAL:8) = 1
	ENDIF

;ターゲットからプレイヤーに顔面騎乗中
;ELSEIF LOCAL:8 >= 0
;	SELECTCASE GET_COM_INITIATIVE()
;		;プレイヤーに主導権
;		CASE 0
;			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を顔の上に跨がらせたまま、下から%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:3%%LOCALS:4%ワレメを貪るように舐め上げ%TARGET_STR("た", "てきた")%…
;		;ターゲットに主導権
;		CASE 1
;			PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の顔に跨ったまま、グリグリと腰を押し付けて自身の%LOCALS:2%%LOCALS:3%%LOCALS:4%ワレメを舐めさせた…
;		;第三者に主導権
;		CASEELSE
;			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に%ANAME(MTAR:0)%の秘唇を舐めるように命令した
;			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を顔の上に跨がらせたまま、下から%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:3%%LOCALS:4%ワレメを舐め上げた…
;	ENDSELECT
;	;顔面騎乗の地の文をカットする
;	MEQUIP_CUT:(LOCAL:8) = 1

;プレイヤーが拘束中
ELSEIF IS_BIND(MPLY:0)
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:3%%LOCALS:1%%LOCALS:4%ワレメを顔に押し当てさせると、舌で激しく舐め回し%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%は身動きの取れない%ANAME(MPLY:0)%の口に%LOCALS:2%%LOCALS:3%%LOCALS:1%%LOCALS:4%ワレメを押し付け、舐めさせ奉仕させた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に対して、%ANAME(MPLY:0)%に秘唇を舐めさせるように命令した
			PRINTFORMW %ANAME(MTAR:0)%は身動きの取れない%ANAME(MPLY:0)%の口に%LOCALS:2%%LOCALS:3%%LOCALS:1%%LOCALS:4%ワレメを押し付け、舐めさせた…
	ENDSELECT

ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:3%%LOCALS:1%%LOCALS:4%ワレメを舌で舐め上げ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			IF GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_サド)
				LOCALS:4 = サディステックな笑みを浮かべながら、
			ELSEIF GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_淫乱)
				LOCALS:4 = 恍惚とした表情を浮かべながら、
			ENDIF
			PRINTFORMW %ANAME(MTAR:0)%は%LOCALS:4%%ANAME(MPLY:0)%の口に%LOCALS:2%%LOCALS:3%%LOCALS:1%%LOCALS:4%ワレメを押し付け、舐めさせ奉仕させた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に%ANAME(MTAR:0)%の秘唇を舐めるように命令した
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:3%%LOCALS:1%%LOCALS:4%ワレメを舌で舐め上げた…
	ENDSELECT
ENDIF


IF CUM_CUR_AMOUNT:(MTAR:0):射精部位_膣内
	PRINTFORM %SPLIT_RAND("熱い/粘つく/濃厚な/新鮮な", 1)%精液が

	IF ABL:(MPLY:0):精愛 >= 4
		PRINTFORML %SPLIT_RAND("精液好きの/精愛味覚を得た/精液に飢えた", 1)%%ANAME(MPLY:0)%の口に滴ると、%PRONOUN(MPLY:0)%は悦びの呻き声を漏らした…
	ELSE
		PRINTFORML %ANAME(MPLY:0)%の口に滴るが、%PRONOUN(MPLY:0)%は奉仕を余儀なくされた…
	ENDIF
	;target eats 1/4th + 3 cum, up to 20
	LOCAL = LIMIT(MIN(((CUM_CUR_AMOUNT:(MTAR:0):射精部位_膣内 / 4) + 3), CUM_CUR_AMOUNT:(MTAR:0):射精部位_膣内) , 0, 20)
	CUM_CUR_AMOUNT:(MTAR:0):射精部位_膣内 -= LOCAL
	CUM_CUR_AMOUNT:(MPLY:0):射精部位_口 += LOCAL
	SIF CUM_CUR_AMOUNT:(MTAR:0):射精部位_膣内 == 0
		PRINTFORML %ANAME(MTAR:0)%の%STR_BODY("膣", MPLY:0)%の精液は全て%ANAME(MPLY:0)%が飲み干した
	;Cumlove exp increases by amount of cum
	EXP:(MPLY:0):精愛経験値 += LOCAL
	;Swallowing exp
	SIF ABL:(MPLY:0):精愛 >= 4
		EXP:(MPLY:0):口内射精経験 ++
	WAIT
ENDIF


;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST2

IF MTAR:0 != MASTER || CONFIG:441
	LOCAL:2 = CUP:(MTAR:0):快Ｃ

	IF TCVAR:(MTAR:0):52
		;気絶中
		PRINTFORML %ANAME(MTAR:0)%は秘唇を舐め上げられるたびに、ビクビクと体を震わせている
	ELSEIF LOCAL:2 < 300
		PRINTFORML %ANAME(MTAR:0)%のそこはまだしっとりと閉じたままだ。快楽を感じるにはもう少しかかるかもしれない
	ELSEIF LOCAL:2 < 1000
		PRINTFORML %ANAME(MTAR:0)%はクンニの刺激にじっと耐えているものの、確実に感じてきているようだ
	ELSEIF LOCAL:2 < 3000
		PRINTFORM 敏感な豆を舐められると感じるのか、%ANAME(MTAR:0)%は
		IF TALENT:(MTAR:0):恥じらい
			PRINT 羞恥に頬を赤く染め手で自分の口を抑えて声が出るのをこらえている
		ELSEIF TALENT:(MTAR:0):解放 || TALENT:(MTAR:0):快感に素直
			PRINTL 熱い吐息を吐きながら快楽に身をゆだねている
		ELSEIF TALENT:(MTAR:0):抑圧 || TALENT:(MTAR:0):孤高 || TALENT:(MTAR:0):快感の否定
			PRINTL 快楽を否定するように首を振って耐えている
		ELSE
			PRINTL 秘所から愛液を垂らしつつわずかに小さく吐息を漏らした
		ENDIF
	ELSEIF LOCAL:2 < 6000
		PRINTFORML %ANAME(MTAR:0)%の小さな豆を舌で擦り上げてやると%ANAME(MTAR:0)%は可愛らしい声を漏らしながらがくがくと身体を揺らした
	ELSEIF LOCAL:2 < 10000
		PRINTFORML %ANAME(MTAR:0)%は明らかな快楽の声をあげ、無意識に%ANAME(MPLY:0)%の頭を手で押さえてさらにその部分を押し付けてきた
	ELSE
		IF TALENT:(MTAR:0):Ｃ敏感
			PRINTFORML %ANAME(MTAR:0)%は敏感なクリトリスへの責めに激しく身もだえし、あられも無い声を張り上げ続けた
		ELSE
			PRINTFORML %ANAME(MTAR:0)%はクリトリスへの刺激に激しい喘ぎ声を上げながら悶え続けた
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN2
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_2()
RETURN POWER(2, 性的嗜好_愛撫したい)

@COM_PREFERENCE_TARGET_2()
RETURN POWER(2, 性的嗜好_愛撫されたい)

