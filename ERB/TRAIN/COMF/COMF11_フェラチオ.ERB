;フェラチオ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME11
;挿入中
FOR LOCAL, 0, MTAR_NUM
	IF GROUPMATCH(IS_INSERT_SINGLE(MTAR:LOCAL, "Ｐ"), 1, 3)
		LOCALS:1 = お掃除
		BREAK
	ELSEIF IS_INSERT_SINGLE(MTAR:LOCAL, "Ｐ") == 2
		LOCALS:1 = Ａお掃除
		BREAK
	ELSE
		LOCALS:1 = 
	ENDIF
NEXT

IF MTAR_NUM > 3
	LOCALS:0 = %LOCALS:1%輪フェラ
ELSEIF MTAR_NUM == 3
	LOCALS:0 = %LOCALS:1%三本フェラ
ELSEIF MTAR_NUM == 2
	LOCALS:0 = %LOCALS:1%二本フェラ
ELSEIF MPLY_NUM == 3 && MTAR_NUM == 1
	LOCALS:0 = Ｔ%LOCALS:1%フェラ
ELSEIF MPLY_NUM == 2 && MTAR_NUM == 1
	LOCALS:0 = Ｗ%LOCALS:1%フェラ
ELSE
	;プレイヤーからターゲットに顔面騎乗中 or ターゲットからプレイヤーにクンニ中
	IF IS_RIDE(MPLY:0, MTAR:0) || SEARCH_EQUIP(2, MTAR:0, MPLY:0) >= 0
		LOCALS:0 = シックスナイン
	;ターゲットからプレイヤーにフェラ中
	ELSEIF SEARCH_EQUIP(11, MTAR:0, MPLY:0) >= 0
		LOCALS:0 = 相互フェラ
	ELSE
		LOCALS:0 = %LOCALS:1%フェラ
	ENDIF
ENDIF
LOCAL:1 = 0
FOR LOCAL, 0, MTAR_NUM
	IF HAS_PENIS(MTAR:LOCAL)
		LOCAL:1 = 1
	ENDIF
NEXT
SIF !LOCAL:1
	LOCALS:0 = ペニバン%LOCALS:0%

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE11
;共通部分
CALL COM_ABLE_COMMON(11)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で3人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 3
	RETURN 0
;ターゲットが必要
SIF MTAR_NUM <= 0
	RETURN 0

FOR LOCAL:0, 0, MTAR_NUM
	;penis or strapon
	SIF !HAS_PENIS(MTAR:(LOCAL:0)) && !IS_EQUIP_PLAYER((MTAR:(LOCAL:0)), 50)
		RETURN 0
NEXT

FOR LOCAL:0, 0, MPLY_NUM
	FOR LOCAL:1, 0, MTAR_NUM
		SIF !CAN_LICK_GROIN(MPLY:(LOCAL:0), MTAR:(LOCAL:1))
			RETURN 0
		SIF !P_STACKABLE(MPLY:(LOCAL:0), MTAR:(LOCAL:1), 11)
			RETURN 0
		;If a player is riding a target or getting cunnilingus/bj/rimjob from them already, then it's a 69, and can only be 1-on-1
		SIF (MPLY_NUM > 1 || MTAR_NUM > 1) && (IS_RIDE(MPLY:LOCAL, MTAR:(LOCAL:1)) || SEARCH_EQUIP(2, MTAR:(LOCAL:1), MPLY:LOCAL) >= 0 || SEARCH_EQUIP(11, MTAR:(LOCAL:1), MPLY:LOCAL) >= 0 || SEARCH_EQUIP(8, MTAR:(LOCAL:1), MPLY:LOCAL) >= 0)
			RETURN 0
	NEXT
NEXT

RETURN 1

;-------------------------------------------------
;快Ｍソースの倍率を取得する関数 ARG:0=PLAYERのキャラ番号
;-------------------------------------------------
@COM11_RATE_M(ARG:0)
#FUNCTION
LOCAL:5 = 1000
SELECTCASE ABL:(ARG:0):奉仕
	CASE 0
		TIMES LOCAL:5, 0.00
		;TIMES SOURCE:(LOCAL:2):不潔, 4.00
	CASE 1
		TIMES LOCAL:5, 0.10
		;TIMES SOURCE:(LOCAL:2):不潔, 2.50
	CASE 2
		TIMES LOCAL:5, 0.30
		;TIMES SOURCE:(LOCAL:2):不潔, 1.50
	CASE 3
		TIMES LOCAL:5, 0.80
		;TIMES SOURCE:(LOCAL:2):不潔, 1.00
	CASE 4
		TIMES LOCAL:5, 1.00
		;TIMES SOURCE:(LOCAL:2):不潔, 0.50
	CASEELSE
		LOCAL:5 = LOCAL:5 * (100 + (ABL:(ARG:0):奉仕 - 5) * 3) / 100
		;TIMES SOURCE:(LOCAL:2):不潔, 0.10
ENDSELECT

SELECTCASE ABL:(ARG:0):性技
	CASE 0
		TIMES LOCAL:5, 1.00
	CASE 1
		TIMES LOCAL:5, 1.10
	CASE 2
		TIMES LOCAL:5, 1.20
	CASE 3
		TIMES LOCAL:5, 1.30
	CASE 4
		TIMES LOCAL:5, 1.40
	CASEELSE
		LOCAL:5 = LOCAL:5 * ((ABL:(ARG:0):性技 - 5) * 2 + 150) / 100
ENDSELECT

RETURNF LOCAL:5

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM11
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

TFLAG:16 = 0
;挿入中
FOR LOCAL, 0, MTAR_NUM
	IF IS_INSERT_SINGLE(MTAR:LOCAL, "Ｐ")
		TFLAG:16 = IS_INSERT_SINGLE(MTAR:LOCAL, "Ｐ")
		BREAK
	ENDIF
NEXT


;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 120

	EXP:(LOCAL:2):性技経験値 += MAX(MTAR_NUM / 2 + 1, 1)
	EXP:(LOCAL:2):口淫経験 += 1

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 400)
	SOURCE:(LOCAL:2):接触 = 50
	SOURCE:(LOCAL:2):快Ｍ = 600 * COM11_RATE_M(LOCAL:2) / 1000
	SOURCE:(LOCAL:2):性行動 = 300

	;主導権に応じた優越・受動のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 150, 120)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		LOCAL:4 = SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 1500) * LOCAL:10 / 100
		IF TALENT:(LOCAL:2):舌使い
			TIMES LOCAL:4, 1.50
		ENDIF
		SOURCE:(LOCAL:3):快Ｐ += LOCAL:4

	NEXT
	IF MTAR_NUM == 1
		;ペニスへのキス
		CALL KISS_COMMON(LOCAL:2, @"%ANAME(MTAR:0)%のペニス", GET_SITUATION_BY_TRAIN_MODE())
	ELSE
		CALL KISS_COMMON(LOCAL:2, @"%ANAME(MTAR:0)%たちのペニス", (GET_SITUATION_BY_TRAIN_MODE() == "和姦" ? "乱交" # "輪姦"))
	ENDIF
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	;●ソースの計算
	DOWNBASE:(LOCAL:1):体力 += 60

	SOURCE:(LOCAL:1):接触 = 50
	SOURCE:(LOCAL:1):性行動 = 180

	;主導権に応じた優越・受動のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 150, 70)
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 7

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP11
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP11(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
ENDSELECT

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 20

	EXP:(LOCAL:2):性技経験値 += MAX(MEQUIP_TARGET_NUM:(ARG:0) / 2 + 1, 1)
	EXP:(LOCAL:2):口淫経験 += 1

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 150)
	SOURCE:(LOCAL:2):接触 += 25
	SOURCE:(LOCAL:2):快Ｍ += 300 * COM11_RATE_M(LOCAL:2) / 1000
	SOURCE:(LOCAL:2):性行動 += 100

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		LOCAL:4 = SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 500) * LOCAL:10 / 100
		IF TALENT:(LOCAL:2):舌使い
			TIMES LOCAL:4, 1.50
		ENDIF
		SOURCE:(LOCAL:3):快Ｐ += LOCAL:4

	NEXT
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 10

	SOURCE:(LOCAL:1):接触 += 25
	SOURCE:(LOCAL:1):性行動 += 60
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE11(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%にフェラ中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP11(ARG:0)
;If anyone has a penis, "penis" is used instead of "strapon"
LOCALS:0 = %BAR_NAME(MEQUIP_TARGET:(ARG:0):0)%
FOR LOCAL, 0, MEQUIP_TARGET_NUM:(ARG:0)
	SIF HAS_PENIS(MEQUIP_TARGET:(ARG:0):LOCAL)
		LOCALS:0 = %BAR_NAME(MEQUIP_TARGET:(ARG:0):LOCAL)%
NEXT
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%の%LOCALS:0%を口で咥え込んで舐め回している…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP11(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER11(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 85

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"精愛Lv{ABL:(ARG:0):精愛}", ABL:(ARG:0):精愛 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 3, 30))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -1)
ENDIF
IF TALENT:(ARG:0):汚臭鈍感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭鈍感", 1)
ENDIF
IF TALENT:(ARG:0):汚臭敏感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭敏感", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF
IF TALENT:(ARG:0):男嫌い
	CALL COM_ORDER_ELEMENT(ARG:0, "男嫌い", -12)
ENDIF
IF TALENT:(ARG:0):キス未経験
	IF TALENT:(ARG:0):一線越えない
		CALL COM_ORDER_ELEMENT(ARG:0, "キス未経験", -15)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "キス未経験", -10)
	ENDIF
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE11
LOCAL:5 = SEARCH_EQUIP(101, MPLY:0, MTAR:0)
LOCAL:6 = SEARCH_EQUIP(102, MPLY:0, MTAR:0)
LOCAL:7 = SEARCH_EQUIP(11, MTAR:0, MPLY:0)
LOCAL:8 = SEARCH_EQUIP(2, MTAR:0, MPLY:0)

IF MTAR_NUM == 1
	LOCALS:6 = 
ELSE
	LOCALS:6 = たち
ENDIF

IF TFLAG:16 == 1
	LOCALS:7 = 膣から引き抜かれたばかりの
ELSEIF TFLAG:16 == 2
	LOCALS:7 = アナルから引き抜かれたばかりの
ELSE
	LOCALS:7 = 
ENDIF

IF HAS_PENIS(MTAR:0)
	LOCALS:3 = 竿
	LOCALS:4 = ペニス
	LOCALS:5 = ペニスを口で気持ち良くする
ELSE
	LOCALS:3 = 張り型
	LOCALS:4 = 張り型
	LOCALS:5 = 張り型にフェラする
ENDIF

;Blowjob orgy chaos gets a generic description
IF MPLY_NUM >=2 && MTAR_NUM >=2
	SELECTCASE GET_COM_INITIATIVE()
		;giver (MPLY) has initiative
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%たちは順番に%ANAME(MTAR:0)%たちの%LOCALS:3%を舐め回し、しゃぶっている…
		;taker (MTAR) has initiative
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%たちは%ANAME(MPLY:0)%に命じ、%LOCALS:3%を順番に舐め回し、しゃぶらせている…
		;third person initiative
		CASEELSE
			PRINTFORMW %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に命じ、%ANAME(MTAR:0)%の%LOCALS:3%を順番に舐め回し、しゃぶらせている…
	ENDSELECT


;３人
ELSEIF MPLY_NUM == 3
	LOCAL:0 = RAND:3
	LOCAL:1 = (LOCAL:0 + 1) % 3
	LOCAL:2 = (LOCAL:0 + 2) % 3
	IF IS_PIERCED(MTAR:0, ピアス_ペニス)
		LOCALS:1 = ピアスの取り付けられた
	ELSE
		LOCALS:1 = 
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			IF HAS_PENIS(MTAR:0)
				PRINTFORML %ANAME(MPLY:(LOCAL:1))%と%ANAME(MPLY:(LOCAL:2))%は左右から顔を寄せ、二人で%LOCALS:7%%ANAME(MTAR:0)%%LOCALS:6%の%LOCALS:1%竿に舌を這わせて責め立て%TARGET_STR("た", "てきた")%
				PRINTFORMW さらに%ANAME(MPLY:(LOCAL:0))%が正面から亀頭を舐め回して刺激している…
			ELSE
				PRINTFORML %ANAME(MPLY:(LOCAL:1))%と%ANAME(MPLY:(LOCAL:2))%は左右から顔を寄せ、二人で%LOCALS:7%%ANAME(MTAR:0)%%LOCALS:6%の張り型にいやらしく舌を絡め%TARGET_STR("た", "てきた")%
				PRINTFORMW さらに%ANAME(MPLY:(LOCAL:0))%が正面から張り型の先をチロチロと舌先で舐め回している…
			ENDIF
		;ターゲットに主導権
		CASE 1
			PRINTFORML %ANAME(MPLY:(LOCAL:1))%と%ANAME(MPLY:(LOCAL:2))%は左右から顔を寄せ、二人で%LOCALS:7%%ANAME(MTAR:0)%%LOCALS:6%の%LOCALS:1%%LOCALS:3%に舌を絡めて奉仕した
			PRINTFORMW さらに%ANAME(MPLY:(LOCAL:0))%が正面から%LOCALS:3%の先っぽの部分にチロチロと舌を這わせている…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%の三人に、%ANAME(MTAR:0)%%LOCALS:6%の%LOCALS:5%ように命令した
			IF HAS_PENIS(MTAR:0)
				PRINTFORML %ANAME(MPLY:(LOCAL:1))%と%ANAME(MPLY:(LOCAL:2))%は左右から顔を寄せ、二人で%LOCALS:7%%ANAME(MTAR:0)%%LOCALS:6%の%LOCALS:1%竿に舌を這わせた
				PRINTFORMW さらに%ANAME(MPLY:(LOCAL:0))%が正面から亀頭を舐め回して刺激している…
			ELSE
				PRINTFORML %ANAME(MPLY:(LOCAL:1))%と%ANAME(MPLY:(LOCAL:2))%は左右から顔を寄せ、二人で%LOCALS:7%%ANAME(MTAR:0)%%LOCALS:6%の張り型に舌を這わせた
				PRINTFORMW さらに%ANAME(MPLY:(LOCAL:0))%が正面から張り型の先をチロチロと舌先で舐め回している…
			ENDIF
	ENDSELECT

;２人
ELSEIF MPLY_NUM == 2
	IF IS_PIERCED(MTAR:0, ピアス_ペニス)
		LOCALS:1 = ピアスの取り付けられた
	ELSE
		LOCALS:1 = 
	ENDIF
	IF SEARCH_EQUIP(GETNUM(TRAINNAME, "キス"), MPLY:0, MPLY:1) != -1 || SEARCH_EQUIP(GETNUM(TRAINNAME, "キス"), MPLY:1, MPLY:0) != -1 && GET_COM_INITIATIVE() == 1
		PRINTFORMW %ANAME(MTAR:0)%は熱烈なキスを交わす%ANAME(MPLY:0)%と%ANAME(MPLY:1)%の唇の隙間に%LOCALS:7%%LOCALS:1%%LOCALS:4%をねじ込み、奉仕させた…
	ELSE
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				IF HAS_PENIS(MTAR:0)
					PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は左右から顔を寄せ、二人で%LOCALS:7%%ANAME(MTAR:0)%%LOCALS:6%の%LOCALS:1%%LOCALS:4%に舌を這わせて責め立て%TARGET_STR("た", "てきた")%…
				ELSE
					PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は左右から顔を寄せ、二人で%LOCALS:7%%ANAME(MTAR:0)%%LOCALS:6%の%LOCALS:1%%LOCALS:4%を、見せつけるようにいやらしく舐め回し%TARGET_STR("た", "てきた")%…
				ENDIF
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は左右から顔を寄せ、二人で協力して%LOCALS:7%%ANAME(MTAR:0)%%LOCALS:6%の%LOCALS:1%%LOCALS:4%を舐めしゃぶった…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%に、%ANAME(MTAR:0)%%LOCALS:6%の%LOCALS:5%ように命令した
				IF HAS_PENIS(MTAR:0)
					PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は左右から顔を寄せ、二人で%LOCALS:7%%ANAME(MTAR:0)%%LOCALS:6%の%LOCALS:4%に舌を這わせて責め立てた…
				ELSE
					PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は左右から顔を寄せ、二人で%LOCALS:7%%ANAME(MTAR:0)%%LOCALS:6%の%LOCALS:4%を、見せつけるようにいやらしく舐め回した…
				ENDIF
		ENDSELECT
	ENDIF
;１人(シックスナイン)
ELSEIF LOCAL:5 >= 0 || LOCAL:6 >= 0 || LOCAL:7 >= 0 || LOCAL:8 >= 0
	IF LOCAL:6 >= 0
		LOCALS:0 = アナル
	ELSEIF LOCAL:5 >= 0 && !HAS_VAGINA(MPLY:0)
		LOCALS:0 = 玉
	ELSEIF LOCAL:7 >= 0
		LOCALS:0 = %BAR_NAME(MPLY:0)%
	ELSE
		LOCALS:0 = 秘唇
	ENDIF
	IF IS_PIERCED(MTAR:0, ピアス_ペニス)
		LOCALS:1 = ピアスの取り付けられた
	ELSE
		LOCALS:1 = 
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に自身の%LOCALS:0%を舐めさせながら、%LOCALS:7%%ANAME(MTAR:0)%の%LOCALS:1%%LOCALS:3%を舐め回し%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の%LOCALS:0%を舐めながら、%LOCALS:7%%ANAME(MPLY:0)%に自身の%LOCALS:1%%LOCALS:3%を咥えさせた…
		;第三者に主導権
		CASEELSE
			IF LOCAL:5 >= 0  && !HAS_VAGINA(MPLY:0)
				PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、%ANAME(MTAR:0)%の%LOCALS:1%%LOCALS:5%ように命令した
			ELSEIF LOCAL:7 >= 0
				PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%と%ANAME(MTAR:0)%に、互いの男性器を舐め合うように命令した
			ELSE
				PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%と%ANAME(MTAR:0)%に、シックスナインの体勢で絡み合うように命令した
			ENDIF
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に自身の%LOCALS:0%を舐めさせながら、%LOCALS:7%%ANAME(MTAR:0)%の%LOCALS:1%%LOCALS:3%を舐め回した…
	ENDSELECT
	;顔面騎乗・フェラ・クンニの地の文をカット
	IF LOCAL:5 >= 0
		MEQUIP_CUT:(LOCAL:5) = 1
	ENDIF
	IF LOCAL:6 >= 0
		MEQUIP_CUT:(LOCAL:6) = 1
	ENDIF
	IF LOCAL:7 >= 0
		MEQUIP_CUT:(LOCAL:7) = 1
	ENDIF
	IF LOCAL:8 >= 0
		MEQUIP_CUT:(LOCAL:8) = 1
	ENDIF

;プレイヤーが１人＆拘束中
ELSEIF IS_BIND(MPLY:0)
	IF TALENT:(MPLY:0):体格 == 体格_子供 || TALENT:(MPLY:0):体格 == 体格_小柄
		LOCALS:0 = 小さな口
	ELSE
		LOCALS:0 = 口
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は自身の%LOCALS:0%に%LOCALS:7%%ANAME(MTAR:0)%%LOCALS:6%の%LOCALS:1%%LOCALS:4%を挿れさせ、じゅるじゅると音を立ててしゃぶった…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%%LOCALS:6%は身動きの取れない%ANAME(MPLY:0)%の%LOCALS:0%に無理やり%LOCALS:7%%LOCALS:1%%LOCALS:4%を押し込み舐めさせた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%%LOCALS:6%の%LOCALS:1%%LOCALS:4%を口で気持ち良くするように命令した
			PRINTFORMW %ANAME(MPLY:0)%は差し出された%LOCALS:7%%ANAME(MTAR:0)%%LOCALS:6%の%LOCALS:1%%LOCALS:4%を%LOCALS:0%で咥え込み、じゅるじゅると音を立ててしゃぶった…
	ENDSELECT

	;縄で縛るの地の文をカットする
	LOCAL:0 = SEARCH_EQUIP(85, -1, MPLY:0)
	IF LOCAL:0 >= 0
		MEQUIP_CUT:(LOCAL:0) = 1
	ENDIF
	LOCAL:0 = SEARCH_EQUIP(208, -1, MPLY:0)
	IF LOCAL:0 >= 0
		MEQUIP_CUT:(LOCAL:0) = 1
	ENDIF


;１人
ELSE
	IF TALENT:(MPLY:0):体格 == 体格_子供 || TALENT:(MPLY:0):体格 == 体格_小柄
		LOCALS:0 = 小さな口で必死に
	ELSE
		LOCALS:0 = 口で
	ENDIF
	SELECTCASE MTAR_NUM
		CASE 1
			IF IS_PIERCED(MTAR:0, ピアス_ペニス)
				LOCALS:1 = ピアスの取り付けられた
			ELSE
				LOCALS:1 = 
			ENDIF
			SELECTCASE GET_COM_INITIATIVE()
				;プレイヤーに主導権
				CASE 0
					PRINTFORM %ANAME(MPLY:0)%は%LOCALS:7%%ANAME(MTAR:0)%の%LOCALS:1%%LOCALS:4%を%LOCALS:0%咥え込み、
					IF ABL:(MPLY:0):性技 >= 3
						PRINTFORMW 巧みな舌使いで舐めしゃぶっ%TARGET_STR("た", "てきた")%…
					ELSE
						PRINTFORMW 不慣れな舌使いで責めたて%TARGET_STR("た", "てきた")%…
					ENDIF
				;ターゲットに主導権
				CASE 1
					PRINTFORM %ANAME(MPLY:0)%は%LOCALS:7%%ANAME(MTAR:0)%の%LOCALS:1%%LOCALS:4%を%LOCALS:0%咥え込み、
					IF ABL:(MPLY:0):性技 >= 3
						PRINTFORMW 巧みな舌使いで奉仕している…
					ELSE
						PRINTFORMW 不慣れな舌使いで奉仕している…
					ENDIF
				;第三者に主導権
				CASEELSE
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%の%LOCALS:4%を口で気持ち良くするように命令した
					PRINTFORM %ANAME(MPLY:0)%は%LOCALS:7%%ANAME(MTAR:0)%の%LOCALS:1%%LOCALS:4%を%LOCALS:0%咥え込み、
					IF ABL:(MPLY:0):性技 >= 3
						PRINTFORMW 巧みな舌使いで舐めしゃぶった…
					ELSE
						PRINTFORMW 不慣れな舌使いで舐め回した…
					ENDIF
			ENDSELECT
		;一人が二人の竿を
		CASE 2
			IF IS_PIERCED(MTAR:0, ピアス_ペニス) && IS_PIERCED(MTAR:1, ピアス_ペニス)
				LOCALS:1 = ピアスの取り付けられた
			ELSE
				LOCALS:1 = 
			ENDIF
			SELECTCASE GET_COM_INITIATIVE()
				;プレイヤーに主導権
				CASE 0
					PRINTFORM %ANAME(MPLY:0)%は%LOCALS:7%%ANAME(MTAR:0)%と%ANAME(MTAR:1)%の%LOCALS:1%%LOCALS:4%を%LOCALS:0%咥え込み、
					IF ABL:(MPLY:0):性技 >= 3
						PRINTFORMW 巧みな舌使いで舐めしゃぶっ%TARGET_STR("た", "てきた")%…
					ELSE
						PRINTFORMW 不慣れな舌使いで責めたて%TARGET_STR("た", "てきた")%…
					ENDIF
				;ターゲットに主導権
				CASE 1
					PRINTFORM %ANAME(MPLY:0)%は%LOCALS:7%%ANAME(MTAR:0)%と%ANAME(MTAR:1)%の%LOCALS:1%%LOCALS:4%を%LOCALS:0%咥え込み、
					IF ABL:(MPLY:0):性技 >= 3
						PRINTFORMW 巧みな舌使いで奉仕している…
					ELSE
						PRINTFORMW 不慣れな舌使いで奉仕している…
					ENDIF
				;第三者に主導権
				CASEELSE
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%と%ANAME(MTAR:1)%の%LOCALS:4%を口で気持ち良くするように命令した
					PRINTFORM %ANAME(MPLY:0)%は%LOCALS:7%%ANAME(MTAR:0)%の%LOCALS:1%%LOCALS:4%を%LOCALS:0%咥え込み、
					IF ABL:(MPLY:0):性技 >= 3
						PRINTFORMW 巧みな舌使いで舐めしゃぶった…
					ELSE
						PRINTFORMW 不慣れな舌使いで舐め回した…
					ENDIF
			ENDSELECT
		;一人が三人の竿を
		CASE 3
			IF IS_PIERCED(MTAR:0, ピアス_ペニス) && IS_PIERCED(MTAR:1, ピアス_ペニス) && IS_PIERCED(MTAR:2, ピアス_ペニス)
				LOCALS:1 = ピアスの取り付けられた
			ELSE
				LOCALS:1 = 
			ENDIF
			SELECTCASE GET_COM_INITIATIVE()
				;プレイヤーに主導権
				CASE 0
					PRINTFORM %ANAME(MPLY:0)%は%LOCALS:7%%ANAME(MTAR:0)%と%ANAME(MTAR:1)%と%ANAME(MTAR:2)%の%LOCALS:1%%LOCALS:4%を%LOCALS:0%咥え込み、
					IF ABL:(MPLY:0):性技 >= 3
						PRINTFORMW 巧みな舌使いで舐めしゃぶっ%TARGET_STR("た", "てきた")%…
					ELSE
						PRINTFORMW 不慣れな舌使いで責めたて%TARGET_STR("た", "てきた")%…
					ENDIF
				;ターゲットに主導権
				CASE 1
					PRINTFORM %ANAME(MPLY:0)%は%LOCALS:7%%ANAME(MTAR:0)%と%ANAME(MTAR:1)%と%ANAME(MTAR:2)%の%LOCALS:1%%LOCALS:4%を%LOCALS:0%咥え込み、
					IF ABL:(MPLY:0):性技 >= 3
						PRINTFORMW 巧みな舌使いで奉仕している…
					ELSE
						PRINTFORMW 不慣れな舌使いで奉仕している…
					ENDIF
				;第三者に主導権
				CASEELSE
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%と%ANAME(MTAR:1)%と%ANAME(MTAR:2)%の%LOCALS:4%を口で気持ち良くするように命令した
					PRINTFORM %ANAME(MPLY:0)%は%LOCALS:7%%ANAME(MTAR:0)%の%LOCALS:1%%LOCALS:4%を%LOCALS:0%咥え込み、
					IF ABL:(MPLY:0):性技 >= 3
						PRINTFORMW 巧みな舌使いで舐めしゃぶった…
					ELSE
						PRINTFORMW 不慣れな舌使いで舐め回した…
					ENDIF
			ENDSELECT
		CASEELSE
			IF IS_PIERCED(MTAR:0, ピアス_ペニス) && IS_PIERCED(MTAR:1, ピアス_ペニス) && IS_PIERCED(MTAR:2, ピアス_ペニス)
				LOCALS:1 = ピアスの取り付けられた
			ELSE
				LOCALS:1 = 
			ENDIF
			SELECTCASE GET_COM_INITIATIVE()
				;プレイヤーに主導権
				CASE 0
					PRINTFORM %ANAME(MPLY:0)%は%LOCALS:7%%ANAME(MTAR:0)%たちの%LOCALS:1%%LOCALS:4%を%LOCALS:0%咥え込み、
					IF ABL:(MPLY:0):性技 >= 3
						PRINTFORMW 巧みな舌使いで舐めしゃぶっ%TARGET_STR("た", "てきた")%…
					ELSE
						PRINTFORMW 不慣れな舌使いで責めたて%TARGET_STR("た", "てきた")%…
					ENDIF
				;ターゲットに主導権
				CASE 1
					PRINTFORM %ANAME(MPLY:0)%は%LOCALS:7%%ANAME(MTAR:0)%たちの%LOCALS:1%%LOCALS:4%を%LOCALS:0%咥え込み、
					IF ABL:(MPLY:0):性技 >= 3
						PRINTFORMW 巧みな舌使いで奉仕している…
					ELSE
						PRINTFORMW 不慣れな舌使いで奉仕している…
					ENDIF
				;第三者に主導権
				CASEELSE
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%たちの%LOCALS:4%を口で気持ち良くするように命令した
					PRINTFORM %ANAME(MPLY:0)%は%LOCALS:7%%ANAME(MTAR:0)%たちの%LOCALS:1%%LOCALS:4%を%LOCALS:0%咥え込み、
					IF ABL:(MPLY:0):性技 >= 3
						PRINTFORMW 巧みな舌使いで舐めしゃぶった…
					ELSE
						PRINTFORMW 不慣れな舌使いで舐め回した…
					ENDIF
			ENDSELECT
	ENDSELECT
ENDIF


;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST11
FOR LOCAL:0, 0, MPLY_NUM
	IF (MPLY:LOCAL != MASTER || CONFIG:441) && !IS_ANIMAL(MPLY:LOCAL)
		IF (PALAM:(MPLY:LOCAL):欲情 >= PALAMLV:7 || (PALAM:(MPLY:LOCAL):欲情 >= PALAMLV:5 && TCVAR:(MPLY:LOCAL):60)) && ((ABL:(MPLY:LOCAL):欲望 >= ランク閾値:ランク_その他:ランク_D) || ABL:(MPLY:LOCAL):精愛 >= ランク閾値:ランク_その他:ランク_C)
			SELECTCASE RAND:2
				CASE 0
					SELECTCASE RAND:2
						CASE 0
							PRINTFORM %ANAME(MPLY:LOCAL)%は%SPLIT_RAND("興奮で鼻息を荒くしながら/情欲に突き動かされるまま", 1)%、
						CASE 1
							PRINTFORM %SPLIT_RAND("興奮しきった様子の/発情しきった様子の", 1)%%ANAME(MPLY:LOCAL)%は、
					ENDSELECT
					PRINTFORML %SPLIT_RAND("じゅぼじゅぼと音を立てて/下品な音を立てて/涎を垂らして/激しく頭を前後させながら/夢中で/必死に", 1)%%ANAME(MTAR:0)%のペニス%SPLIT_RAND("にしゃぶり付いている/に吸い付いている", 1)%…
				CASE 1
					PRINTFORM %ANAME(MPLY:LOCAL)%は%SPLIT_RAND("興奮で息を荒げながら/蕩けたような顔をしながら/熱に浮かされたような顔をしながら", 1)%、
					PRINTFORML %SPLIT_RAND("味わうように/形を確かめるように", 1)%%ANAME(MTAR:0)%のペニス%SPLIT_RAND("をしゃぶっている/を頬張っている/へ舌を這わせている/を舐めまわしている", 1)%…
			ENDSELECT
		ENDIF
	ENDIF
NEXT
;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN11
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_11()
RETURN POWER(2, 性的嗜好_奉仕したい)

@COM_PREFERENCE_TARGET_11()
RETURN POWER(2, 性的嗜好_奉仕されたい)

@COM_STACK_SPERM_MTAR_TO_MPLY_11()
RETURN RAND:3 ? 射精部位_口 # 射精部位_顔
