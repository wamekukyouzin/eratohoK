;縄

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME85
LOCALS:0 = 縄で縛

RESULTS:0 = %LOCALS:0%る
RESULTS:1 = %LOCALS:0%らせられる
RESULTS:2 = %LOCALS:0%らせる
RESULTS:3 = %LOCALS:0%られる
RESULTS:4 = %LOCALS:0%らせる
RESULTS:5 = 縄縛り見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE85
;共通部分
CALL COM_ABLE_COMMON(85)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;縄が必要
SIF !ITEM:縄 && !ITEM:A_縄
	RETURN 0
;プレイヤーがＶかＡに挿入されているなら不可
SIF IS_INSERT_SINGLE(MPLY:0, "ＶＡ")
	RETURN 0
;プレイヤーが顔面騎乗されているなら不可
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
;ターゲットが既に拘束中なら不可
SIF IS_BIND(MTAR:0)
	RETURN 0
;プレイヤーが奉仕中なら不可(尻尾系除く)
SIF IS_EQUIP_PLAYER(MPLY:0, 1, 2, 3, 4, 5) || IS_EQUIP_PLAYER(MPLY:0, 10, 11, 12, 13, 14, 15, 16) || IS_EQUIP_PLAYER(MPLY:0, 103, 104, 106, 107)
	RETURN 0
;ターゲットが奉仕中なら不可(尻尾系除く)
SIF IS_EQUIP_PLAYER(MTAR:0, 1, 2, 3, 4, 5) || IS_EQUIP_PLAYER(MTAR:0, 10, 11, 12, 13, 14, 15, 16) || IS_EQUIP_PLAYER(MTAR:0, 103, 104, 106, 107)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM85
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤーについて処理
DOWNBASE:(MPLY:0):体力 += 100
DOWNBASE:(MPLY:0):気力 += 100
EXP:(MPLY:0):嗜虐経験値 += 1

SOURCE:(MPLY:0):嗜虐 = 300
SOURCE:(MPLY:0):逸脱 = 100
SOURCE:(MPLY:0):接触 = 30

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 170, 50)

;●ターゲットについて処理
DOWNBASE:(MTAR:0):体力 += 60
DOWNBASE:(MTAR:0):気力 += 60

EXP:(MTAR:0):緊縛経験 += 1
EXP:(MTAR:0):被虐経験値 += 1

SOURCE:(MTAR:0):苦痛 = 80 + MIN(ABL:(MPLY:0):武闘, 90) / 2
SOURCE:(MTAR:0):露出 = 50
SOURCE:(MTAR:0):逸脱 = 200
SOURCE:(MTAR:0):接触 = 30

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 0, 170)

IF GETBIT(TALENT:(MPLY:0):淫乱系, 素質_淫乱_サド)
	TIMES SOURCE:(MTAR:0):苦痛, 1.50
ENDIF

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 6

;レズ・ＢＬ経験基準値
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP85
;継続コマンドかつフィルタリング不可
RETURN 2

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP85(ARG:0)
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

DOWNBASE:(LOCAL:3):体力 += 10
DOWNBASE:(LOCAL:3):気力 += 10

EXP:(LOCAL:3):緊縛経験 += 1
EXP:(LOCAL:3):被虐経験値 += 1

SOURCE:(LOCAL:3):苦痛 += 30
SOURCE:(LOCAL:3):逸脱 += 150

;倒錯度変化
TCVAR:(LOCAL:3):50 += 4

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE85(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%を縄で拘束中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP85(ARG:0)
PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%は縄で縛られ、身動き出来ないでいる…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP85(ARG:0)
PRINTFORMW %EQUIP_TARGET_ANAME(ARG:0)%の縄を解いた

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER85(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 110

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"サドLv{ABL:(ARG:0):サド}", ABL:(ARG:0):サド * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):大人しい
	CALL COM_ORDER_ELEMENT(ARG:0, "大人しい", -3)
ENDIF
IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ気質
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ気質", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド)
	CALL COM_ORDER_ELEMENT(ARG:0, "サド", 40)
ELSE
	IF ABL:(ARG:0):主導度Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE85
SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を縄で縛っ%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %ANAME(MTAR:0)%は、%ANAME(MPLY:0)%に対して縄で自身を縛るように命令した
		PRINTFORML %ANAME(MPLY:0)%は言われるまま、%ANAME(MTAR:0)%を縄で縛った…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%ANAME(MTAR:0)%を縄で拘束するように命令した
		PRINTFORML %ANAME(MPLY:0)%は言われるまま、%ANAME(MTAR:0)%の体に縄を巻き付け縛り付けた…
ENDSELECT
