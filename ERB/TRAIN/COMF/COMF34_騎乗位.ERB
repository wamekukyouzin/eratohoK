;騎乗位

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME34
IF IS_INSERT_SINGLE(MTAR:0, "Ａ") && IS_INSERT_MUTUAL(MPLY, MTAR:0) != 2
	LOCALS:0 = 二穴騎乗位
ELSE
	LOCALS:0 = 騎乗位
ENDIF

RESULTS:0 = %LOCALS:0%で奉仕させる
RESULTS:1 = %LOCALS:0%で責められる
RESULTS:2 = %LOCALS:0%で跨る
RESULTS:3 = %LOCALS:0%で奉仕させられる
RESULTS:4 = %LOCALS:0%で突き上げさせる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE34
;共通部分
CALL COM_ABLE_COMMON(34)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;対象にＶが必要
SIF !HAS_VAGINA(MTAR:0)
	RETURN 0
SIF IS_V_HOLD(MTAR:0)
	RETURN 0
;assucked except anal doggy or sitting backwards
SIF IS_INSERT_SINGLE(MTAR:0, "Ａ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ａ"), 2, 4)
	RETURN 0
SIF !CAN_FUCK(MPLY:0, MTAR:0, 34)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM34
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;既に挿入中なら現在の体位を取得
TFLAG:17 = 0
IF IS_INSERT_MUTUAL(MPLY:0, MTAR:0) == 1
	SELECTCASE GET_SEX_POSITION(MPLY:0, MTAR:0)
		CASE 1
			TFLAG:17 = 1
		CASE 2
			TFLAG:17 = 1
		CASE 3
			TFLAG:17 = 2
		CASE 4
			TFLAG:17 = 2
		CASE 5
			TFLAG:17 = 3
		CASE 6
			TFLAG:17 = 4
		CASE 7
			TFLAG:17 = 1
		CASE 8
			TFLAG:17 = 1
	ENDSELECT
ENDIF
IF TFLAG:17 == 0
	SELECTCASE PREVCOM
		CASE 30
			TFLAG:17 = 1
		CASE 31
			TFLAG:17 = 1
		CASE 32
			TFLAG:17 = 2
		CASE 33
			TFLAG:17 = 2
		CASE 34
			TFLAG:17 = 3
		CASE 35
			TFLAG:17 = 4
		CASE 37
			TFLAG:17 = 1
		CASE 38
			TFLAG:17 = 1
	ENDSELECT
ENDIF

;挿入関係を一時リセット
CALL CLEAR_INSERT_FLAG(MPLY:0, "Ｐ")

;●プレイヤー側の処理
DOWNBASE:(MPLY:0):体力 += 120

EXP:(MPLY:0):性技経験値 += 1
EXP:(MPLY:0):性交経験値 += 2

SOURCE:(MPLY:0):快Ｐ = SENSE_SEX_TARGET_P(MTAR:0, MPLY:0, 1500)
SOURCE:(MPLY:0):露出 = 200
SOURCE:(MPLY:0):性行動 = 450

IF MTAR:0 == MASTER
	CALL ADD_SOURCE_AIZYOU(MPLY:0, 80)
ELSEIF MPLY:0 != MASTER
	SOURCE:(MPLY:0):露出 += 200
ENDIF

;性交系の共通処理
CALL COM_COMMON_SEX_P(MPLY:0, MTAR:0)

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 180, 150)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●ターゲット側の処理
DOWNBASE:(MTAR:0):体力 += 160

EXP:(MTAR:0):性技経験値 += 1
EXP:(MTAR:0):性交経験値 += 2

SOURCE:(MTAR:0):快Ｖ = SENSE_SEX_PLAYER(MTAR:0, MPLY:0, 1500)
SOURCE:(MTAR:0):性行動 = 450

IF MPLY:0 == MASTER
	CALL ADD_SOURCE_AIZYOU(MTAR:0, 80)
ELSEIF MTAR:0 != MASTER
	SOURCE:(MTAR:0):露出 += 200
ENDIF

;性交系の共通処理
CALL COM_COMMON_SEX_V(MTAR:0, MPLY:0)

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 200, 150)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MTAR:0):4 = 1

;体位フラグのセット
TCVAR:(MPLY:0):31 = 5
TCVAR:(MTAR:0):33 = 5

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP34
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP34(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;ソースを退避
CALL PUTOUT_SOURCE(LOCAL:2)
CALL PUTOUT_SOURCE(LOCAL:3)

DOWNBASE:(LOCAL:2):体力 += 20

EXP:(LOCAL:2):性技経験値 += 1
EXP:(LOCAL:2):性交経験値 += 1

SOURCE:(LOCAL:2):快Ｐ += SENSE_SEX_TARGET_P(LOCAL:3, LOCAL:2, 750)
SOURCE:(LOCAL:2):露出 += 200
SOURCE:(LOCAL:2):性行動 += 150

;性交系の共通処理
CALL COM_COMMON_SEX_P(LOCAL:2, LOCAL:3)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

DOWNBASE:(LOCAL:3):体力 += 20

EXP:(LOCAL:3):性技経験値 += 1
EXP:(LOCAL:3):性交経験値 += 1

SOURCE:(LOCAL:3):快Ｖ += SENSE_SEX_PLAYER(LOCAL:2, LOCAL:3, 750)
SOURCE:(LOCAL:3):露出 += 200
SOURCE:(LOCAL:3):性行動 += 150

;性交系の共通処理
CALL COM_COMMON_SEX_V(LOCAL:3, LOCAL:2)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:3):4 = 1

;退避したソースを加算
CALL SUM_SOURCE(LOCAL:2)
CALL SUM_SOURCE(LOCAL:3)

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE34(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%の膣に騎乗位で挿入中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP34(ARG:0)
LOCAL:0 = MEQUIP_TARGET:(ARG:0):0
SELECTCASE RAND:2
	CASE 0
			;ターゲットが行動不能
		IF !IS_PLAYABLE(LOCAL:0)
			PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が下から%EQUIP_TARGET_ANAME(ARG:0)%の膣を突き上げている…
		ELSE
			PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%が%EQUIP_PLAYER_ANAME(ARG:0)%の上で腰を振っている…
		ENDIF
	CASE 1
			;ターゲットが行動不能
		IF !IS_PLAYABLE(LOCAL:0)
			PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は激しく気絶した%EQUIP_TARGET_ANAME(ARG:0)%の腰を突き上げている…
		ELSE
			SELECTCASE GET_COM_INITIATIVE()
					;プレイヤーに主導権
				CASE 0
					SELECTCASE RAND:2
						CASE 0
							PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は騎乗位の体勢で挿入したままの%EQUIP_TARGET_ANAME(ARG:0)%に腰を振らせている…
						CASE 1
							PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の腰を掴んだまま腰を突き上げ、%EQUIP_TARGET_ANAME(ARG:0)%を責め立てている…
					ENDSELECT
					;ターゲットに主導権
				CASE 1
					PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%は%EQUIP_PLAYER_ANAME(ARG:0)%のソレを締め付けながら、さらに腰を動かして責め立てている…
					;第三者に主導権
				CASEELSE
					PRINTFORML %ANAME(TFLAG:45)%は%EQUIP_PLAYER_ANAME(ARG:0)%に騎乗位で跨る%EQUIP_TARGET_ANAME(ARG:0)%に対して、もっと激しく腰を振るように命令し
					PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%は%EQUIP_PLAYER_ANAME(ARG:0)%のソレを締め付けながら、命じられるままにひたすら腰を振っている…
			ENDSELECT
		ENDIF
ENDSELECT

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP34(ARG:0)
LOCAL:0 = MEQUIP_TARGET:(ARG:0):0
IF IS_BIND(LOCAL:0) || !IS_PLAYABLE(LOCAL:0)
	PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の体を持ち上げて膣から%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を引き抜いた…
ELSE
	PRINTFORMW %EQUIP_TARGET_ANAME(ARG:0)%は腰を上げて%EQUIP_PLAYER_ANAME(ARG:0)%の%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を膣から引き抜いた…
ENDIF

;-------------------------------------------------
;固有の実行判定(ターゲット側)
;-------------------------------------------------
@COM_ORDER_TARGET34(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 120

;膣性交共通
CALL COM_ORDER_VSEX_V(ARG:0)
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE34
IF IS_PIERCED(MTAR:0, ピアス_ラビア)
	LOCALS:0 = ピアスの取り付けられた
ELSE
	LOCALS:0 = 
ENDIF
IF TALENT:(MTAR:0):処女 == 1
	LOCALS:0 = まだ乙女の
ELSEIF PALAM:(MTAR:0):潤滑 >= PALAMLV:7
	LOCALS:0 = 濡れそぼった
ELSE
	LOCALS:0 = 
ENDIF
IF IS_OVULATION(MTAR:0)
	LOCALS:0 = %LOCALS:0%危険日の
ENDIF

IF TALENT:(MTAR:0):体格 == 体格_小柄
	LOCALS:0 = %LOCALS:0%幼いワレメ
ELSE
	LOCALS:0 = %LOCALS:0%秘裂
ENDIF

;ペニスバンド装着中
IF IS_EQUIP_PLAYER((MPLY:0), 50)
	;ターゲットが行動不能
	IF !IS_PLAYABLE(MTAR:0)
		;第三者に主導権
		IF !GROUPMATCH(GET_COM_INITIATIVE(), 0, 1)
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%を騎乗位の体勢で犯すように命令した
		ENDIF
		IF TALENT:(MTAR:0):処女
			PRINTFORML %ANAME(MPLY:0)%はぐったりとした%ANAME(MTAR:0)%の体を腰の上に乗せ、下から%ANAME(MTAR:0)%の%LOCALS:0%へと張り型を突き挿れた…
		ELSE
			PRINTFORML %ANAME(MPLY:0)%はぐったりとした%ANAME(MTAR:0)%の体を腰の上に乗せ、下から張り型で%ANAME(MTAR:0)%の%LOCALS:0%を突き上げた…
		ENDIF

	ELSE
		IF TALENT:(MTAR:0):処女
			SELECTCASE GET_COM_INITIATIVE()
				;プレイヤーに主導権
				CASE 0
					PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に、騎乗位で処女を捧げるように要求した
					PRINTFORML %ANAME(MTAR:0)%は言われるまま%ANAME(MPLY:0)%の腰に跨り、反り立つ張り型を自身の%LOCALS:0%へと挿入した…
				;ターゲットに主導権
				CASE 1
					IF MTAR:0 == MASTER
						PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の上に跨り、反り立つ張り型を自身の%LOCALS:0%へと挿入し%PLAYER_STR("た", "てきた")%…
					ELSE
						PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の上に跨ると、ゆっくりと自身の%LOCALS:0%に張り型を挿入し%PLAYER_STR("た", "てきた")%…
					ENDIF
				;第三者に主導権
				CASEELSE
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に、騎乗位で%ANAME(MPLY:0)%に跨るように命令した
					PRINTFORML %ANAME(MTAR:0)%は言われるまま%ANAME(MPLY:0)%の腰に跨り、反り立つ張り型を自身の%LOCALS:0%へと挿入した…
			ENDSELECT

		;既に挿入済みの場合
		ELSEIF TFLAG:17
			SELECTCASE GET_COM_INITIATIVE()
				;プレイヤーに主導権
				CASE 0
					;前回が正常位・後背位・対面座位・背面座位
					IF GROUPMATCH(TFLAG:17, 1, 2)
						PRINTFORML %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%を上に跨がらせ、騎乗位の体勢で腰を振らせた…
					;前回が騎乗位
					ELSEIF TFLAG:17 == 3
						PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の腰を掴むと、さらに腰を突き上げて%ANAME(MTAR:0)%を責め立て%TARGET_STR("た", "てきた")%…
					;前回が背面騎乗位
					ELSE
						PRINTFORML %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%を正面に向かせ、騎乗位の体勢で腰を振らせた…
					ENDIF
				;ターゲットに主導権
				CASE 1
					;前回が正常位・後背位
					IF TFLAG:17 == 1
						PRINTFORML %ANAME(MTAR:0)%は挿入したまま体を入れ替え、騎乗位の体勢で腰を動かし始めた…
					;前回が対面座位・背面座位
					ELSEIF TFLAG:17 == 2
						PRINTFORML %ANAME(MTAR:0)%は挿入したまま%ANAME(MPLY:0)%を押し倒すと、騎乗位の体勢で腰を動かし始めた…
					;前回が騎乗位
					ELSEIF TFLAG:17 == 3
						PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の張り型を締め付けながら、さらに腰を動かして快楽を貪っている…
					;前回が背面騎乗位
					ELSE
						PRINTFORML %ANAME(MTAR:0)%は挿入したまま正面に向き直ると、今度は騎乗位の体勢で腰を動かし始めた…
					ENDIF
				;第三者に主導権
				CASEELSE
					;前回が正常位・後背位・対面座位・背面座位
					IF GROUPMATCH(TFLAG:17, 1, 2)
						PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に対し、%ANAME(MPLY:0)%の上になって騎乗位で腰を振るように命令した
						PRINTFORMW %ANAME(MTAR:0)%は挿入したまま%ANAME(MPLY:0)%を押し倒すと、%ANAME(MPLY:0)%の上に跨って自ら腰を動かした…
					;前回が騎乗位
					ELSEIF TFLAG:17 == 2
						PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に騎乗位で跨る%ANAME(MTAR:0)%に対して、もっと激しく腰を振るように命令した
						PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の張り型を締め付けながら、命じられるままにひたすら腰を跳ねさせた…
					;前回が背面騎乗位
					ELSEIF TFLAG:17 == 3
						PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に背を向けて腰を振る%ANAME(MTAR:0)%に対し、正面を向いて騎乗位の体勢になるように命令した
						PRINTFORMW %ANAME(MTAR:0)%は挿入したまま体を回して%ANAME(MPLY:0)%の正面に向き直り、再び腰を動かし始めた…
					ENDIF
			ENDSELECT

		ELSE
			SELECTCASE GET_COM_INITIATIVE()
				;プレイヤーに主導権
				CASE 0
					PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に、騎乗位で跨って腰を振るように要求した
					PRINTFORML %ANAME(MTAR:0)%は言われるまま%ANAME(MPLY:0)%の腰に跨り、反り立つ張り型を自身の%LOCALS:0%へと挿入した…
				;ターゲットに主導権
				CASE 1
					IF MTAR:0 != MASTER && (GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_淫乱) || TALENT:(MTAR:0):Ｓ気質)
						PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%を押し倒すと、反り立つ張り型を自身の%LOCALS:0%で咥え込ん%PLAYER_STR("だ", "できた")%…
					ELSE
						PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の上に跨ると、反り立つ張り型を自身の%LOCALS:0%で咥え込ん%PLAYER_STR("だ", "できた")%…
					ENDIF
				;第三者に主導権
				CASEELSE
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に、騎乗位で%ANAME(MPLY:0)%に跨るように命令した
					PRINTFORML %ANAME(MTAR:0)%は言われるまま%ANAME(MPLY:0)%の腰に跨り、反り立つ張り型を自身の%LOCALS:0%へと挿入した…
			ENDSELECT

			IF ABL:(MTAR:0):性技 >= 3 && MTAR:0 != MASTER
				PRINTFORM %ANAME(MTAR:0)%は
				IF TALENT:(MTAR:0):体格 == 体格_小柄 || TALENT:(MTAR:0):体格 == 体格_子供
					PRINT 小さな身体で懸命に
				ELSEIF GET_BUSTSIZE(MTAR:0) > 0
					PRINTFORM その%STR_BODY("胸：長", MTAR:0)%を揺らしながら
				ELSE
					PRINT 自ら
				ENDIF
				PRINTL 腰を動かして快楽を貪っている…
			ENDIF
		ENDIF
	ENDIF

;ペニスバンド不使用
ELSE
	;ターゲットが行動不能
	IF !IS_PLAYABLE(MTAR:0)
		;第三者に主導権
		IF !GROUPMATCH(GET_COM_INITIATIVE(), 0, 1)
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%を騎乗位の体勢で犯すように命令した
		ENDIF
		IF TALENT:(MTAR:0):処女
			PRINTFORML %ANAME(MPLY:0)%はぐったりとした%ANAME(MTAR:0)%の体を腰の上に乗せ、下から%ANAME(MTAR:0)%の%LOCALS:0%へとペニスを突き挿れた…
		ELSE
			PRINTFORML %ANAME(MPLY:0)%はぐったりとした%ANAME(MTAR:0)%の体を腰の上に乗せ、下から%ANAME(MTAR:0)%の%LOCALS:0%を激しく突き上げた…
		ENDIF

	ELSE
		IF TALENT:(MTAR:0):処女
			SELECTCASE GET_COM_INITIATIVE()
				;プレイヤーに主導権
				CASE 0
					PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に、騎乗位で処女を捧げるように要求した
					PRINTFORML %ANAME(MTAR:0)%は言われるまま%ANAME(MPLY:0)%の上に跨り、ゆっくりと腰を下ろした…
				;ターゲットに主導権
				CASE 1
					IF MTAR:0 == MASTER
						PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の上に跨り、反り立つペニスを自身の%LOCALS:0%へと挿入し%PLAYER_STR("た", "てきた")%…
					ELSE
						PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の上に跨ると、少し緊張した様子でペニスを自身の%LOCALS:0%へと挿入し%PLAYER_STR("た", "てきた")%…
					ENDIF
				;第三者に主導権
				CASEELSE
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に、騎乗位で%ANAME(MPLY:0)%に跨るように命令した
					PRINTFORML %ANAME(MTAR:0)%は言われるまま%ANAME(MPLY:0)%の腰に跨り、反り立つペニスを自身の%LOCALS:0%へと挿入した…
			ENDSELECT

		;既に挿入済みの場合
		ELSEIF TFLAG:17
			SELECTCASE GET_COM_INITIATIVE()
				;プレイヤーに主導権
				CASE 0
					;前回が正常位・後背位・対面座位・背面座位
					IF GROUPMATCH(TFLAG:17, 1, 2)
						PRINTFORML %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%を上に跨がらせ、騎乗位の体勢で腰を振らせた…
					;前回が騎乗位
					ELSEIF TFLAG:17 == 3
						PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の腰を掴むと、さらに腰を突き上げて%ANAME(MTAR:0)%を責め立て%TARGET_STR("た", "てきた")%…
					;前回が背面騎乗位
					ELSE
						PRINTFORML %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%を正面に向かせ、騎乗位の体勢で腰を振らせた…
					ENDIF
				;ターゲットに主導権
				CASE 1
					;前回が正常位・後背位
					IF TFLAG:17 == 1
						PRINTFORML %ANAME(MTAR:0)%は挿入したまま体を入れ替え、騎乗位の体勢で腰を動かし始めた…
					;前回が対面座位・背面座位
					ELSEIF TFLAG:17 == 2
						PRINTFORML %ANAME(MTAR:0)%は挿入したまま%ANAME(MPLY:0)%を押し倒すと、騎乗位の体勢で腰を動かし始めた…
					;前回が騎乗位
					ELSEIF TFLAG:17 == 3
						PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%のペニスを締め付けながら、さらに腰を動かして責め立て%PLAYER_STR("た", "てきた")%…
					;前回が背面騎乗位
					ELSE
						PRINTFORML %ANAME(MTAR:0)%は挿入したまま正面に向き直ると、今度は騎乗位の体勢で腰を動かし始めた…
					ENDIF
				;第三者に主導権
				CASEELSE
					;前回が正常位・後背位・対面座位・背面座位
					IF GROUPMATCH(TFLAG:17, 1, 2)
						PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に対し、%ANAME(MPLY:0)%の上になって騎乗位で腰を振るように命令した
						PRINTFORMW %ANAME(MTAR:0)%は挿入したまま%ANAME(MPLY:0)%を押し倒すと、%ANAME(MPLY:0)%の上に跨って自ら腰を動かした…
					;前回が騎乗位
					ELSEIF TFLAG:17 == 3
						PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に騎乗位で跨る%ANAME(MTAR:0)%に対して、もっと激しく腰を振るように命令した
						PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%のペニスを締め付けながら、命じられるままにひたすら腰を跳ねさせた…
					;前回が背面騎乗位
					ELSEIF TFLAG:17 == 4
						PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に背を向けて腰を振る%ANAME(MTAR:0)%に対し、正面を向いて騎乗位の体勢になるように命令した
						PRINTFORMW %ANAME(MTAR:0)%は挿入したまま体を回して%ANAME(MPLY:0)%の正面に向き直り、再び腰を動かし始めた…
					ENDIF
			ENDSELECT

		ELSE
			SELECTCASE GET_COM_INITIATIVE()
				;プレイヤーに主導権
				CASE 0
					PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に、騎乗位で跨って腰を振るように要求した
					PRINTFORML %ANAME(MTAR:0)%は言われるまま%ANAME(MPLY:0)%の腰に跨り、反り立つペニスを自身の%LOCALS:0%へと挿入した…
				;ターゲットに主導権
				CASE 1
					IF MTAR:0 != MASTER && (GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_淫乱) || TALENT:(MTAR:0):Ｓ気質)
						PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%を押し倒すと、反り立つペニスを自身の%LOCALS:0%で咥え込ん%PLAYER_STR("だ", "できた")%…
					ELSE
						PRINTFORML %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の上に跨ると、反り立つペニスを自身の%LOCALS:0%で咥え込ん%PLAYER_STR("だ", "できた")%…
					ENDIF
				;第三者に主導権
				CASEELSE
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に、騎乗位で%ANAME(MPLY:0)%に跨るように命令した
					PRINTFORML %ANAME(MTAR:0)%は言われるまま%ANAME(MPLY:0)%の腰に跨り、反り立つペニスを自身の%LOCALS:0%へと挿入した…
			ENDSELECT

			IF ABL:(MTAR:0):性技 >= 3 && MTAR:0 != MASTER
				PRINTFORM %ANAME(MTAR:0)%は
				IF TALENT:(MTAR:0):体格 == 体格_小柄 || TALENT:(MTAR:0):体格 == 体格_子供
					PRINT 小さな身体で懸命に
				ELSEIF GET_BUSTSIZE(MTAR:0) > 0
					PRINTFORM その%STR_BODY("胸：長", MTAR:0)%を揺らしながら
				ELSE
					PRINT 自ら
				ENDIF
				PRINT 腰を動かして
				SELECTCASE GET_COM_INITIATIVE()
					;主導権がプレイヤー側
					CASE 0
						PRINTFORML %ANAME(MPLY:0)%に奉仕している…
					;主導権がターゲット側
					CASE 1
						PRINTFORML %ANAME(MPLY:0)%を責め立てている…
					;その他
					CASEELSE
						PRINTL いる…
				ENDSELECT
			ENDIF
		ENDIF
	ENDIF
ENDIF
WAIT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST34

SIF IS_PIERCED(MPLY:0, ピアス_ペニス)
	PRINTFORML %ANAME(MPLY:0)%のペニスに取り付けられたピアスが、%ANAME(MTAR:0)%の膣内をごりごりと抉っていく……

IF MPLY:0 != MASTER
ENDIF

IF MTAR:0 != MASTER
	LOCAL:2 = CUP:(MTAR:0):快Ｖ

	IF TCVAR:(MTAR:0):52
		;気絶中
		PRINTFORML %ANAME(MTAR:0)%は突き上げられるたびにガクガクと体を震わせ、口端からだらしなく涎を垂らしている
	ELSEIF LOCAL:2 < 300 
		PRINTFORML %ANAME(MTAR:0)%は必死に腰を動かすが、まだ快楽を得るには遠いようで、少し苦しそうだ
	ELSEIF LOCAL:2 < 1000
		PRINTFORML %ANAME(MTAR:0)%は苦しそうにしながらもその表情には快楽の兆しが見える
	ELSEIF LOCAL:2 < 3000
		PRINTFORML %ANAME(MTAR:0)%は腰を振りながらこらえきれない快楽に可愛らしい声を漏らしている
	ELSEIF LOCAL:2 < 6000
		PRINTFORML %ANAME(MTAR:0)%は大分慣れてきたのか、快楽に突き上げられながら自ら進んで腰を振っている
	ELSEIF LOCAL:2 < 10000
		PRINTFORM %ANAME(MTAR:0)%は
		IF TALENT:(MTAR:0):抑圧 || TALENT:(MTAR:0):孤高 || TALENT:(MTAR:0):快感の否定
			PRINTFORML 快楽を否定する言葉を吐きつつ、こらえ切れない喘ぎ声を上げ続けている
		ELSE
			IF TALENT:(MTAR:0):体格 == 体格_小柄
				PRINTFORM その小さな身体で
			ENDIF
			PRINTFORML 一生懸命腰を振り背中を反らしながら快楽の喘ぎ声を上げた
		ENDIF
	ELSE
		PRINTFORML %ANAME(MTAR:0)%は腰を振りながら緩みきった、だらしない顔で快楽を貪っている
	ENDIF
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN34
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_34()
RETURN POWER(2, 性的嗜好_性交したい)

@COM_PREFERENCE_TARGET_34()
RETURN POWER(2, 性的嗜好_性交されたい)

@COM_STACK_SPERM_MPLY_TO_MTAR_34()
RETURN 射精部位_膣内
