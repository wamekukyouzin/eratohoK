;忠誠を誓わせる(逆調教)

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME298
RESULTS:0 = 忠誠を誓わせる

;-------------------------------------------------
;このコマンドはフィルタ不可
;-------------------------------------------------
@COM_FILTER_FORCE_OFF298
RETURN 1

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE298
;共通部分
CALL COM_ABLE_COMMON(298)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーとターゲットのうちいずれかが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0) || !IS_PLAYABLE(MTAR:0)
	RETURN 0
;捕虜逆調教のとき限定
SIF FLAG:調教モード != 5 || !CFLAG:MASTER:捕虜先
	RETURN 0
;主人公がターゲットのとき限定
SIF MTAR:0 != MASTER
	RETURN 0
;プレイヤーの口が塞がっているなら不可
SIF IS_M_HOLD(MPLY:0)
	RETURN 0
;ターゲットの口が塞がっているなら不可
SIF IS_M_HOLD(MTAR:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM298
;説得してからの期間を0にする
FLAG:勧誘経過期間 = 0

LOCAL:5 = GET_COUNTRY_BOSS(CFLAG:(MPLY:0):所属)

;口上表示
CALL KOJO_EVENT(MPLY:0, 450)

CALL ASK_YN("仕官する", "断る")

;仕官する
IF RESULT == 0
	;口上表示
	CALL KOJO_EVENT(MPLY:0, 451)

	;主人公の縄を解く
	CALL RELEASE_EQUIP_EX(85, -1, MASTER)

	PRINTL 
	PRINTFORMW %ANAME(MASTER)%は%ANAME(LOCAL:5)%の士官になりました
	;所属国家の変更
	CALL CHANGE_COUNTRY(MASTER, CFLAG:(MPLY:0):所属, 1)
	PLAYER_HIRED_COUNTER = 10
;断る
ELSE
	;口上表示
	CALL KOJO_EVENT(MPLY:0, 452)
ENDIF

RETURN 1

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN298
RETURN コマンド_ウフフ
