;手淫

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME10

IF MTAR_NUM > 1
	LOCALS:0 = 同時手コキ
ELSEIF MPLY_NUM == 3
	LOCALS:0 = Ｔ手コキ
ELSEIF MPLY_NUM == 2
	LOCALS:0 = Ｗ手コキ
ELSE
	LOCALS:1 = 
	LOCALS:2 = 
	LOCAL:0 = IS_INSERT_MUTUAL(MTAR:0, MPLY:0)
	IF LOCAL:0 == 2
		LOCALS:1 = Ａ
	ENDIF
	IF GROUPMATCH(LOCAL:0, 1, 2)
		SELECTCASE GET_SEX_POSITION(MTAR:0, MPLY:0)
			CASE 1
				LOCALS:2 = 正常位
			CASE 2
				LOCALS:2 = 後背位
			CASE 3
				LOCALS:2 = 対面座位
			CASE 4
				LOCALS:2 = 背面座位
			CASE 5
				LOCALS:2 = 騎乗位
			CASE 6
				LOCALS:2 = 背面騎乗位
		ENDSELECT
	ENDIF
	LOCALS:0 = 手コキ%LOCALS:1%%LOCALS:2%
ENDIF

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE10
;共通部分
CALL COM_ABLE_COMMON(10)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で3人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 3
	RETURN 0
;ターゲットは最大でプレイヤーの二倍まで
SIF MTAR_NUM <= 0 || MTAR_NUM > MPLY_NUM * 2
	RETURN 0

FOR LOCAL:1, 0, MTAR_NUM
	SIF !HAS_PENIS(MTAR:(LOCAL:1))
		RETURN 0
	FOR LOCAL, 0, MPLY_NUM
		SIF !CAN_REACH_GROIN(MPLY:LOCAL, MTAR:(LOCAL:1))
			RETURN 0
		SIF !P_STACKABLE(MPLY:LOCAL, MTAR:(LOCAL:1), 10)
			RETURN 0
	NEXT
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM10
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0


;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 100

	EXP:(LOCAL:2):性技経験値 += MAX(MTAR_NUM / 2 + 1, 1)

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 300)
	SOURCE:(LOCAL:2):接触 = 100
	SOURCE:(LOCAL:2):性行動 = 210

	SELECTCASE ABL:(LOCAL:2):奉仕
		CASE 0
			TIMES SOURCE:(LOCAL:2):不潔, 4.00
		CASE 1
			TIMES SOURCE:(LOCAL:2):不潔, 2.50
		CASE 2
			TIMES SOURCE:(LOCAL:2):不潔, 1.50
		CASE 3
			TIMES SOURCE:(LOCAL:2):不潔, 1.00
		CASE 4
			TIMES SOURCE:(LOCAL:2):不潔, 0.50
		CASEELSE
			TIMES SOURCE:(LOCAL:2):不潔, 0.10
	ENDSELECT

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 150, 80)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｐ += SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 1200) * LOCAL:10 / 100

	NEXT
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 60

	SOURCE:(LOCAL:1):接触 = 50
	SOURCE:(LOCAL:1):性行動 = 180

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 120, 50)
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP10
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP10(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
ENDSELECT

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 20

	EXP:(LOCAL:2):性技経験値 += MAX(MEQUIP_TARGET_NUM:(ARG:0) / 2 + 1, 1)

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 100)
	SOURCE:(LOCAL:2):接触 += 50
	SOURCE:(LOCAL:2):性行動 += 70

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｐ += SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 400) * LOCAL:10 / 100

	NEXT
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 10

	SOURCE:(LOCAL:1):接触 += 25
	SOURCE:(LOCAL:1):性行動 += 60
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE10(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%に手コキ中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP10(ARG:0)
LOCAL:2 = 0
IF MEQUIP_PLAYER_NUM:(ARG:0) == 1 && MEQUIP_TARGET_NUM:(ARG:0) == 1
	IF GROUPMATCH(IS_INSERT_MUTUAL(MEQUIP_PLAYER:(ARG:0):0, MEQUIP_TARGET:(ARG:0):0), 3, 4)
		LOCAL:2 = 1
	ELSEIF GROUPMATCH(SELECTCOM, 30, 31, 32, 33, 34, 35, 40, 41, 42, 43, 44, 45) && MTAR:0 == MEQUIP_PLAYER:(ARG:0):0 && MPLY:0 == MEQUIP_TARGET:(ARG:0):0
		LOCAL:2 = 1
	ENDIF
ENDIF
IF LOCAL:2
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は挿入された%EQUIP_TARGET_ANAME(ARG:0)%のペニスの根本を指でシゴき上げている…
ELSE
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%のペニスを手で握りシゴき上げている…
ENDIF

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP10(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER10(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 70

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"精愛Lv{ABL:(ARG:0):精愛}", ABL:(ARG:0):精愛 * 1)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -1)
ENDIF
IF TALENT:(ARG:0):汚臭鈍感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭鈍感", 1)
ENDIF
IF TALENT:(ARG:0):汚臭敏感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭敏感", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF
IF TALENT:(ARG:0):男嫌い
	CALL COM_ORDER_ELEMENT(ARG:0, "男嫌い", -7)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE10
;ターゲットが複数
IF MTAR_NUM > 1
	LOCALS:0 = 
	IF GETBIT(TALENT:(MPLY:0):淫乱系, 素質_淫乱_淫乱) && (MPLY:0 != MASTER || CONFIG:441)
		LOCALS:0 = うっとりとした表情を浮かべながら、
	ENDIF
	IF IS_PIERCED(MTAR:0, ピアス_ペニス) && IS_PIERCED(MTAR:1, ピアス_ペニス)
		LOCALS:1 = ピアスの取り付けられた
	ELSE
		LOCALS:1 = 
	ENDIF
	IF MPLY_NUM > 1
		LOCALS:2 = たち
	ELSE
		LOCALS:2 = 
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%%LOCALS:2%は%LOCALS:0%左右の手で%ANAME(MTAR:0)%たちの%LOCALS:1%ペニスを激しくシゴ%TARGET_STR("きたてた", "いてきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MPLY:0)%%LOCALS:2%は%LOCALS:0%左右の手で%ANAME(MTAR:0)%たちの%LOCALS:1%ペニスを擦りあげて奉仕している…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%たちの%LOCALS:1%ペニスに手で奉仕するように命令した
			PRINTFORMW %ANAME(MPLY:0)%%LOCALS:2%は%LOCALS:0%左右の手で%ANAME(MTAR:0)%たちの竿をシゴき上げた…
	ENDSELECT

;プレイヤーが３人
ELSEIF MPLY_NUM == 3
	IF IS_PIERCED(MTAR:0, ピアス_ペニス)
		LOCALS:1 = ピアスの取り付けられた
	ELSE
		LOCALS:1 = 
	ENDIF
	IF MTAR_NUM > 1
		LOCALS:2 = たち
	ELSE
		LOCALS:2 = 
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%の三人は、一斉に%ANAME(MTAR:0)%%LOCALS:2%の%LOCALS:1%竿や玉に指を絡めて責め立て%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%の三人は、協力しながら%ANAME(MTAR:0)%%LOCALS:2%の%LOCALS:1%竿や玉に指を絡めて奉仕している…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%の三人に、%ANAME(MTAR:0)%%LOCALS:2%の%LOCALS:1%ペニスを手でシゴくように命令した
			PRINTFORMW %ANAME(MPLY:0)%たちは、全員で%ANAME(MTAR:0)%%LOCALS:2%の竿や玉に指を絡めて責め立てた…
	ENDSELECT

;プレイヤーが２人
ELSEIF MPLY_NUM == 2
	IF IS_PIERCED(MTAR:0, ピアス_ペニス)
		LOCALS:1 = ピアスの取り付けられた
	ELSE
		LOCALS:1 = 
	ENDIF
	IF MTAR_NUM > 1
		LOCALS:2 = たち
	ELSE
		LOCALS:2 = 
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は二人で分担しながら、それぞれの指で%ANAME(MTAR:0)%%LOCALS:2%の%LOCALS:1%竿や玉を弄り回し責め立て%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は二人で分担しながら、それぞれの指で%ANAME(MTAR:0)%%LOCALS:2%の%LOCALS:1%竿や玉に奉仕している…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%に、%ANAME(MTAR:0)%%LOCALS:2%の%LOCALS:1%ペニスを手でシゴくように命令した
			PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は二人で分担しながら、それぞれの指で%ANAME(MTAR:0)%%LOCALS:2%の竿や玉を弄り回し責め立てた…
	ENDSELECT

;プレイヤーが１人(触手オナホ中)
ELSEIF IS_EQUIP_TARGET(MTAR:0, 205)
	IF IS_PIERCED(MTAR:0, ピアス_ペニス)
		LOCALS:1 = ピアスの取り付けられた
	ELSE
		LOCALS:1 = 
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%LOCALS:1%ペニスを咥え込んだ触手を手で掴むと、
			PRINTFORMW それをグポグポと激しく前後させて竿をシゴき上げ%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%LOCALS:1%ペニスを咥え込んだ触手を手で掴むと、それをグポグポと前後させて竿への奉仕を行った…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%の%LOCALS:1%ペニスに手で奉仕するように命令した
			PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%のペニスを咥え込んだ触手を手で掴むと、
			PRINTFORMW それをグポグポと激しく前後させて竿をシゴき上げた…
	ENDSELECT

;プレイヤーが１人
ELSE
	LOCALS:0 = 
	IF GETBIT(TALENT:(MPLY:0):淫乱系, 素質_淫乱_淫乱) && MPLY:0 != MASTER
		LOCALS:0 = うっとりとした表情を浮かべながら、
	ENDIF
	IF IS_PIERCED(MTAR:0, ピアス_ペニス)
		LOCALS:1 = ピアスの取り付けられた
	ELSE
		LOCALS:1 = 
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%%ANAME(MTAR:0)%の%LOCALS:1%ペニスを激しくシゴきたて責め立て%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%%ANAME(MTAR:0)%の%LOCALS:1%ペニスを擦りあげて奉仕している…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%の%LOCALS:1%ペニスに手で奉仕するように命令した
			PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%%ANAME(MTAR:0)%の竿を激しくシゴき上げた…
	ENDSELECT
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN10
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_10()
RETURN POWER(2, 性的嗜好_奉仕したい)

@COM_PREFERENCE_TARGET_10()
RETURN POWER(2, 性的嗜好_奉仕されたい)

@COM_STACK_SPERM_MTAR_TO_MPLY_10()
RETURN 射精部位_手
