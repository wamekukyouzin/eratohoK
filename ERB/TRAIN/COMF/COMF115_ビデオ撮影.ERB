;露出プレイ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME115
LOCALS:0 = ビデオ撮影
RESULTS:0 = %LOCALS:0%を行う
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE115
;共通部分
CALL COM_ABLE_COMMON(115)
SIF RESULT == 0
	RETURN 0
;プレイヤーは1人
SIF MPLY_NUM != 1
	RETURN 0

;プレイヤーに主導権が必要
SIF FLAG:主導権所有者 != MPLY:0 && FLAG:主導権所有者 != -1 && !FLAG:RECHECKING
	RETURN 0

;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0) && !FLAG:RECHECKING
	RETURN 0

;既に撮影しているならダメ
SIF TFLAG:70 > 0 && !FLAG:RECHECKING
	RETURN 0

SIF ITEM:ビデオカメラ == 0 || ITEM:ビデオテープ == 0
	RETURN 0

SIF IS_BIND(MPLY:0) && !FLAG:RECHECKING
	RETURN 0

RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM115
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

ITEM:ビデオテープ --

;●全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	SOURCE:(LOCAL:2):嗜虐 = 20
	SOURCE:(LOCAL:2):露出 = 800

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 50, 160)
NEXT

;●プレイヤー以外の全参加者について処理
FOR LOCAL:0, 0, CHARANUM
	SIF !IS_PARTICIPATE_TRAIN(LOCAL) || LOCAL:0 == MPLY:0
		CONTINUE

	DOWNBASE:LOCAL:体力 += 80

	SOURCE:LOCAL:逸脱 = 80
	SOURCE:LOCAL:露出 = 1000
	SOURCE:LOCAL:不安 = 30

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL, 160, 80)
NEXT

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

TFLAG:70 = 1
TFLAG:71 = 0

FOR LOCAL, 0, CHARANUM
	SIF !IS_PARTICIPATE_TRAIN()
		CONTINUE
	TFLAG:71 += SUM_EX(LOCAL)
NEXT

RETURN 1


;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP115
;継続コマンドかつフィルタリング不可
RETURN 2
;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP115(ARG:0)
;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	SOURCE:(LOCAL:2):嗜虐 += 10
	SOURCE:(LOCAL:2):露出 += 500
	SOURCE:(LOCAL:2):逸脱 += 10
	SOURCE:(LOCAL:2):不安 += 15

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 10, 50)

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 += 3
NEXT


;●プレイヤー以外の全参加者について処理
FOR LOCAL:0, 0, CHARANUM
	SIF !IS_PARTICIPATE_TRAIN(LOCAL) || LOCAL:0 == MPLY:0
		CONTINUE
	;帰ってたらダメ
	SIF TCVAR:(LOCAL:0):53
		CONTINUE
	DOWNBASE:LOCAL:体力 += 40

	SOURCE:LOCAL:逸脱 = 10
	SOURCE:LOCAL:露出 = 700
	SOURCE:LOCAL:不安 = 10

	EXP:LOCAL:撮影経験 ++

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL, 80, 20)

	;倒錯度変化基準値
	TCVAR:LOCAL:50 += 3
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE115(ARG:0)
RESULTS = ビデオ撮影中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP115(ARG:0)
SIF MTAR:0 >= 0
	PRINTFORM %ANAME(MTAR:0)%の

PRINTFORML 痴態をカメラが撮影している…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP115(ARG:0)

PRINTFORMW 撮影を終えた
TFLAG:70 = 2

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER115(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 110

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -2)
ENDIF
IF TALENT:(ARG:0):恥薄い
	CALL COM_ORDER_ELEMENT(ARG:0, "恥薄い", 5)
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE115

LOCALS:0 = %ANAME(MPLY:0)%

PRINTFORMW %LOCALS:0%はビデオを回し始めた…

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN115
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_115()
RETURN POWER(2, 性的嗜好_辱めたい)

@COM_PREFERENCE_TARGET_115()
RETURN POWER(2, 性的嗜好_辱められたい) + POWER(2, 性的嗜好_撮影されたい)

