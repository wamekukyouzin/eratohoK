;愛撫

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME0
IF MTAR_NUM >= 2
	LOCALS:0 = 同時愛撫
ELSEIF MPLY_NUM >= 2
	LOCALS:0 = 一斉愛撫
ELSE
	LOCALS:0 = 愛撫
ENDIF

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE0
;共通部分
CALL COM_ABLE_COMMON(0)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で5人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 5
	RETURN 0
;ターゲットは最大で2人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 2
	RETURN 0
;全てのプレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	;行動不能なら不可
	SIF !IS_PLAYABLE(MPLY:(LOCAL:0))
		RETURN 0
	;顔面騎乗されているなら不可
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	;土下座しているなら不可
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 110)
		RETURN 0
	;拘束中なら不可
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
	;プレイヤーが胸愛撫・クンニ・手コキ・フェラ・パイズリ・尻コキ・電気按摩中なら不可
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 1, 2, 10, 11, 12, 15, 103)
		RETURN 0
	;全てのターゲットについて判定
	FOR LOCAL:1, 0, MTAR_NUM
		;ターゲットからプレイヤーに挿入中で体位が後背位・背面座位なら不可
		SIF GROUPMATCH(GET_SEX_POSITION(MTAR:(LOCAL:1), MPLY:(LOCAL:0)), 2, 4)
			RETURN 0
		;プレイヤーがターゲットに足コキ中なら不可
		SIF SEARCH_EQUIP(14, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
		;プレイヤーからターゲット／ターゲットからプレイヤーに足舐め中なら不可
		SIF SEARCH_EQUIP_IC(104, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
	NEXT
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM0
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
	CASE 4
		TIMES LOCAL:10, 0.50
	CASE 5
		TIMES LOCAL:10, 0.45
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 100

	EXP:(LOCAL:2):性技経験値 += MAX(MTAR_NUM / 2 + 1, 1)

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 100)
	SOURCE:(LOCAL:2):接触 = 100
	SOURCE:(LOCAL:2):性行動 = 90

	;主人公がターゲットなら愛情のソース追加
	IF IS_MTAR(MASTER)
		CALL ADD_SOURCE_AIZYOU(LOCAL:2, 50)
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 100, 20)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｃ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 500) * LOCAL:10 / 100
		SOURCE:(LOCAL:3):快Ｂ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 300) * LOCAL:10 / 100

		;射精箇所と対象をセット
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, 射精部位_無駄)
	NEXT
NEXT

;●全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 60

	SOURCE:(LOCAL:2):露出 = 100
	SOURCE:(LOCAL:2):接触 = 100
	SOURCE:(LOCAL:2):性行動 = 180

	;主人公がプレイヤーなら愛情のソース追加
	IF IS_MPLY(MASTER)
		CALL ADD_SOURCE_AIZYOU(LOCAL:2, 100)
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 60, 0)

	;射精箇所と対象をセット
	FOR LOCAL:1, 0, MPLY_NUM
		LOCAL:3 = MPLY:(LOCAL:1)
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, 射精部位_無駄)
	NEXT
NEXT

;1-1のときのみキスを追加
IF MPLY_NUM == 1 && MTAR_NUM == 1
	;ターゲットに口の汚れがあって汚れ無視がない or 口が使用中 or キス未経験ならキスしない
	;IF (!GET_STAIN(LOCAL:1, "口", "Ｐ", "精", "Ａ") || TALENT:(MPLY:0):汚れ無視) && !TALENT:(MPLY:0):キス未経験 && !TALENT:(MTAR:0):キス未経験
	IF !TALENT:(MPLY:0):キス未経験 && !TALENT:(MTAR:0):キス未経験 && !IS_EQUIP_PLAYER(MPLY:0, 2, 11, 12, 20, 104) && !IS_EQUIP_PLAYER(MTAR:0, 2, 11, 12, 20, 104) && !IS_RIDDEN(MPLY:0) && !IS_RIDDEN(MTAR:0)
		TFLAG:17 = 1

		EXP:(MPLY:0):キス経験 += 1
		EXP:(MTAR:0):キス経験 += 1

		SOURCE:(MPLY:0):快Ｍ = SENSE_HOUSHI(MTAR:0, MPLY:0, 100)
		SOURCE:(MTAR:0):快Ｍ = SENSE_HOUSHI(MPLY:0, MTAR:0, 100)

		SOURCE:(MTAR:0):奉仕 = SERVE_HOUSHI(LOCAL:2, 50)

		TIMES SOURCE:(MPLY:0):愛情, 2.00
		TIMES SOURCE:(MTAR:0):愛情, 2.00

		;奴隷の口⇔調教者の口の汚れが移動
		STAIN:(MPLY:0):0 |= STAIN:(MTAR:0):0
		STAIN:(MTAR:0):0 |= STAIN:(MPLY:0):0
	ENDIF
ENDIF

;●全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	;挿入中だと性能低下
	IF IS_INSERT_SINGLE(LOCAL:1, "全")
		TIMES SOURCE:(LOCAL:1):快Ｃ, 0.80
		TIMES SOURCE:(LOCAL:1):快Ｂ, 0.80
		TIMES SOURCE:(LOCAL:1):快Ｍ, 0.80
	ENDIF
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = -1

;レズ・ＢＬ経験基準値
TFLAG:51 = 5

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER0(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 60

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF

;合意
IF !TALENT:(ARG:0):合意
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE0
;1-1のときのみキスを追加
LOCALS:0 = 
IF MPLY_NUM == 1 && MTAR_NUM == 1
	;キスをした場合
	IF TFLAG:17
		LOCALS:0 = キスをしながら
	ENDIF
ENDIF

;ターゲットが2人
IF MTAR_NUM == 2
	LOCALS:1 = %ANAME(MTAR:0)%と%ANAME(MTAR:1)%
;ターゲットが1人
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

;プレイヤーが複数
IF MPLY_NUM >= 3
	LOCALS:2 = %ANAME(MPLY:0)%達
;プレイヤーが2人
ELSEIF MPLY_NUM == 2
	LOCALS:2 = %ANAME(MPLY:0)%と%ANAME(MPLY:1)%
;プレイヤーが1人
ELSE
	LOCALS:2 = %ANAME(MPLY:0)%
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %LOCALS:2%は%LOCALS:0%%LOCALS:1%の肌に優しく指を這わせ撫で回し%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %LOCALS:2%は%LOCALS:1%に言われるまま、%LOCALS:0%%LOCALS:1%の肌に触れてゆっくりと撫で回した…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%LOCALS:2%に%LOCALS:1%の体を愛撫するように命令した
		PRINTFORMW %LOCALS:2%は%LOCALS:0%%LOCALS:1%の肌に優しく指を這わせ撫で回した…
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST0
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	IF LOCAL:1 != MASTER
		LOCAL:2 = CUP:(LOCAL:1):快Ｃ + CUP:(LOCAL:1):快Ｂ + CUP:(LOCAL:1):快Ｍ

		IF TCVAR:(LOCAL:1):52
			;気絶中
			PRINTFORML %ANAME(LOCAL:1)%は意識を失ったままビクビクと体を震わせている
		ELSEIF LOCAL:2 < 100
			IF TALENT:(LOCAL:1):反抗的 && !TALENT:(LOCAL:1):合意
				PRINTFORML %ANAME(LOCAL:1)%は愛撫されながらも反抗的な態度を取っている
			ELSE
				PRINTFORML %ANAME(LOCAL:1)%はむずかゆそうに身体をよじるが快楽を感じるまでには至っていないようだ
			ENDIF
		ELSEIF LOCAL:2 < 300
			PRINTFORML %ANAME(LOCAL:1)%はこちらの愛撫に反応して身体を震わせている
		ELSEIF LOCAL:2 < 1000
			PRINTFORML %ANAME(LOCAL:1)%は軽い電撃でも身体に走ったかのように小さく震えた
		ELSEIF LOCAL:2 < 3000
			PRINTFORML %ANAME(LOCAL:1)%は明らかに快楽を感じている様子で艶やかな喘ぎ声をあげている
		ELSEIF LOCAL:2 < 6000
			PRINTFORML %ANAME(LOCAL:1)%は愛撫に激しく快楽を感じ、自らせがむように手を首にまわしてきた
		ELSE
			PRINTFORML %ANAME(LOCAL:1)%はこちらの愛撫だけで激しく感じまくっており、さらなる刺激を求めてしなだれかかってきた
		ENDIF
	ENDIF
NEXT
