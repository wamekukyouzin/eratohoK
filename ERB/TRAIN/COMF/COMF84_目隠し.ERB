;目隠し

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME84
LOCALS:0 = 目隠し

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE84
;共通部分
CALL COM_ABLE_COMMON(84)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF (MPLY_NUM <= 0 || MPLY_NUM > 1) && !FLAG:RECHECKING
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;目隠しが必要
SIF !ITEM:目隠し && !ITEM:A_目隠し
	RETURN 0
FOR LOCAL, 0, MTAR_NUM
	SIF !CAN_REACH_BODY(MPLY:0, MTAR:LOCAL)
		RETURN 0
NEXT

RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM84
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤーについて処理
SOURCE:(MPLY:0):嗜虐 = 200
SOURCE:(MPLY:0):逸脱 = 80
SOURCE:(MPLY:0):接触 = 20
EXP:(MPLY:0):嗜虐経験値 += 1

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 40)

;●ターゲットについて処理
FOR LOCAL, 0, MTAR_NUM
	EXP:(MTAR:(LOCAL:0)):緊縛経験 += 1
	EXP:(MTAR:(LOCAL:0)):被虐経験値 += 1

	SOURCE:(MTAR:(LOCAL:0)):恐怖 = 100
	SOURCE:(MTAR:(LOCAL:0)):不安 = 100
	SOURCE:(MTAR:(LOCAL:0)):逸脱 = 150
	SOURCE:(MTAR:(LOCAL:0)):接触 = 20
NEXT

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 0, 130)

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 5

;レズ・ＢＬ経験基準値
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP84
;継続コマンドかつフィルタリング不可
RETURN 2

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP84(ARG:0)

FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):LOCAL

	EXP:(LOCAL:3):緊縛経験 += 1
	EXP:(LOCAL:3):被虐経験値 += 1

	SOURCE:(LOCAL:3):恐怖 += 50
	SOURCE:(LOCAL:3):不安 += 50
	SOURCE:(LOCAL:3):逸脱 += 100

	;倒錯度変化
	TCVAR:(LOCAL:3):50 += 3
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE84(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%に目隠し中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP84(ARG:0)
PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%は布で視界を塞がれている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP84(ARG:0)
PRINTFORMW %EQUIP_TARGET_ANAME(ARG:0)%の目隠しを外した

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER84(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 110

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"サドLv{ABL:(ARG:0):サド}", ABL:(ARG:0):サド * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):大人しい
	CALL COM_ORDER_ELEMENT(ARG:0, "大人しい", -3)
ENDIF
IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ気質
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ気質", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド)
	CALL COM_ORDER_ELEMENT(ARG:0, "サド", 40)
ELSE
	IF ABL:(ARG:0):主導度Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE84

IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%たち
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:1%の目の周りに布を巻き付けて視界を奪っ%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %LOCALS:1%は%ANAME(MPLY:0)%に布を渡すと、それで目隠しするように命令した
		PRINTFORML %ANAME(MPLY:0)%は言われるまま、%LOCALS:1%の目の周りに布を巻き付けて目隠しした…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%LOCALS:1%に目隠しするように命令した
		PRINTFORML %ANAME(MPLY:0)%は言われるまま、%LOCALS:1%の目の周りに布を巻き付けて目隠しした…
ENDSELECT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN84
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_84()
RETURN POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_84()
RETURN POWER(2, 性的嗜好_虐げられたい) + POWER(2, 性的嗜好_縛られたい)

