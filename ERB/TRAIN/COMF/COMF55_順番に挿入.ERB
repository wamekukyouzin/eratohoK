;順番に挿入

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME55
LOCALS:0 = 順番に挿入

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE55
;共通部分
CALL COM_ABLE_COMMON(55)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは2人以上
SIF MTAR_NUM < 2
	RETURN 0
;プレイヤーとターゲットのうち両方に行動不能または拘束中のキャラがいるなら不可
IF !IS_PLAYABLE(MPLY:0) || IS_BIND(MPLY:0)
	FOR LOCAL:0, 0, MTAR_NUM
		IF !IS_PLAYABLE(MTAR:(LOCAL:0)) || IS_BIND(MTAR:(LOCAL:0))
			RETURN 0
		ENDIF
	NEXT
ENDIF
;プレイヤーに竿がなく、ペニスバンド装着中でもないと不可
SIF !PENIS(MPLY:0) && !IS_EQUIP_PLAYER((MPLY:0), 50)
	RETURN 0
;プレイヤーが顔面騎乗されているなら不可
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;プレイヤーが顔面騎乗中なら不可
SIF IS_RIDE(MPLY:0)
	RETURN 0
;プレイヤーが土下座しているなら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 110)
	RETURN 0
;プレイヤーが各種奉仕を受けている・各種奉仕を行っているなら不可
SIF IS_EQUIP(MPLY:0, 1, 2, 3, 4, 20, 21, 22) || IS_EQUIP(MPLY:0, 10, 11, 12, 13, 14, 15, 16, 17) || IS_EQUIP(MPLY:0, 87, 88, 90, 103, 104, 106, 107)
	RETURN 0
;プレイヤーが触手オナホされているなら不可
SIF IS_EQUIP_TARGET(MPLY:0, 205)
	RETURN 0
;プレイヤーがセルフオナホ中ならダメ
SIF IS_EQUIP_PLAYER(MPLY:0, 116)
	RETURN 0
;全てのターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	;Ｖが必要
	SIF !VAGINA(MTAR:(LOCAL:0))
		RETURN 0
	;Ｖが埋まっているなら不可
	SIF IS_V_HOLD(MTAR:(LOCAL:0))
		RETURN 0
	;プレイヤーとターゲットの挿入関係を取得
	LOCAL:1 = IS_INSERT_MUTUAL(MPLY:0, MTAR:(LOCAL:0))
	;ターゲットからプレイヤーに挿入中なら不可
	SIF LOCAL:1 == 3 || LOCAL:1 == 4
		RETURN 0
	;顔面騎乗中なら不可
	SIF IS_RIDE(MTAR:(LOCAL:0))
		RETURN 0
	;クンニされているなら不可
	SIF IS_EQUIP_TARGET(MTAR:(LOCAL:0), 2)
		RETURN 0
	;貝合わせ中・双頭バイブ使用中なら不可
	SIF IS_EQUIP(MTAR:(LOCAL:0), 21, 22)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM55
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;挿入関係を一時リセット
CALL CLEAR_INSERT_FLAG(MPLY:0, "Ｐ")

;プレイヤーが行動不能または拘束中なら騎乗位挿入にする
LOCAL:6 = !IS_PLAYABLE(MPLY:0) || IS_BIND(MPLY:0)

;●プレイヤー側の処理
EXP:(MPLY:0):性技経験値 += 2
EXP:(MPLY:0):性交経験値 += 3

;プレイヤーの射精先を強制リセット
CALL RESET_STACK_SPERM(MPLY:0)

LOCAL:5 = 0
FOR LOCAL:0, 0, MTAR_NUM
	IF LOCAL:6
		SOURCE:(MPLY:0):快Ｐ = SENSE_SEX_TARGET_P(MTAR:(LOCAL:0), MPLY:0, 1500)
	ELSE
		SOURCE:(MPLY:0):快Ｐ = SENSE_SEX_PLAYER_P(MTAR:(LOCAL:0), MPLY:0, 1500)
	ENDIF

	CALL DOUTEI_COMMON(MPLY:0, @"%ANAME(MTAR:(LOCAL:0))%たちの膣", 喪失_乱交)

	;性交系の共通処理
	CALL COM_COMMON_SEX_P(MPLY:0, MTAR:(LOCAL:0))

	LOCAL:5 += SOURCE:(MPLY:0):快Ｐ
	SOURCE:(MPLY:0):快Ｐ = 0
	CALL STACK_SPERM(MPLY:0, MTAR:(LOCAL:0), 1, 1)
NEXT
SOURCE:(MPLY:0):快Ｐ = LOCAL:5 / MTAR_NUM
SOURCE:(MPLY:0):露出 = 100
SOURCE:(MPLY:0):性行動 = 450

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 200, 100)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●ターゲット側の処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	EXP:(LOCAL:2):性技経験値 += 2
	EXP:(LOCAL:2):性交経験値 += 3

	IF LOCAL:6
		SOURCE:(LOCAL:2):快Ｖ = SENSE_SEX_PLAYER(LOCAL:2, MPLY:0, MAX(1200 - MTAR_NUM * 75, 600))
	ELSE
		SOURCE:(LOCAL:2):快Ｖ = SENSE_SEX_TARGET(LOCAL:2, MPLY:0, MAX(1200 - MTAR_NUM * 75, 600))
	ENDIF
	SOURCE:(LOCAL:2):露出 = 400
	SOURCE:(LOCAL:2):性行動 = 450

	;性交系の共通処理
	CALL COM_COMMON_SEX_V(LOCAL:2, MPLY:0)

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 150, 100)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;ターゲットのＶ⇔プレイヤーのＰの汚れが移動
	;STAIN:(LOCAL:2):3 |= STAIN:(MPLY:0):2
	;STAIN:(MPLY:0):2 |= STAIN:(LOCAL:2):3
NEXT



;体位フラグのセット
;TCVAR:(MPLY:0):31 = 1
;TCVAR:(MTAR:0):33 = 1

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP55(ARG:0)
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;挿入関係を一時リセット

FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)
	DOWNBASE:(LOCAL:1):体力 += 50
	DOWNBASE:(LOCAL:1):気力 += 50

	;プレイヤーが行動不能または拘束中なら騎乗位挿入にする
	LOCAL:6 = !IS_PLAYABLE(LOCAL:1) || IS_BIND(LOCAL:1)

	;●プレイヤー側の処理
	EXP:(LOCAL:1):性技経験値 += 2
	EXP:(LOCAL:1):性交経験値 += 2

	;プレイヤーの射精先を強制リセット
	CALL RESET_STACK_SPERM(LOCAL:1)

	LOCAL:5 = 0
	FOR LOCAL:2, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:2)
		IF LOCAL:6
			SOURCE:(LOCAL:1):快Ｐ = SENSE_SEX_TARGET_P(LOCAL:3, LOCAL:1, 1500)
		ELSE
			SOURCE:(LOCAL:1):快Ｐ = SENSE_SEX_PLAYER_P(LOCAL:3, LOCAL:1, 1500)
		ENDIF

		;性交系の共通処理
		CALL COM_COMMON_SEX_P(LOCAL:1, LOCAL:3)

		LOCAL:5 += SOURCE:(LOCAL:1):快Ｐ
		SOURCE:(LOCAL:1):快Ｐ = 0
		CALL STACK_SPERM(LOCAL:1,LOCAL:3, 1, 1)

	NEXT
	SOURCE:(LOCAL:1):快Ｐ = LOCAL:5 / MEQUIP_PLAYER_NUM:(ARG:0)
	SOURCE:(LOCAL:1):露出 += 100
	SOURCE:(LOCAL:1):性行動 += 450

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 200, 100)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:1):4 = 1
NEXT
;体位フラグのセット
;TCVAR:(MPLY:0):31 = 1
;TCVAR:(MTAR:0):33 = 1

;●ターゲット側の処理
FOR LOCAL:1, 0,  MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 =  MEQUIP_TARGET:(ARG:0):(LOCAL:1)
	LOCAL:3 = MEQUIP_PLAYER:(ARG:0):(RAND:(MEQUIP_PLAYER_NUM:(ARG:0)))

	EXP:(LOCAL:2):性技経験値 += 2
	EXP:(LOCAL:2):性交経験値 += 2
	EXP:(LOCAL:2):輪姦経験 += MEQUIP_PLAYER_NUM:(ARG:0) / 2
	
	IF LOCAL:6
		SOURCE:(LOCAL:2):快Ｖ += SENSE_SEX_PLAYER(LOCAL:2, LOCAL:3, MAX(1200 - MTAR_NUM * 75, 600))
	ELSE
		SOURCE:(LOCAL:2):快Ｖ += SENSE_SEX_TARGET(LOCAL:2, LOCAL:3, MAX(1200 - MTAR_NUM * 75, 600))
	ENDIF
	SOURCE:(LOCAL:2):露出 += 400
	SOURCE:(LOCAL:2):性行動 += 450

	;性交系の共通処理
	CALL COM_COMMON_SEX_V(LOCAL:2, LOCAL:3)

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 150, 100)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;ターゲットのＶ⇔プレイヤーのＰの汚れが移動
	;STAIN:(LOCAL:2):3 |= STAIN:(MPLY:0):2
	;STAIN:(MPLY:0):2 |= STAIN:(LOCAL:2):3
NEXT

RETURN 1


;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP55
RETURN 1

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER55(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 90

;膣性交共通
CALL COM_ORDER_VSEX_P(ARG:0)
RETURN 1

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE55(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%に順番に挿入中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP55(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%を順番に犯している……

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP55(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%から離れた


;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE55
IF MTAR_NUM >= 3
	LOCALS:0 = %ANAME(MTAR:0)%たち
	LOCALS:2 = 横一列に
	LOCALS:3 = 順番に
ELSE
	LOCALS:0 = %ANAME(MTAR:0)%と%ANAME(MTAR:1)%
	LOCALS:2 = 左右に
	LOCALS:3 = 交互に
ENDIF

;プレイヤーが行動不能または拘束中の場合(騎乗位)
IF !IS_PLAYABLE(MPLY:0) || IS_BIND(MPLY:0)
	IF TCVAR:(MPLY:0):52
		LOCALS:1 = 失神して意識のない
	ELSEIF IS_BIND(MPLY:0)
		LOCALS:1 = 拘束されて身動きの取れない
	ELSEIF TCVAR:(MPLY:0):51
		LOCALS:1 = 疲労して動けない
	ELSE
		LOCALS:1 = 
	ENDIF

	;ペニスバンド装着中
	IF IS_EQUIP_PLAYER((MPLY:0), 50)
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORMW %ANAME(MPLY:0)%は張り型の上に%LOCALS:0%を%LOCALS:3%跨らせ、下から腰を突き上げて快感を与えていった…
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %LOCALS:0%は%LOCALS:1%%ANAME(MPLY:0)%の腰に%LOCALS:3%跨り、張り型の感触を楽しんだ…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%は%LOCALS:0%に、%LOCALS:1%%ANAME(MPLY:0)%の張り型に%LOCALS:3%跨るように命令した
				PRINTFORMW %LOCALS:0%は%LOCALS:1%%ANAME(MPLY:0)%の腰に%LOCALS:3%跨り、張り型をそれぞれの膣で咥え込んでいった…
		ENDSELECT

	;ペニスバンド不使用
	ELSE
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORMW %ANAME(MPLY:0)%はペニスの上に%LOCALS:0%を%LOCALS:3%跨らせ、下から腰を突き上げて快感を与えていった…
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %LOCALS:0%は%LOCALS:1%%ANAME(MPLY:0)%の腰に%LOCALS:3%跨り、ペニスの感触を楽しんだ…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%は%LOCALS:0%に、%LOCALS:1%%ANAME(MPLY:0)%の竿の上に%LOCALS:3%跨るように命令した
				PRINTFORMW %LOCALS:0%は%LOCALS:1%%ANAME(MPLY:0)%の腰に%LOCALS:3%跨り、ペニスをそれぞれの膣で咥え込んでいった…
		ENDSELECT
	ENDIF

;プレイヤーが行動可能な場合
ELSE
	;ペニスバンド装着中
	IF IS_EQUIP_PLAYER((MPLY:0), 50)
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%を%LOCALS:2%並べると、%LOCALS:3%張り型で膣内を犯していった…
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %LOCALS:0%は一斉に%ANAME(MPLY:0)%を押し倒すと、%LOCALS:3%上に跨って張り型をそれぞれの膣で咥え込ん%PLAYER_STR("でいった", "できた")%…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%LOCALS:0%を%LOCALS:3%犯すように命令した
				PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:2%並べられた%LOCALS:0%の雌穴に、%LOCALS:3%張り型を挿入していった…
		ENDSELECT

	;ペニスバンド不使用
	ELSE
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%を%LOCALS:2%並べると、%LOCALS:3%ペニスを突き入れていった…
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %LOCALS:0%は一斉に%ANAME(MPLY:0)%を押し倒すと、%LOCALS:3%上に跨りそれぞれの膣でペニスを咥え込ん%PLAYER_STR("でいった", "できた")%…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%LOCALS:0%を%LOCALS:3%犯すように命令した
				PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:2%並べられた%LOCALS:0%の雌穴に、%LOCALS:3%ペニスを挿入していった…
		ENDSELECT
	ENDIF
ENDIF
