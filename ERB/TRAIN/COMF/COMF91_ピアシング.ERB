;ピアシング

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME91
LOCALS:0 = ピアシング

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE91
;共通部分
CALL COM_ABLE_COMMON(91)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;ピアスを持ってなくて、対象が外すべきピアスをつけてないならダメ
SIF ITEM:ピアス <= 0 && TALENT:(MTAR:0):ピアス == 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
SIF !CAN_REACH_BODY(MPLY:0, MTAR:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM91
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%におしゃれさせてやることにした……
PRINTFORML
FOR LOCAL, 0, ピアス_最大ビット + 1
	IF IS_PIERCED(MTAR:0, LOCAL)
		PRINTFORML [{LOCAL:0}] %GET_PIERCE_NAME(LOCAL:0)%のピアスを外す
	ELSEIF ITEM:ピアス > 0
		IF IS_HOLED(MTAR:0, LOCAL)
			SIF CAN_PIERCE(MTAR:0, LOCAL)
				PRINTFORML [{LOCAL:0}] %GET_PIERCE_NAME(LOCAL:0)%にピアスをつける
		ELSE
			SIF CAN_PIERCE(MTAR:0, LOCAL)
				PRINTFORML [{LOCAL:0}] %GET_PIERCE_NAME(LOCAL:0)%にピアス穴を開け、ピアスをつける
		ENDIF
	ELSE
		SETCOLOR カラー_選択不可
		PRINTPLAINFORM [{LOCAL:0}] %GET_PIERCE_NAME(LOCAL:0)%につけるためのピアスがない
		PRINTL
		RESETCOLOR
	ENDIF
NEXT
PRINTFORML [99] 戻る
$INPUT_LOOP
INPUT
IF RESULT == 99
	PRINTFORML やはりやめておいた
	RETURN 0
ELSEIF IS_PIERCED(MTAR:0, RESULT)
	CALL REMOVE_PIERCE(MTAR:0, RESULT)
	;●プレイヤーについて判定
	DOWNBASE:(MPLY:0):体力 += 70
	SOURCE:(MPLY:0):嗜虐 = 70
	SOURCE:(MPLY:0):逸脱 = 50
	SOURCE:(MPLY:0):接触 = 30
	;主導権に応じた優越・受動のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 100, 30)
	DOWNBASE:(MTAR:0):体力 += 50
	;●ターゲットについて判定
	SOURCE:(MTAR:0):苦痛 = 50
	SOURCE:(MTAR:0):露出 = 50
	SOURCE:(MTAR:0):逸脱 = 50
	SOURCE:(MTAR:0):接触 = 30
	;主導権に応じた優越・受動のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 20, 70)
ELSEIF CAN_PIERCE(MTAR:0, RESULT) && ITEM:ピアス > 0
	ITEM:ピアス --
	CALL SET_PIERCE(MTAR:0, RESULT)
	;●プレイヤーについて判定
	DOWNBASE:(MPLY:0):体力 += 100
	SOURCE:(MPLY:0):嗜虐 = 150
	SOURCE:(MPLY:0):逸脱 = 50
	SOURCE:(MPLY:0):接触 = 30
	;主導権に応じた優越・受動のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 160, 60)
	DOWNBASE:(MTAR:0):体力 += 500
	;●ターゲットについて判定
	SOURCE:(MTAR:0):苦痛 = 500
	SOURCE:(MTAR:0):露出 = 50
	SOURCE:(MTAR:0):逸脱 = 150
	SOURCE:(MTAR:0):接触 = 30
	;主導権に応じた優越・受動のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 50, 150)
ELSE
	GOTO INPUT_LOOP
ENDIF

;主導度変化基準値
TFLAG:49 = 5

;倒錯度変化基準値
TFLAG:50 = 7

;レズ・ＢＬ経験基準値
TFLAG:51 = 7

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER91(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 110

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):大人しい
	CALL COM_ORDER_ELEMENT(ARG:0, "大人しい", -3)
ENDIF
IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ気質
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ気質", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド)
	CALL COM_ORDER_ELEMENT(ARG:0, "サド", 15)
ELSE
	IF ABL:(ARG:0):主導度Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;固有の実行判定(ターゲット側)
;-------------------------------------------------
@COM_ORDER_TARGET91(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 120
;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"マゾLv{ABL:(ARG:0):マゾ}", ABL:(ARG:0):マゾ * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):大人しい
	CALL COM_ORDER_ELEMENT(ARG:0, "大人しい", 3)
ENDIF
IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -5)
ENDIF
IF TALENT:(ARG:0):Ｍ気質
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｍ気質", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_マゾ)
	CALL COM_ORDER_ELEMENT(ARG:0, "マゾ", 15)
ELSE
	IF ABL:(ARG:0):主導度Ｕ <= -500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ <= -300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ <= -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ < 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE91

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN91
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_91()
RETURN POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_91()
RETURN POWER(2, 性的嗜好_虐げられたい)

