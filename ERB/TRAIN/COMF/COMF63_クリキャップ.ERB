;クリキャップ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME63
LOCALS:0 = クリキャップ

RESULTS:0 = %LOCALS:0%で責める
RESULTS:1 = %LOCALS:0%で奉仕する
RESULTS:2 = %LOCALS:0%で奉仕させる
RESULTS:3 = %LOCALS:0%で責められる
RESULTS:4 = %LOCALS:0%で責めさせる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE63
;共通部分
CALL COM_ABLE_COMMON(63)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;クリキャップが必要
SIF !ITEM:クリキャップ && !ITEM:A_クリキャップ
	RETURN 0
FOR LOCAL, 0, MTAR_NUM
	SIF !HAS_VAGINA(MTAR:LOCAL)
		RETURN 0
	;tentacle C rub
	SIF IS_EQUIP_TARGET(MTAR:LOCAL, 203)
		RETURN 0
	SIF !CAN_REACH_GROIN(MPLY:0, MTAR:LOCAL) && !FLAG:RECHECKING
		RETURN 0
NEXT

RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM63
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●プレイヤーについて処理
DOWNBASE:(MPLY:0):体力 += 60

EXP:(MPLY:0):性技経験値 += MTAR_NUM / 2 + 1

SOURCE:(MPLY:0):奉仕 = SERVE_HOUSHI(MPLY:0, 200)
SOURCE:(MPLY:0):接触 = 10
SOURCE:(MPLY:0):性行動 = 90

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 20)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

FOR LOCAL, 0, MTAR_NUM
	LOCAL:2 = MTAR:LOCAL
	;●ターゲットについて処理
	DOWNBASE:(LOCAL:2):体力 += 60

	SOURCE:(LOCAL:2):露出 = 50
	SOURCE:(LOCAL:2):逸脱 = 120
	SOURCE:(LOCAL:2):接触 = 10
	SOURCE:(LOCAL:2):性行動 = 180
	IF TALENT:(MPLY:0):技師
		SOURCE:(LOCAL:2):快Ｃ = SENSE_HOUSHI(MPLY:0, LOCAL:2, 900)
	ELSE
		SOURCE:(LOCAL:2):快Ｃ = SENSE_HOUSHI(MPLY:0, LOCAL:2, 600)
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 50, 20)
NEXT
;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 1

;レズ・ＢＬ経験基準値
TFLAG:51 = 2

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP63
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP63(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
SOURCE:(LOCAL:2):性行動 += 30

FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)
	IF TALENT:(LOCAL:2):技師
		SOURCE:(LOCAL:3):快Ｃ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 600)
	ELSE
		SOURCE:(LOCAL:3):快Ｃ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 400)
	ENDIF
	SOURCE:(LOCAL:3):露出 += 20
	SOURCE:(LOCAL:3):逸脱 += 50
	SOURCE:(LOCAL:3):性行動 += 60
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE63(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%にクリキャップを装着中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP63(ARG:0)
PRINTFORML 振動するキャップが%EQUIP_TARGET_ANAME(ARG:0)%のクリトリスを責め続けている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP63(ARG:0)
PRINTFORMW %EQUIP_TARGET_ANAME(ARG:0)%のクリトリスを責め立てるキャップを外した

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER63(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 65

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE63
IF IS_PIERCED(MTAR:0, ピアス_クリトリス)
	LOCALS:0 = ピアスの取り付けられた
ELSE
	LOCALS:0 = 
ENDIF

IF PALAM:(MTAR:0):潤滑 >= PALAMLV:7
	LOCALS:0 = %LOCALS:0%てらてらと濡れる
ELSE
	LOCALS:0 = %LOCALS:0%
ENDIF

IF TALENT:(MTAR:0):体格 == 体格_小柄
	LOCALS:0 = %LOCALS:0%包皮に包まれた淫豆
ELSE
	LOCALS:0 = %LOCALS:0%ぷくりと膨れた秘豆
ENDIF

IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%たち
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:1%の%LOCALS:0%に震えるキャップを取り付け%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %LOCALS:1%は%ANAME(MPLY:0)%にキャップを握らせ、自身の%LOCALS:0%に取り付けるよう命じた…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、%LOCALS:1%のクリトリスにキャップを付けるように命令した
		PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:1%の%LOCALS:0%に震えるキャップを取り付けた…
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST63

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN63
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_63()
RETURN POWER(2, 性的嗜好_道具を使いたい)

@COM_PREFERENCE_TARGET_63()
RETURN POWER(2, 性的嗜好_道具を使われたい)

