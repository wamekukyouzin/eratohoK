;ピアシング

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME113
LOCALS:0 = 体毛管理

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE113
;共通部分
CALL COM_ABLE_COMMON(113)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
SIF ITEM:剃刀 <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
;プレイヤーとターゲットが挿入されているなら不可
SIF IS_INSERT_SINGLE(MPLY:0, "全") || IS_INSERT_SINGLE(MTAR:0, "全")
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM113
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0


PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の毛を管理することにした……
PRINTFORML
PRINTFORML 陰毛を
FOR LOCAL, 0, 11
	CALL PRINTBUTTON_EXL(@"[{LOCAL:0}] %GET_INMOU_NAME(LOCAL:0)%にさせる", LOCAL:0, 1, TALENT:(MTAR:0):陰毛現在値 == LOCAL:0)
NEXT

PRINTFORML 腋毛を
FOR LOCAL, 100, 110
	CALL PRINTBUTTON_EXL(@"[{LOCAL:0}] %GET_ARMPIT_NAME(LOCAL:0 - 100)%にさせる", LOCAL:0, 1, TALENT:(MTAR:0):腋毛現在値 == LOCAL:0 - 100)
NEXT

PRINTFORML [99] 戻る
$INPUT_LOOP
INPUT
IF RESULT == 99
	PRINTFORML やはりやめておいた
	RETURN 0
ELSEIF (INRANGE_INMOU(RESULT) && TALENT:(MTAR:0):陰毛現在値 != RESULT)
	LOCAL:0 = RESULT
	LOCAL:1 = 0
	IF LOCAL:0 == 10
		LOCAL:1 = 1
		LOCAL:0 = 陰毛_標準
	ENDIF
	PRINTFORML %ANAME(MTAR:0)%に、陰毛を\@ LOCAL:1 == 1 ? ハート型 # %GET_INMOU_NAME(LOCAL:0)% \@にするよう伝えた
	IF GET_INMOU(MTAR:0) > LOCAL:0
		PRINTFORML %ANAME(MTAR:0)%の陰毛は、%ANAME(MPLY:0)%の伝えた理想よりも多めだ……
		PRINTFORML いっそこの場で剃りますか？
		CALL ASK_YN
		IF RESULT == 0
			PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を押さえると、その股座に剃刀を這わせ始めた……
			PRINTFORML 己の陰部が剃り上げられていくのを、%ANAME(MTAR:0)%は顔を赤らめて眺めていた……
			TALENT:(MTAR:0):陰毛現在値 = LOCAL:0
			SIF LOCAL:1
				TALENT:(MTAR:0):陰毛現在値 = 陰毛_ハート型
		ELSE
			PRINTFORML 後で自分で処理させることにした
		ENDIF
	ENDIF
	TALENT:(MTAR:0):陰毛目標値 = LOCAL:0
	SIF LOCAL:1
		TALENT:(MTAR:0):陰毛目標値 = 陰毛_ハート型

	DOWNBASE:(MTAR:0):体力 += 80
	SOURCE:(MTAR:0):逸脱 = 80
	SOURCE:(MTAR:0):露出 = 100
	SOURCE:(MTAR:0):不安 = 30
	;主導度変化基準値
	TFLAG:49 = 5

	;倒錯度変化基準値
	TFLAG:50 = 7

	;レズ・ＢＬ経験基準値
	TFLAG:51 = 7

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 160, 80)
ELSEIF INRANGE_ARMPIT(RESULT - 100) && TALENT:(MTAR:0):腋毛現在値 != RESULT - 100
	LOCAL:0 = RESULT - 100
	LOCAL:1 = 0
	IF LOCAL:0 == 10
		LOCAL:1 = 1
		LOCAL:0 = 腋毛_普通
	ENDIF
	PRINTFORML %ANAME(MTAR:0)%に、腋毛を%GET_ARMPIT_NAME(LOCAL:0)%にするよう伝えた
	IF GET_INMOU(MTAR:0) > LOCAL:0
		PRINTFORML %ANAME(MTAR:0)%の腋毛は、%ANAME(MPLY:0)%の伝えた理想よりも多めだ……
		PRINTFORML いっそこの場で剃りますか？
		CALL ASK_YN
		IF RESULT == 0
			PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を押さえると、その腋に剃刀を這わせ始めた……
			PRINTFORML 己の腋毛が剃り上げられていくのを、%ANAME(MTAR:0)%は顔を赤らめて眺めていた……
			TALENT:(MTAR:0):腋毛現在値 = LOCAL:0
		ELSE
			PRINTFORML 後で自分で処理させることにした
		ENDIF
	ENDIF
	TALENT:(MTAR:0):腋毛目標値 = LOCAL:0

	DOWNBASE:(MTAR:0):体力 += 80
	SOURCE:(MTAR:0):逸脱 = 120
	SOURCE:(MTAR:0):露出 = 100
	SOURCE:(MTAR:0):不安 = 50
	;主導度変化基準値
	TFLAG:49 = 5

	;倒錯度変化基準値
	TFLAG:50 = 7

	;レズ・ＢＬ経験基準値
	TFLAG:51 = 7

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 160, 80)
ELSE
	GOTO INPUT_LOOP
ENDIF



RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER113(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 110

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):大人しい
	CALL COM_ORDER_ELEMENT(ARG:0, "大人しい", -3)
ENDIF
IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -2)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ気質
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ気質", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド)
	CALL COM_ORDER_ELEMENT(ARG:0, "サド", 15)
ELSE
	IF ABL:(ARG:0):主導度Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 1


;-------------------------------------------------
;固有の実行判定(ターゲット側)
;-------------------------------------------------
@COM_ORDER_TARGET113(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 120
;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 3)
CALL COM_ORDER_ELEMENT(ARG:0, @"露出Lv{ABL:(ARG:0):露出}", ABL:(ARG:0):露出 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):大人しい
	CALL COM_ORDER_ELEMENT(ARG:0, "大人しい", 3)
ENDIF
IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -5)
ENDIF
IF TALENT:(ARG:0):解放
	CALL COM_ORDER_ELEMENT(ARG:0, "解放", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_露出狂)
	CALL COM_ORDER_ELEMENT(ARG:0, "露出狂", 15)
ELSE
	IF ABL:(ARG:0):主導度Ｕ <= -500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 15)
	ELSEIF ABL:(ARG:0):主導度Ｕ <= -300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 10)
	ELSEIF ABL:(ARG:0):主導度Ｕ <= -100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ < 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", -5)
	ENDIF

	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 15)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 10)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
	ENDIF
ENDIF

RETURN 

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE113

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN113
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_113()
RETURN POWER(2, 性的嗜好_辱めたい)

@COM_PREFERENCE_TARGET_113()
RETURN POWER(2, 性的嗜好_辱められたい)

