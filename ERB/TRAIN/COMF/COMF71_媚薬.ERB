;媚薬

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME71
LOCALS:0 = 媚薬

RESULTS:0 = %LOCALS:0%を飲ませる
RESULTS:1 = %LOCALS:0%を飲まさせられる
RESULTS:2 = %LOCALS:0%を飲まさせる
RESULTS:3 = %LOCALS:0%を飲まされる
RESULTS:4 = %LOCALS:0%を飲まさせる
RESULTS:5 = %LOCALS:0%使用見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE71
;共通部分
CALL COM_ABLE_COMMON(71)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM <= 0 
	RETURN 0
;媚薬が必要
SIF ITEM:媚薬 < MTAR_NUM && ITEM:A_媚薬 < MTAR_NUM
	RETURN 0

FOR LOCAL, 0, MTAR_NUM
	;既にターゲットが媚薬を使われているなら不可
	SIF TCVAR:(MTAR:LOCAL):60
		RETURN 0
	;ターゲットの口が塞がっているなら不可
	SIF IS_M_HOLD(MTAR:LOCAL)
		RETURN 0
	SIF !CAN_REACH_BODY(MPLY:0, MTAR:LOCAL)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM71
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;媚薬を減少させる(追加アイテム使用時は減少させない)
IF ITEM:A_媚薬 <= 0
	ITEM:媚薬 = MAX(ITEM:媚薬 - MTAR_NUM, 0)
ENDIF

;●プレイヤー側の処理

FOR LOCAL, 0, MTAR_NUM

	;●ターゲット側の処理
	;媚薬使用フラグを立てる
	TCVAR:(MTAR:LOCAL):60 = 1

	SOURCE:(MTAR:LOCAL):逸脱 = 100

	SELECTCASE ABL:(MTAR:LOCAL):欲望
		CASE 0
			SOURCE:(MTAR:LOCAL):反感 = 400
			SOURCE:(MTAR:LOCAL):不安 = 800
		CASE 1
			SOURCE:(MTAR:LOCAL):反感 = 300
			SOURCE:(MTAR:LOCAL):不安 = 600
		CASE 2
			SOURCE:(MTAR:LOCAL):反感 = 250
			SOURCE:(MTAR:LOCAL):不安 = 500
		CASE 3
			SOURCE:(MTAR:LOCAL):反感 = 150
			SOURCE:(MTAR:LOCAL):不安 = 300
		CASE 4
			SOURCE:(MTAR:LOCAL):反感 = 80
			SOURCE:(MTAR:LOCAL):不安 = 160
		CASEELSE
			SOURCE:(MTAR:LOCAL):反感 = MAX(50 - (ABL:(MTAR:LOCAL):欲望 - 5) * 20, 0)
			SOURCE:(MTAR:LOCAL):不安 = MAX(100 - (ABL:(MTAR:LOCAL):欲望 - 5) * 20, 0)
	ENDSELECT

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(MTAR:LOCAL, 20, 80)
NEXT

;主導度変化基準値
TFLAG:49 = 1

;倒錯度変化基準値
TFLAG:50 = 2

;レズ・ＢＬ経験基準値
TFLAG:51 = 0

;経過時間を無理矢理1減らす
TFLAG:55 --

RETURN 1

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER71(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 65

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF

IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE71
IF MTAR_NUM > 1
	LOCALS:0 = たち
ELSE
	LOCALS:0 = 
ENDIF
SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%%LOCALS:0%に媚薬を飲ませ%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %ANAME(MTAR:0)%%LOCALS:0%は%ANAME(MPLY:0)%に媚薬を飲ませるように命令した
		PRINTFORMW %ANAME(MPLY:0)%は言われるまま%ANAME(MTAR:0)%%LOCALS:0%の口に媚薬を注ぎ込んだ…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%ANAME(MTAR:0)%%LOCALS:0%に媚薬を飲ませるように命令した
		PRINTFORMW %ANAME(MPLY:0)%は言われるまま%ANAME(MTAR:0)%%LOCALS:0%の口に媚薬を注ぎ、飲み込ませた…
ENDSELECT
;-------------------------------------------------
;地の文(実行後)
;-------------------------------------------------
@COM_TEXT_AFTER71
IF MTAR_NUM > 1
	LOCALS:0 = たち
ELSE
	LOCALS:0 = 
ENDIF
IF MTAR:0 != MASTER || CONFIG:441
	IF (TCVAR:(MTAR:0):52) || (TCVAR:(MTAR:0):53 == 1)
		PRINTFORML 意識を失ったまま飲まされた薬によって、%ANAME(MTAR:0)%%LOCALS:0%の体は熱く火照り始めている
	ELSE
		SIF TCVAR:(MTAR:0):51
			PRINT 疲れて身動きできぬまま
		SIF TALENT:(MTAR:0):妖術知識 || TALENT:(MTAR:0):技師
			PRINT 飲まされた薬の効果に思い当り、
		IF IS_LOVER(MTAR:0) && MPLY:0 == MASTER
			PRINTFORML 火照り始めた肉体を%ANAME(MTAR:0)%%LOCALS:0%は持て余している
		;捕虜調教のメインターゲット
		ELSEIF FLAG:調教モード == 2 && FINDELEMENT(PRISONER_TARGET, MTAR:0) != -1
			PRINTFORML %ANAME(MTAR:0)%%LOCALS:0%は%ANAME(MPLY:0)%に使われた媚薬によって、自分の意思とは無関係に身体が疼き始めている
		ELSE
			PRINTFORML %ANAME(MTAR:0)%%LOCALS:0%の体は媚薬によって、敏感になっている
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN71
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_71()
RETURN POWER(2, 性的嗜好_道具を使いたい)

@COM_PREFERENCE_TARGET_71()
RETURN POWER(2, 性的嗜好_道具を使われたい)

