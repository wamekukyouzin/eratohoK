;頭を撫でる

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME321
RESULTS:0 = 頭を撫でる
RESULTS:1 = 頭を撫でさせられる
RESULTS:2 = 頭を撫でてもらう
RESULTS:3 = 頭を撫でられる

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE321
;共通部分
CALL COM_ABLE_COMMON(321)
SIF RESULT == 0
	RETURN 0
;主人公以外が実行する場合、好感度が500以上必要
SIF MPLY:0 != MASTER && CFLAG:(MPLY:0):2 < 500
	RETURN 0
;怪我なら不可
SIF CFLAG:(MTAR:0):行動不能状態 == 3
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM321
;実行できるかの判定
CALL COM_ORDER_COMMON
IF RESULT == 0
	RETURN 0
ENDIF

IF MPLY:0 == MASTER
	LOCAL:0 = 0
	IF TALENT:(MTAR:0):幼児 || TALENT:(MTAR:0):幼稚
		LOCAL:0 = 5
	ENDIF
	;コマンドの成否をTFLAG:18にセット
	CALL JUDGE_COM_RESULT(MTAR:0, 5 + LOCAL:0, 10 - LOCAL:0)
	LOCAL:1 = MTAR:0
ELSE
	;コマンドの成否をTFLAG:18にセット
	CALL JUDGE_COM_RESULT(MPLY:0, 5, 10)
	LOCAL:1 = MPLY:0
ENDIF

;固定で入るソース
SOURCE:(MPLY:0):接触 = 250
SOURCE:(MTAR:0):接触 = 250

;親密に応じた歓楽のソース追加
SOURCE:(MPLY:0):歓楽 = -10
SOURCE:(MTAR:0):歓楽 = -10
CALL ADD_SOURCE_KANRAKU(MPLY:0, 100)
CALL ADD_SOURCE_KANRAKU(MTAR:0, 100)
SOURCE:(MPLY:0):歓楽 = MAX(SOURCE:(MPLY:0):歓楽, 0)
SOURCE:(MTAR:0):歓楽 = MAX(SOURCE:(MTAR:0):歓楽, 0)

;親密に応じた愛情のソース追加
CALL ADD_SOURCE_AIZYOU(MPLY:0, 130)
CALL ADD_SOURCE_AIZYOU(MTAR:0, 130)

;主導権に応じたソースの追加
CALL ADD_SOURCE_INITIATIVE_N(MPLY:0, 150, 150)
CALL ADD_SOURCE_INITIATIVE_N(MTAR:0, 150, 150)

IF !TALENT:(MTAR:0):幼児 && !TALENT:(MTAR:0):幼稚 && !TALENT:(MTAR:0):プライド低い
	IF TALENT:(MTAR:0):プライド高い
		SOURCE:(MTAR:0):反感 = 150
		TIMES SOURCE:(MTAR:0):歓楽, 0.50
	ELSE
		SOURCE:(MTAR:0):反感 = 60
		TIMES SOURCE:(MTAR:0):歓楽, 0.80
	ENDIF
ENDIF

IF TALENT:(MPLY:0):母性
	TIMES SOURCE:(MTAR:0):愛情, 1.50
ENDIF

;失敗
IF TFLAG:18 == -1
	TIMES SOURCE:(MPLY:0):歓楽, 0.20
	TIMES SOURCE:(MPLY:0):愛情, 0.20
	TIMES SOURCE:(MTAR:0):歓楽, 0.20
	TIMES SOURCE:(MTAR:0):愛情, 0.20
	TIMES SOURCE:(MTAR:0):反感, 2.00
	SOURCE:(MPLY:0):不満 += 500
	SOURCE:(MTAR:0):不満 += 500
	TFLAG:37 -= 5
;成功
ELSEIF TFLAG:18 == 0

;大成功
ELSE
	TIMES SOURCE:(MPLY:0):歓楽, 2.00
	TIMES SOURCE:(MPLY:0):愛情, 2.00
	TIMES SOURCE:(MTAR:0):歓楽, 2.00
	TIMES SOURCE:(MTAR:0):愛情, 2.00
	TIMES SOURCE:(MTAR:0):反感, 0.50
	TFLAG:37 += 5
ENDIF

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = -1

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER321(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 30

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 3)

;好感度
IF CFLAG:(ARG:0):2 < 400
	IF CFLAG:(ARG:0):2 < 50
		LOCAL:0 = -40
	ELSEIF CFLAG:(ARG:0):2 < 150
		LOCAL:0 = -20
	ELSEIF CFLAG:(ARG:0):2 < 300
		LOCAL:0 = -10
	ELSE
		LOCAL:0 = -5
	ENDIF
	CALL COM_ORDER_ELEMENT(ARG:0, @"好感度不足", LOCAL:0)
ELSE
	LOCAL:0 = CFLAG:(ARG:0):2 / 75
	CALL COM_ORDER_ELEMENT(ARG:0, @"好感度", LOCAL:0)
ENDIF

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 5)
ENDIF
IF TALENT:(ARG:0):母性
	CALL COM_ORDER_ELEMENT(ARG:0, "母性", 6)
ENDIF
IF TFLAG:54
	CALL COM_ORDER_ELEMENT(ARG:0, "デート中", 4)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE321
;キス中
IF IS_EQUIP(MPLY:0, 342)
	LOCALS:0 = キスをしながら
	;地の文カット
	LOCAL:0 = SEARCH_EQUIP_IC(342, MPLY:0, MTAR:0)
	IF LOCAL:0 >= 0
		MEQUIP_CUT:(LOCAL:0) = 1
	ENDIF
ELSE
	LOCALS:0 = 
ENDIF

;プレイヤーが主人公側
IF MPLY:0 == MASTER
	;主導権が主人公側
	IF IS_INITIATIVE(MASTER)
		PRINTFORM %ANAME(MPLY:0)%は%LOCALS:0%%ANAME(MTAR:0)%
		IF CFLAG:(MTAR:0):2 >= 800
			PRINTFORMW を抱き寄せて優しく頭を撫でた
		ELSE
			PRINTFORMW の頭を撫でた
		ENDIF
	;主導権が相手側
	ELSE
		PRINTFORM %ANAME(MTAR:0)%は%LOCALS:0%%ANAME(MPLY:0)%
		IF CFLAG:(MTAR:0):2 >= 800
			PRINTFORMW に体を預け、頭を撫でるように要求した
		ELSE
			PRINTFORMW に、頭を撫でて欲しいとねだった
		ENDIF
	ENDIF

	IF TFLAG:18 == 1
		IF TALENT:(MTAR:0):幼児 || TALENT:(MTAR:0):幼稚
			PRINTFORML %ANAME(MTAR:0)%は眼を細め、心地良さそうにしている…
		ELSE
			PRINTFORML %ANAME(MTAR:0)%は照れ臭そうに、笑みを浮かべている…
		ENDIF
	ELSEIF TFLAG:18 == 0
		IF TALENT:(MTAR:0):幼児 || TALENT:(MTAR:0):幼稚
			PRINTFORML %ANAME(MTAR:0)%は嬉しそうにしている…
		ELSE
			PRINTFORML %ANAME(MTAR:0)%は頬を紅潮させ、恥ずかしそうにしている…
		ENDIF
	ELSEIF TFLAG:18 == -1
		PRINTFORML %ANAME(MTAR:0)%は子供扱いされて不機嫌そうだ…
	ENDIF
;プレイヤーが相手側
ELSE
	;主導権が主人公側
	IF IS_INITIATIVE(MASTER)
		PRINTFORM %ANAME(MTAR:0)%は%LOCALS:0%%ANAME(MPLY:0)%に
		IF CFLAG:(MPLY:0):2 >= 800
			PRINTFORMW 抱きついて、甘えながら頭を撫でてもらった
		ELSE
			PRINTFORMW 頭を撫でてもらった
		ENDIF

		IF TFLAG:18 == 1
			IF TALENT:(MPLY:0):母性
				PRINTFORML %ANAME(MPLY:0)%は我が子を愛おしむかの様に、撫でてくれた…
			ELSE
				PRINTFORML %ANAME(MPLY:0)%は笑顔で優しく撫でてくれた…
			ENDIF
		ELSEIF TFLAG:18 == 0
			IF TALENT:(MPLY:0):母性
				PRINTFORML %ANAME(MPLY:0)%は笑顔で優しく、ただ撫でてくれた…
			ELSE
				PRINTFORML %ANAME(MPLY:0)%は呆れた様に苦笑しながらも、優しく撫でてくれた…
			ENDIF
		ELSEIF TFLAG:18 == -1
			PRINTFORML %ANAME(MPLY:0)%は心底呆れたような表情でため息をついた…
		ENDIF
	;主導権が相手側
	ELSE
		PRINTFORM %ANAME(MPLY:0)%は%LOCALS:0%%ANAME(MTAR:0)%
		IF CFLAG:(MPLY:0):2 >= 800
			PRINTFORMW を抱き寄せると、甘やかすように優しく頭を撫でた
		ELSE
			PRINTFORMW の頭を撫でてきた
		ENDIF

		IF TFLAG:18 == 1
			PRINTFORML %ANAME(MPLY:0)%はとても楽しそうだ…
		ELSEIF TFLAG:18 == 0
			PRINTFORML %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の反応を見て楽しんでいる…
		ELSEIF TFLAG:18 == -1
			PRINTFORML が、%ANAME(MPLY:0)%はすぐに飽きてしまったようだ…
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------
;地の文(パラメータ・刻印変動後)
;-------------------------------------------------
@COM_TEXT_LAST321
;日常コマンドに共通の地の文
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN321
RETURN コマンド_日常
