;アナルフィスト

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME88
LOCALS:0 = アナルフィスト

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE88
;共通部分
CALL COM_ABLE_COMMON(88)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
SIF IS_A_HOLD(MTAR:0)
	RETURN 0
SIF !CAN_REACH_GROIN(MPLY:0, MTAR:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM88
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;ターゲットのＡ拡張経験による諸々の倍率
LOCAL:10 = MIN(100 + EXP:(MTAR:0):Ａ拡張経験 * 10 + MIN(ABL:(MTAR:0):Ａ感, 6) * 30, 600)

;●プレイヤー側の処理
DOWNBASE:(MPLY:0):体力 += 120
EXP:(MPLY:0):嗜虐経験値 += RAND(1, 4)

SOURCE:(MPLY:0):嗜虐 = 1000
SOURCE:(MPLY:0):逸脱 = 600
SOURCE:(MPLY:0):性行動 = 90

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 80)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●ターゲット側の処理
DOWNBASE:(MTAR:0):体力 += 180

EXP:(MTAR:0):Ａ拡張経験 += 1
EXP:(MTAR:0):被虐経験値 += RAND(1, 4)

SOURCE:(MTAR:0):快Ａ += SENSE_HOUSHI(MPLY:0, MTAR:0, 500) * LOCAL:10 / 100
SOURCE:(MTAR:0):苦痛 = 5000 * 100 / LOCAL:10
SOURCE:(MTAR:0):逸脱 = 600
SOURCE:(MTAR:0):露出 = 400
SOURCE:(MTAR:0):性行動 = 150

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 50, 160)

IF PALAM:(MTAR:0):潤滑 < PALAMLV:1
	TIMES SOURCE:(MTAR:0):快Ａ, 0.05
	TIMES SOURCE:(MTAR:0):苦痛, 2.50
ELSEIF PALAM:(MTAR:0):潤滑 < PALAMLV:3
	TIMES SOURCE:(MTAR:0):快Ａ, 0.20
	TIMES SOURCE:(MTAR:0):苦痛, 2.00
ELSEIF PALAM:(MTAR:0):潤滑 < PALAMLV:5
	TIMES SOURCE:(MTAR:0):快Ａ, 0.60
	TIMES SOURCE:(MTAR:0):苦痛, 1.50
ELSEIF PALAM:(MTAR:0):潤滑 < PALAMLV:7
	TIMES SOURCE:(MTAR:0):快Ａ, 0.85
	TIMES SOURCE:(MTAR:0):苦痛, 1.20
ELSE
	TIMES SOURCE:(MTAR:0):快Ａ, 1.00
	TIMES SOURCE:(MTAR:0):苦痛, 1.00
ENDIF

IF GETBIT(TALENT:(MPLY:0):淫乱系, 素質_淫乱_サド)
	TIMES SOURCE:(MTAR:0):苦痛, 1.30
ENDIF

IF TALENT:(MTAR:0):体格 == 体格_小柄 && !TALENT:(MPLY:0):体格 == 体格_小柄
	TIMES SOURCE:(MTAR:0):苦痛, 1.30
ENDIF

CALL TIGHTNESS_DECREASE_A(MTAR:0, RAND(5, 8))
CALL VIRGIN_COMMON_A(MTAR:0, @"%ANAME(MPLY:0)%の腕", GET_SITUATION_BY_TRAIN_MODE())

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 12

;レズ・ＢＬ経験基準値
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP88
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP88(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;ソースを退避
CALL PUTOUT_SOURCE(LOCAL:2)
CALL PUTOUT_SOURCE(LOCAL:3)

;ターゲットのＶ拡張経験による諸々の倍率
LOCAL:10 = MIN(100 + EXP:(LOCAL:3):Ａ拡張経験 * 10 + MIN(ABL:(LOCAL:3):Ａ感, 6) * 30, 600)

;●プレイヤーの処理
DOWNBASE:(LOCAL:2):体力 += 20

EXP:(LOCAL:2):性技経験値 += 1
EXP:(LOCAL:2):嗜虐経験値 += 1

SOURCE:(LOCAL:2):嗜虐 += 500
SOURCE:(LOCAL:2):逸脱 += 300
SOURCE:(LOCAL:2):性行動 += 30

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

;倒錯度変化基準値
TCVAR:(LOCAL:2):50 += 12

;●ターゲットの処理
DOWNBASE:(LOCAL:3):体力 += 30

EXP:(LOCAL:3):Ａ拡張経験 += 1
EXP:(LOCAL:3):被虐経験値 += 1

SOURCE:(LOCAL:3):快Ａ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 300) * LOCAL:10 / 100
SOURCE:(LOCAL:3):苦痛 += 2000 * 100 / LOCAL:10
SOURCE:(LOCAL:3):逸脱 += 300
SOURCE:(LOCAL:3):露出 += 200
SOURCE:(LOCAL:3):性行動 += 50

IF PALAM:(LOCAL:3):潤滑 < PALAMLV:1
	TIMES SOURCE:(LOCAL:3):快Ａ, 0.05
	TIMES SOURCE:(LOCAL:3):苦痛, 2.50
ELSEIF PALAM:(LOCAL:3):潤滑 < PALAMLV:3
	TIMES SOURCE:(LOCAL:3):快Ａ, 0.20
	TIMES SOURCE:(LOCAL:3):苦痛, 2.00
ELSEIF PALAM:(LOCAL:3):潤滑 < PALAMLV:5
	TIMES SOURCE:(LOCAL:3):快Ａ, 0.60
	TIMES SOURCE:(LOCAL:3):苦痛, 1.50
ELSEIF PALAM:(LOCAL:3):潤滑 < PALAMLV:7
	TIMES SOURCE:(LOCAL:3):快Ａ, 0.85
	TIMES SOURCE:(LOCAL:3):苦痛, 1.20
ELSE
	TIMES SOURCE:(LOCAL:3):快Ａ, 1.00
	TIMES SOURCE:(LOCAL:3):苦痛, 1.00
ENDIF

IF GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_サド)
	TIMES SOURCE:(LOCAL:3):苦痛, 1.30
ENDIF

IF TALENT:(LOCAL:3):体格 == 体格_小柄 && !TALENT:(LOCAL:2):体格 == 体格_小柄
	TIMES SOURCE:(LOCAL:3):苦痛, 1.30
ENDIF

CALL TIGHTNESS_DECREASE_A(LOCAL:3, RAND(5, 8))

;倒錯度変化基準値
TCVAR:(LOCAL:3):50 += 12

;退避したソースを加算
CALL SUM_SOURCE(LOCAL:2)
CALL SUM_SOURCE(LOCAL:3)

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE88(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%にアナルフィスト中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP88(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%のアナルに拳を押し込んでいる…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP88(ARG:0)
PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%のアナルから拳を引き抜いた…

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER88(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 150

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"サドLv{ABL:(ARG:0):サド}", ABL:(ARG:0):サド * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド)
	CALL COM_ORDER_ELEMENT(ARG:0, "サド", 50)
ELSEIF FLAG:主導権所有者 == ARG:0
	;主導度
	IF ABL:(ARG:0):主導度Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 20)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度", 5)
	ELSEIF ABL:(ARG:0):主導度Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度不足", -50)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "主導度不足", -200)
	ENDIF

	;倒錯度
	IF ABL:(ARG:0):倒錯度 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 20)
	ELSEIF ABL:(ARG:0):倒錯度 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 5)
	ELSEIF ABL:(ARG:0):倒錯度 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度不足", -150)
	ELSEIF ABL:(ARG:0):倒錯度 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度不足", -300)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度不足", -500)
	ENDIF
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE88
LOCALS:1 = 無理やり
IF EXP:(MTAR:0):Ａ拡張経験 >= 50
	LOCALS:0 = 拡張されてすっかり広がりきった
	LOCALS:1 = 
ELSEIF EXP:(MTAR:0):Ａ拡張経験 >= 20
	LOCALS:0 = 拡張されて良く広がるようになった
	LOCALS:1 = 
ELSE
	LOCALS:0 = 
ENDIF

IF TALENT:(MTAR:0):体格 == 体格_小柄
	LOCALS:0 = %LOCALS:0%小さなアナル
ELSE
	LOCALS:0 = %LOCALS:0%アナル
ENDIF

IF TALENT:(MTAR:0):美尻
	LOCALS:2 = %STR_BODY("尻：尻肉：長：愛撫：感触", MTAR:0)%を割り開き、
ELSEIF GET_HIPSIZE(MTAR:0) >= 1
	LOCALS:2 = %STR_BODY("尻肉：長", MTAR:0)%をかきわけ、
ELSE
	LOCALS:2 = 
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の%LOCALS:2%%LOCALS:0%を、拳で%LOCALS:1%押し広げて陵辱し%TARGET_STR("た", "てきた")%…
	;ターゲットに主導権
	CASE 1
		PRINTFORMW %ANAME(MTAR:0)%は%LOCALS:2%%ANAME(MPLY:0)%の前に%LOCALS:0%を晒すと、中に拳を突き入れて犯すように命令した…
		IF ABL:(MPLY:0):サド >= 3
			PRINTFORMW %ANAME(MPLY:0)%は言われるまま、%ANAME(MTAR:0)%のアナルを%LOCALS:1%押し広げながら拳を突き入れていった…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%は戸惑いながらも、力を込めてゆっくりと拳を%ANAME(MTAR:0)%のアナルに収めていった…
		ENDIF
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%のアナルを拳で犯すように命令した
		IF ABL:(MPLY:0):サド >= 3
			PRINTFORMW %ANAME(MPLY:0)%は言われるまま、%ANAME(MTAR:0)%の%LOCALS:2%アナルを%LOCALS:1%押し広げながら拳を突き入れていった…
		ELSE
			PRINTFORMW %ANAME(MPLY:0)%は戸惑いながらも、%LOCALS:2%力を込めてゆっくりと拳を%ANAME(MTAR:0)%のアナルに収めていった…
		ENDIF
ENDSELECT

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN88
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_88()
RETURN POWER(2, 性的嗜好_虐げたい)

@COM_PREFERENCE_TARGET_88()
RETURN POWER(2, 性的嗜好_虐げられたい)

