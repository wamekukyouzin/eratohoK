;お腹の音を聴く

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME326
RESULTS:0 = お腹の音を聴く
RESULTS:1 = お腹の音を聴かされる
RESULTS:2 = お腹の音を聴かせる
RESULTS:3 = お腹の音を聴かれる

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE326
;共通部分
CALL COM_ABLE_COMMON(326)
SIF RESULT == 0
	RETURN 0
;ターゲットが妊娠していないと不可
SIF !TALENT:(MTAR:0):妊娠
	RETURN 0
;プレイヤーが怪我なら不可
SIF CFLAG:(MPLY:0):行動不能状態 == 3
	RETURN 0
;キス中は不可
SIF IS_EQUIP(MPLY:0, 342)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM326
;実行判定
CALL COM_ORDER_COMMON
IF RESULT == 0
	RETURN 0
ENDIF

IF MPLY:0 == MASTER
	;コマンドの成否をTFLAG:18にセット
	CALL JUDGE_COM_RESULT(MTAR:0, 0, 0)
	LOCAL:1 = MTAR:0
ELSE
	;コマンドの成否をTFLAG:18にセット
	CALL JUDGE_COM_RESULT(MPLY:0, 0, 0)
	LOCAL:1 = MPLY:0
ENDIF

;●プレイヤー側の処理
;固定で獲得するソース
SOURCE:(MPLY:0):歓楽 = 50
SOURCE:(MPLY:0):接触 = 20

;親密に応じた歓楽のソース追加
CALL ADD_SOURCE_KANRAKU(MPLY:0, 150)

;親密に応じた愛情のソース追加
CALL ADD_SOURCE_AIZYOU(MPLY:0, 50)

;主導権に応じたソースの追加
CALL ADD_SOURCE_INITIATIVE_N(MPLY:0, 50, 50)

;●ターゲット側の処理
;固定で獲得するソース
SOURCE:(MTAR:0):歓楽 = 30
SOURCE:(MTAR:0):接触 = 20

;親密に応じた歓楽のソース追加
CALL ADD_SOURCE_KANRAKU(MTAR:0, 100)

;親密に応じた愛情のソース追加
CALL ADD_SOURCE_AIZYOU(MTAR:0, 100)

;主導権に応じたソースの追加
CALL ADD_SOURCE_INITIATIVE_N(MTAR:0, 50, 50)

;失敗
IF TFLAG:18 == -1
	TIMES SOURCE:(MPLY:0):歓楽, 0.10
	TIMES SOURCE:(MPLY:0):愛情, 0.50
	TIMES SOURCE:(MTAR:0):歓楽, 0.10
	TIMES SOURCE:(MTAR:0):愛情, 0.50
	SOURCE:(MPLY:0):不満 += 500
	SOURCE:(MTAR:0):不満 += 500
	TFLAG:37 -= 5
;成功
ELSEIF TFLAG:18 == 0

;大成功
ELSE
	TIMES SOURCE:(MPLY:0):歓楽, 2.00
	TIMES SOURCE:(MPLY:0):愛情, 2.00
	TIMES SOURCE:(MTAR:0):歓楽, 2.00
	TIMES SOURCE:(MTAR:0):愛情, 2.00
	TFLAG:37 += 5
ENDIF

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = -1

RETURN 1

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER326(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 10

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 3)

;好感度
IF CFLAG:(ARG:0):2 < 300
	IF CFLAG:(ARG:0):2 < 0
		LOCAL:0 = -30
	ELSEIF CFLAG:(ARG:0):2 < 100
		LOCAL:0 = -10
	ELSE
		LOCAL:0 = -5
	ENDIF
	CALL COM_ORDER_ELEMENT(ARG:0, @"好感度不足", LOCAL:0)
ELSE
	LOCAL:0 = CFLAG:(ARG:0):2 / 75
	CALL COM_ORDER_ELEMENT(ARG:0, @"好感度", LOCAL:0)
ENDIF

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -3)
ENDIF
IF TALENT:(ARG:0):好奇心
	CALL COM_ORDER_ELEMENT(ARG:0, "好奇心", 8)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE326
IF CFLAG:(MTAR:0):行動不能状態 == 行動不能_臨月
	LOCALS:0 = すっかり大きくなった
ELSEIF TALENT:(MTAR:0):妊娠
	LOCALS:0 = 明らかに膨らんだ
ELSE
	LOCALS:0 = 
ENDIF

;プレイヤーが主人公側
IF MPLY:0 == MASTER
	;主導権が主人公側
	IF IS_INITIATIVE(MASTER)
		PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%%ANAME(MTAR:0)%のお腹に耳を当ててみた
	;主導権が相手側
	ELSE
		PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に言われて%LOCALS:0%お腹に耳を当ててみた
	ENDIF

	;最後のやつ死に分岐になるけどまぁいいや
	IF CFLAG:(MTAR:0):行動不能状態 == 行動不能_臨月
		PRINTFORML 時折お腹の中の子が動く音が聞こえる…
	ELSEIF TALENT:(MTAR:0):妊娠
		PRINTFORML わずかに子供の動く音が聞こえた気がする…
	ELSE
		PRINTFORML さすがにまだ早すぎたのか、特に何も聞こえなかった…
	ENDIF
;プレイヤーが相手側
ELSE
	;主導権が主人公側
	IF IS_INITIATIVE(MASTER)
		PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%に%LOCALS:0%お腹の音を聴かせてみた
	;主導権が相手側
	ELSE
		PRINTFORMW %ANAME(MPLY:0)%は%LOCALS:0%%ANAME(MTAR:0)%のお腹に耳を当てた
	ENDIF

	IF CFLAG:(MTAR:0):行動不能状態 == 行動不能_臨月
		PRINTFORML 時折お腹の中の子が動く音が聞こえるらしく、%ANAME(MPLY:0)%は楽しそうだ…
	ELSEIF TALENT:(MTAR:0):妊娠
		PRINTFORML %ANAME(MPLY:0)%は何か聞こえるのか、真剣な表情で耳を澄ませている…
	ELSE
		PRINTFORML %ANAME(MPLY:0)%はじっと耳を澄ませているが、特に何も聞こえなかったようだ…
	ENDIF
ENDIF

;-------------------------------------------------
;地の文(パラメータ・刻印変動後)
;-------------------------------------------------
@COM_TEXT_LAST326
;日常コマンドに共通の地の文
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN326
RETURN コマンド_日常
