;パイズリ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME12

IF IS_RIDE(MPLY:0, MTAR:0)
	LOCALS:1 = "顔騎"
ELSE
	LOCALS:1 = 
ENDIF

IF IS_FEMALE(MPLY:0)
	IF GET_BUSTSIZE(MPLY:0) >= 0
		LOCALS:1 = パイズリ
	ELSE
		LOCALS:1 = ナイズリ
	ENDIF
ELSE
	LOCALS:1 = 胸ズリ
ENDIF

IF MPLY_NUM == 3
	LOCALS:0 = Ｔ%LOCALS:1%
ELSEIF MPLY_NUM == 2
	LOCALS:0 = Ｗ%LOCALS:1%
ELSEIF SEARCH_EQUIP(107, MPLY:0, MTAR:0) >= 0
	LOCALS:0 = パイズリフェラ
ELSE
	LOCALS:0 = %LOCALS:1%
ENDIF

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE12
;共通部分
CALL COM_ABLE_COMMON(12)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で3人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 3
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;ターゲットに竿が必要
SIF !HAS_PENIS(MTAR:0)
	RETURN 0
SIF !P_STACKABLE(MPLY:0, MTAR:0, 12)
	RETURN 0
;grovelling
SIF IS_EQUIP_PLAYER(MTAR:0, 110)
	RETURN 0
FOR LOCAL:0, 0, MPLY_NUM
	;this covers most common problems
	SIF !CAN_REACH_BODY(MPLY:(LOCAL:0), MTAR:0)
		RETURN 0
	SIF IS_RIDDEN(MPLY:(LOCAL:0), MTAR:0)
		RETURN 0
	SIF IS_INSERT_MUTUAL(MPLY:(LOCAL:0), MTAR:0) > 0
		RETURN 0
    ;getting assjob
	SIF SEARCH_EQUIP(15, MTAR:0, MPLY:(LOCAL:0)) >= 0
		RETURN 0
NEXT

RETURN 1

;-------------------------------------------------
;快Ｂソースの倍率を取得する関数 ARG:0=PLAYERのキャラ番号
;-------------------------------------------------
@COM12_RATE_B(ARG:0)
#FUNCTION
LOCAL:5 = 1000
SELECTCASE ABL:(ARG:0):奉仕
	CASE 0
		TIMES LOCAL:5, 0.50
	CASE 1
		TIMES LOCAL:5, 0.80
	CASE 2
		TIMES LOCAL:5, 1.00
	CASE 3
		TIMES LOCAL:5, 1.20
	CASE 4
		TIMES LOCAL:5, 1.40
	CASEELSE
		LOCAL:5 = LOCAL:5 * (150 + (ABL:(LOCAL:2):奉仕 - 5) * 2) / 100
ENDSELECT

SELECTCASE ABL:(ARG:0):性技
	CASE 0
		TIMES LOCAL:5, 1.00
	CASE 1
		TIMES LOCAL:5, 1.10
	CASE 2
		TIMES LOCAL:5, 1.20
	CASE 3
		TIMES LOCAL:5, 1.30
	CASE 4
		TIMES LOCAL:5, 1.40
	CASEELSE
		LOCAL:5 = LOCAL:5 * ((ABL:(LOCAL:2):性技 - 5) * 2 + 150) / 100
ENDSELECT

RETURNF LOCAL:5

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM12
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

LOCAL:1 = -1
;フェラ状況を待避
IF MPLY_NUM == 1 && MTAR_NUM == 1
	LOCAL:1 = SEARCH_EQUIP(11, MPLY:0, MTAR:0)
ENDIF


;パイズリ状況を復元
IF LOCAL:1 >= 0
	CALL SET_EQUIP(11, 1)
ENDIF

;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 120

	EXP:(LOCAL:2):性技経験値 += MAX(MTAR_NUM / 2 + 1, 1)
	EXP:(LOCAL:2):口淫経験 += 1

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 450)
	SOURCE:(LOCAL:2):接触 = 50
	SOURCE:(LOCAL:2):快Ｂ = 200 * COM12_RATE_B(LOCAL:2) / 1000
	SOURCE:(LOCAL:2):性行動 = 300

	;口枷装着中なら無視
	IF !IS_EQUIP_TARGET(LOCAL:2, 86)
		SOURCE:(LOCAL:2):快Ｍ = 100 * COM11_RATE_M(LOCAL:2) / 1000
	ENDIF

	;主導権に応じた優越・受動のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 160, 140)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		LOCAL:4 = SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 1600) * LOCAL:10 / 100
		;口枷装着中なら効果低下
		IF !IS_EQUIP_TARGET(LOCAL:2, 86)
			TIMES LOCAL:4, 0.90
		ELSEIF TALENT:(LOCAL:3):舌使い
			TIMES LOCAL:4, 1.20
		ENDIF
		SOURCE:(LOCAL:3):快Ｐ += LOCAL:4

		;射精箇所と対象をセット
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, RAND:3 ? 射精部位_胸 # 射精部位_顔)
	NEXT
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	;●ソースの計算
	DOWNBASE:(LOCAL:1):体力 += 60

	SOURCE:(LOCAL:1):接触 = 50
	SOURCE:(LOCAL:1):性行動 = 180

	;主導権に応じた優越・受動のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 150, 80)
NEXT

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 7

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP12
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP12(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
ENDSELECT

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 20

	EXP:(LOCAL:2):性技経験値 += MAX(MEQUIP_TARGET_NUM:(ARG:0) / 2 + 1, 1)
	EXP:(LOCAL:2):口淫経験 += 1

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 170)
	SOURCE:(LOCAL:2):接触 += 25
	SOURCE:(LOCAL:2):快Ｂ += 75 * COM12_RATE_B(LOCAL:2) / 1000
	SOURCE:(LOCAL:2):快Ｍ += 40 * COM11_RATE_M(LOCAL:2) / 1000
	SOURCE:(LOCAL:2):性行動 += 100

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		LOCAL:4 = SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 550) * LOCAL:10 / 100
		IF TALENT:(LOCAL:3):舌使い
			TIMES LOCAL:4, 1.20
		ENDIF
		SOURCE:(LOCAL:3):快Ｐ += LOCAL:4

		;射精箇所と対象をセット
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, RAND:3 ? 射精部位_胸 # 射精部位_顔)
	NEXT
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:1):体力 += 10

	SOURCE:(LOCAL:1):接触 += 25
	SOURCE:(LOCAL:1):性行動 += 60
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE12(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%にパイズリ中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP12(ARG:0)
LOCAL:2 = 0
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	IF GET_BUSTSIZE(MEQUIP_PLAYER:(ARG:0):(LOCAL:0)) >= 0
		LOCAL:2 = 1
		BREAK
	ENDIF
NEXT
IF LOCAL:2
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%のペニスを胸で挟んで擦り上げている…
ELSE
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%のペニスを薄い胸で擦り上げている…
ENDIF

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP12(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER12(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 95

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"精愛Lv{ABL:(ARG:0):精愛}", ABL:(ARG:0):精愛 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -3)
ENDIF
IF TALENT:(ARG:0):汚臭鈍感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭鈍感", 1)
ENDIF
IF TALENT:(ARG:0):汚臭敏感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭敏感", -3)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -3)
ENDIF
IF TALENT:(ARG:0):男嫌い
	CALL COM_ORDER_ELEMENT(ARG:0, "男嫌い", -12)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE12
LOCAL:5 = SEARCH_EQUIP(101, MPLY:0, MTAR:0)
LOCAL:6 = SEARCH_EQUIP(102, MPLY:0, MTAR:0)
LOCAL:7 = SEARCH_EQUIP(11, MTAR:0, MPLY:0)
LOCAL:8 = SEARCH_EQUIP(2, MTAR:0, MPLY:0)

IF IS_PIERCED(MTAR:0, ピアス_ペニス)
	LOCALS:0 = ピアスの取り付けられた
ELSE
	LOCALS:0 = 
ENDIF

;プレイヤーが３人
IF MPLY_NUM == 3
	IF IS_PIERCED(MPLY:0, ピアス_乳首) && IS_PIERCED(MPLY:1, ピアス_乳首) && IS_PIERCED(MPLY:2, ピアス_乳首)
		LOCALS:1 = ピアスの取り付けられた
	ELSE
		LOCALS:1 = 
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%の三人は、合わせて６つの%LOCALS:1%乳房を%ANAME(MTAR:0)%の%LOCALS:0%ペニスに押し付け、一斉に擦り上げて責め立て%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%の合わせて６つの%LOCALS:1%乳房を%LOCALS:0%ペニスに押し付けさせ、全員で協力して擦り上げさせた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%の三人に、胸で%ANAME(MTAR:0)%の%LOCALS:0%ペニスを気持ち良くするように命令した
			PRINTFORMW %ANAME(MPLY:0)%たちは、合計で６つの%LOCALS:1%乳房を%ANAME(MTAR:0)%の%LOCALS:0%ペニスに押し付け、全員で一斉に擦り上げた…
	ENDSELECT

;プレイヤーが２人
ELSEIF MPLY_NUM == 2
	IF IS_PIERCED(MPLY:0, ピアス_乳首) && IS_PIERCED(MPLY:1, ピアス_乳首)
		LOCALS:1 = ピアスの取り付けられた
	ELSE
		LOCALS:1 = 
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			;２人とも胸が薄い
			IF GET_BUSTSIZE(MPLY:0) <= -1 && GET_BUSTSIZE(MPLY:1) <= -1
				PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は、二人で協力して%ANAME(MTAR:0)%の%LOCALS:0%ペニスに%LOCALS:1%薄い胸を擦り付けて責め立て%TARGET_STR("た", "てきた")%…
			ELSE
				PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は左右から%ANAME(MTAR:0)%の%LOCALS:0%ペニスを%LOCALS:1%胸で挟み、包み込むように擦り上げて責め立て%TARGET_STR("た", "てきた")%…
			ENDIF
		;ターゲットに主導権
		CASE 1
			;２人とも胸が薄い
			IF GET_BUSTSIZE(MPLY:0) <= -1 && GET_BUSTSIZE(MPLY:1) <= -1
				PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%の%LOCALS:1%薄い胸に%LOCALS:0%ペニスを押し当て、二人に竿を擦り上げさせて奉仕させた…
			ELSE
				PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%の%LOCALS:1%胸の間に%LOCALS:0%ペニスを挟み込むと、二人に竿を擦り上げさせて奉仕させた…
			ENDIF
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%と%ANAME(MPLY:1)%に、%LOCALS:1%胸で%ANAME(MTAR:0)%の%LOCALS:0%ペニスを気持ち良くするように命令した
			;２人とも胸が薄い
			IF GET_BUSTSIZE(MPLY:0) <= -1 && GET_BUSTSIZE(MPLY:1) <= -1
				PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は、二人で協力して%ANAME(MTAR:0)%の%LOCALS:0%ペニスに%LOCALS:1%薄い胸を擦り付けて責め立てた…
			ELSE
				PRINTFORMW %ANAME(MPLY:0)%と%ANAME(MPLY:1)%は左右から%ANAME(MTAR:0)%の%LOCALS:0%ペニスを%LOCALS:1%胸で挟み、包み込むように擦り上げて責め立てた…
			ENDIF
	ENDSELECT

;１人(シックスナイン)
ELSEIF LOCAL:5 >= 0 || LOCAL:6 >= 0 || LOCAL:7 >= 0 || LOCAL:8 >= 0
	IF LOCAL:6 >= 0
		LOCALS:5 = アナル
	ELSEIF LOCAL:5 >= 0 && !HAS_VAGINA(MPLY:0)
		LOCALS:5 = 玉
	ELSEIF LOCAL:7 >= 0
		LOCALS:5 = %BAR_NAME(MPLY:0)%
	ELSE
		LOCALS:5 = 秘唇
	ENDIF
	IF IS_PIERCED(MPLY:0, ピアス_乳首)
		LOCALS:2 = ピアスの取り付けられた
	ELSE
		LOCALS:2 = 
	ENDIF
	IF IS_PIERCED(MTAR:0, ピアス_ペニス)
		LOCALS:3 = ピアスの取り付けられた
	ELSE
		LOCALS:3 = 
	ENDIF
	LOCAL:0 = GET_BUSTSIZE(MPLY:0)
	IF LOCAL:0 <= -1
		LOCALS:0 = その%STR_BODY("胸：長：接触", MPLY:0)%を%ANAME(MTAR:0)%の%LOCALS:3%%BAR_NAME(MTAR:0)%に擦り付け
		LOCALS:1 = その%STR_BODY("胸：長：接触", MPLY:0)%を%LOCALS:3%%BAR_NAME(MTAR:0)%に擦り付けさせて奉仕させた…
	ELSEIF LOCAL:0 == 1
		LOCALS:0 = その%STR_BODY("胸：長：接触", MPLY:0)%で%ANAME(MTAR:0)%の%LOCALS:3%%BAR_NAME(MTAR:0)%を挟み込み擦り上げ
		LOCALS:1 = その%STR_BODY("胸：長：接触", MPLY:0)%で%LOCALS:3%%BAR_NAME(MTAR:0)%を挟ませて奉仕させた…
	ELSEIF LOCAL:0 == 2
		LOCALS:0 = その%STR_BODY("胸：長：接触", MPLY:0)%で%ANAME(MTAR:0)%の%LOCALS:3%%BAR_NAME(MTAR:0)%を包み込み擦り上げ
		LOCALS:1 = その%STR_BODY("胸：長：接触", MPLY:0)%で%LOCALS:3%%BAR_NAME(MTAR:0)%を包み込ませて奉仕させた…
	ELSE
		LOCALS:0 = その%STR_BODY("胸：長：接触", MPLY:0)%で%ANAME(MTAR:0)%の%LOCALS:3%%BAR_NAME(MTAR:0)%を挟み込み擦り上げ
		LOCALS:1 = その%STR_BODY("胸：長：接触", MPLY:0)%で%LOCALS:3%%BAR_NAME(MTAR:0)%を挟ませて奉仕させた…
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に自身の%LOCALS:5%を舐めさせながら、%LOCALS:0%て責め立て%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の%LOCALS:5%を舐めながら、%LOCALS:1%
		;第三者に主導権
		CASEELSE
			PRINTFORMW %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、%ANAME(MTAR:0)%の%BAR_NAME(MTAR:0)%に胸で奉仕するように命令した…
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%に自身の%LOCALS:5%を舐めさせながら、%LOCALS:0%た…
	ENDSELECT
	;顔面騎乗・フェラ・クンニの地の文をカット
	IF LOCAL:5 >= 0
		MEQUIP_CUT:(LOCAL:5) = 1
	ENDIF
	IF LOCAL:6 >= 0
		MEQUIP_CUT:(LOCAL:6) = 1
	ENDIF
	IF LOCAL:7 >= 0
		MEQUIP_CUT:(LOCAL:7) = 1
	ENDIF
	IF LOCAL:8 >= 0
		MEQUIP_CUT:(LOCAL:8) = 1
	ENDIF

;プレイヤーが１人＆拘束中
ELSEIF IS_BIND(MPLY:0)
	LOCAL:0 = GET_BUSTSIZE(MPLY:0)
	IF IS_PIERCED(MPLY:0, ピアス_乳首)
		LOCALS:2 = ピアスの取り付けられた
	ELSE
		LOCALS:2 = 
	ENDIF
	IF IS_PIERCED(MTAR:0, ピアス_ペニス)
		LOCALS:3 = ピアスの取り付けられた
	ELSE
		LOCALS:3 = 
	ENDIF
	IF LOCAL:0 <= -1
		LOCALS:0 = %STR_BODY("胸：長：接触", MPLY:0)%に%LOCALS:3%ペニスを擦り付けて
		LOCALS:1 = %STR_BODY("胸：長：接触", MPLY:0)%に自身の%LOCALS:3%ペニスを擦りつけた
	ELSEIF LOCAL:0 == 1
		LOCALS:0 = %STR_BODY("胸：長：接触", MPLY:0)%で%LOCALS:3%ペニスを挟み込んで
		LOCALS:1 = %STR_BODY("胸：長：接触", MPLY:0)%に自身の%LOCALS:3%ペニスを挟み込み擦り上げた
	ELSEIF LOCAL:0 == 2
		LOCALS:0 = %STR_BODY("胸：長：接触", MPLY:0)%で%LOCALS:3%ペニスを挟み込んで
		LOCALS:1 = %STR_BODY("胸：長：接触", MPLY:0)%に自身の%LOCALS:3%ペニスを挟み込み擦り上げた
	ELSE
		LOCALS:0 = %STR_BODY("胸：長：接触", MPLY:0)%で%LOCALS:3%ペニスを挟み込んで
		LOCALS:1 = %STR_BODY("胸：長：接触", MPLY:0)%に自身の%LOCALS:3%ペニスを挟み込み擦り上げた
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は拘束された状態のまま自身の胸を突き出すと、%ANAME(MTAR:0)%に対してこの%LOCALS:0%オナニーするように命令し%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%は身動きの取れない%ANAME(MPLY:0)%の%LOCALS:1%…
		;第三者に主導権
		CASEELSE
			PRINTFORMW %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に対し、%ANAME(MPLY:0)%の胸を犯すように命令した…
			PRINTFORMW %ANAME(MTAR:0)%は身動きの取れない%ANAME(MPLY:0)%の%LOCALS:1%…
	ENDSELECT

	;縄で縛るの地の文をカットする
	LOCAL:0 = SEARCH_EQUIP(85, -1, MPLY:0)
	IF LOCAL:0 >= 0
		MEQUIP_CUT:(LOCAL:0) = 1
	ENDIF
	LOCAL:0 = SEARCH_EQUIP(208, -1, MPLY:0)
	IF LOCAL:0 >= 0
		MEQUIP_CUT:(LOCAL:0) = 1
	ENDIF
;プレイヤーが１人
ELSE
	LOCAL:0 = GET_BUSTSIZE(MPLY:0)
	IF IS_PIERCED(MPLY:0, ピアス_乳首)
		LOCALS:0 = ピアスの取り付けられた
	ELSE
		LOCALS:0 = 
	ENDIF
	IF IS_PIERCED(MTAR:0, ピアス_ペニス)
		LOCALS:1 = ピアスの取り付けられた
	ELSE
		LOCALS:1 = 
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORM %ANAME(MPLY:0)%は
			IF MPLY:0 != MASTER && ABL:(MPLY:0):サド >= 3
				PRINTFORM %ANAME(MTAR:0)%に挑発するような視線を向けながら、
			ENDIF
			IF LOCAL:0 <= -1
				PRINTFORM %STR_BODY("胸：長：接触", MPLY:0)%で懸命に%ANAME(MTAR:0)%の%LOCALS:1%ペニスを
			ELSEIF LOCAL:0 == 1
				PRINTFORM %STR_BODY("胸：長：接触", MPLY:0)%で%ANAME(MTAR:0)%の%LOCALS:1%ペニスを包み込んで
			ELSEIF LOCAL:0 == 2
				PRINTFORM その%STR_BODY("胸：長：接触", MPLY:0)%で%ANAME(MTAR:0)%の%LOCALS:1%ペニスを包み込んで
			ELSE
				PRINTFORM %STR_BODY("胸：長：接触", MPLY:0)%で%ANAME(MTAR:0)%の%LOCALS:1%ペニスをはさんで
			ENDIF
			PRINTFORMW 擦りあげて責め立て%TARGET_STR("た", "てきた")%……
		;ターゲットに主導権
		CASE 1
			PRINTFORM %ANAME(MPLY:0)%は
			IF MPLY:0 != MASTER && TALENT:(MPLY:0):恋慕
				PRINTFORM うっとりとした表情をしながら、
			ENDIF
			IF LOCAL:0 <= -1
				PRINTFORM %STR_BODY("胸：長：接触", MPLY:0)%で懸命に%ANAME(MTAR:0)%の%LOCALS:1%ペニスを
			ELSEIF LOCAL:0 == 1
				PRINTFORM %STR_BODY("胸：長：接触", MPLY:0)%で%ANAME(MTAR:0)%の%LOCALS:1%ペニスを包み込んで
			ELSEIF LOCAL:0 == 2
				PRINTFORM その%STR_BODY("胸：長：接触", MPLY:0)%で%ANAME(MTAR:0)%の%LOCALS:1%ペニスを包み込んで
			ELSE
				PRINTFORM %STR_BODY("胸：長：接触", MPLY:0)%で%ANAME(MTAR:0)%の%LOCALS:1%ペニスをはさんで
			ENDIF
			PRINTFORMW 擦りあげて奉仕している…
		;第三者に主導権
		CASEELSE
			PRINTFORMW %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、%ANAME(MTAR:0)%の%LOCALS:1%%BAR_NAME(MTAR:0)%に胸で奉仕するように命令した…
			PRINTFORM %ANAME(MPLY:0)%は
			IF LOCAL:0 <= -1
				PRINTFORM %STR_BODY("胸：長：接触", MPLY:0)%で懸命に%ANAME(MTAR:0)%のペニスを
			ELSEIF LOCAL:0 == 1
				PRINTFORM %STR_BODY("胸：長：接触", MPLY:0)%で%ANAME(MTAR:0)%のペニスを包み込んで
			ELSEIF LOCAL:0 == 2
				PRINTFORM その%STR_BODY("胸：長：接触", MPLY:0)%で%ANAME(MTAR:0)%のペニスを包み込んで
			ELSE
				PRINTFORM %STR_BODY("胸：長：接触", MPLY:0)%で%ANAME(MTAR:0)%のペニスをはさんで
			ENDIF
			PRINTFORMW 擦りあげた……
	ENDSELECT
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN12
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_12()
RETURN POWER(2, 性的嗜好_奉仕したい)

@COM_PREFERENCE_TARGET_12()
RETURN POWER(2, 性的嗜好_奉仕されたい)

