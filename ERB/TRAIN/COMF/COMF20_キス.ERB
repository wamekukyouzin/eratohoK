;キス

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME20
IF MTAR_NUM >= 2 || MPLY_NUM >= 2
	LOCALS:0 = Ｗキス
ELSE
	LOCALS:0 = キス
ENDIF

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE20
;共通部分
CALL COM_ABLE_COMMON(20)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で2人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 2
	RETURN 0
;ターゲットは最大で2人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 2
	RETURN 0
;2-2は不可
SIF MPLY_NUM == 2 && MTAR_NUM == 2
	RETURN 0

LOCAL:2 = 0
;全プレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	;プレイヤーが行動不能なら不可
	SIF !IS_PLAYABLE(MPLY:(LOCAL:0))
		RETURN 0
	;挿入中はプレイヤーが一人の場合のみ許可
	SIF IS_INSERT_SINGLE(MPLY:(LOCAL:0), "全") && MPLY_NUM >= 2
		RETURN 0
	;プレイヤーが顔面騎乗されているなら不可
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	;プレイヤーがクンニ・フェラ・乳首吸い・パイズリ・足舐め中なら不可
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 2, 9, 11, 12, 104)
		RETURN 0
	;口枷装着中なら不可
	SIF IS_EQUIP_TARGET(MPLY:(LOCAL:0), 86)
		RETURN 0
	;プレイヤーが拘束中なら不可
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
	;プレイヤーが土下座しているなら不可
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 110)
		RETURN 0
	;全ターゲットについて判定
	FOR LOCAL:1, 0, MTAR_NUM
		;プレイヤーとターゲットが相互に貝合わせ中なら不可
		SIF SEARCH_EQUIP_IC(21, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
		;プレイヤーとターゲットが相互に双頭バイブで結合中なら不可
		SIF SEARCH_EQUIP_IC(22, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
		;プレイヤーからターゲット／ターゲットからプレイヤーに尻コキ中なら不可
		SIF SEARCH_EQUIP_IC(15, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
		;プレイヤーからターゲット／ターゲットからプレイヤーに電気按摩中なら不可
		SIF SEARCH_EQUIP_IC(103, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
	NEXT
NEXT
;全ターゲットについて判定
FOR LOCAL:0, 0, MTAR_NUM
	;挿入中はターゲットが一人の場合のみ許可
	SIF IS_INSERT_SINGLE(MTAR:(LOCAL:0), "全") && MTAR_NUM >= 2
		RETURN 0
	;ターゲットが顔面騎乗されているなら不可
	SIF IS_RIDDEN(MTAR:(LOCAL:0))
		RETURN 0
	;ターゲットが土下座しているなら不可
	SIF IS_EQUIP_PLAYER(MTAR:(LOCAL:0), 110)
		RETURN 0
	;ターゲットが乳首吸い・フェラ・パイズリ・足舐め中なら不可
	SIF IS_EQUIP_PLAYER(MTAR:(LOCAL:0), 9, 11, 12, 104)
		RETURN 0
	;口枷装着中なら不可
	SIF IS_EQUIP_TARGET(MTAR:(LOCAL:0), 86)
		RETURN 0
	;行動不能の判定
	SIF !IS_PLAYABLE(MTAR:(LOCAL:0))
		LOCAL:2 ++
NEXT
;２人以上のターゲットが行動不能の場合は不可
SIF LOCAL:2 >= 2
	RETURN 0
;１人が行動不能の場合は1-1以外不可
SIF LOCAL:2 && (MPLY_NUM >= 2 || MTAR_NUM >= 2)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM20
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;逆方向からのMEQUIPを解除
FOR LOCAL:0, 0, MPLY_NUM
	FOR LOCAL:1, 0, MTAR_NUM
		CALL RELEASE_EQUIP_EX(20, MTAR:(LOCAL:1), MPLY:(LOCAL:0))
	NEXT
NEXT

;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 60

	EXP:(LOCAL:2):性技経験値 += MTAR_NUM / 2 + 1
	EXP:(LOCAL:2):キス経験 += 1

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 100)
	SOURCE:(LOCAL:2):接触 = 50
	SOURCE:(LOCAL:2):性行動 = 90

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 50, 30)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1
	IF MTAR_NUM == 1
		CALL KISS_COMMON(MPLY:0, @"%ANAME(MTAR:0)%の唇", GET_SITUATION_BY_TRAIN_MODE())
	ELSE
		CALL KISS_COMMON(MPLY:0, @"%ANAME(MTAR:0)%たちの唇", (GET_SITUATION_BY_TRAIN_MODE() == 喪失_和姦 ? 喪失_乱交 # 喪失_輪姦))
	ENDIF
	
	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｍ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 300) * LOCAL:10 / 100

		;射精箇所と対象をセット
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, 0)
	NEXT
NEXT

;●全ターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 60

	EXP:(LOCAL:2):性技経験値 += MPLY_NUM / 2 + 1
	EXP:(LOCAL:2):キス経験 += 1

	SOURCE:(LOCAL:2):奉仕 = SERVE_HOUSHI(LOCAL:2, 100)
	SOURCE:(LOCAL:2):接触 = 50
	SOURCE:(LOCAL:2):性行動 = 90

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 50, 30)

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1
	
	CALL KISS_COMMON(LOCAL:2, @"%ANAME(MPLY:0)%の唇", GET_SITUATION_BY_TRAIN_MODE())
	
	;全プレイヤーに与える快感系ソースを計算
	FOR LOCAL:1, 0, MPLY_NUM
		LOCAL:3 = MPLY:(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｍ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 300) * LOCAL:10 / 100

		;射精箇所と対象をセット
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, 0)
	NEXT
NEXT

;奴隷の口⇔調教者の口の汚れが移動
;STAIN:(LOCAL:1):0 |= STAIN:MASTER:0
;STAIN:MASTER:0 |= STAIN:(LOCAL:1):0

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = -2

;レズ・ＢＬ経験基準値
TFLAG:51 = 3

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP20
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP20(ARG:0)
;●人数補正の設定
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	EXP:(LOCAL:2):性技経験値 += MAX(MEQUIP_TARGET_NUM:(ARG:0) / 2 + 1, 1)
	EXP:(LOCAL:2):キス経験 += 1

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 40)
	SOURCE:(LOCAL:2):接触 += 25
	SOURCE:(LOCAL:2):性行動 += 30

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 -= 2

	;全ターゲットに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｍ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 150) * LOCAL:10 / 100

		;射精箇所と対象をセット
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, 0, 0)
	NEXT
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):体力 += 10

	EXP:(LOCAL:2):性技経験値 += MAX(MEQUIP_PLAYER_NUM:(ARG:0) / 2 + 1, 1)
	EXP:(LOCAL:2):キス経験 += 1

	SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 40)
	SOURCE:(LOCAL:2):接触 += 25
	SOURCE:(LOCAL:2):性行動 += 30

	;奉仕経験値を得られるコマンドのフラグ
	TCVAR:(LOCAL:2):4 = 1

	;倒錯度変化基準値
	TCVAR:(LOCAL:2):50 -= 2

	;全プレイヤーに与える快感系ソースを計算
	FOR LOCAL:1, 0, MEQUIP_PLAYER_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_PLAYER:(ARG:0):(LOCAL:1)
		SOURCE:(LOCAL:3):快Ｍ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 150) * LOCAL:10 / 100

		;射精箇所と対象をセット
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, 0, 0)
	NEXT
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE20(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%とキス中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP20(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%と舌を絡ませてキスをしている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP20(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER20(ARG:0)
CALL COM_ORDER_COMMON20(ARG:0)
RETURN 1

@COM_ORDER_TARGET20(ARG:0)
CALL COM_ORDER_COMMON20(ARG:0)
RETURN 1

@COM_ORDER_COMMON20(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 60

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"Ｍ感Lv{ABL:(ARG:0):Ｍ感}", ABL:(ARG:0):Ｍ感 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -4)
ENDIF
IF TALENT:(ARG:0):汚臭鈍感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭鈍感", 1)
ENDIF
IF TALENT:(ARG:0):汚臭敏感
	CALL COM_ORDER_ELEMENT(ARG:0, "汚臭敏感", -1)
ENDIF
IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF

IF IS_MPLY(ARG:0)
	IF TALENT:(ARG:0):男嫌い
		FOR LOCAL:0, 0, MTAR_NUM
			IF IS_MALE(MTAR:(LOCAL:0))
				CALL COM_ORDER_ELEMENT(ARG:0, "男嫌い", -7)
				BREAK
			ENDIF
		NEXT
	ENDIF
	IF TALENT:(ARG:0):女嫌い
		FOR LOCAL:0, 0, MTAR_NUM
			IF IS_FEMALE(MTAR:(LOCAL:0))
				CALL COM_ORDER_ELEMENT(ARG:0, "女嫌い", -7)
				BREAK
			ENDIF
		NEXT
	ENDIF
ELSE
	IF TALENT:(ARG:0):男嫌い
		FOR LOCAL:0, 0, MPLY_NUM
			IF IS_MALE(MPLY:(LOCAL:0))
				CALL COM_ORDER_ELEMENT(ARG:0, "男嫌い", -7)
				BREAK
			ENDIF
		NEXT
	ENDIF
	IF TALENT:(ARG:0):女嫌い
		FOR LOCAL:0, 0, MPLY_NUM
			IF IS_FEMALE(MPLY:(LOCAL:0))
				CALL COM_ORDER_ELEMENT(ARG:0, "女嫌い", -7)
				BREAK
			ENDIF
		NEXT
	ENDIF
ENDIF

IF TALENT:(ARG:0):キス未経験
	IF TALENT:(ARG:0):一線越えない
		CALL COM_ORDER_ELEMENT(ARG:0, "キス未経験", -45)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "キス未経験", -30)
	ENDIF
ENDIF

;汚れの判定
;IF !TALENT:(ARG:0):汚れ無視
;	LOCAL:0 = 0
	;愛液の汚れ
;	SIF STAIN:PLAYER:2 & 1
;		LOCAL:0 += 1
	;精液の汚れ
;	SIF STAIN:PLAYER:2 & 4
;		LOCAL:0 += 3
	;アナルの汚れ
;	SIF STAIN:PLAYER:2 & 8
;		LOCAL:0 += 7
;	SIF STAIN:PLAYER:2 & 16
;		LOCAL:0 += 15

;	SIF TALENT:(ARG:0):汚臭鈍感
;		LOCAL:0 /= 3
;	SIF TALENT:(ARG:0):汚臭敏感
;		LOCAL:0 *= 2

	;汚れの影響がやや小さい
;	LOCAL:0 /= 2

	;汚れあり
;	IF LOCAL:0 > 0
;		CALL COM_ORDER_ELEMENT(ARG:0, "汚れあり", -LOCAL:0)
;	ENDIF
;ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF

;好感度
LOCAL:0 = 0
IF CFLAG:(ARG:0):2 < 500
	IF CFLAG:(ARG:0):2 < 100
		LOCAL:0 = -30
	ELSEIF CFLAG:(ARG:0):2 < 300
		LOCAL:0 = -10
	ELSE
		LOCAL:0 = -5
	ENDIF

	CALL COM_ORDER_ELEMENT(ARG:0, @"好感度不足", LOCAL:0)
ELSE
	LOCAL:0 = CFLAG:(ARG:0):2 / 80
	CALL COM_ORDER_ELEMENT(ARG:0, @"好感度", LOCAL:0)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE20
;ターゲットが２人
IF MTAR_NUM == 2
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%、%ANAME(MTAR:1)%の二人を抱き寄せ、交互に舌を絡ませながら唾液を交換し%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORML %ANAME(MTAR:0)%と%ANAME(MTAR:1)%は、%ANAME(MPLY:0)%に対してキスをするように要求した
			PRINTFORMW %ANAME(MPLY:0)%は言われるまま%ANAME(MTAR:0)%と%ANAME(MTAR:1)%に口付け、交互に舌を絡ませながら唾液を交換した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対して、%ANAME(MTAR:0)%、%ANAME(MTAR:1)%の二人とキスするように命令した
			PRINTFORMW %ANAME(MPLY:0)%は言われるまま%ANAME(MTAR:0)%と%ANAME(MTAR:1)%に口付け、交互に舌を絡ませながら唾液を交換した…
	ENDSELECT

;ターゲットが１人
ELSE
	;プレイヤーが２人
	IF MPLY_NUM == 2
		LOCALS:0 = %ANAME(MPLY:0)%と%ANAME(MPLY:1)%
		LOCALS:1 = 二人
	;プレイヤーが１人
	ELSE
		LOCALS:0 = %ANAME(MPLY:0)%
		LOCALS:1 = %ANAME(MPLY:0)%
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %LOCALS:0%は%ANAME(MTAR:0)%の口腔を隅々まで舐め回し、ねっとりと舌を絡め合いながら唾液を交換し%TARGET_STR("た", "てきた")%…
			IF MTAR:0 != MASTER && (TALENT:(MTAR:0):恋慕 || TALENT:(MTAR:0):恋人)
				PRINTFORMW %ANAME(MTAR:0)%はうっとりとした表情を浮かべながら、自らも積極的に舌を絡めて%LOCALS:1%を悦ばせた…
			ENDIF
		;ターゲットに主導権
		CASE 1
			PRINTFORML %ANAME(MTAR:0)%は%LOCALS:0%に対してキスをするように要求した
			PRINTFORMW %LOCALS:0%は言われるまま%ANAME(MTAR:0)%に口付け、口腔を舐め回しながら唾液を交換した…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%LOCALS:0%に%ANAME(MTAR:0)%とキスするように命令した
			PRINTFORMW %LOCALS:0%は言われるまま%ANAME(MTAR:0)%に口付け、口腔を舐め回しながら唾液を交換した…
			IF MTAR:0 != MASTER && (TALENT:(MTAR:0):恋慕 || TALENT:(MTAR:0):恋人)
				PRINTFORMW %ANAME(MTAR:0)%はうっとりとした表情を浮かべながら、自らも積極的に舌を絡めて%LOCALS:1%を悦ばせた…
			ENDIF
	ENDSELECT
ENDIF
