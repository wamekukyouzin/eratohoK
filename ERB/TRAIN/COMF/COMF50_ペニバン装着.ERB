;ペニバン装着

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME50
LOCALS:0 = ペニバン
IF MTAR_NUM > 0
	RESULTS:0 = %LOCALS:0%装着を見せつける
	RESULTS:1 = %LOCALS:0%装着させられる
	RESULTS:2 = %LOCALS:0%装着させる
	RESULTS:3 = %LOCALS:0%装着を見せつけられる
	RESULTS:4 = %LOCALS:0%装着させる
	RESULTS:5 = %LOCALS:0%装着見せつけ
ELSE
	RESULTS:0 = %LOCALS:0%をつける
ENDIF

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE50
;共通部分
CALL COM_ABLE_COMMON(50)
SIF RESULT == 0
	RETURN 0
;プレイヤーは1人以上必要
SIF MPLY_NUM <= 0 
	RETURN 0
FOR LOCAL, 0, MPLY_NUM
	;プレイヤーが行動不能なら不可
	SIF !IS_PLAYABLE(MPLY:LOCAL)
		RETURN 0
	;プレイヤーが拘束中なら不可
	SIF IS_BIND(MPLY:LOCAL)
		RETURN 0
	;プレイヤーに元から竿があるなら不可
	SIF HAS_PENIS(MPLY:LOCAL)
		RETURN 0
	;ペニスバンドが必要
	SIF ITEM:ペニスバンド == 0 && ITEM:A_ペニスバンド == 0
		RETURN 0
	;プレイヤーが性交中なら不可
	SIF IS_INSERT_SINGLE(MPLY:LOCAL, "全")
		RETURN 0
	;プレイヤーが貝合わせ中・双頭バイブ使用中なら不可
	SIF IS_EQUIP(MPLY:LOCAL, 21, 22)
		RETURN 0
	;プレイヤーが自慰・顔面騎乗中なら不可
	SIF IS_EQUIP_PLAYER(MPLY:LOCAL, 100, 101, 102)
		RETURN 0
	;プレイヤーがクンニ・指挿れ・Ａ愛撫・Ｖフィスト・Ａフィスト・電気按摩・触手挿入・触手Ａ挿入・触手クリ責めされているなら不可
	SIF IS_EQUIP_TARGET(MPLY:LOCAL, 2, 3, 4, 87, 88, 103, 201, 202, 203)
		RETURN 0
	;既にペニバンつけてるなら駄目
	SIF IS_EQUIP_PLAYER(MPLY:LOCAL, 50)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM50
PRINTFORMW %ANAME(MPLY:0)%\@ MPLY_NUM > 1 ? たち # \@はペニスバンドを装着した…

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE50

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP50
;継続コマンドかつフィルタリング不可
RETURN 2

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP50(ARG:0)
;特になし

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE50(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%がペニバンを装着中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP50(ARG:0)

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP50(ARG:0)
PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%のペニスバンドを外した
;挿入状態をクリアする
FOR LOCAL, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	CALL CLEAR_INSERT_FLAG(MEQUIP_PLAYER:(ARG:0):LOCAL, "Ｐ")
NEXT
