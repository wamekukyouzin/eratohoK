;対面座位

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME32
LOCALS:0 = 対面座位

RESULTS:0 = %LOCALS:0%で挿入
RESULTS:1 = %LOCALS:0%で挿入させられる
RESULTS:2 = %LOCALS:0%で挿入させる
RESULTS:3 = %LOCALS:0%で挿入される
RESULTS:4 = %LOCALS:0%で挿入させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE32
;共通部分
CALL COM_ABLE_COMMON(32)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;対象にＶが必要
SIF !HAS_VAGINA(MTAR:0)
	RETURN 0
;実行者に竿がなく、ペニスバンド装着中でもないと不可
SIF !HAS_PENIS(MPLY:0) && !IS_EQUIP_PLAYER(MPLY:0, 50)
	RETURN 0
;プレイヤーとターゲットの挿入関係を取得
LOCAL:0 = IS_INSERT_MUTUAL(MPLY:0, MTAR:0)
;ターゲットからプレイヤーに挿入中なら不可
SIF LOCAL:0 == 3 || LOCAL:0 == 4
	RETURN 0
;ターゲットのＶがプレイヤーの竿以外で埋まっているなら不可
SIF IS_V_HOLD(MTAR:0) && LOCAL:0 != 1
	RETURN 0
;ターゲットのＡが他のキャラの竿で埋まっていて、体位がＡ背面座位・Ａ騎乗位・Ａ背面騎乗位以外なら不可
SIF IS_INSERT_SINGLE(MTAR:0, "Ａ") && LOCAL:0 != 2 && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ａ"), 4, 5, 6)
	RETURN 0
;プレイヤーが顔面騎乗されているなら不可
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;プレイヤーが顔面騎乗中なら不可
SIF IS_RIDE(MPLY:0)
	RETURN 0
;ターゲットが顔面騎乗されているなら不可
SIF IS_RIDDEN(MTAR:0)
	RETURN 0
;プレイヤーが土下座しているなら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 110)
	RETURN 0
;ターゲットが土下座しているなら不可
SIF IS_EQUIP_PLAYER(MTAR:0, 110)
	RETURN 0
;ターゲットが顔面騎乗中なら不可
SIF IS_RIDE(MTAR:0)
	RETURN 0
;プレイヤーが拘束中なら不可
SIF IS_BIND(MPLY:0)
	RETURN 0
;プレイヤーが貝合わせ中・双頭バイブ使用中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 21) || IS_EQUIP_TARGET(MPLY:0, 21) || IS_EQUIP_PLAYER(MPLY:0, 22) || IS_EQUIP_TARGET(MPLY:0, 22)
	RETURN 0
;プレイヤーが貝合わせ中・双頭バイブ使用中なら不可
SIF IS_EQUIP_PLAYER(MTAR:0, 21) || IS_EQUIP_TARGET(MTAR:0, 21) || IS_EQUIP_PLAYER(MTAR:0, 22) || IS_EQUIP_TARGET(MTAR:0, 22)
	RETURN 0
;ターゲットがプレイヤーにＷ手コキ、Ｔ手コキ中なら不可
LOCAL:0 = SEARCH_EQUIP(10, MTAR:0, MPLY:0)
SIF LOCAL:0 >= 0 && MEQUIP_PLAYER_NUM:(LOCAL:0) >= 2
	RETURN 0
;プレイヤーが素股・足コキ・尻コキ・イラマ・電気按摩中なら不可
SIF IS_EQUIP_PLAYER(MPLY:0, 13, 14, 15, 90, 103, 107)
	RETURN 0
;プレイヤーがフェラ・パイズリ・素股・足コキ・尻コキ・オナホコキ・電気按摩・触手オナホされているなら不可
SIF IS_EQUIP_TARGET(MPLY:0, 11, 12, 13, 14, 15, 16, 103, 205)
	RETURN 0
;プレイヤーがセルフオナホ中ならダメ
SIF IS_EQUIP_PLAYER(MPLY:0, 116)
	RETURN 0
;ターゲットがクンニされているなら不可
SIF IS_EQUIP_TARGET(MTAR:0, 2)
	RETURN 0
;ターゲットからプレイヤーにイラマ中なら不可
SIF SEARCH_EQUIP(90, MTAR:0, MPLY:0) >= 0
	RETURN 0
;プレイヤーからターゲット／ターゲットからプレイヤーに足舐め中なら不可
SIF SEARCH_EQUIP_IC(104, MPLY:0, MTAR:0) >= 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM32
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;既に挿入中なら現在の体位を取得
TFLAG:17 = 0
IF IS_INSERT_MUTUAL(MPLY:0, MTAR:0) == 1
	SELECTCASE GET_SEX_POSITION(MPLY:0, MTAR:0)
		CASE 1
			TFLAG:17 = 2
		CASE 2
			TFLAG:17 = 3
		CASE 3
			TFLAG:17 = 1
		CASE 4
			TFLAG:17 = 3
		CASE 5
			TFLAG:17 = 2
		CASE 6
			TFLAG:17 = 3
		CASE 7
			TFLAG:17 = 3
		CASE 8
			TFLAG:17 = 2
	ENDSELECT
ENDIF
IF TFLAG:17 == 0
	SELECTCASE PREVCOM
		CASE 30
			TFLAG:17 = 2
		CASE 31
			TFLAG:17 = 3
		CASE 32
			TFLAG:17 = 1
		CASE 33
			TFLAG:17 = 3
		CASE 34
			TFLAG:17 = 2
		CASE 35
			TFLAG:17 = 3
		CASE 37
			TFLAG:17 = 3
		CASE 38
			TFLAG:17 = 2
	ENDSELECT
ENDIF

;挿入関係を一時リセット
CALL CLEAR_INSERT_FLAG(MPLY:0, "Ｐ")

;●プレイヤー側の処理
DOWNBASE:(MPLY:0):体力 += 140

EXP:(MPLY:0):性技経験値 += 1
EXP:(MPLY:0):性交経験値 += 2

SOURCE:(MPLY:0):快Ｐ = SENSE_SEX_PLAYER_P(MTAR:0, MPLY:0, 900)
SOURCE:(MPLY:0):露出 = 50
SOURCE:(MPLY:0):性行動 = 450

IF MTAR:0 == MASTER
	CALL ADD_SOURCE_AIZYOU(MPLY:0, 130)
ELSEIF MPLY:0 != MASTER
	SOURCE:(MPLY:0):露出 += 200
ENDIF

;性交系の共通処理
CALL COM_COMMON_SEX_P(MPLY:0, MTAR:0)

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 80)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●ターゲット側の処理
DOWNBASE:(MTAR:0):体力 += 100

EXP:(MTAR:0):性技経験値 += 1
EXP:(MTAR:0):性交経験値 += 2

SOURCE:(MTAR:0):快Ｃ = SENSE_SEX_TARGET(MPLY:0, MTAR:0, 300)
SOURCE:(MTAR:0):快Ｖ = SENSE_SEX_TARGET(MPLY:0, MTAR:0, 900)
SOURCE:(MTAR:0):露出 = 200
SOURCE:(MTAR:0):性行動 = 450

IF MPLY:0 == MASTER
	CALL ADD_SOURCE_AIZYOU(MTAR:0, 130)
ELSEIF MTAR:0 != MASTER
	SOURCE:(MTAR:0):露出 += 200
ENDIF

;性交系の共通処理
CALL COM_COMMON_SEX_V(MTAR:0, MPLY:0)

;快Ｃの補正
IF PALAM:(MTAR:0):潤滑 < PALAMLV:1
	TIMES SOURCE:(MTAR:0):快Ｃ, 0.30
ELSEIF PALAM:(MTAR:0):潤滑 < PALAMLV:3
	TIMES SOURCE:(MTAR:0):快Ｃ, 0.70
ELSEIF PALAM:(MTAR:0):潤滑 < PALAMLV:5
	TIMES SOURCE:(MTAR:0):快Ｃ, 1.00
ELSEIF PALAM:(MTAR:0):潤滑 < PALAMLV:7
	TIMES SOURCE:(MTAR:0):快Ｃ, 1.20
ELSEIF PALAM:(MTAR:0):潤滑 >= PALAMLV:7
	TIMES SOURCE:(MTAR:0):快Ｃ, 1.40
ENDIF

IF MPLY:0 == MASTER
	IF CFLAG:(MTAR:0):2 <= 100
		TIMES SOURCE:(MTAR:0):快Ｃ, 0.50
	ELSEIF CFLAG:(MTAR:0):2 <= 300
		TIMES SOURCE:(MTAR:0):快Ｃ, 0.70
	ELSEIF CFLAG:(MTAR:0):2 <= 500
		TIMES SOURCE:(MTAR:0):快Ｃ, 0.80
	ELSEIF CFLAG:(MTAR:0):2 <= 800
		TIMES SOURCE:(MTAR:0):快Ｃ, 1.00
	ELSEIF CFLAG:(MTAR:0):2 <= 1500
		TIMES SOURCE:(MTAR:0):快Ｃ, 1.30
	ELSE
		SOURCE:(MTAR:0):快Ｃ = SOURCE:(MTAR:0):快Ｃ * ((CFLAG:(MTAR:0):2 - 1500) / 200 + 150) / 100
	ENDIF
ENDIF

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 150, 100)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MTAR:0):4 = 1

;奴隷のＶ⇔調教者のＰの汚れが移動
STAIN:(MTAR:0):3 |= STAIN:(MPLY:0):2
STAIN:(MPLY:0):2 |= STAIN:(MTAR:0):3

;射精箇所と対象をセット(ターゲット側)
CALL STACK_SPERM(MTAR:0, MPLY:0, 0)

;体位フラグのセット
TCVAR:(MPLY:0):31 = 3
TCVAR:(MTAR:0):33 = 3

;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = -1

;レズ・ＢＬ経験基準値
TFLAG:51 = 7

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP32
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP32(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;ソースを退避
CALL PUTOUT_SOURCE(LOCAL:2)
CALL PUTOUT_SOURCE(LOCAL:3)

DOWNBASE:(LOCAL:2):体力 += 20

EXP:(LOCAL:2):性技経験値 += 1
EXP:(LOCAL:2):性交経験値 += 1

SOURCE:(LOCAL:2):快Ｐ += SENSE_SEX_PLAYER_P(LOCAL:3, LOCAL:2, 450)
SOURCE:(LOCAL:2):露出 += 25
SOURCE:(LOCAL:2):性行動 += 150

;性交系の共通処理
CALL COM_COMMON_SEX_P(LOCAL:2, LOCAL:3)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

;倒錯度変化基準値
TCVAR:(LOCAL:2):50 -= 1

DOWNBASE:(LOCAL:3):体力 += 20

EXP:(LOCAL:3):性技経験値 += 1
EXP:(LOCAL:3):性交経験値 += 1

SOURCE:(LOCAL:3):快Ｃ += SENSE_SEX_TARGET(LOCAL:2, LOCAL:3, 150)
SOURCE:(LOCAL:3):快Ｖ += SENSE_SEX_TARGET(LOCAL:2, LOCAL:3, 450)
SOURCE:(LOCAL:3):露出 += 100
SOURCE:(LOCAL:3):性行動 += 150

;性交系の共通処理
CALL COM_COMMON_SEX_V(LOCAL:3, LOCAL:2)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:3):4 = 1

;倒錯度変化基準値
TCVAR:(LOCAL:3):50 -= 1

;退避したソースを加算
CALL SUM_SOURCE(LOCAL:2)
CALL SUM_SOURCE(LOCAL:3)

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE32(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%の膣に対面座位で挿入中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP32(ARG:0)
	SELECTCASE RAND:2
		CASE 0
			PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%の膣が%EQUIP_PLAYER_ANAME(ARG:0)%の%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を咥え込んでいる…
		CASE 1
			LOCAL:0 = MEQUIP_TARGET:(ARG:0):0
			;ターゲットが行動不能
			IF !IS_PLAYABLE(LOCAL:0)
				PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は気絶した%EQUIP_TARGET_ANAME(ARG:0)%を抱きしめたまま、激しく腰を突き上げている…
			ELSE
				SELECTCASE GET_COM_INITIATIVE()
					;プレイヤーに主導権
					CASE 0
						PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%を抱きしめたまま、さらに激しく腰を突き上げている…
					;ターゲットに主導権
					CASE 1
						PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%は%EQUIP_PLAYER_ANAME(ARG:0)%を抱きしめる腕に力を入れると、自ら快感を貪るように艶めかしく腰を振っている…
					;第三者に主導権
					CASEELSE
						PRINTFORML %ANAME(TFLAG:45)%は%EQUIP_TARGET_ANAME(ARG:0)%に対面座位で挿入する%EQUIP_PLAYER_ANAME(ARG:0)%に対し、もっと激しく腰を動かすように命令し
						PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の体を抱きしめ直すと、さらに力を込めて%EQUIP_TARGET_ANAME(ARG:0)%の膣を突き上げている…
				ENDSELECT
			ENDIF
	ENDSELECT

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP32(ARG:0)
PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の膣内から%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を引き抜いた…

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER32(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 100

;膣性交共通
CALL COM_ORDER_VSEX_P(ARG:0)
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE32
IF IS_PIERCED(MTAR:0, ピアス_ラビア)
	LOCALS:0 = ピアスの取り付けられた
ELSE
	LOCALS:0 = 
ENDIF
IF TALENT:(MTAR:0):処女 == 1
	LOCALS:0 = まだ乙女の
ELSEIF PALAM:(MTAR:0):潤滑 >= PALAMLV:7
	LOCALS:0 = 濡れそぼった
ELSE
	LOCALS:0 = 
ENDIF
IF IS_OVULATION(MTAR:0)
	LOCALS:0 = %LOCALS:0%危険日の
ENDIF

IF TALENT:(MTAR:0):小柄体型
	LOCALS:0 = %LOCALS:0%幼いワレメ
ELSE
	LOCALS:0 = %LOCALS:0%秘裂
ENDIF

;ペニスバンド装着中
IF IS_EQUIP_PLAYER(MPLY:0, 50)
	;既に挿入済みの場合
	IF TFLAG:17
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				;前回が対面座位
				IF TFLAG:17 == 1
					PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を抱きしめたまま、さらに激しく腰を突き上げ%TARGET_STR("た", "てきた")%…
				;正常位・騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を抱きしめると、再び腰を動かし始めた…
				;後背位・背面座位・背面騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を正面に向けると、%ANAME(MTAR:0)%を抱きしめて腰を突き上げ%TARGET_STR("た", "てきた")%…
				ENDIF
			;ターゲットに主導権
			CASE 1
				;前回が対面座位
				IF TFLAG:17 == 1
					PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%を抱きしめる腕に力を入れると、自ら快感を貪るように艶めかしく腰を振っ%PLAYER_STR("た", "てきた")%…
				;正常位・騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORMW %ANAME(MTAR:0)%は挿入したまま%ANAME(MPLY:0)%の体を抱きしめると、密着したまま自ら快感を貪るように腰を動かし始めた…
				;後背位・背面座位・背面騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の正面に向き直りその体を抱きしめると、密着したまま自ら快感を貪るように腰を動かし始めた…
				ENDIF
			;第三者に主導権
			CASEELSE
				;前回が対面座位
				IF TFLAG:17 == 1
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に対面座位で挿入する%ANAME(MPLY:0)%に対し、もっと激しく腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の体を抱きしめ直すと、さらに力を込めて%ANAME(MTAR:0)%の膣を突き上げ始めた…
				;正常位・騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、体位を対面座位に変えて再び腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を抱きしめると、再び腰を動かし始めた…
				;後背位・背面座位・背面騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、体位を対面座位に変えて再び腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を正面に向けると、%ANAME(MTAR:0)%を抱きしめて腰を突き上げた…
				ENDIF
		ENDSELECT

	ELSE
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を抱きしめながら張り型を%LOCALS:0%へと挿入し、腰を突き上げ%TARGET_STR("た", "てきた")%…
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%を抱きしめながら張り型を自らの%LOCALS:0%で咥え込み、腰を動かした…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、対面座位で%ANAME(MTAR:0)%と交わるように命令した
				PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を抱きしめながら張り型を%LOCALS:0%へと挿入し、腰を突き上げた…
		ENDSELECT
	ENDIF

;ペニスバンド不使用
ELSE
	;既に挿入済みの場合
	IF TFLAG:17
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				;前回が対面座位
				IF TFLAG:17 == 1
					PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を抱きしめたまま、さらに激しく腰を突き上げ%TARGET_STR("た", "てきた")%…
				;正常位・騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を抱きしめると、再び腰を動かし始めた…
				;後背位・背面座位・背面騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を正面に向けると、%ANAME(MTAR:0)%を抱きしめて腰を突き上げ%TARGET_STR("た", "てきた")%…
				ENDIF
			;ターゲットに主導権
			CASE 1
				;前回が対面座位
				IF TFLAG:17 == 1
					PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%を抱きしめる腕に力を入れると、艶めかしく腰を振って%ANAME(MPLY:0)%のペニスを擦り上げ%PLAYER_STR("た", "てきた")%…
				;正常位・騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORMW %ANAME(MTAR:0)%は挿入したまま%ANAME(MPLY:0)%の体を抱きしめると、密着したまま腰を動かして%ANAME(MPLY:0)%のペニスを擦り上げ%PLAYER_STR("た", "てきた")%…
				;後背位・背面座位・背面騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%の正面に向き直りその体を抱きしめると、密着したまま腰を動かして%ANAME(MPLY:0)%のペニスを擦り上げ%PLAYER_STR("た", "てきた")%…
				ENDIF
			;第三者に主導権
			CASEELSE
				;前回が対面座位
				IF TFLAG:17 == 1
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MTAR:0)%に対面座位で挿入する%ANAME(MPLY:0)%に対し、もっと激しく腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の体を抱きしめ直すと、さらに力を込めて%ANAME(MTAR:0)%の膣を突き上げ始めた…
				;正常位・騎乗位
				ELSEIF TFLAG:17 == 2
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、体位を対面座位に変えて再び腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を抱きしめると、再び腰を動かし始めた…
				;後背位・背面座位・背面騎乗位
				ELSEIF TFLAG:17 == 3
					PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に対し、体位を対面座位に変えて再び腰を動かすように命令した
					PRINTFORMW %ANAME(MPLY:0)%は挿入したまま%ANAME(MTAR:0)%の体を正面に向けると、%ANAME(MTAR:0)%を抱きしめて腰を突き上げた…
				ENDIF
		ENDSELECT

	ELSE
		SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
			CASE 0
				PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を抱きしめながら%LOCALS:0%へと挿入し、腰を突き上げ%TARGET_STR("た", "てきた")%…
			;ターゲットに主導権
			CASE 1
				PRINTFORMW %ANAME(MTAR:0)%は%ANAME(MPLY:0)%を抱きしめながら腰を降ろして、%LOCALS:0%で%ANAME(MPLY:0)%のペニスを飲み込ん%TARGET_STR("だ", "できた")%…
			;第三者に主導権
			CASEELSE
				PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、対面座位で%ANAME(MTAR:0)%と交わるように命令した
				PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を抱きしめながら%LOCALS:0%へと挿入し、腰を突き上げた…
		ENDSELECT
	ENDIF
ENDIF

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST32
SIF IS_PIERCED(MPLY:0, ピアス_ペニス)
	PRINTFORML %ANAME(MPLY:0)%のペニスに取り付けられたピアスが、%ANAME(MTAR:0)%の膣内をごりごりと抉っていく……

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN32
RETURN コマンド_ウフフ
