;自国に誘う

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME551
RESULTS:0 = 自国に誘う

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE551
;共通部分
CALL COM_ABLE_COMMON(551)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;主人公がプレイヤーのとき限定
SIF MPLY:0 != MASTER
	RETURN 0
;自国の仕官は不可
SIF CFLAG:(MTAR:0):所属 == CFLAG:(MPLY:0):所属
	RETURN 0
;会いに行く・閨に呼ぶ・捕虜会話限定
SIF !GROUPMATCH(FLAG:調教モード, 0, 1, 6)
	RETURN 0
;主人公がどこかの勢力に所属している場合のみ
SIF FLAG:調教モード == 6 && CFLAG:(MPLY:0):所属 == 0
	RETURN 0
;プレイヤーの口が塞がっているなら不可
SIF IS_M_HOLD(MPLY:0)
	RETURN 0
;特殊キャラは不可
SIF IS_SP_CHARA(MTAR:0)
	RETURN 0
;慰安モブも不可
SIF TALENT:(MTAR:0):慰安モブ
	RETURN 0
;相手が君主なら不可
FOR LOCAL:0, 0, MAX_COUNTRY
	SIF COUNTRY_BOSS:(LOCAL:0) == GET_ID(MTAR:0)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM551
;●実行値の計算
CVARSET TCVAR, 24, -10000
CVARSET TCVAR, 25, 10000

;ターゲットが自国の捕虜の場合
IF CFLAG:(MTAR:0):捕虜先 == CFLAG:(MPLY:0):所属
	TCVAR:(MTAR:0):25 = 30
;それ以外の場合
ELSE
	TCVAR:(MTAR:0):25 = 80
ENDIF

CALL COM_ORDER_BASE(MTAR:0)

IF TALENT:(MTAR:0):反抗的
	CALL COM_ORDER_ELEMENT(MTAR:0, "反抗的", -5)
ENDIF
IF TALENT:(MTAR:0):気丈
	CALL COM_ORDER_ELEMENT(MTAR:0, "気丈", -5)
ENDIF
IF TALENT:(MTAR:0):素直
	CALL COM_ORDER_ELEMENT(MTAR:0, "素直", 5)
ENDIF
IF TALENT:(MTAR:0):プライド高い
	CALL COM_ORDER_ELEMENT(MTAR:0, "プライド高い", -15)
ENDIF
IF TALENT:(MTAR:0):プライド低い
	CALL COM_ORDER_ELEMENT(MTAR:0, "プライド低い", 5)
ENDIF
IF TALENT:(MTAR:0):好奇心
	CALL COM_ORDER_ELEMENT(MTAR:0, "好奇心", 5)
ENDIF
IF TALENT:(MTAR:0):保守的
	CALL COM_ORDER_ELEMENT(MTAR:0, "保守的", -8)
ENDIF
IF TALENT:(MTAR:0):孤高
	CALL COM_ORDER_ELEMENT(MTAR:0, "孤高", -(20 * KOKOU_RATE(MTAR:0) / 100))
ENDIF
IF TALENT:(MTAR:0):一線越えない
	CALL COM_ORDER_ELEMENT(MTAR:0, "一線越えない", -15)
ENDIF
IF GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_淫乱) && FLAG:ウフフフラグ
	CALL COM_ORDER_ELEMENT(MTAR:0, "淫乱", 5)
ENDIF

IF TALENT:(MTAR:0):親愛
	CALL COM_ORDER_ELEMENT(MTAR:0, "親愛", 50)
ELSEIF TALENT:(MTAR:0):恋慕
	CALL COM_ORDER_ELEMENT(MTAR:0, "恋慕", 20)
ELSEIF TALENT:(MTAR:0):親友
	CALL COM_ORDER_ELEMENT(MTAR:0, "親友", 20)
ENDIF

IF TALENT:(MTAR:0):恋人
	CALL COM_ORDER_ELEMENT(MTAR:0, "恋人", 10)
ENDIF

IF TALENT:(MTAR:0):隷属
	CALL COM_ORDER_ELEMENT(MTAR:0, "隷属", 60)
ELSEIF TALENT:(MTAR:0):服従
	CALL COM_ORDER_ELEMENT(MTAR:0, "服従", 30)
ENDIF

IF TALENT:(MTAR:0):烙印
	CALL COM_ORDER_ELEMENT(MTAR:0, "烙印", 10)
ENDIF

IF TALENT:(MTAR:0):所有者
	CALL COM_ORDER_ELEMENT(MTAR:0, "所有者", 25)
ELSEIF TALENT:(MTAR:0):主人
	CALL COM_ORDER_ELEMENT(MTAR:0, "主人", 10)
ENDIF


LOCAL:0 = GET_PALAMLV(PALAM:(MTAR:0):欲情)
IF LOCAL:0 >= 1
	CALL COM_ORDER_ELEMENT(MTAR:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 30))
ENDIF

IF TALENT:(MTAR:0):プライド高い
	LOCAL:0 = 4
ELSEIF TALENT:(MTAR:0):プライド低い
	LOCAL:0 = 1
ELSE
	LOCAL:0 = 2
ENDIF

IF MARK:(MTAR:0):既成事実 >= 1
	CALL COM_ORDER_ELEMENT(MTAR:0, @"既成事実Lv{MARK:(MTAR:0):既成事実}", MARK:(MTAR:0):既成事実 * 3 * LOCAL:0)
ENDIF

IF FLAG:調教モード == 6
	CALL COM_ORDER_ELEMENT(MTAR:0, "捕虜会話", -180)
ELSEIF CFLAG:(MTAR:0):所属 != 0
	;CALL COM_ORDER_ELEMENT(MTAR:0, "仕えている君主がいる", -45)
	;今の君主への好印象-悪印象を10で割ったもの
	LOCAL:0 = (REL_LIKE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):所属)) - REL_HATE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):所属))) / 10
	IF LOCAL:0 >= 0
		CALL COM_ORDER_ELEMENT(MTAR:0, @"今の君主の%ANAME(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):所属))%に好印象", -(LOCAL:0))
	ELSEIF LOCAL:0 <= 0
		CALL COM_ORDER_ELEMENT(MTAR:0, @"今の君主の%ANAME(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):所属))%に悪印象", -(LOCAL:0))
	ENDIF
ENDIF

;誘われた勢力の君主への好印象-悪印象を10で割ったもの
LOCAL:0 = (REL_LIKE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:MASTER:所属)) - REL_HATE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))) / 10
IF LOCAL:0 >= 0
	CALL COM_ORDER_ELEMENT(MTAR:0, @"君主の%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%に好印象", LOCAL:0)
ELSEIF LOCAL:0 <= 0
	CALL COM_ORDER_ELEMENT(MTAR:0, @"君主の%ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:所属))%に悪印象", LOCAL:0)
ENDIF

IF CFLAG:(MTAR:0):2 < 300
	CALL COM_ORDER_ELEMENT(MTAR:0, "好感度不足", -20)
ELSEIF CFLAG:(MTAR:0):2 < 500
	CALL COM_ORDER_ELEMENT(MTAR:0, "好感度不足", -10)
ELSEIF CFLAG:(MTAR:0):2 < 800
	CALL COM_ORDER_ELEMENT(MTAR:0, "好感度不足", -5)
ENDIF

;判定結果を表示
PRINTFORMW  = {TCVAR:(MTAR:0):24} %TOSTR_COMPARE(TCVAR:(MTAR:0):24, TCVAR:(MTAR:0):25)% 実行値{TCVAR:(MTAR:0):25}

;判定成功
IF TCVAR:(MTAR:0):24 >= TCVAR:(MTAR:0):25
	;捕虜会話の場合
	IF FLAG:調教モード == 6
		;口上表示
		CALL KOJO_EVENT(MTAR:0, 54)
		PRINTL 

		PRINTFORML %ANAME(MASTER)%は%ANAME(MTAR:0)%の手引で牢屋を抜け出しました
		PRINTFORMW %ANAME(MTAR:0)%が我が国の士官になりました
		CALL CHANGE_COUNTRY(MTAR:0, CFLAG:MASTER:所属, 1)
		CALL CAPTURE(MASTER, 0)
		;強制終了フラグを立てる
		TFLAG:38 = 1

	ELSE
		;口上表示
		CALL KOJO_EVENT(MTAR:0, 52)
		PRINTL 

		;自国の捕虜ないし放浪中の場合
		IF CFLAG:(MTAR:0):捕虜先 == CFLAG:MASTER:所属 || CFLAG:(MTAR:0):特殊状態 == 特殊状態_放浪
			PRINTFORMW %ANAME(MTAR:0)%が我が国の士官になりました
			CALL CHANGE_COUNTRY(MTAR:0, CFLAG:MASTER:所属, 1)
		ELSE
			CALL CHECK_COUNTRY_RELATION(CFLAG:MASTER:所属, CFLAG:(MTAR:0):所属)

			;永久同盟中の国家に所属している場合
			IF RESULT == 4
				PRINTFORMW %ANAME(MTAR:0)%が我が国の士官になりました
				CALL CHANGE_COUNTRY(MTAR:0, CFLAG:MASTER:所属, 1)

			;それ以外の場合
			ELSE
				PRINTFORMW %ANAME(MTAR:0)%は我が国の士官となることを約束しました
				PRINTFORMW 同盟・連合が解除され次第、%ANAME(MASTER)%の国へ移動します
				;移動フラグを立てておく
				CFLAG:(MTAR:0):自国勧誘成功フラグ = 1
			ENDIF
		ENDIF
	ENDIF

;判定失敗
ELSE
	;捕虜会話の場合
	IF FLAG:調教モード == 6
		;口上表示
		CALL KOJO_EVENT(MTAR:0, 55)

	ELSE
		;口上表示
		CALL KOJO_EVENT(MTAR:0, 53)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN551
RETURN コマンド_共通
