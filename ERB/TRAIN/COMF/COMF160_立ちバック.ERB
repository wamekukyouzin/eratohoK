;屈曲位

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME160
LOCALS:0 = 立ちバック
RESULTS:0 = %LOCALS:0%で挿入
RESULTS:1 = %LOCALS:0%で挿入させられる
RESULTS:2 = %LOCALS:0%で挿入させる
RESULTS:3 = %LOCALS:0%で挿入される
RESULTS:4 = %LOCALS:0%で挿入させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE160
;共通部分
CALL COM_ABLE_COMMON(160)
SIF RESULT == 0
	RETURN 0
;プレイヤーは最大で1人まで
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;ターゲットは最大で1人まで
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;対象にＶが必要
SIF !HAS_VAGINA(MTAR:0)
	RETURN 0
SIF IS_V_HOLD(MTAR:0)
	RETURN 0
;already fucked in ass, unless by frontal standing
SIF IS_INSERT_SINGLE(MTAR:0, "Ａ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ａ"), 9)
	RETURN 0
SIF !CAN_FUCK(MPLY:0, MTAR:0, 160)
	RETURN 0
;player and target have to face each other
SIF LICKING_BODY(MPLY:0, MTAR:0) || LICKING_BODY(MTAR:0, MPLY:0)
	RETURN 0
;target has to face player
SIF REACHING_BODY(MTAR:0, MPLY:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM160
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;既に挿入中なら現在の体位を取得
TFLAG:17 = 0
IF IS_INSERT_MUTUAL(MPLY:0, MTAR:0) == 1
	SELECTCASE GET_SEX_POSITION(MPLY:0, MTAR:0)
		CASE 1
			TFLAG:17 = 1
		CASE 2
			TFLAG:17 = 3
		CASE 3
			TFLAG:17 = 2
		CASE 4
			TFLAG:17 = 3
		CASE 5
			TFLAG:17 = 2
		CASE 6
			TFLAG:17 = 3
		CASE 7
			TFLAG:17 = 3
		CASE 8
			TFLAG:17 = 1
	ENDSELECT
ENDIF
IF TFLAG:17 == 0
	SELECTCASE PREVCOM
		CASE 30
			TFLAG:17 = 1
		CASE 31
			TFLAG:17 = 3
		CASE 32
			TFLAG:17 = 2
		CASE 33
			TFLAG:17 = 3
		CASE 34
			TFLAG:17 = 2
		CASE 35
			TFLAG:17 = 3
		CASE 37
			TFLAG:17 = 3
		CASE 38
			TFLAG:17 = 1
	ENDSELECT
ENDIF

;挿入関係を一時リセット
CALL CLEAR_INSERT_FLAG(MPLY:0, "Ｐ")

;●プレイヤー側の処理
DOWNBASE:(MPLY:0):体力 += 160

EXP:(MPLY:0):性技経験値 += 1
EXP:(MPLY:0):性交経験値 += 2

SOURCE:(MPLY:0):快Ｐ = SENSE_SEX_PLAYER_P(MTAR:0, MPLY:0, 1500)
SOURCE:(MPLY:0):性行動 = 450

IF MTAR:0 == MASTER
	CALL ADD_SOURCE_AIZYOU(MPLY:0, 20)
ELSEIF MPLY:0 != MASTER
	SOURCE:(MPLY:0):露出 += 200
ENDIF

;性交系の共通処理
CALL COM_COMMON_SEX_P(MPLY:0, MTAR:0)

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 200, 80)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●ターゲット側の処理
DOWNBASE:(MTAR:0):体力 += 120

EXP:(MTAR:0):性技経験値 += 1
EXP:(MTAR:0):性交経験値 += 2

SOURCE:(MTAR:0):快Ｖ = SENSE_SEX_TARGET(MPLY:0, MTAR:0, 1500)
SOURCE:(MTAR:0):露出 = 400
SOURCE:(MTAR:0):性行動 = 450

IF MPLY:0 == MASTER
	CALL ADD_SOURCE_AIZYOU(MTAR:0, 20)
ELSEIF MTAR:0 != MASTER
	SOURCE:(MTAR:0):露出 += 200
ENDIF

;性交系の共通処理
CALL COM_COMMON_SEX_V(MTAR:0, MPLY:0)

;主導権に応じた優越・屈従のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 120, 180)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MTAR:0):4 = 1

;体位フラグのセット
TCVAR:(MPLY:0):31 = 2
TCVAR:(MTAR:0):33 = 2

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 1

;レズ・ＢＬ経験基準値
TFLAG:51 = 3

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP160
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP160(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;ソースを退避
CALL PUTOUT_SOURCE(LOCAL:2)
CALL PUTOUT_SOURCE(LOCAL:3)

DOWNBASE:(LOCAL:2):体力 += 20

EXP:(LOCAL:2):性技経験値 += 1
EXP:(LOCAL:2):性交経験値 += 1

SOURCE:(LOCAL:2):快Ｐ += SENSE_SEX_PLAYER_P(LOCAL:3, LOCAL:2, 750)
SOURCE:(LOCAL:2):性行動 += 150

;性交系の共通処理
CALL COM_COMMON_SEX_P(LOCAL:2, LOCAL:3)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

;倒錯度変化基準値
TCVAR:(LOCAL:2):50 += 1

DOWNBASE:(LOCAL:3):体力 += 20

EXP:(LOCAL:3):性技経験値 += 1
EXP:(LOCAL:3):性交経験値 += 1

SOURCE:(LOCAL:3):快Ｖ += SENSE_SEX_TARGET(LOCAL:2, LOCAL:3, 750)
SOURCE:(LOCAL:3):露出 += 200
SOURCE:(LOCAL:3):性行動 += 150

;性交系の共通処理
CALL COM_COMMON_SEX_V(LOCAL:3, LOCAL:2)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:3):4 = 1

;倒錯度変化基準値
TCVAR:(LOCAL:3):50 += 1

;退避したソースを加算
CALL SUM_SOURCE(LOCAL:2)
CALL SUM_SOURCE(LOCAL:3)

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE160(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%の膣に立ちバックで挿入中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP160(ARG:0)
IF TALENT:(MEQUIP_TARGET:(ARG:0):0):処女 == 1
	LOCALS:0 = まだ乙女の
ELSEIF PALAM:(MEQUIP_TARGET:(ARG:0):0):潤滑 >= PALAMLV:7
	LOCALS:0 = 濡れそぼった
ELSE
	LOCALS:0 = 
ENDIF
IF IS_OVULATION(MEQUIP_TARGET:(ARG:0):0)
	LOCALS:0 = %LOCALS:0%危険日の
ENDIF

IF TALENT:(MEQUIP_TARGET:(ARG:0):0):体格 == 体格_小柄
	LOCALS:0 = %LOCALS:0%幼いワレメ
ELSE
	LOCALS:0 = %LOCALS:0%秘裂
ENDIF
SELECTCASE RAND:2
	CASE 0
		PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%の膣が%EQUIP_PLAYER_ANAME(ARG:0)%の%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を咥え込んでいる…
	CASE 1
		LOCAL:0 = MEQUIP_TARGET:(ARG:0):0
			;ターゲットが行動不能
		IF !IS_PLAYABLE(LOCAL:0)
			SELECTCASE RAND:2
				CASE 0
					PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は気絶した%EQUIP_TARGET_ANAME(ARG:0)%を無理やり立たせ、膣を後ろから激しく突いている…
				CASE 1
					PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%はぐったりしている%EQUIP_TARGET_ANAME(ARG:0)%を無理やり立たせ、%LOCALS:0%に後ろから何度も腰を打ち付けている…
			ENDSELECT
		ELSE
			SELECTCASE GET_COM_INITIATIVE()
					;プレイヤーに主導権
				CASE 0
					SELECTCASE RAND:2
						CASE 0
							PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は立ち上がり尻を突き出した%EQUIP_TARGET_ANAME(ARG:0)%の腰を掴み、激しく膣奥を突いている…
						CASE 1
							PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は後ろから%EQUIP_TARGET_ANAME(ARG:0)%の%LOCALS:0%に突き入れ、何度も腰を打ち付けている…
					ENDSELECT
					;ターゲットに主導権
				CASE 1
					PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%は%EQUIP_PLAYER_ANAME(ARG:0)%に見せつけるように尻を振りながら、もっと激しく腰を動かすように要求し
					SELECTCASE RAND:2
						CASE 0
							PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の腰を掴みなおすと、後ろから何度も腰を打ち付けて%EQUIP_TARGET_ANAME(ARG:0)%の膣奥を突いている…
						CASE 1
							PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の腰を掴みなおすと、後ろから何度も腰を打ち付け%LOCALS:0%に突き入れている…
					ENDSELECT
					;第三者に主導権
				CASEELSE
					PRINTFORML %ANAME(TFLAG:45)%は%EQUIP_TARGET_ANAME(ARG:0)%に立ちバックで挿入している%EQUIP_PLAYER_ANAME(ARG:0)%に対し、もっと激しく腰を動かすように命令し
					SELECTCASE RAND:2
						CASE 0
							PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は言われるまま腰の動きを激しくし、後ろから何度も%EQUIP_TARGET_ANAME(ARG:0)%の膣奥を突いている…
						CASE 1
							PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は言われるまま腰の動きを激しくし、後ろから何度も%EQUIP_TARGET_ANAME(ARG:0)%の%LOCALS:0%に突き入れている…
					ENDSELECT
			ENDSELECT
		ENDIF
ENDSELECT

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP160(ARG:0)
PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%は%EQUIP_TARGET_ANAME(ARG:0)%の膣内から%BAR_NAME(MEQUIP_PLAYER:(ARG:0):0)%を引き抜いた…

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER160(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 90

;膣性交共通
CALL COM_ORDER_VSEX_P(ARG:0)
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE160
IF IS_PIERCED(MTAR:0, ピアス_ラビア)
	LOCALS:0 = ピアスの取り付けられた
ELSE
	LOCALS:0 = 
ENDIF
IF TALENT:(MTAR:0):処女 == 1
	LOCALS:0 = まだ乙女の
ELSEIF PALAM:(MTAR:0):潤滑 >= PALAMLV:7
	LOCALS:0 = 濡れそぼった
ELSE
	LOCALS:0 = 
ENDIF
IF IS_OVULATION(MTAR:0)
	LOCALS:0 = %LOCALS:0%危険日の
ENDIF

IF TALENT:(MTAR:0):体格 == 体格_小柄
	LOCALS:0 = %LOCALS:0%幼いワレメ
ELSE
	LOCALS:0 = %LOCALS:0%秘裂
ENDIF


IF TFLAG:17 == 1 && (PALAM:(MTAR:0):欲情 >= PALAMLV:10 || (PALAM:(MTAR:0):欲情 >= PALAMLV:7 && TCVAR:(MTAR:0):60)) && ABL:(MTAR:0):Ｖ感 >= ランク閾値:ランク_その他:ランク_C
	SELECTCASE RAND:2
		CASE 0
			PRINTFORML %ANAME(MTAR:0)%は情欲に流されるまま、おねだりするように%STR_BODY("尻：外見：修飾", MTAR:0)%お尻を突き出している…
		CASE 1
			PRINTFORML %ANAME(MTAR:0)%は立ったままで%BAR_NAME(MPLY:0)%を突き込まれながら、発情しきった雌そのものの顔で喘いでいる…
	ENDSELECT
ENDIF


;ペニスバンド装着中
IF IS_EQUIP_PLAYER(MPLY:0, 50)
	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は立たせた%ANAME(MTAR:0)%の%LOCALS:0%に張り型を後ろから突き入れ、何度も腰を打ち付け%TARGET_STR("た", "てきた")%…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%は立ったまま%ANAME(MPLY:0)%に%STR_BODY("尻：外見", MTAR:0)%を向け、後ろから%LOCALS:0%に張り型を挿入させた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、立ちバックで%ANAME(MTAR:0)%を犯すように命令した
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を立たせて尻を向けさせると、装着した張り型を%ANAME(MTAR:0)%の%LOCALS:0%に突き入れて腰を振った…
	ENDSELECT
;ペニスバンド不使用
ELSE
	SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は立たせた%ANAME(MTAR:0)%の%LOCALS:0%に後ろからペニスを突き入れ、何度も腰を打ち付け%TARGET_STR("た", "てきた")%…
			;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%は立ったまま%ANAME(MPLY:0)%に%STR_BODY("尻：外見", MTAR:0)%を向け、後ろから%LOCALS:0%にペニスを挿入させた…
			;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、立ちバックで%ANAME(MTAR:0)%を犯すように命令した
			PRINTFORMW %ANAME(MPLY:0)%は%ANAME(MTAR:0)%を立たせて尻を向けさせると、%ANAME(MTAR:0)%の%LOCALS:0%にペニスを突き入れて腰を振った…
	ENDSELECT
ENDIF


;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST160
SIF IS_PIERCED(MPLY:0, ピアス_ペニス)
	PRINTFORML %ANAME(MPLY:0)%のペニスに取り付けられたピアスが、%ANAME(MTAR:0)%の膣内をごりごりと抉っていく……

IF NOWEX:(MTAR:0):Ｖ絶頂 >= 8
	SELECTCASE RAND:2
		CASE 0
			PRINTFORML %ANAME(MTAR:0)%は崩れそうになる膝をどうにか支えながら、全身をビクビクと震わせている…
		CASE 1
			PRINTFORML %ANAME(MTAR:0)%は激しい絶頂の余韻を味わいながら、%BAR_NAME(MPLY:0)%を咥え込んだ尻を淫らにくねらせている…
	ENDSELECT
ENDIF

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN160
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_160()
RETURN POWER(2, 性的嗜好_性交したい)

@COM_PREFERENCE_TARGET_160()
RETURN POWER(2, 性的嗜好_性交されたい)

@COM_STACK_SPERM_MPLY_TO_MTAR_160()
RETURN 射精部位_膣内
