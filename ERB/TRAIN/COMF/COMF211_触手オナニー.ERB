;触手オナニー

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME211
LOCALS:0 = 触手オナニー

RESULTS:0 = %LOCALS:0%見せつけ
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%を見せつけられる
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE211
;共通部分
CALL COM_ABLE_COMMON(211)
SIF RESULT == 0
	RETURN 0
;プレイヤーは1人以上
SIF MPLY_NUM <= 0
	RETURN 0
;ターゲットは1人以上
SIF MTAR_NUM <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;多対多は不可
SIF MPLY_NUM >= 2 && MTAR_NUM >= 2
	RETURN 0
;全てのプレイヤーについて判定
FOR LOCAL:0, 0, MPLY_NUM
	;触手召喚中でないなら不可
	SIF !IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 200)
		RETURN 0
	;拘束中なら不可
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
	;顔面騎乗中なら不可
	SIF IS_RIDE(MPLY:(LOCAL:0))
		RETURN 0
	;ペニスを挿入中なら不可
	SIF PENIS(MPLY:(LOCAL:0)) && IS_INSERT_SINGLE(MPLY:(LOCAL:0), "Ｐ")
		RETURN 0
	;ペニスへの奉仕を受けているなら不可
	SIF IS_EQUIP_TARGET(MPLY:(LOCAL:0), 10, 11, 12, 13, 14, 15, 16, 17) || IS_EQUIP_TARGET(MPLY:(LOCAL:0), 103, 106, 107, 205, 206)
		RETURN 0
	;ペニスがなく、クリへの奉仕を受けているなら不可
	SIF !PENIS(MPLY:(LOCAL:0)) && (IS_EQUIP_TARGET(MPLY:(LOCAL:0), 2, 60, 63, 203) || IS_EQUIP(MPLY:(LOCAL:0), 21))
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM211
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;各プレイヤーの絡む自慰・触手オナニーの継続状態を解除
FOR LOCAL:0, 0, MPLY_NUM
	CALL RELEASE_EQUIP_EX(100, MPLY:(LOCAL:0), -1)
	CALL RELEASE_EQUIP_EX(211, MPLY:(LOCAL:0), -1)
NEXT

;●全てのプレイヤーについて処理
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	EXP:(LOCAL:2):性技経験値 += 1
	EXP:(LOCAL:2):妖術経験値 += 1
	EXP:(LOCAL:2):触手経験値 += 2
	EXP:(LOCAL:2):自慰経験値 += 3

	SOURCE:(LOCAL:2):露出 = 1200 + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 600)
	SOURCE:(LOCAL:2):触手 = 300 + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 150)
	SOURCE:(LOCAL:2):逸脱 = 300 - (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 150)
	SOURCE:(LOCAL:2):性行動 = 240 + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 120)

	LOCAL:3 = 400 + ABL:(LOCAL:2):妖術 * 4

	SELECTCASE ABL:(LOCAL:2):自慰
		CASE 0
			TIMES LOCAL:3, 1.00
		CASE 1
			TIMES LOCAL:3, 1.20
		CASE 2
			TIMES LOCAL:3, 1.50
		CASE 3
			TIMES LOCAL:3, 1.80
		CASE 4
			TIMES LOCAL:3, 2.00
		CASEELSE
			LOCAL:3 = LOCAL:3 * (420 + (ABL:(LOCAL:2):自慰 - 5)) / 200
	ENDSELECT

	IF PENIS(LOCAL:2)
		SOURCE:(LOCAL:2):快Ｐ = SENSE_HOUSHI_P(LOCAL:2, LOCAL:2, LOCAL:3 * 3 / 2) + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 200)
	ELSE
		SOURCE:(LOCAL:2):快Ｃ = SENSE_HOUSHI(LOCAL:2, LOCAL:2, LOCAL:3) + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 200)
		SOURCE:(LOCAL:2):快Ｂ = SENSE_HOUSHI(LOCAL:2, LOCAL:2, LOCAL:3 / 2) + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 200)
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 80, 120)

	FOR LOCAL:1, 0, MTAR_NUM
		;射精箇所と対象をセット
		CALL STACK_SPERM(LOCAL:2, MTAR:(LOCAL:1), 0)
	NEXT
NEXT

;主導権の所在を取得
LOCAL:11 = GET_COM_INITIATIVE()

;●全てのターゲットについて処理
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	SOURCE:(LOCAL:1):触手 = 50
	SOURCE:(LOCAL:1):性行動 = 120

	;ターゲットに主導権
	IF LOCAL:11 == 1 || LOCAL:11 == 2
		SOURCE:(LOCAL:1):嗜虐 = 60
	;ターゲットに主導権がない
	ELSE
		SOURCE:(LOCAL:1):逸脱 = 150
		SOURCE:(LOCAL:1):恐怖 = MAX(200 - 30 * ABL:(LOCAL:1):欲望 - 60 * ABL:(LOCAL:1):触手, 0)
	ENDIF

	;主導権に応じた優越・屈従のソース追加
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 90, 90)
NEXT

;主導度変化基準値
TFLAG:49 = 3

;倒錯度変化基準値
TFLAG:50 = 4

;レズ・ＢＬ経験基準値
TFLAG:51 = 1

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP211
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP211(ARG:0)
;●全プレイヤーについて処理
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	EXP:(LOCAL:2):自慰経験値 += 1

	SOURCE:(LOCAL:2):露出 += 600 + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 300)
	SOURCE:(LOCAL:2):触手 += 100 + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 50)
	SOURCE:(LOCAL:2):逸脱 += 100 - (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 50)
	SOURCE:(LOCAL:2):性行動 += 80 + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 40)

	LOCAL:3 = 400 + ABL:(LOCAL:2):妖術 * 4

	SELECTCASE ABL:(LOCAL:2):自慰
		CASE 0
			TIMES LOCAL:3, 1.00
		CASE 1
			TIMES LOCAL:3, 1.20
		CASE 2
			TIMES LOCAL:3, 1.50
		CASE 3
			TIMES LOCAL:3, 1.80
		CASE 4
			TIMES LOCAL:3, 2.00
		CASEELSE
			LOCAL:3 = LOCAL:3 * (420 + (ABL:(LOCAL:2):自慰 - 5)) / 200
	ENDSELECT

	IF PENIS(LOCAL:2)
		SOURCE:(LOCAL:2):快Ｐ = SENSE_HOUSHI_P(LOCAL:2, LOCAL:2, LOCAL:3 * 3 / 2)  + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 200)
	ELSE
		SOURCE:(LOCAL:2):快Ｃ = SENSE_HOUSHI(LOCAL:2, LOCAL:2, LOCAL:3) + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 200)
		SOURCE:(LOCAL:2):快Ｂ = SENSE_HOUSHI(LOCAL:2, LOCAL:2, LOCAL:3 / 2) + (GETBIT(TALENT:(LOCAL:2):淫乱系, 素質_淫乱_苗床) * 200)
	ENDIF

	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		;射精箇所と対象をセット(上書きしない)
		CALL STACK_SPERM(MEQUIP_TARGET:(ARG:0):(LOCAL:1), LOCAL:2, 0, 0)
	NEXT
NEXT

;●全ターゲットについて処理
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	SOURCE:(LOCAL:2):性行動 += 40
NEXT

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE211(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が触手オナニー中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP211(ARG:0)
LOCAL:1 = 1
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	IF !PENIS(MEQUIP_PLAYER:(ARG:0):(LOCAL:0))
		LOCAL:1 = 0
	ENDIF
NEXT

;全員にペニスがある
IF LOCAL:1
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は自ら触手を操ってペニスを擦り上げている…
ELSE
	PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は自ら触手を操ってオナニーしている…
ENDIF


;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP211(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER211(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 115

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 3)
CALL COM_ORDER_ELEMENT(ARG:0, @"露出Lv{ABL:(ARG:0):露出}", ABL:(ARG:0):露出 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"自慰Lv{ABL:(ARG:0):自慰}", ABL:(ARG:0):自慰 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"触手Lv{ABL:(ARG:0):触手}", ABL:(ARG:0):触手 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -12)
ENDIF
IF TALENT:(ARG:0):恥薄い
	CALL COM_ORDER_ELEMENT(ARG:0, "恥薄い", 8)
ENDIF
IF TALENT:(ARG:0):自制心
	CALL COM_ORDER_ELEMENT(ARG:0, "自制心", -6)
ENDIF
IF TALENT:(ARG:0):目立ちたがり
	CALL COM_ORDER_ELEMENT(ARG:0, "目立ちたがり", 6)
ENDIF
IF TALENT:(ARG:0):自慰しやすい
	CALL COM_ORDER_ELEMENT(ARG:0, "自慰しやすい", 10)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -2)
ENDIF

IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド) || GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_マゾ) || ABL:(ARG:0):倒錯度 >= 800
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 32)
ELSEIF ABL:(ARG:0):倒錯度 >= 500
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 24)
ELSEIF ABL:(ARG:0):倒錯度 >= 300
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 16)
ELSEIF ABL:(ARG:0):倒錯度 >= 100
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 8)
ELSE
	CALL COM_ORDER_ELEMENT(ARG:0, "倒錯度", 0)
ENDIF

IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE211
;プレイヤーが竿あり・竿なし混合かどうかを判定
LOCAL:1 = 0
FOR LOCAL:0, 1, MPLY_NUM
	IF PENIS(MPLY:(LOCAL:0)) != PENIS(MPLY:0)
		LOCAL:0 = 1
		BREAK
	ENDIF
NEXT

IF MTAR_NUM == 1
	LOCALS:1 = %ANAME(MTAR:0)%
ELSEIF MTAR_NUM == 2
	LOCALS:1 = %ANAME(MTAR:0)%と%ANAME(MTAR:1)%
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%たち
ENDIF

IF MPLY_NUM == 1
	LOCALS:2 = %ANAME(MPLY:0)%
ELSEIF MPLY_NUM == 2
	LOCALS:2 = %ANAME(MPLY:0)%と%ANAME(MPLY:1)%
ELSE
	LOCALS:2 = %ANAME(MPLY:0)%たち
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		IF LOCAL:1
			PRINTFORMW %LOCALS:2%は、自らの触手を操ってオナニーする様子を%LOCALS:1%に見せつけ%TARGET_STR("た", "てきた")%…
		ELSEIF PENIS(MPLY:0)
			PRINTFORMW %LOCALS:2%は、自らの触手をペニスに巻きつけて擦り上げる様子を%LOCALS:1%に見せつけ%TARGET_STR("た", "てきた")%…
		ELSE
			PRINTFORMW %LOCALS:2%は、自らの触手を自身の体に絡めてオナニーする様子を%LOCALS:1%に見せつけ%TARGET_STR("た", "てきた")%…
		ENDIF
	;ターゲットに主導権
	CASE 1
		PRINTFORML %LOCALS:1%は%LOCALS:2%に、自分の触手でオナニーするように命令した
		IF LOCAL:1
			PRINTFORMW %LOCALS:2%は言われるまま、%LOCALS:1%の目の前で自らの触手を使ってオナニーした…
		ELSEIF PENIS(MPLY:0)
			PRINTFORMW %LOCALS:2%は言われるまま、%LOCALS:1%の目の前で自らの触手をペニスに巻きつけてシゴき上げた…
		ELSE
			PRINTFORMW %LOCALS:2%は言われるまま、%LOCALS:1%の目の前で自らの触手を自身の体に絡めてオナニーした…
		ENDIF
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%LOCALS:2%に、%LOCALS:1%の目の前で触手オナニーするように命令した
		IF LOCAL:1
			PRINTFORMW %LOCALS:2%は言われるまま、自らの触手を使ってオナニーした…
		ELSEIF PENIS(MPLY:0)
			PRINTFORMW %LOCALS:2%は言われるまま、自らの触手をペニスに巻きつけてシゴき上げた…
		ELSE
			PRINTFORMW %LOCALS:2%は言われるまま、自らの触手を自身の体に絡めてオナニーした…
		ENDIF
ENDSELECT

;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST211
