;フェラチオ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME24
;挿入中

LOCALS:0 = 指フェラ

RESULTS:0 = %LOCALS:0%する
RESULTS:1 = %LOCALS:0%させられる
RESULTS:2 = %LOCALS:0%させる
RESULTS:3 = %LOCALS:0%される
RESULTS:4 = %LOCALS:0%させる
RESULTS:5 = %LOCALS:0%見せつけ

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE24
;共通部分
CALL COM_ABLE_COMMON(24)
SIF RESULT == 0
	RETURN 0
;1-1限定
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0

FOR LOCAL:0, 0, MPLY_NUM
	;気絶してたらダメ
	SIF !IS_PLAYABLE(MPLY:(LOCAL:0))
		RETURN 0
	;ターゲットの口が塞がっているなら不可
	SIF IS_M_HOLD(MPLY:(LOCAL:0))
		RETURN 0
NEXT

;both bound
SIF IS_BIND(MPLY:0) && IS_BIND(MTAR:0)
	RETURN 0

RETURN 1

;-------------------------------------------------
;快Ｍソースの倍率を取得する関数 ARG:0=PLAYERのキャラ番号
;-------------------------------------------------
@COM24_RATE_M(ARG:0)
#FUNCTION
LOCAL:5 = 1000
SELECTCASE ABL:(ARG:0):奉仕
	CASE 0
		TIMES LOCAL:5, 0.00
		;TIMES SOURCE:(LOCAL:2):不潔, 4.00
	CASE 1
		TIMES LOCAL:5, 0.10
		;TIMES SOURCE:(LOCAL:2):不潔, 2.50
	CASE 2
		TIMES LOCAL:5, 0.30
		;TIMES SOURCE:(LOCAL:2):不潔, 1.50
	CASE 3
		TIMES LOCAL:5, 0.80
		;TIMES SOURCE:(LOCAL:2):不潔, 1.00
	CASE 4
		TIMES LOCAL:5, 1.00
		;TIMES SOURCE:(LOCAL:2):不潔, 0.50
	CASEELSE
		LOCAL:5 = LOCAL:5 * (100 + (ABL:(ARG:0):奉仕 - 5) * 3) / 100
		;TIMES SOURCE:(LOCAL:2):不潔, 0.10
ENDSELECT

SELECTCASE ABL:(ARG:0):性技
	CASE 0
		TIMES LOCAL:5, 1.00
	CASE 1
		TIMES LOCAL:5, 1.10
	CASE 2
		TIMES LOCAL:5, 1.20
	CASE 3
		TIMES LOCAL:5, 1.30
	CASE 4
		TIMES LOCAL:5, 1.40
	CASEELSE
		LOCAL:5 = LOCAL:5 * ((ABL:(ARG:0):性技 - 5) * 2 + 150) / 100
ENDSELECT

RETURNF LOCAL:5

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM24
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0


;●全プレイヤーについて判定
DOWNBASE:(MPLY:0):体力 += 80

EXP:(MPLY:0):性技経験値 += 1
EXP:(MPLY:0):口淫経験 += 1

SOURCE:(MPLY:0):奉仕 = SERVE_HOUSHI(MPLY:0, 300)
SOURCE:(MPLY:0):接触 = 50
SOURCE:(MPLY:0):快Ｍ = 400 * COM24_RATE_M(MPLY:0) / 1000
SOURCE:(MPLY:0):性行動 = 200

;主導権に応じた優越・受動のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 100, 100)

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(MPLY:0):4 = 1

;●全ターゲットについて判定

;●ソースの計算
DOWNBASE:(MTAR:0):体力 += 20

SOURCE:(MTAR:0):接触 = 30
SOURCE:(MTAR:0):性行動 = 120

;主導権に応じた優越・受動のソース追加
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 150, 70)

CALL STACK_SPERM(MPLY:0, MTAR:0, 0)

;主導度変化基準値
TFLAG:49 = 1

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 5

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP24
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP24(ARG:0)


;●全プレイヤーについて判定
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0

DOWNBASE:(LOCAL:2):体力 += 15

EXP:(LOCAL:2):性技経験値 += 1
EXP:(LOCAL:2):口淫経験 += 1

SOURCE:(LOCAL:2):奉仕 += SERVE_HOUSHI(LOCAL:2, 150)
SOURCE:(LOCAL:2):接触 += 15
SOURCE:(LOCAL:2):快Ｍ += 200 * COM24_RATE_M(LOCAL:2) / 1000
SOURCE:(LOCAL:2):性行動 += 70

;奉仕経験値を得られるコマンドのフラグ
TCVAR:(LOCAL:2):4 = 1

LOCAL:1 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

DOWNBASE:(LOCAL:1):体力 += 10

SOURCE:(LOCAL:1):接触 += 15
SOURCE:(LOCAL:1):性行動 += 30


;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE24(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%に指フェラ中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP24(ARG:0)

PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%が%EQUIP_TARGET_ANAME(ARG:0)%の指を口で咥え込んで舐め回している…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP24(ARG:0)

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_PLAYER24(ARG:0)
;実行値の設定
TCVAR:(ARG:0):25 = 85

;共通部分
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"欲望Lv{ABL:(ARG:0):欲望}", ABL:(ARG:0):欲望 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"奉仕Lv{ABL:(ARG:0):奉仕}", ABL:(ARG:0):奉仕 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"精愛Lv{ABL:(ARG:0):精愛}", ABL:(ARG:0):精愛 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):欲情)
CALL COM_ORDER_ELEMENT(ARG:0, @"欲情Lv{LOCAL:0}", MIN(LOCAL:0 * 3, 30))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "媚薬", 6)
ENDIF

IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -1)
ENDIF

IF TALENT:(ARG:0):献身的
	CALL COM_ORDER_ELEMENT(ARG:0, "献身的", 6)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -1)
ENDIF

;合意
IF !TALENT:(ARG:0):合意 && !TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "合意なし", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE24

;プレイヤーが１人＆拘束中
IF IS_BIND(MPLY:0)
	IF TALENT:(MPLY:0):体格 == 体格_子供 || TALENT:(MPLY:0):体格 == 体格_小柄
		LOCALS:0 = 小さな口
	ELSE
		LOCALS:0 = 口
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;プレイヤーに主導権
		CASE 0
			PRINTFORMW %ANAME(MPLY:0)%は自身の%LOCALS:0%に%ANAME(MTAR:0)%の指を挿れさせ、じゅるじゅると音を立ててしゃぶった…
		;ターゲットに主導権
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)%は身動きの取れない%ANAME(MPLY:0)%の%LOCALS:0%に無理やり指を押し込み舐めさせた…
		;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%の指を口で気持ち良くするように命令した
			PRINTFORMW %ANAME(MPLY:0)%は差し出された%ANAME(MTAR:0)%の指を%LOCALS:0%で咥え込み、じゅるじゅると音を立ててしゃぶった…
	ENDSELECT

	;縄で縛るの地の文をカットする
	LOCAL:0 = SEARCH_EQUIP(85, -1, MPLY:0)
	IF LOCAL:0 >= 0
		MEQUIP_CUT:(LOCAL:0) = 1
	ENDIF
	LOCAL:0 = SEARCH_EQUIP(208, -1, MPLY:0)
	IF LOCAL:0 >= 0
		MEQUIP_CUT:(LOCAL:0) = 1
	ENDIF


;１人
ELSE
	IF TALENT:(MPLY:0):体格 == 体格_子供 || TALENT:(MPLY:0):体格 == 体格_小柄
		LOCALS:0 = 小さな口で必死に
	ELSE
		LOCALS:0 = 口で
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
				;プレイヤーに主導権
		CASE 0
			PRINTFORM %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の指を%LOCALS:0%咥え込み、
			IF ABL:(MPLY:0):性技 >= 3
				PRINTFORMW 巧みな舌使いで舐めしゃぶっ%TARGET_STR("た", "てきた")%…
			ELSE
				PRINTFORMW 不慣れな舌使いで責めたて%TARGET_STR("た", "てきた")%…
			ENDIF
				;ターゲットに主導権
		CASE 1
			PRINTFORM %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の指を%LOCALS:0%咥え込み、
			IF ABL:(MPLY:0):性技 >= 3
				PRINTFORMW 巧みな舌使いで奉仕している…
			ELSE
				PRINTFORMW 不慣れな舌使いで奉仕している…
			ENDIF
				;第三者に主導権
		CASEELSE
			PRINTFORML %ANAME(TFLAG:45)%は%ANAME(MPLY:0)%に、%ANAME(MTAR:0)%の指を口で気持ち良くするように命令した
			PRINTFORM %ANAME(MPLY:0)%は%ANAME(MTAR:0)%の指を%LOCALS:0%咥え込み、
			IF ABL:(MPLY:0):性技 >= 3
				PRINTFORMW 巧みな舌使いで舐めしゃぶった…
			ELSE
				PRINTFORMW 不慣れな舌使いで舐め回した…
			ENDIF
	ENDSELECT

ENDIF


;--------------------------------------------------------
;地の文(パラメータ・刻印変動後)
;--------------------------------------------------------
@COM_TEXT_LAST24
FOR LOCAL:0, 0, MPLY_NUM
	IF (MPLY:LOCAL != MASTER || CONFIG:441) && !IS_ANIMAL(MPLY:LOCAL)
		IF (PALAM:(MPLY:LOCAL):欲情 >= PALAMLV:7 || (PALAM:(MPLY:LOCAL):欲情 >= PALAMLV:5 && TCVAR:(MPLY:LOCAL):60)) && ((ABL:(MPLY:LOCAL):欲望 >= ランク閾値:ランク_その他:ランク_D) || ABL:(MPLY:LOCAL):精愛 >= ランク閾値:ランク_その他:ランク_C)
			SELECTCASE RAND:2
				CASE 0
					SELECTCASE RAND:2
						CASE 0
							PRINTFORM %ANAME(MPLY:LOCAL)%は%SPLIT_RAND("興奮で鼻息を荒くしながら/情欲に突き動かされるまま", 1)%、
						CASE 1
							PRINTFORM %SPLIT_RAND("興奮しきった様子の/発情しきった様子の", 1)%%ANAME(MPLY:LOCAL)%は、
					ENDSELECT
					PRINTFORML %SPLIT_RAND("じゅぼじゅぼと音を立てて/下品な音を立てて/涎を垂らして/激しく頭を前後させながら/夢中で/必死に", 1)%%ANAME(MTAR:0)%の指%SPLIT_RAND("にしゃぶり付いている/に吸い付いている", 1)%…
				CASE 1
					PRINTFORM %ANAME(MPLY:LOCAL)%は%SPLIT_RAND("興奮で息を荒げながら/蕩けたような顔をしながら/熱に浮かされたような顔をしながら", 1)%、
					PRINTFORML %SPLIT_RAND("味わうように/形を確かめるように", 1)%%ANAME(MTAR:0)%の指%SPLIT_RAND("をしゃぶっている/を頬張っている/へ舌を這わせている/を舐めまわしている", 1)%…
			ENDSELECT
		ENDIF
	ENDIF
NEXT
;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN24
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_24()
RETURN POWER(2, 性的嗜好_奉仕したい)

@COM_PREFERENCE_TARGET_24()
RETURN POWER(2, 性的嗜好_奉仕されたい)

