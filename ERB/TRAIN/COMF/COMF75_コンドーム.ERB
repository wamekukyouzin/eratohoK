;ローション

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME75
LOCALS:0 = コンドーム

IF MTAR_NUM > 0
	RESULTS:0 = %LOCALS:0%装着を見せつける
	RESULTS:1 = %LOCALS:0%装着させられる
	RESULTS:2 = %LOCALS:0%装着させる
	RESULTS:3 = %LOCALS:0%装着を見せつけられる
	RESULTS:4 = %LOCALS:0%装着させる
	RESULTS:5 = %LOCALS:0%装着見せつけ
ELSE
	RESULTS:0 = %LOCALS:0%をつける
ENDIF
;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE75
;共通部分
CALL COM_ABLE_COMMON(75)
SIF RESULT == 0
	RETURN 0
;プレイヤーは1人以上
SIF MPLY_NUM <= 0
	RETURN 0
;プレイヤーが行動不能なら不可
SIF !IS_PLAYABLE(MPLY:0) && !FLAG:RECHECKING
	RETURN 0
FOR LOCAL, 0, MPLY_NUM
	;プレイヤーにチンポが必要
	SIF !HAS_PENIS(MPLY:(LOCAL:0))
		RETURN 0
	;プレイヤーがＰ挿入中なら不可
	SIF IS_INSERT_SINGLE(MPLY:(LOCAL:0), "Ｐ") && !FLAG:RECHECKING
		RETURN 0
	;既にゴムつけてるなら駄目
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 75)
		RETURN 0
NEXT
;コンドームが人数分必要
SIF ITEM:コンドーム < MPLY_NUM && ITEM:A_コンドーム < MPLY_NUM && !FLAG:RECHECKING
	RETURN 0
;プレイヤーが拘束中ならターゲットが必要
SIF IS_BIND(MPLY:0) && MTAR_NUM <= 0 && !FLAG:RECHECKING
	RETURN 0
RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM75
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0
IF MTAR_NUM == 0
	IF MPLY_NUM >= 2
		LOCALS = %ANAME(MPLY:0)%たち
	ELSE
		LOCALS = %ANAME(MPLY:0)%
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		CASE 0
			PRINTFORMW %LOCALS:0%はペニスにコンドームをかぶせた……
		CASE 1
			;ねぇよ
		CASE 2
			PRINTFORML %ANAME(TFLAG:45)%は%LOCALS:0%に、ペニスにコンドームを付けるよう要求した…
			PRINTFORMW %LOCALS:0%は言われるまま、自身のペニスにコンドームを取り付けた…
	ENDSELECT
ENDIF

;ローションを１つ減少させる(追加アイテム使用時は減少させない)
IF ITEM:A_コンドーム <= 0
	ITEM:コンドーム = MAX(ITEM:コンドーム - MPLY_NUM, 0)
ENDIF

;主導度変化基準値
TFLAG:49 = 1

;倒錯度変化基準値
TFLAG:50 = 0

;レズ・ＢＬ経験基準値
TFLAG:51 = 2

;経過時間を無理矢理1減らす
TFLAG:55 --

RETURN 1

;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE75
IF MPLY_NUM >= 2
	LOCALS = %ANAME(MPLY:0)%たち
ELSE
	LOCALS = %ANAME(MPLY:0)%
ENDIF
IF MTAR_NUM == 0
	LOCALS:1 = 
ELSEIF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%たち
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

IF IS_BIND(MPLY:0)
	PRINTFORMW %LOCALS:1%は動けない%LOCALS:0%の%BAR_NAME(MPLY:0)%にゴムを被せた……
ELSE 
	SELECTCASE MTAR_NUM
		CASE 0
			;ここに書いても実行されないんで本文にて記述
		CASEELSE
			SELECTCASE GET_COM_INITIATIVE()
			;プレイヤーに主導権
				CASE 0
					PRINTFORMW %LOCALS:0%は%LOCALS:1%に、ペニスにコンドームを被せるのを見せつけ%TARGET_STR("た", "てきた")%…
			;ターゲットに主導権
				CASE 1
					PRINTFORML %LOCALS:1%は%LOCALS:0%に、ペニスにコンドームを付けるよう要求した…
					PRINTFORMW %LOCALS:0%は言われるまま、自身のペニスにコンドームを取り付けた…
			;第三者に主導権
				CASEELSE
					PRINTFORML %ANAME(TFLAG:45)%は%LOCALS:0%に、%LOCALS:1%の目の前でペニスにコンドームを付けるよう要求した…
					PRINTFORMW %LOCALS:0%は言われるまま、自身のペニスにコンドームを取り付けた…
			ENDSELECT
	ENDSELECT
ENDIF
;-------------------------------------------------
;地の文(実行後)
;-------------------------------------------------
@COM_TEXT_AFTER75

SIF MTAR_NUM == 0
	RETURN


IF MPLY_NUM >= 2
	LOCALS = %ANAME(MPLY:0)%たち
ELSE
	LOCALS = %ANAME(MPLY:0)%
ENDIF
IF MTAR_NUM == 0
	LOCALS:1 = 
ELSEIF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%たち
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

IF MTAR:0 != MASTER || CONFIG:441
	IF IS_LOVER(MTAR:0) && MPLY:0 == MASTER
		IF GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_孕みたがり)　|| GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_生ハメ依存)
			PRINTFORML %ANAME(MTAR:0)%は生のほうが絶対気持ちいいのにとつぶやきながら、%LOCALS:0%のペニスにつけられたコンドームを眺め%TARGET_STR("た", "てきた")%…
		ELSE
			SIF TALENT:(MTAR:0):育児中
				PRINT 育児の最中の
			IF TALENT:(MTAR:0):妊娠
				PRINTFORML %ANAME(MTAR:0)%はお腹が大きくなったことを心配してくれることに幸福感を感じながら、%LOCALS:0%のペニスにつけられたコンドームを眺め%TARGET_STR("た", "てきた")%…
			ELSE
				PRINTFORML %ANAME(MTAR:0)%は体を心配してくれることを喜びながら%LOCALS:0%のペニスにつけられたコンドームを眺め%TARGET_STR("た", "てきた")%…
			ENDIF
		ENDIF
	;捕虜調教のメインターゲット
	ELSEIF FLAG:調教モード == 2 && FINDELEMENT(PRISONER_TARGET, MTAR:0) != -1
		IF TALENT:(MTAR:0):妊娠
			PRINTFORML %ANAME(MTAR:0)%は既に妊娠しているのに無意味だと思いながら、%LOCALS:0%のペニスにつけられたコンドームを眺め%TARGET_STR("た", "てきた")%…
		ELSE
			IF GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_孕みたがり)　|| GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_生ハメ依存)
				PRINTFORML %ANAME(MTAR:0)%はどこか残念そうにしながらも、%LOCALS:0%のペニスにつけられたコンドームを眺め%TARGET_STR("た", "てきた")%…
			ELSE
				IF TALENT:(MTAR:0):男嫌い && IS_MALE(MTAR:0)
					PRINT 男に犯されるのだということに嫌悪感をいだきつつも、
				ELSEIF TALENT:(MTAR:0):プライド高い
					PRINT 屈辱に渋面を作りながらも
				ELSEIF TALENT:(MTAR:0):貞操観念
					PRINT これから犯されることを想像して嫌悪感をいだきつつも、
				ENDIF
				PRINTFORML 生でされるよりマシだと考え、%ANAME(MTAR:0)%は%LOCALS:0%のペニスにつけられたコンドームを眺め%TARGET_STR("た", "てきた")%…
			ENDIF
		ENDIF
	ELSE
		IF GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_孕みたがり)　|| GETBIT(TALENT:(MTAR:0):淫乱系, 素質_淫乱_生ハメ依存)
			PRINTFORML %ANAME(MTAR:0)%は生のほうが絶対気持ちいいのにとつぶやきつつ、%LOCALS:0%のペニスにつけられたコンドームを眺め%TARGET_STR("た", "てきた")%…
		ELSE
			SIF TALENT:(MTAR:0):育児中
				PRINT まだ育児中の
			IF TALENT:(MTAR:0):妊娠
				PRINTFORML お腹の子のことも考え、%ANAME(MTAR:0)%は避妊を肯定的に考え%TARGET_STR("た", "てきた")%…
			ELSE
				PRINTFORML 妊娠を避けるためだからと、%ANAME(MTAR:0)%は避妊を肯定的に考え%TARGET_STR("た", "てきた")%…
			ENDIF
		ENDIF
	ENDIF
ENDIF
;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP75
;継続コマンドかつフィルタリング不可
RETURN 2

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP75(ARG:0)
;特になし

;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE75(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%がコンドームを装着中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP75(ARG:0)

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP75(ARG:0)
PRINTFORMW %EQUIP_PLAYER_ANAME(ARG:0)%のコンドームを外した

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN75
RETURN コマンド_ウフフ

;--------------------
;性嗜好用関数
;--------------------
@COM_PREFERENCE_PLAYER_75()
RETURN POWER(2, 性的嗜好_道具を使いたい)

@COM_PREFERENCE_TARGET_75()
RETURN POWER(2, 性的嗜好_道具を使われたい)

