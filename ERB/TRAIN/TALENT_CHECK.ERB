;調教終了時のイベント、特に素質関連

;-------------------------------------------------
;恋慕等の取得
;-------------------------------------------------
@TALENT_CHECK(ARG:0)

SIF IS_ANIMAL(ARG:0)
	RETURN 0

CALL TALENT_CHECK_LOSE_MIND(ARG:0)

SIF !(TALENT:(ARG:0):虚ろ || TALENT:(ARG:0):崩壊)
	CALL TALENT_CHECK_FALLEN(ARG:0)

CALL GET_FALLEN_BONUS(ARG:0)

CALL TALENT_CHECK_OTHERS(ARG:0)

CALL TALENT_CHECK_EROS(ARG:0)

SIF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_淫乱)
	CALL TALENT_CHECK_PART(ARG:0)

;-------------------------------------------------
;プライド高いを所有するキャラクターが屈服した場合の処理
;ARG:1は色変更を行わないフラグ	ARG:2は先頭の改行を行わないフラグ
;-------------------------------------------------
@TALENT_LOSE_PRIDE(ARG:0, ARG:1, ARG:2)
SIF !ARG:1
	SETCOLOR カラー_注意
IF TALENT:(ARG:0):プライド高い
	SIF !ARG:2
		PRINTL 
	PRINTFORMW %ANAME(ARG:0)%は<プライド高い>を失った
	TALENT:(ARG:0):プライド高い = 0
	IF TALENT:(ARG:0):孤高 || TALENT:(ARG:0):気丈 || !TALENT:(ARG:0):臆病 || TALENT:(ARG:0):生意気 || TALENT:(ARG:0):ツンデレ || TALENT:(ARG:0):反抗的
		PRINTL 
		PRINTFORML 自尊心を失った%ANAME(ARG:0)%はすっかり弱気になったようだ……
		ABL:(ARG:0):主導度Ｎ -= (ABL:(ARG:0):主導度Ｎ + 1000) / 5 * (TALENT:(ARG:0):気丈 + 1)
		ABL:(ARG:0):主導度Ｕ -= (ABL:(ARG:0):主導度Ｕ + 1000) / 5 * (TALENT:(ARG:0):気丈 + 1)
		ABL:(ARG:0):主導度Ｎ = LIMIT(ABL:(ARG:0):主導度Ｎ, -1000, 1000)
		ABL:(ARG:0):主導度Ｕ = LIMIT(ABL:(ARG:0):主導度Ｕ, -1000, 1000)
		IF TALENT:(ARG:0):孤高
			PRINTFORMW %ANAME(ARG:0)%は<孤高>を失った
			TALENT:(ARG:0):孤高 = 0
		ENDIF
		IF TALENT:(ARG:0):気丈
			PRINTFORMW %ANAME(ARG:0)%は<気丈>を失った
			TALENT:(ARG:0):気丈 = 0
		ELSEIF !TALENT:(ARG:0):臆病
			PRINTFORMW %ANAME(ARG:0)%は<臆病>を得た
			TALENT:(ARG:0):臆病 = 1
		ENDIF
		;反発系の素質から一つだけ失う
		IF TALENT:(ARG:0):反抗的
			PRINTFORMW %ANAME(ARG:0)%は<反抗的>を失った
			TALENT:(ARG:0):反抗的 = 0
		ELSEIF TALENT:(ARG:0):生意気
			PRINTFORMW %ANAME(ARG:0)%は<生意気>を失った
			TALENT:(ARG:0):生意気 = 0
		ELSEIF TALENT:(ARG:0):ツンデレ
			PRINTFORMW %ANAME(ARG:0)%は<ツンデレ>を失った
			TALENT:(ARG:0):ツンデレ = 0
		ENDIF
	ENDIF
ENDIF
SIF !ARG:1
	RESETCOLOR

;-------------------------------------------------
;特殊勢力による淫乱取得時などの貞操観念喪失・貞操無頓着入手時の処理
;ARG:1は色変更を行わないフラグ
;-------------------------------------------------
@TALENT_LOSE_TEISOU(ARG:0, ARG:1)
SIF !ARG:1
	SETCOLOR カラー_注意
IF TALENT:(ARG:0):貞操観念
	PRINTFORMW %ANAME(ARG:0)%は<貞操観念>を失い、<貞操無頓着>を得た
	TALENT:(ARG:0):貞操観念 = 0
	TALENT:(ARG:0):貞操無頓着 = 1
ELSEIF !TALENT:(ARG:0):貞操無頓着
	PRINTFORMW %ANAME(ARG:0)%は<貞操無頓着>を得た
	TALENT:(ARG:0):貞操無頓着 = 1
ENDIF
SIF !ARG:1
	RESETCOLOR

;-------------------------------------------------
;虚ろの喪失・回復処理
;-------------------------------------------------
@TALENT_CHECK_LOSE_MIND(ARG:0)

SIF TALENT:(ARG:0):崩壊
	RETURN 0

IF !TALENT:(ARG:0):虚ろ && CFLAG:(ARG:0):崩壊 >= 1000 + MAX(MAXBASE:(ARG:0):精神力, 1000)
	CALL SINGLE_EMPTY_LINE()
	CALL KOJO_EVENT(ARG:0, 78)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<虚ろ>を得た
	RESETCOLOR
	TALENT:(ARG:0):虚ろ = 1

;[虚ろ]の回復
ELSEIF TALENT:(ARG:0):虚ろ && CFLAG:(ARG:0):崩壊 < -500 + MAX(MAXBASE:(ARG:0):精神力, 1000)
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子が変わった…
	PRINTFORML 疲弊していた%ANAME(ARG:0)%の心はどうにか平常に戻ったようだ
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<虚ろ>を失った
	RESETCOLOR
	TALENT:(ARG:0):虚ろ = 0
ENDIF

;[崩壊]の取得
IF !TALENT:(ARG:0):崩壊 && CFLAG:(ARG:0):崩壊 >= 3500 + MAX(MAXBASE:(ARG:0):精神力, 1000)
	CALL SINGLE_EMPTY_LINE()
	CALL KOJO_EVENT(ARG:0, 79)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<崩壊>を得た
	RESETCOLOR
	TALENT:(ARG:0):崩壊 = 1
ENDIF

;-------------------------------------------------
;恋慕・服従・淫乱
;-------------------------------------------------
@TALENT_CHECK_FALLEN(ARG:0)

LOCAL:1 = 0


IF !IS_SLAVE(ARG:0) && !IS_LOVER(ARG:0) && !TALENT:(ARG:0):親友 && IS_SAMESEX(MASTER, ARG:0) && !TALENT:(ARG:0):両刀 && CFLAG:(ARG:0):好感度 >= 1500
	CALL SINGLE_EMPTY_LINE()
	CALL KOJO_EVENT(ARG:0, 61)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<親友>を得た
	RESETCOLOR
	TALENT:(ARG:0):親友 = 1
ENDIF

IF !IS_SLAVE(ARG:0)
	IF !TALENT:(ARG:0):恋慕 && CFLAG:(ARG:0):好感度 > CFLAG:(ARG:0):従属度 && CFLAG:(ARG:0):好感度 >= 1500  && CFLAG:(ARG:0):依存度 >= 300 && (!IS_SAMESEX(MASTER, ARG:0) || TALENT:(ARG:0):両刀 || TALENT:(ARG:0):恋人)
		CALL SINGLE_EMPTY_LINE()
		CALL KOJO_EVENT(ARG:0, 60)
		SETCOLOR カラー_注意
		IF TALENT:(ARG:0):親友
			PRINTFORMW %ANAME(ARG:0)%は<親友>を失い<恋慕>を得た
		ELSE
			PRINTFORMW %ANAME(ARG:0)%は<恋慕>を得た
		ENDIF
		RESETCOLOR
		TALENT:(ARG:0):恋慕 = 1
		TALENT:(ARG:0):親友 = 0
		;堕とした人数＋１
		FLAG:陥落人数 += !(CFLAG:(ARG:0):陥落済み)
		CFLAG:(ARG:0):陥落済み = 1
		LOCAL:1 = 1
	ENDIF

	IF !TALENT:(ARG:0):親愛 && CFLAG:(ARG:0):好感度 >= 10000 && CFLAG:(ARG:0):依存度 >= 3000 && ABL:(ARG:0):奉仕 >= ランク閾値:ランク_その他:ランク_C && TALENT:(ARG:0):恋慕
		CALL SINGLE_EMPTY_LINE()
		CALL KOJO_EVENT(ARG:0, 62)
		SETCOLOR カラー_注意
		PRINTFORMW %ANAME(ARG:0)%は<親愛>を得た
		RESETCOLOR
		TALENT:(ARG:0):親愛 = 1
		LOCAL:1 = 2
	ENDIF
ENDIF

IF !IS_LOVER(ARG:0)
	IF !TALENT:(ARG:0):服従 && CFLAG:(ARG:0):従属度 >= 1500 && CFLAG:(ARG:0):依存度 >= 300 && !TALENT:(ARG:0):恋慕
		CALL SINGLE_EMPTY_LINE()
		CALL KOJO_EVENT(ARG:0, 63)
		SETCOLOR カラー_注意
		IF TALENT:(ARG:0):親友
			PRINTFORMW %ANAME(ARG:0)%は<親友>を失い<服従>を得た
		ELSE
			PRINTFORMW %ANAME(ARG:0)%は<服従>を得た
		ENDIF
		RESETCOLOR
		SIF TALENT:(ARG:0):プライド高い
			CALL TALENT_LOSE_PRIDE(ARG:0)
		TALENT:(ARG:0):親友 = 0
		TALENT:(ARG:0):服従 = 1
		;堕とした人数＋１
		FLAG:陥落人数 += !(CFLAG:(ARG:0):陥落済み)
		CFLAG:(ARG:0):陥落済み = 1
		LOCAL:1 = 3
	ENDIF

	IF !TALENT:(ARG:0):隷属 && CFLAG:(ARG:0):従属度 >= 10000 && CFLAG:(ARG:0):依存度 >= 3000 && ABL:(ARG:0):奉仕 >= ランク閾値:ランク_その他:ランク_C && TALENT:(ARG:0):服従
		CALL SINGLE_EMPTY_LINE()
		CALL KOJO_EVENT(ARG:0, 64)
		SETCOLOR カラー_注意
		PRINTFORMW %ANAME(ARG:0)%は<隷属>を得た
		RESETCOLOR
		TALENT:(ARG:0):隷属 = 1
		LOCAL:1 = 4
	ENDIF
ENDIF

LOCAL:0 = ABL:(ARG:0):Ｃ感 + ABL:(ARG:0):Ｖ感 + ABL:(ARG:0):Ａ感 + ABL:(ARG:0):Ｂ感 + ABL:(ARG:0):Ｍ感 + ABL:(ARG:0):射精
{
IF !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_淫乱) && ABL:(ARG:0):欲望 >= ランク閾値:ランク_その他:ランク_B && MAX(ABL:(ARG:0):精愛, ABL:(ARG:0):奉仕) >= ランク閾値:ランク_その他:ランク_C &&
MAX(ABL:(ARG:0):性交, ABL:(ARG:0):レズ, ABL:(ARG:0):自慰) >= ランク閾値:ランク_その他:ランク_D && LOCAL:0 >= 20 && EXP:(ARG:0):絶頂経験 >= 500
}
	CALL SINGLE_EMPTY_LINE()
	CALL KOJO_EVENT(ARG:0, 65)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<淫乱>を得た
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_淫乱
	LOCAL:1 = 5
ENDIF
SELECTCASE LOCAL:1
	;恋慕
	CASE 1
		IF TALENT:(ARG:0):一線越えない
			CALL COLORPRINT(@"%ANAME(ARG:0)%は<一線越えない>を失った", カラー_注意, "W")
			TALENT:(ARG:0):一線越えない = 0
		ENDIF
		IF TALENT:(ARG:0):無関心
			CALL COLORPRINT(@"%ANAME(ARG:0)%は<無関心>を失った", カラー_注意, "W")
			TALENT:(ARG:0):無関心 = 0
		ENDIF
	;親愛
	CASE 2
		IF TALENT:(ARG:0):孤高
			CALL COLORPRINT(@"%ANAME(ARG:0)%は<孤高>を失った", カラー_注意, "W")
			TALENT:(ARG:0):孤高 = 0
		ENDIF
	;従属
	CASE 3
		IF TALENT:(ARG:0):一線越えない
			CALL COLORPRINT(@"%ANAME(ARG:0)%は<一線越えない>を失った", カラー_注意, "W")
			TALENT:(ARG:0):一線越えない = 0
		ENDIF
		IF TALENT:(ARG:0):反抗的
			CALL COLORPRINT(@"%ANAME(ARG:0)%は<反抗的>を失った", カラー_注意, "W")
			TALENT:(ARG:0):反抗的 = 0
		ENDIF
	;隷属
	CASE 4
		IF TALENT:(ARG:0):抑圧
			CALL COLORPRINT(@"%ANAME(ARG:0)%は<抑圧>を失った", カラー_注意, "W")
			TALENT:(ARG:0):抑圧 = 0
		ENDIF
	;淫乱
	CASE 5
		;対象が特殊勢力の陥落系か雌犬か肉便器か苗床を所持しているか、遠隔調教のターゲット実行回数が20回以上なら貞操観念を喪失
		IF TALENT:(ARG:0):貞操観念 && (IS_FALLEN_TO_SP_COUNTRY(ARG:0) || GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_雌犬) || GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_精液便女) || GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_苗床) || COM_EXP:(ARG:0):398 >= 200)
			CALL COLORPRINT(@"%ANAME(ARG:0)%は<貞操観念>を失った", カラー_注意, "W")
			TALENT:(ARG:0):貞操観念 = 0
		ENDIF
		IF TALENT:(ARG:0):快感の否定
			CALL COLORPRINT(@"%ANAME(ARG:0)%は<快感の否定>を失った", カラー_注意, "W")
			TALENT:(ARG:0):快感の否定 = 0
		ENDIF
ENDSELECT


;-------------------------------------------------
;陥落ボーナス取得
;-------------------------------------------------
@GET_FALLEN_BONUS(ARG:0)

SIF FLAG:陥落人数 < (FLAG:陥落ボーナス受領数 + 1) * 5
	RETURN 0

CALL SINGLE_EMPTY_LINE()
CALL COLORPRINT(@"今まで他人を陥落させてきた経験で、何か得られる気がする……", カラー_注意, "L")
CALL ASK_MULTI_JUDGE("謎の魅力", !TALENT:MASTER:謎の魅力, "天の御遣い", !TALENT:MASTER:天の御遣い, "技師", !TALENT:MASTER:技師, "絶倫", !TALENT:MASTER:絶倫, "君主の器", !TALENT:MASTER:君主の器, "求心力", !TALENT:MASTER:求心力, "金", 1)
SELECTCASE RESULT
	CASE 0
		CALL COLORPRINT(@"%ANAME(MASTER)%は<謎の魅力>を身につけた", カラー_注意, "W")
		TALENT:MASTER:謎の魅力 = 1
	CASE 1
		CALL COLORPRINT(@"%ANAME(MASTER)%は<天の御遣い>を身につけた", カラー_注意, "W")
		TALENT:MASTER:天の御遣い = 1
	CASE 2
		CALL COLORPRINT(@"%ANAME(MASTER)%は<技師>を身につけた", カラー_注意, "W")
		TALENT:MASTER:技師 = 1
	CASE 3
		CALL COLORPRINT(@"%ANAME(MASTER)%は<絶倫>を身につけた", カラー_注意, "W")
		TALENT:MASTER:絶倫 = 1
	CASE 4
		CALL COLORPRINT(@"%ANAME(MASTER)%は<君主の器>を身につけた", カラー_注意, "W")
		TALENT:MASTER:君主の器 = 1
	CASE 5
		CALL COLORPRINT(@"%ANAME(MASTER)%は<求心力>を身につけた", カラー_注意, "W")
		TALENT:MASTER:求心力 = 1
	CASE 6
		CALL COLORPRINT(@"金50000を手に入れた", カラー_注意, "W")
		MONEY += 50000
ENDSELECT
FLAG:陥落ボーナス受領数 ++

RETURN 1

;-------------------------------------------------
;その他素質取得
;-------------------------------------------------
@TALENT_CHECK_OTHERS(ARG:0)
;酔い累積値が150000以上で酒豪が付く	下戸の場合は下戸を失う
IF !TALENT:(ARG:0):酒豪 && CFLAG:(ARG:0):酔い累積値 >= 150000
	CFLAG:(ARG:0):酔い累積値 = 0 
	CALL SINGLE_EMPTY_LINE()
	CALL KOJO_EVENT(ARG:0, 74)
	SETCOLOR カラー_注意
	IF TALENT:(ARG:0):下戸
		PRINTFORMW %ANAME(ARG:0)%は<下戸>を失った
		TALENT:(ARG:0):下戸 = 0
	ELSE
		PRINTFORMW %ANAME(ARG:0)%は<酒豪>を得た
		TALENT:(ARG:0):酒豪 = 1
	ENDIF
	RESETCOLOR
ENDIF

;おもらし経験30以上でおもらし癖
IF !TALENT:(ARG:0):おもらし癖 && EXP:(ARG:0):おもらし経験 >= 30
	CALL SINGLE_EMPTY_LINE()
	CALL KOJO_EVENT(ARG:0, 76)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<おもらし癖>を得た
	RESETCOLOR
	TALENT:(ARG:0):おもらし癖 = 1
ENDIF


;-------------------------------------------------
;エロ素質
;-------------------------------------------------
@TALENT_CHECK_EROS(ARG:0)
;輪姦経験200以上、欲望B以上、性交C以上で肉便器
IF !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_精液便女) && EXP:(ARG:0):輪姦経験 >= 200 && ABL:(ARG:0):欲望 >= ランク閾値:ランク_その他:ランク_B && ABL:(ARG:0):性交 >= ランク閾値:ランク_その他:ランク_C
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい……
	IF TALENT:(ARG:0):プライド高い
		PRINTFORMW %ANAME(ARG:0)%は犯される快感に負け、自分が性処理用の便器であることを受け入れてしまった
	ELSE
		PRINTFORMW %ANAME(ARG:0)%は犯される快感を覚え、便器として使われる悦びに身を捧げることにしたようだ
	ENDIF
	CALL KOJO_EVENT(ARG:0,  82)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<精液便女>になった
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_精液便女
	SIF TALENT:(ARG:0):プライド高い
		CALL TALENT_LOSE_PRIDE(ARG:0)
	CALL TALENT_LOSE_TEISOU(ARG:0)
ENDIF

;薬物依存が300を越えると薬物依存を取得
IF !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_薬物中毒) && CFLAG:(ARG:0):薬物依存 > 300
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい……
	PRINTFORMW %ANAME(ARG:0)%は、度重なる薬物の使用により、それから離れられなくなった
	CALL KOJO_EVENT(ARG:0, 85)
	SETCOLOR カラー_注意	
	PRINTFORMW %ANAME(ARG:0)%は<薬物中毒>を得た
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_薬物中毒
ENDIF



;-------------------------------------------------
;部位特化素質＆サドマゾの取得処理
;一回の調教で取得できる素質は１つのみ
;-------------------------------------------------
@TALENT_CHECK_PART(ARG:0)
;[サド] 奉仕A以上、性技B以上、ウフフ主導度1000以上、倒錯度800以上で、今回の主人公の絶頂回数が30以上、サドB以上
IF !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_サド) && ABL:(ARG:0):奉仕 >= ランク閾値:ランク_その他:ランク_A && ABL:(ARG:0):性技 >= ランク閾値:ランク_その他:ランク_B && ABL:(ARG:0):主導度Ｕ >= 1000 && ABL:(ARG:0):倒錯度 >= 800 && SUM_EX(MASTER) >= 30 && ABL:(ARG:0):サド >= ランク閾値:ランク_その他:ランク_B
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい…
	PRINTFORML %ANAME(ARG:0)%は他者を性的に虐めることに強い快感を覚えるようになった…
	CALL KOJO_EVENT(ARG:0, 71)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<サド>を得た
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_サド
ENDIF

;[マゾ] ウフフ主導度-1000以下、倒錯度800以上、緊縛経験50以上、苦痛快楽経験200以上、今回の絶頂回数が100以上、マゾB以上
IF  !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_マゾ) && ABL:(ARG:0):主導度Ｕ <= -1000 && ABL:(ARG:0):倒錯度 >= 800 && EXP:(ARG:0):緊縛経験 >= 50 && EXP:(ARG:0):苦痛快楽経験 >= 200 && SUM_EX(ARG:0) >= 100 && ABL:(ARG:0):マゾ >= ランク閾値:ランク_その他:ランク_B
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい…
	PRINTFORML %ANAME(ARG:0)%はすっかり虐められる快感の虜になってしまったようだ…
	CALL KOJO_EVENT(ARG:0, 72)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<マゾ>を得た
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_マゾ
ENDIF

;[淫核] 竿なし、Ｃ感覚Ｓランク以上、倒錯度100以上、今回のＣ絶頂回数が50以上
IF  !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_淫核) && !HAS_PENIS(ARG:0) && ABL:(ARG:0):Ｃ感 >= ランク閾値:ランク_その他:ランク_S && ABL:(ARG:0):倒錯度 >= 100 && EX:(ARG:0):Ｃ絶頂 >= 50
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい…
	PRINTFORML %ANAME(ARG:0)%はクリトリスのもたらす雌の快楽にすっかり溺れてしまったようだ…
	CALL KOJO_EVENT(ARG:0, 66)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<淫核>を得た
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_淫核
ENDIF

;[淫壷] 「Ｖ感覚Ａランク以上且つ、性交Dランク以上且つ、Ｖ拡張経験30以上」もしくは「Ｖ感覚Ｓランク以上且つ、性交Ｃランク以上」で、今回のＶ絶頂回数が50以上
IF  !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_淫壷) && ((ABL:(ARG:0):Ｖ感 >= ランク閾値:ランク_その他:ランク_A && ABL:(ARG:0):性交 >= ランク閾値:ランク_その他:ランク_D && EXP:(ARG:0):Ｖ拡張経験 >= 30) || (ABL:(ARG:0):Ｖ感 >= ランク閾値:ランク_その他:ランク_S && ABL:(ARG:0):性交 >= ランク閾値:ランク_その他:ランク_C)) && EX:(ARG:0):Ｖ絶頂 >= 50
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい…
	PRINTFORML %ANAME(ARG:0)%の淫らな蜜壷は、常に男のモノを求めて疼くようになってしまった…
	CALL KOJO_EVENT(ARG:0, 67)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<淫壷>を得た
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_淫壷
ENDIF

;[尻穴狂い] 「Ａ感覚Ａランク以上且つ、Ａ拡張経験50以上」もしくは「Ａ感覚Ｓランク以上、倒錯度800以上」で、今回のＡ絶頂回数が50以上
IF  !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_尻穴狂い) && ((ABL:(ARG:0):Ａ感 >= ランク閾値:ランク_その他:ランク_A && EXP:(ARG:0):Ａ拡張経験 >= 50) || (ABL:(ARG:0):Ａ感 >= ランク閾値:ランク_その他:ランク_S && ABL:(ARG:0):倒錯度 >= 800)) && EX:(ARG:0):Ａ絶頂 >= 50
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい…
	PRINTFORML %ANAME(ARG:0)%の尻穴はすっかり快楽を得るための淫らな器官に変わってしまった…
	CALL KOJO_EVENT(ARG:0, 68)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<尻穴狂い>を得た
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_尻穴狂い
ENDIF

;[淫乳] Ｂ感覚Ｓランク以上、倒錯度100以上、今回のＢ絶頂回数が50以上
IF  !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_淫乳) && ABL:(ARG:0):Ｂ感 >= ランク閾値:ランク_その他:ランク_S && ABL:(ARG:0):倒錯度 >= 100 && EX:(ARG:0):Ｂ絶頂 >= 50
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい…
	PRINTFORML %ANAME(ARG:0)%の胸はわずかの刺激でも声を上げてしまうほど淫らに開発され尽くしてしまった…
	CALL KOJO_EVENT(ARG:0, 69)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<淫乳>を得た
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_淫乳
ENDIF

;[蕩唇] Ｍ感覚Ａランク以上、キス経験200以上、口淫経験100以上、今回のＭ絶頂回数が50以上
IF  !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_蕩唇) && ABL:(ARG:0):Ｍ感 >= ランク閾値:ランク_その他:ランク_A && EXP:(ARG:0):キス経験 >= 200 && EXP:(ARG:0):口淫経験 >= 100 && EX:(ARG:0):Ｍ絶頂 >= 50
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい…
	PRINTFORML %ANAME(ARG:0)%の口は下の口と同様の、性的快楽を感じるための雌の性器へと変わってしまった…
	CALL KOJO_EVENT(ARG:0, 70)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<蕩唇>を得た
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_蕩唇
ENDIF

;[射精狂い] 竿あり、Ｃ感覚Ｃランク以上、射精Ａランク以上、今回の射精回数が30以上
IF  !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_射精狂い) && HAS_PENIS(ARG:0) && ABL:(ARG:0):Ｃ感 >= ランク閾値:ランク_その他:ランク_C && ABL:(ARG:0):射精 >= ランク閾値:ランク_その他:ランク_A && EX:(ARG:0):射精 >= 30
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい…
	PRINTFORML %ANAME(ARG:0)%の肉竿は何度精を放っても次の射精を待ちわびて疼くようになってしまった…
	CALL KOJO_EVENT(ARG:0, 73)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<射精狂い>を得た
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_射精狂い
ENDIF

;[苗床] 触手Ｃランク以上、触手出産経験100以上
IF  !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_苗床) && ABL:(ARG:0):触手 >= ランク閾値:ランク_その他:ランク_C && EXP:(ARG:0):触手出産経験 >= 100
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい…
	PRINTFORML %ANAME(ARG:0)%は触手に依存し、触手の苗床にされることに悦びを覚えるようになったようだ
	CALL KOJO_EVENT(ARG:0, 86)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<苗床>になった
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_苗床
ENDIF

;[生ハメ依存] Ｖ(Ａ）感Ａランク以上、Ｖ（Ａ）性交経験200以上、今回の絶頂回数100以上
IF !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_生ハメ依存) && ((ABL:(ARG:0):Ｖ感 >= ランク閾値:ランク_その他:ランク_A && EXP:(ARG:0):Ｖ性交経験 >= 200 && EXP:(ARG:0):膣内射精経験 >= 150) || (ABL:(ARG:0):Ａ感 >= ランク閾値:ランク_その他:ランク_A && EXP:(ARG:0):Ａ性交経験 >= 200 && EXP:(ARG:0):腸内射精経験 >= 150)) && SUM_EX(ARG:0) >= 100
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい…
	PRINTFORML %ANAME(ARG:0)%は幾度となく性交を繰り返した結果、生ハメでないと満足できない身体になったようだ
	CALL KOJO_EVENT(ARG:0, 87)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<生ハメ依存>を得た
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_生ハメ依存
ENDIF

;[孕みたがり] 出産Cと性交がＡ以上
IF  !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_孕みたがり) && ABL:(ARG:0):出産 >= ランク閾値:ランク_その他:ランク_C && ABL:(ARG:0):性交 >= ランク閾値:ランク_その他:ランク_A
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい…
	PRINTFORML %ANAME(ARG:0)%は幾度となく妊娠と出産を繰り返した結果、その快楽にすっかりハマッたようだ
	CALL KOJO_EVENT(ARG:0, 88)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<孕みたがり>を得た
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_孕みたがり
ENDIF

;[露出狂] 露出A,欲望A,自慰C
IF  !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_露出狂) && ABL:(ARG:0):露出 >= ランク閾値:ランク_その他:ランク_A && ABL:(ARG:0):欲望 >= ランク閾値:ランク_その他:ランク_A && ABL:(ARG:0):自慰 >= ランク閾値:ランク_その他:ランク_C
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい…
	PRINTFORML %ANAME(ARG:0)%は己の肉体を見せつける快感にすっかりハマッたようだ
	CALL KOJO_EVENT(ARG:0, 92)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<露出狂>を得た
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_露出狂
ENDIF

;獣姦経験200以上、欲望A以上、性交C以上で雌犬
IF  !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_雌犬) && EXP:(ARG:0):獣姦経験 >= 200 && ABL:(ARG:0):欲望 >= ランク閾値:ランク_その他:ランク_A && ABL:(ARG:0):性交 >= ランク閾値:ランク_その他:ランク_B
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい……
	PRINTFORMW %ANAME(ARG:0)%は、獣と交わる快感を忘れられなくなってしまったようだ
	CALL KOJO_EVENT(ARG:0,  83)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<雌犬>になった
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_雌犬
ENDIF

;女限定、売春経験100以上、欲望B以上、性技B以上で娼婦
IF  !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_娼婦) && IS_FEMALE(ARG:0) && EXP:(ARG:0):売春経験 >= 100 && ABL:(ARG:0):欲望 >= ランク閾値:ランク_その他:ランク_A && ABL:(ARG:0):性技 >= ランク閾値:ランク_その他:ランク_B && ABL:(ARG:0):奉仕 >= ランク閾値:ランク_その他:ランク_B
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい……
	PRINTFORMW %ANAME(ARG:0)%は、自らの身体を金で売ることに悦びを見いだすようになった
	CALL KOJO_EVENT(ARG:0,  84)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<娼婦>になった
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_娼婦
	CALL TALENT_LOSE_TEISOU(ARG:0)
ENDIF

;女限定、撮影経験300以上、欲望A以上、性技A、露出C以上でＡＶ女優
IF  !GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_ＡＶ女優) && IS_FEMALE(ARG:0) && EXP:(ARG:0):撮影経験 >= 300 && ABL:(ARG:0):欲望 >= ランク閾値:ランク_その他:ランク_A && ABL:(ARG:0):性技 >= ランク閾値:ランク_その他:ランク_A && ABL:(ARG:0):露出 >= ランク閾値:ランク_その他:ランク_C
	CALL SINGLE_EMPTY_LINE()
	PRINTFORMW %ANAME(ARG:0)%の様子がおかしい……
	PRINTFORMW %ANAME(ARG:0)%は、自らの痴態を記録に残すことに喜びを覚えるようになった
	CALL KOJO_EVENT(ARG:0,  89)
	SETCOLOR カラー_注意
	PRINTFORMW %ANAME(ARG:0)%は<ＡＶ女優>になった
	RESETCOLOR
	SETBIT TALENT:(ARG:0):淫乱系, 素質_淫乱_ＡＶ女優
	CALL TALENT_LOSE_TEISOU(ARG:0)
ENDIF
