;調教中の情報表示

;-------------------------------------------------
;調教中の情報表示
;-------------------------------------------------
@SHOW_STATUS
;ターン最初の変数の初期化
TFLAG:0 = 0

;体力気力を0～最大値の範囲に収める
CALL LIFE_LIMIT

;逆調教なら常時主人公をプレイヤーに
IF GROUPMATCH(FLAG:調教モード, 4, 5, 7)
	CALL CLEAR_MPLY()
	CALL CLEAR_MTAR()
	SIF FLAG:調教モード != 7
	CALL ADD_MPLY(MASTER)
ENDIF

;主導権を握っているキャラが疲労・失神・睡眠・離脱状態になっていれば主導権を解除
IF FLAG:主導権所有者 >= 0
	IF TCVAR:(FLAG:主導権所有者):51 || TCVAR:(FLAG:主導権所有者):52 || TCVAR:(FLAG:主導権所有者):53
		FLAG:主導権所有者 = -1
	ENDIF
ENDIF

CUSTOMDRAWLINE ─
PRINTL ◇現在の状態◇
CALL SHOW_EQUIP

CUSTOMDRAWLINE ─

;調教コマンドの表示
CALL SHOW_COMMAND

CUSTOMDRAWLINE ─
;逆調教または主人公が行動不能
IF GROUPMATCH(FLAG:調教モード, 4, 5, 7) || !IS_PLAYABLE(MASTER)
	PRINT ◇キャラの情報◇
ELSE
	PRINT ◇対象の選択◇
ENDIF
PRINT    
IF GROUPMATCH(FLAG:調教モード, 0, 1, 2, 6)
	PRINTBUTTON "900[実行／対象の反転]", 900
	PRINTBUTTON "　901[命令させろ]", 901
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAIN 900[実行／対象の反転]
	PRINTPLAIN 　901[命令させろ]
	RESETCOLOR
ENDIF
PRINTL 

;逆調教
IF GROUPMATCH(FLAG:調教モード, 4, 5, 7)
	PRINTFORML %TOSTR_SPACE(13)%%TOSTR_SPACE(43)%機嫌 体力 気力 状態
;参加人数が2人以上
ELSEIF TFLAG:44 >= 2
	PRINTFORML %TOSTR_SPACE(13)%<主導権><実行>  <対象>%TOSTR_SPACE(43)%機嫌 体力 気力    敏/鈍   状態
;参加人数が1人
ELSE
	;会いに行く
	IF FLAG:調教モード == 0
		IF FLAG:ウフフフラグ
			PRINTFORML %TOSTR_SPACE(13)%<主導権>%TOSTR_SPACE(47)%機嫌 酔い 体力 気力    敏/鈍   状態
		ELSE
			PRINTFORML %TOSTR_SPACE(13)%<主導権>%TOSTR_SPACE(47)%機嫌 酔い
		ENDIF
	;子育て
	ELSEIF FLAG:調教モード == 3
		PRINTFORML %TOSTR_SPACE(13)%<主導権>%TOSTR_SPACE(47)%機嫌
	ELSE
		PRINTFORML %TOSTR_SPACE(13)%<主導権>%TOSTR_SPACE(47)%機嫌 体力 気力    敏/鈍   状態
	ENDIF
ENDIF

;対象キャラの選択コマンド
CALL SHOW_MTAR_SELECTOR

CUSTOMDRAWLINE ─

PRINT ◇状況◇
;非ウフフ
IF !FLAG:ウフフフラグ
	;ムード表示
	PRINT     ムード 
	LOCAL:0 = MAX(0, TFLAG:37)
	LOCAL:1 = MAX(0, TFLAG:37 - 60)
	SETCOLOR 0x00FF00
	FOR LOCAL:2, 0, 6
		IF LOCAL:0 / 10 > LOCAL:2
			PRINTFORM %UNICODE(0x2665)%
		ELSE
			PRINTFORM _
		ENDIF
	NEXT
	SETCOLOR 0xFF80FF
	FOR LOCAL:2, 0, 26
		IF LOCAL:1 / 10 > LOCAL:2
			PRINTFORM %UNICODE(0x2665)%
		ELSE
			PRINTFORM _
		ENDIF
	NEXT
	RESETCOLOR
	PRINTL
	PRINT         
ENDIF

;残り時間表示
PRINT   残り時間 
FOR LOCAL:0, 0, TFLAG:56
	IF FLAG:ウフフフラグ
		SETCOLOR 0xFF8080
	;デートにより追加された時間(7番以降)は紫色で表示
	ELSEIF LOCAL:0 >= 7
		SETCOLOR 0xFF00FF
	ELSE
		SETCOLOR カラー_注意
	ENDIF
	IF TFLAG:55 > LOCAL:0
		PRINT ★
	ELSE
		PRINT ☆
	ENDIF
NEXT
RESETCOLOR
PRINTFORM %TOSTR_SPACE(54 - TFLAG:56 * 2)%

;逆調教・主人公が行動不能のときは終了ボタンをグレー表示(自分の意志で終了できない)
IF !GROUPMATCH(FLAG:調教モード, 4, 5, 7) && IS_PLAYABLE(MASTER)
	PRINTBUTTON "999[行動終了]", 999
ELSE
	SETCOLOR カラー_選択不可
	PRINTFORM 999[行動終了]
	RESETCOLOR
ENDIF
PRINTL

;状況表示
PRINTFORML               場所 <%TRAIN_PLACE%>
CUSTOMDRAWLINE ─



;-------------------------------------------------
;汚れ表示
;-------------------------------------------------
@STAIN_INFO
REPEAT 6
	SIF COUNT == 2 && !HAS_PENIS(MASTER)
		CONTINUE
	SIF COUNT == 3 && !HAS_VAGINA(MASTER)
		CONTINUE
	PRINTS CALLNAME:MASTER
	IF COUNT == 0
		PRINT の口：
	ELSEIF COUNT == 1
		PRINT の手：
	ELSEIF COUNT == 2
		PRINT のＰ：
	ELSEIF COUNT == 3
		PRINT のＶ：
	ELSEIF COUNT == 4
		PRINT のＡ：
	ELSEIF COUNT == 5
		PRINT のＢ：
	ENDIF
	SIF STAIN:MASTER:COUNT & 1
		PRINT <Ｖ>
	SIF STAIN:MASTER:COUNT & 2
		PRINT <Ｐ>
	SIF STAIN:MASTER:COUNT & 4
		PRINT <精>
	SIF STAIN:MASTER:COUNT & 8
		PRINT <Ａ>
	SIF STAIN:MASTER:COUNT & 16
		PRINT <乳>
	PRINTL 
REND

REPEAT 6
	SIF COUNT == 2 && !HAS_PENIS(TARGET)
		CONTINUE
	SIF COUNT == 3 && !HAS_VAGINA(TARGET)
		CONTINUE
	PRINTFORM %ANAME()%
	IF COUNT == 0
		PRINT の口：
	ELSEIF COUNT == 1
		PRINT の手：
	ELSEIF COUNT == 2
		PRINT のＰ：
	ELSEIF COUNT == 3
		PRINT のＶ：
	ELSEIF COUNT == 4
		PRINT のＡ：
	ELSEIF COUNT == 5
		PRINT のＢ：
	ENDIF
	
	SIF STAIN:COUNT & 1
		PRINT <Ｖ>
	SIF STAIN:COUNT & 2
		PRINT <Ｐ>
	SIF STAIN:COUNT & 4
		PRINT <精>
	SIF STAIN:COUNT & 8
		PRINT <Ａ>
	SIF STAIN:COUNT & 16
		PRINT <乳>
	PRINTL 
REND
WAIT
