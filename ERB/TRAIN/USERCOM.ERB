;調教コマンドの基幹部分とそれに関連する処理

;-------------------------------------------------
;実行可能なコマンドの記録
;-------------------------------------------------
@CREATE_COMABLE_LIST

;リストのクリア
VARSET COM_ABLE_FLAG
VARSET COM_CONFLICT_FLAG

;モード別のコマンドを走査
FOR LOCAL:0, 0, VARSIZE("TRAINNAME")
	SIF TRAINNAME:(LOCAL:0) == ""
		CONTINUE
	;実行可能かどうかを判定
	CALL CHECK_COMABLE(LOCAL:0)
	;COM_ABLE_FLAGに実行可能かどうか保存
	;1の場合は普通に実行可能、2は干渉、3の場合は干渉コマンド外したら可能な継続実行
	SIF GROUPMATCH(RESULT, 1, 3)
		COM_ABLE_FLAG:LOCAL = 1
	IF GROUPMATCH(RESULT, 2, 3)
		COM_CONFLICT_FLAG:(LOCAL) = 1
		SIF FLAG:CANCELLING
			COM_ABLE_FLAG:LOCAL = 1
	ENDIF
	;リストの記録位置を一つ進める
NEXT

;-------------------------------------------------
;コマンドARG:0が実行可能ならCOM_LISTに記録する
;-------------------------------------------------
@CHECK_COMABLE(ARG:0)
;TRAINNAMEが定義されていないなら無視
IF TRAINNAME:(ARG:0) != ""
	;コマンドフィルタ
	IF !COM_FILTER:(ARG:0)
		; ;Only check the currently selected category. Saves some processing time.
		; TRYCALLFORM COM_CATEGORY{ARG:0}
		; IF (RESULT != TFLAG:7) && (ARG:0 != 291)
		; 	RETURN 0
		; ENDIF

		;If a continuable action is already equipped with the same players and targets, make it selectable
        ;Note: Doing this here instead of in each COM_ABLE means it works for the user, but not autoselect)
		TRYCALLFORM COM_IS_EQUIP{ARG:0}
		IF RESULT && COM_ALREADY_HAPPENING(ARG:0)
                ;Non-repeatable continuable actions: Strap-on, strapon gag, condom, blindfold, rope, gag, nose hook, exposure play draw on body, shoot video, enema&plug, summon tentacles
			IF GROUPMATCH(ARG:0, 50, 68, 75, 78, 84, 85, 86, 93, 112, 114, 115, 142, 200)
				RETURN 0
			ELSE
				RETURN 1
			ENDIF
		ENDIF

		COM_ENABLE = 1
		TRYCALLFORM COM_ABLE{ARG:0}
		COM_ENABLE = 0
		LOCAL:1 = RESULT
		LOCAL:2 = RESULT

		IF FLAG:CANCELLING
            ;clear out mequip, except for actions like "Summon Tentacles" that strictly enable other actions, which we move to the top
            ;This way, we're only checking conditions like HAS_PENIS or IS_PLAYABLE instead of action-related conditions
			LOCAL:4 = 0
			FOR LOCAL, 0, MEQUIP_NUM
				IF GROUPMATCH(MEQUIP:LOCAL, 50, 200)
					CALL SWAP_MEQUIP(LOCAL, LOCAL:4)
					LOCAL:4++
				ENDIF
			NEXT
			LOCAL:5 = MEQUIP_NUM
			MEQUIP_NUM = LOCAL:4
			COM_ENABLE = 1
			TRYCALLFORM COM_ABLE{ARG:0}
			COM_ENABLE = 0
			LOCAL:2 = RESULT
		ENDIF

        ;Print the disabled actions - using this for debugging
        ;SIF !RESULT && (ARG:0 < 250)
        ;    PRINTFORM %ENTRAINNAME(ARG:0)%

		IF FLAG:CANCELLING
			MEQUIP_NUM = LOCAL:5
		ENDIF

		;初回実行時はだめだが2回目が通るケース
		;つまりFLAG:CANCELLINGが立ってて既存コマンドと干渉するケース
		IF LOCAL:1 == 0 && LOCAL:2 == 1
			RESULT = 0
			TRYCALLFORM COM_IS_EQUIP{ARG:0}
			SELECTCASE RESULT
				CASE 0
					RETURN 2
				CASE 1
					RETURN 2
				CASE 2
					RETURN 3
			ENDSELECT
		ELSEIF LOCAL:1 == LOCAL:2
			RETURN LOCAL:1
		ELSE
			;DEBUGPRINTFORML %TRAINNAME:ARG%
			RETURN 2
		ENDIF
	ENDIF
ENDIF
RETURN 0

;-------------------------------------------------
;調教コマンドの表示＆実行可能コマンドの記録
;-------------------------------------------------
@SHOW_COMMAND
#DIM L_COUNTER
#DIM AUTO_MAIN_CHARA
#DIM SET_MPLY
#DIM SET_MTAR

;実行可能なコマンドの記録
CALL CREATE_COMABLE_LIST

CALL TITLE_CATEGORY_GENERAL("コマンド")
PRINTL 

;逆調教または主人公が行動不能
IF GROUPMATCH(FLAG:調教モード, 調教_逆調教特殊, 調教_逆調教通常, 調教_慰安) || !IS_PLAYABLE(MASTER)
	PRINTPLAINFORM %TOSTR_SPACE(14)%
	PRINTBUTTON @"[なすがまま]", 0
	PRINTL 

	RETURN
ENDIF

;非ウフフ中で最終ターンならコマンド種別のタブを「特殊」で固定
IF !FLAG:ウフフフラグ && TFLAG:55 == TFLAG:56
	TFLAG:7 = 6
ENDIF

;プレイヤー・ターゲットの一覧を文字列として記録
LOCALS:0 = 
LOCALS:1 = 
LOCALS:2 = 
FOR LOCAL:0, 0, MPLY_NUM
	IF LOCAL:0 >= 1
		LOCALS:0 += @"・%ANAME(MPLY:(LOCAL:0))%"
	ELSE
		LOCALS:0 += @"%ANAME(MPLY:(LOCAL:0))%"
	ENDIF
NEXT
IF LOCALS:0 == ""
	LOCALS:0 = ----
ENDIF
FOR LOCAL:0, 0, MTAR_NUM
	IF LOCAL:0 >= 1
		LOCALS:1 += @"・%ANAME(MTAR:(LOCAL:0))%"
	ELSE
		LOCALS:1 += @"%ANAME(MTAR:(LOCAL:0))%"
	ENDIF
NEXT
IF LOCALS:1 == ""
	LOCALS:1 = ----
ENDIF

IF FLAG:ウフフフラグ && (TFLAG:7 != 5 || IS_EQUIP_PLAYER(MPLY:0, 200)) && TFLAG:7 != 6
	PRINT     
	IF IS_MPLY(MASTER)
		IF FLAG:主導権所有者 < 0 || IS_MPLY(FLAG:主導権所有者)
			PRINTFORML 「%LOCALS:0%」が「%LOCALS:1%」に――
		ELSEIF IS_MTAR(FLAG:主導権所有者)
			PRINTFORML 「%LOCALS:0%」が「%LOCALS:1%」の命令で――
		ELSE
			PRINTFORML 「%ANAME(FLAG:主導権所有者)%」の命令で「%LOCALS:0%」が「%LOCALS:1%」に――
		ENDIF
	ELSEIF IS_MTAR(MASTER)
		IF FLAG:主導権所有者 < 0 || IS_MTAR(FLAG:主導権所有者)
			PRINTFORML 「%LOCALS:1%」が「%LOCALS:0%」に命令して――
		ELSEIF IS_MPLY(FLAG:主導権所有者)
			PRINTFORML 「%LOCALS:1%」が「%LOCALS:0%」に――
		ELSE
			PRINTFORML 「%ANAME(FLAG:主導権所有者)%」の命令で「%LOCALS:0%」が「%LOCALS:1%」に――
		ENDIF
	ELSE
		IF FLAG:主導権所有者 < 0 || IS_MPLY(FLAG:主導権所有者)
			PRINTFORML 「%LOCALS:0%」が「%LOCALS:1%」に――
		ELSEIF IS_MTAR(FLAG:主導権所有者)
			PRINTFORML 「%LOCALS:0%」が「%LOCALS:1%」の命令で――
		ELSE
			PRINTFORML 「%ANAME(FLAG:主導権所有者)%」の命令で「%LOCALS:0%」が「%LOCALS:1%」に――
		ENDIF
	ENDIF
ENDIF

LOCAL:2 = 0
CALL SET_TMP_MPLY()
SET_MPLY = RESULT
CALL SET_TMP_MTAR()
SET_MTAR = RESULT

FOR LOCAL:0, 0, 1000

	;COM_FILTERが立ってる場合、対応するCOM_ABLE_FLAGは折れてるので、
	;INPUT側でチェックする必要はなし
	SIF TRAINNAME:LOCAL == "" || COM_FILTER:LOCAL
		CONTINUE

	;コマンドがどのタブに含まれるかを取得
	;現在選択されているタブに対応するコマンドのみを表示
	TRYCALLFORM COM_CATEGORY{LOCAL}
	SIF RESULT != TFLAG:7
		CONTINUE

	;コマンド名称の取得
	FOR LOCAL:3, 0, 6
		RESULTS:(LOCAL:3) = 
	NEXT

	TRYCALLFORM COM_NAME{LOCAL}
	;改行処理
	IF LOCAL:2 >= 1 && LOCAL:2 % 2 == 0
		PRINTL 
	ENDIF
	LOCAL:2 ++

	;主導権に応じて表示するメッセージを変更
	IF IS_MPLY(MASTER)
		IF FLAG:主導権所有者 < 0 || IS_MPLY(FLAG:主導権所有者)
			LOCALS:1 = %RESULTS:0%
		ELSEIF IS_MTAR(FLAG:主導権所有者)
			LOCALS:1 = %RESULTS:1%
		ELSE
			LOCALS:1 = %RESULTS:0%
		ENDIF
	ELSEIF IS_MTAR(MASTER)
		IF FLAG:主導権所有者 < 0 || IS_MTAR(FLAG:主導権所有者)
			LOCALS:1 = %RESULTS:2%
		ELSEIF IS_MPLY(FLAG:主導権所有者)
			LOCALS:1 = %RESULTS:3%
		ELSE
			LOCALS:1 = %RESULTS:0%
		ENDIF
	ELSE
		IF FLAG:主導権所有者 < 0 || IS_MPLY(FLAG:主導権所有者)
			LOCALS:1 = %RESULTS:0%
		ELSEIF IS_MTAR(FLAG:主導権所有者)
			LOCALS:1 = %RESULTS:1%
		ELSE
			LOCALS:1 = %RESULTS:0%
		ENDIF
	ENDIF

	IF COM_ABLE_FLAG:LOCAL
		;コマンドの継続タイプを取得
		RESULT = 0
		TRYCALLFORM COM_IS_EQUIP{LOCAL}
		LOCAL:4 = RESULT
		IF LOCALS:1 != ""
			SIF STRLENS(LOCALS:1) >= 26
				LOCALS:1 = %TOHALF(LOCALS:1)%
			PRINTPLAINFORM %LOCALS:1, 26, RIGHT% 
			IF LOCAL:4 == 2
				SETCOLOR カラー_選択不可
				PRINTPLAINFORM    [----]
				RESETCOLOR
			ELSEIF FLAG:調教モード == 調教_子育て || TFLAG:7 == 6
				PRINTBUTTON @"{LOCAL:0, 3}[実行]", LOCAL:0
			ELSE
				PRINTBUTTON @"{LOCAL:0, 3}[単発]", LOCAL:0
			ENDIF
			IF !(FLAG:調教モード == 調教_子育て || TFLAG:7 == 6)
				PRINTPLAIN  
				IF LOCAL:4 == 0
					SETCOLOR カラー_選択不可
					PRINTPLAINFORM     [----]
					RESETCOLOR
				ELSE
					PRINTBUTTON @"{LOCAL:0 + 1000, 3}[継続]", LOCAL:0 + 1000
				ENDIF
			ELSE
				PRINTPLAIN  
				SETCOLOR カラー_選択不可
				PRINTPLAINFORM     [----]
				RESETCOLOR
			ENDIF
		ELSE
			SETCOLOR カラー_選択不可
			PRINTPLAINFORM %TOSTR_REPEAT("-", 10), 26, RIGHT%    [----]     [----]
			RESETCOLOR
		ENDIF
		IF COM_CONFLICT_FLAG:(LOCAL:0) == 1
			PRINTBUTTON " [干渉]", LOCAL:0 + 10000
			PRINTPLAINFORM %TOSTR_SPACE(1)%
		ELSE
			PRINTPLAINFORM %TOSTR_SPACE(8)%
		ENDIF
	ELSE
		SETCOLOR カラー_選択不可
		PRINTPLAINFORM %LOCALS:1, 26, RIGHT%    [----]     [----]
		RESETCOLOR
		IF COM_CONFLICT_FLAG:(LOCAL:0) == 1
			PRINTBUTTON " [干渉]", LOCAL:0 + 10000
			PRINTPLAINFORM %TOSTR_SPACE(1)%
		ELSE
			PRINTPLAINFORM %TOSTR_SPACE(8)%
		ENDIF
	ENDIF
NEXT
PRINTL 

SIF SET_MPLY
	CALL DEL_MPLY(MPLY:0)
SIF SET_MTAR
	CALL DEL_MTAR(MTAR:0)



CALL COLOR_LINE
PRINTPLAIN             

IF FLAG:ウフフフラグ
	CALL PRINT_SELECT_BUTTON("{愛撫}", 2000, TFLAG:7 == 0)
	PRINTPLAIN  
	CALL PRINT_SELECT_BUTTON("{性交}", 2001, TFLAG:7 == 1)
	PRINTPLAIN  
	CALL PRINT_SELECT_BUTTON("{道具}", 2002, TFLAG:7 == 2)
	PRINTPLAIN  
	CALL PRINT_SELECT_BUTTON("{ＳＭ}", 2003, TFLAG:7 == 3)
	PRINTPLAIN  
	CALL PRINT_SELECT_BUTTON("{羞恥}", 2004, TFLAG:7 == 4)
	PRINTPLAIN  
	CALL PRINT_SELECT_BUTTON("{触手}", 2005, TFLAG:7 == 5)
	PRINTPLAIN  
	CALL PRINT_SELECT_BUTTON("{特殊}", 2006, TFLAG:7 == 6)
ELSEIF TFLAG:55 == TFLAG:56
	CALL PRINT_SELECT_BUTTON("{特殊}", 2006, TFLAG:7 == 6)
	PRINTPLAIN  
ELSEIF FLAG:調教モード == 調教_会う
	CALL PRINT_SELECT_BUTTON("{交流}", 2007, TFLAG:7 == 7)
	PRINTPLAIN  
	CALL PRINT_SELECT_BUTTON("{訓練／内政}", 2008, TFLAG:7 == 8)
	PRINTPLAIN  
	CALL PRINT_SELECT_BUTTON("{特殊}", 2006, TFLAG:7 == 6)
	PRINTPLAIN  
ELSEIF FLAG:調教モード == 調教_捕虜会話
	CALL PRINT_SELECT_BUTTON("{交流}", 2007, TFLAG:7 == 7)
	PRINTPLAIN  
	CALL PRINT_SELECT_BUTTON("{特殊}", 2006, TFLAG:7 == 6)
	PRINTPLAIN  
ENDIF

;-------------------------------------------------
;調教画面における「対象キャラの選択」欄の表示
;-------------------------------------------------
@SHOW_PARTICIPANT_SELECTOR
#DIM 捕虜, VARSIZE("PRISONER_TARGET")
#DIM 慰安者, 8
#DIM カウンタ
VARSET 捕虜, 0
カウンタ = 0

LOCAL:1 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):調教参加フラグ || (FLAG:調教モード != 調教_慰安 && LOCAL:0 == MASTER)
		;捕虜調教のメインターゲットは後回し
		IF FLAG:調教モード == 調教_捕虜調教 && FINDELEMENT(PRISONER_TARGET, LOCAL:0) != -1
			捕虜:カウンタ = LOCAL:1
			カウンタ ++
		;慰安系の調教で慰安参加者でない（ヤる側でない）キャラは後回し
		ELSEIF FLAG:調教モード == 調教_慰安 && !CFLAG:(LOCAL):慰安参加者
			慰安者:カウンタ = LOCAL:1
			カウンタ ++
		ELSE
			CALL SHOW_PARTICIPANT_INFO(LOCAL:0, LOCAL:1)
		ENDIF
		LOCAL:1 ++
	ENDIF
NEXT

カウンタ = 0
IF FLAG:調教モード == 調教_捕虜調教
	PRINTFORML %TOSTR_SPACE(7)%%TOSTR_REPEAT("-", 85)%
	;捕虜調教のメインターゲットについて処理
	FOR LOCAL:0, 0, CHARANUM
		IF FINDELEMENT(PRISONER_TARGET, LOCAL:0) != -1
			CALL SHOW_PARTICIPANT_INFO(LOCAL:0, 捕虜:カウンタ)
			カウンタ ++
		ENDIF
	NEXT
ELSEIF FLAG:調教モード == 調教_慰安
	PRINTFORML %TOSTR_SPACE(7)%%TOSTR_REPEAT("-", 85)%
	;民の慰安のメインターゲットの処理
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:LOCAL:調教参加フラグ && !CFLAG:(LOCAL):慰安参加者
			CALL SHOW_PARTICIPANT_INFO(LOCAL:0, 慰安者:カウンタ)
			カウンタ ++
		ENDIF
	NEXT
ENDIF

;-------------------------------------------------
;調教画面におけるキャラARG:0の情報表示
;-------------------------------------------------
@SHOW_PARTICIPANT_INFO(ARG:0, ARG:1)

LOCALS:0 = [＋]
IF IS_MPLY(ARG:0)
	LOCALS:0 = [－]
	SETCOLOR カラー_オレンジ
ENDIF

LOCALS:1 = [＋]
IF IS_MTAR(ARG:0)
	LOCALS:1 = [－]
	SETCOLOR カラー_シアン
ENDIF

IF !GROUPMATCH(TCVAR:(ARG:0):53, 0, 1)
	SETCOLOR カラー_選択不可
ENDIF

PRINTFORM   %SNAME(ARG:0), MAX_CHARANAME_LENGTH / 2, RIGHT%
RESETCOLOR
CALL PRINT_SEX(ARG:0, 1, 1, 4)

IF !GROUPMATCH(FLAG:調教モード, 調教_逆調教特殊, 調教_逆調教通常, 調教_慰安)
	PRINT  
	IF (FLAG:調教モード == 調教_捕虜調教 && FINDELEMENT(PRISONER_TARGET, ARG:0) != -1) || TCVAR:(ARG:0):51 || TCVAR:(ARG:0):52 || TCVAR:(ARG:0):53
		PRINTPLAIN [×]
	ELSEIF FLAG:主導権所有者 == ARG:0
		PRINTBUTTON "[★]", ARG:1 + 4100
	ELSE
		PRINTBUTTON "[  ]", ARG:1 + 4100
	ENDIF
	PRINT  

	IF TFLAG:44 >= 2
		IF !GROUPMATCH(TCVAR:(ARG:0):53, 0, 1)
			SETCOLOR カラー_選択不可
			PRINTPLAINFORM %LOCALS:0%[⇔]%LOCALS:1%[⇔]
			RESETCOLOR
		ELSE
			PRINTBUTTON LOCALS:0, ARG:1 + 4000
			PRINTBUTTON "[⇔]", ARG:1 + 4020
			PRINTBUTTON LOCALS:1, ARG:1 + 4040
			PRINTBUTTON "[⇔]", ARG:1 + 4060
		ENDIF
	ELSE
		IF IS_MPLY(ARG:0)
			PRINT <実行>
		ELSEIF IS_MTAR(ARG:0)
			PRINT <対象>
		ENDIF
	ENDIF
ENDIF
PRINTBUTTON "[情報]", ARG:1 + 4080

;it's nice, WOOT
SIF HAS_PENIS(ARG:0) && TALENT:(ARG:0):童貞
	SETCOLOR カラー_ピンク
CALL PRINT_SHORT_PALAM1(ARG:0, 0, "Ｃ")
RESETCOLOR
SIF TALENT:(ARG:0):処女 && HAS_VAGINA(ARG:0)
	SETCOLOR カラー_ピンク
CALL PRINT_SHORT_PALAM1(ARG:0, 1, "Ｖ", HAS_VAGINA(ARG:0))
RESETCOLOR

SIF TALENT:(ARG:0):アナル処女
	SETCOLOR カラー_ピンク
CALL PRINT_SHORT_PALAM1(ARG:0, 2, "Ａ")
RESETCOLOR

CALL PRINT_SHORT_PALAM1(ARG:0, 3, "Ｂ")

SIF TALENT:(ARG:0):キス未経験
	SETCOLOR カラー_ピンク
CALL PRINT_SHORT_PALAM1(ARG:0, 4, "Ｍ")
RESETCOLOR
CALL PRINT_SHORT_PALAM1(ARG:0, 5, "Ｕ")

CALL PRINT_SHORT_PALAM1(ARG:0, 6, "精", HAS_PENIS(ARG:0))
CALL PRINT_SHORT_PALAM1(ARG:0, 7, "乳", TALENT:(ARG:0):母乳体質)
CALL PRINT_SHORT_PALAM2(ARG:0, 40, "潤")

IF TFLAG:69 && TCVAR:(ARG:0):60
	SETCOLOR 0xff1c99
ELSEIF TFLAG:69
	SETCOLOR 0xffaad9
ELSEIF TCVAR:(ARG:0):60
	SETCOLOR 0xff5eb7
ENDIF
CALL PRINT_SHORT_PALAM2(ARG:0, 20, "欲")

CALL PRINT_SHORT_EMOTION(ARG:0)

;「会いに行く」のとき限定
IF FLAG:調教モード == 調教_会う
	CALL PRINT_SHORT_DRUNK(ARG:0)
ENDIF
;ウフフ中のみ
IF FLAG:ウフフフラグ
	CALL PRINT_SHORT_BASE1(ARG:0, 0, "体")
	CALL PRINT_SHORT_BASE2(ARG:0, 1, "気")

	IF TALENT:(ARG:0):Ｃ敏感
		CALL COLOR_PRINT("Ｃ", カラー_ピンク)
	ELSEIF TALENT:(ARG:0):Ｃ鈍感
		CALL COLOR_PRINT("Ｃ", カラー_選択不可)
	ELSE
		PRINT Ｃ
	ENDIF
	IF TALENT:(ARG:0):Ｖ敏感
		CALL COLOR_PRINT("Ｖ", カラー_ピンク)
	ELSEIF TALENT:(ARG:0):Ｖ鈍感
		CALL COLOR_PRINT("Ｖ", カラー_選択不可)
	ELSE
		PRINT Ｖ
	ENDIF
	IF TALENT:(ARG:0):Ａ敏感
		CALL COLOR_PRINT("Ａ", カラー_ピンク)
	ELSEIF TALENT:(ARG:0):Ａ鈍感
		CALL COLOR_PRINT("Ａ", カラー_選択不可)
	ELSE
		PRINT Ａ
	ENDIF
	IF TALENT:(ARG:0):Ｂ敏感
		CALL COLOR_PRINT("Ｂ", カラー_ピンク)
	ELSEIF TALENT:(ARG:0):Ｂ鈍感
		CALL COLOR_PRINT("Ｂ", カラー_選択不可)
	ELSE
		PRINT Ｂ
	ENDIF
	IF TALENT:(ARG:0):Ｍ敏感
		CALL COLOR_PRINT("Ｍ", カラー_ピンク)
	ELSEIF TALENT:(ARG:0):Ｍ鈍感
		CALL COLOR_PRINT("Ｍ", カラー_選択不可)
	ELSE
		PRINT Ｍ
	ENDIF
	PRINT  
	LOCALS:0 = 
	SIF TCVAR:(ARG:0):53 == 1
		LOCALS:0 += "眠"
	SIF IS_EQUIP_PLAYER((ARG:0), 50)
		LOCALS:0 += "竿"
	SIF IS_EQUIP_PLAYER((ARG:0), 75)
		LOCALS:0 += "ゴ"
	SIF TCVAR:(ARG:0):62
		LOCALS:0 += "尿"
	SIF TCVAR:(ARG:0):催眠中 > 0
		LOCALS:0 += "催"
	PRINTFORM %LOCALS:0%
	IF CFLAG:(ARG:0):避妊薬残ターン
		LOCALS:0 += "避"
	ELSE
		SIF IS_OVULATION(ARG:0) || CFLAG:(ARG:0):排卵誘発剤
			CALL COLOR_PRINT("排", カラー_ピンク)
		SIF TALENT:(ARG:0):妊娠
			CALL COLOR_PRINT("妊", カラー_ピンク)
	ENDIF
	RESETCOLOR
ENDIF
;「捕虜の調教」のとき限定 メインターゲットに精神力ゲージを表示
IF FLAG:調教モード == 調教_捕虜調教 && FINDELEMENT(PRISONER_TARGET, ARG:0) != -1
	PRINTL 
	IF BASE:(ARG:0):精神力 < 0
		PRINT                     崩壊度 
	ELSE
		PRINT                     精神力 
	ENDIF
	CALL PRINT_SHORT_MIND(ARG:0)
ENDIF
PRINTL 

;-------------------------------------------------
;短縮版パラメータ情報の表示(快感系)
;ARG:0はキャラ番号、ARG:1はPALAM番号、ARGS:2は短縮名、ARG:3が0ならグレー表示
;-------------------------------------------------
@PRINT_SHORT_PALAM1(ARG:0, ARG:1, ARGS:2, ARG:3 = 1)
IF !ARG:3
	SETCOLOR カラー_選択不可
ENDIF
PRINTFORM %ARGS:2%
LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):(ARG:1))
SETCOLOR 0xFF00FF
FOR LOCAL:1, 0, 2
	IF LOCAL:0 >= LOCAL:1 + 5
		PRINTFORM %UNICODE(0x2665)%
	ELSEIF LOCAL:0 >= LOCAL:1 + 3
		PRINTFORM %UNICODE(0x2661)%
	ELSEIF LOCAL:0 >= LOCAL:1 + 1
		PRINT -
	ELSE
		PRINT  
	ENDIF
NEXT
RESETCOLOR

;-------------------------------------------------
;短縮版パラメータ情報の表示(快感系以外)
;ARG:0はキャラ番号、ARG:1はPALAM番号、ARGS:2は短縮名
;-------------------------------------------------
@PRINT_SHORT_PALAM2(ARG:0, ARG:1, ARGS:2)
PRINTFORM %ARGS:2%
LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):(ARG:1))
IF LOCAL:0 >= 20
	SETCOLOR カラー_オレンジ
ELSEIF LOCAL:0 >= 10
	SETCOLOR カラー_注意
ELSE
	SETCOLOR カラー_緑
ENDIF
PRINTFORM {LOCAL:0, 2, LEFT}
RESETCOLOR

;-------------------------------------------------
;短縮版パラメータ情報の表示(感情)
;ARG:0はキャラ番号
;-------------------------------------------------
@PRINT_SHORT_EMOTION(ARG:0)
IF TALENT:(ARG:0):特殊勢力陥落系
	SETCOLOR 0xA000A0
	IF !TCVAR:(ARG:0):麻薬
		PRINTFORM  堕  
	ELSE
		PRINTFORM 堕
	ENDIF
	RESETCOLOR
ELSE
	SETCOLOR GETCOLOR_EMOTION(ARG:0)
	IF !TCVAR:(ARG:0):麻薬
		PRINTFORM  %TOSTR_EMOTION(ARG:0)%  
	ELSE
		PRINTFORM %TOSTR_EMOTION(ARG:0)%
	ENDIF
	RESETCOLOR
ENDIF

IF TCVAR:(ARG:0):麻薬
	SETCOLOR カラー_警告
	PRINTFORM 麻 
	RESETCOLOR
ENDIF

;-------------------------------------------------
;短縮版パラメータ情報の表示(酔い)
;ARG:0はキャラ番号
;-------------------------------------------------
@PRINT_SHORT_DRUNK(ARG:0)
PRINT  
IF PALAM:(ARG:0):酔い <= 0
	PRINT 平
ELSEIF PALAM:(ARG:0):酔い <= 500
	SETCOLOR 0xA08080
	PRINT 微
ELSEIF PALAM:(ARG:0):酔い <= 1500
	SETCOLOR 0xD0A0A0
	PRINT 浅
ELSEIF PALAM:(ARG:0):酔い <= 3000
	SETCOLOR 0xFFC0C0
	PRINT 中
ELSEIF PALAM:(ARG:0):酔い <= 6000
	SETCOLOR 0xD08000
	PRINT 深
ELSE
	SETCOLOR 0x804000
	PRINT 泥
ENDIF
RESETCOLOR
PRINT   

;-------------------------------------------------
;短縮版の精神力情報の表示
;ARG:0はキャラ番号
;-------------------------------------------------
@PRINT_SHORT_MIND(ARG:0)

IF TALENT:(ARG:0):崩壊
	CALL PRINT_COLORBAR(100, 100, 43, UNICODE(0x2585), UNICODE(0x2585), カラー_選択不可, 0x404040)
	PRINT  
	CALL COLOR_PRINT("崩壊", カラー_選択不可)
ELSEIF TALENT:(ARG:0):虚ろ && BASE:(ARG:0):精神力 < 0
	LOCAL:0 = CFLAG:(ARG:0):崩壊 * 100 / (3500 + MAX(MAXBASE:(ARG:0):精神力, 1000))
	CALL PRINT_COLORBAR(LOCAL:0, 100, 43, UNICODE(0x2585), UNICODE(0x2585), カラー_警告, 0x404040)
	PRINT  
	CALL COLOR_PRINT("虚ろ", カラー_選択不可)
ELSEIF BASE:(ARG:0):精神力 < 0
	LOCAL:0 = CFLAG:(ARG:0):崩壊 * 100 / (1000 + MAX(MAXBASE:(ARG:0):精神力, 1000))
	CALL PRINT_COLORBAR(LOCAL:0, 100, 43, UNICODE(0x2585), UNICODE(0x2585), カラー_警告, 0x404040)
	PRINT  
	CALL COLOR_PRINT("絶望", カラー_選択不可)
ELSE
	IF MAXBASE:(ARG:0):精神力 > 0
		LOCAL:0 = BASE:(ARG:0):精神力 * 100 / MAXBASE:(ARG:0):精神力
	ELSE
		LOCAL:0 = 100
	ENDIF

	;CALL PRINT_SLIDER(-1000, MAXBASE:(LOCAL:0):精神力, BASE:(LOCAL:0):精神力, 44, 0x4128cc, 0xcb1267)

	IF LOCAL:0 < 0
		LOCAL:1 = 0xA0A0A0
	ELSEIF LOCAL:0 <= 50
		LOCAL:1 = カラー_注意
	ELSEIF LOCAL:0 <= 75
		LOCAL:1 = 0x00FF00
	ELSE
		LOCAL:1 = カラー_シアン
	ENDIF

	CALL PRINT_COLORBAR(BASE:(ARG:0):精神力, MAXBASE:(ARG:0):精神力, 43, UNICODE(0x2585), UNICODE(0x2585), LOCAL:1, 0x404040)
	PRINT  

	SETCOLOR LOCAL:1

	IF LOCAL:0 < 0
		PRINT 絶望
	ELSEIF LOCAL:0 <= 50
		PRINT 屈服
	ELSEIF LOCAL:0 <= 75
		PRINT 消耗
	ELSE
		PRINT 平常
	ENDIF
	RESETCOLOR
	PRINT  
ENDIF



;-------------------------------------------------
;短縮版BASE情報の表示(体力)
;ARG:0はキャラ番号、ARG:1はBASE番号、ARGS:2は短縮名
;-------------------------------------------------
@PRINT_SHORT_BASE1(ARG:0, ARG:1, ARGS:2)
;PRINTFORM %ARGS:2%:

LOCAL:0 = BASE:(ARG:0):(ARG:1) * 100 / MAXBASE:(ARG:0):(ARG:1)

IF LOCAL:0 >= 70
	SETCOLOR 0x8080FF
ELSEIF LOCAL:0 >= 30
	SETCOLOR 0x40FF40
ELSEIF BASE:(ARG:0):(ARG:1) >= 500
	SETCOLOR 0xD0D000
ELSEIF BASE:(ARG:0):(ARG:1) > 0
	SETCOLOR 0xD08000
ELSE
	SETCOLOR 0x808080
ENDIF
PRINTFORM {LOCAL:0, 3, RIGHT}

IF TCVAR:(ARG:0):51
	SETCOLOR カラー_選択不可
	PRINT -
ELSEIF TCVAR:(ARG:0):66
	SETCOLOR 0x40FF40
	PRINT +
ELSE
	PRINT  
ENDIF
RESETCOLOR
PRINT  

;-------------------------------------------------
;短縮版BASE情報の表示(気力)
;ARG:0はキャラ番号、ARG:1はBASE番号、ARGS:2は短縮名
;-------------------------------------------------
@PRINT_SHORT_BASE2(ARG:0, ARG:1, ARGS:2)
;PRINTFORM %ARGS:2%:
LOCAL:0 = BASE:(ARG:0):(ARG:1) * 100 / MAXBASE:(ARG:0):(ARG:1)
IF LOCAL:0 >= 70
	SETCOLOR 0x8080FF
ELSEIF LOCAL:0 >= 30
	SETCOLOR 0x40FF40
ELSEIF BASE:(ARG:0):(ARG:1) >= 300
	SETCOLOR 0xD0D000
ELSEIF BASE:(ARG:0):(ARG:1) > 0
	SETCOLOR 0xD08000
ELSE
	SETCOLOR 0x808080
ENDIF
PRINTFORM {LOCAL:0, 3, RIGHT}
IF TCVAR:(ARG:0):52
	SETCOLOR カラー_選択不可
	PRINT -
ELSEIF TCVAR:(ARG:0):66
	SETCOLOR 0x40FF40
	PRINT +
ELSE
	PRINT  
ENDIF
RESETCOLOR

PRINT  

;---------------------------------------------------------
;実行判定を呼び出してコマンドごとにチェックする処理
;TRYCCALLFORMを使うので式中関数にはできない
;---------------------------------------------------------
@PRE_COMORDER, ARG
;[323]髪を梳いて貰うコマンドだけは途中でINPUTあるので処理しない。
SIF ARG == 323
	RETURN 0

TFLAG:46 = -10000
TRYCCALLFORM COM_ORDER_{ARG}
	;実行できない場合
	IF TFLAG:46 < TFLAG:47
		RETURN 1
	;実行できる場合
	ELSE
		RETURN 0
	ENDIF
CATCH
	;元々実行判定のないコマンドの場合
	RETURN 0
ENDCATCH

;-------------------------------------------------
;独自コマンドの表示処理
;-------------------------------------------------
@SHOW_USERCOM

;-------------------------------------------------
;独自コマンドの入力処理
;-------------------------------------------------
@USERCOM
#DIM FIRST_LINE



;プレイヤー追加・解除
IF RESULT >= 4000 && RESULT < 4020 && TFLAG:44 >= 2
	LOCAL:1 = 0
	FOR LOCAL:0, 0, CHARANUM
		IF IS_PARTICIPATE_TRAIN(LOCAL:0)
			IF RESULT - 4000 == LOCAL:1 && !TCVAR:(LOCAL:0):53
				FLAG:継続コマンド表示絞り込み選択中キャラ = LOCAL:0
				IF IS_MPLY(LOCAL:0)
					CALL DEL_MPLY(LOCAL:0)
				ELSE
					CALL ADD_MPLY(LOCAL:0)
					CALL DEL_MTAR(LOCAL:0)
				ENDIF
				CLEARLINE LINECOUNT - LINES_TRAIN
				;LINES_TRAIN = LINECOUNT
				RETURN 1
			ENDIF
			LOCAL:1 ++
		ENDIF
	NEXT
	CLEARLINE LINES_TRAIN
	;LINES_TRAIN = LINECOUNT
	RETURN 0

;プレイヤー切り替え
ELSEIF RESULT >= 4020 && RESULT < 4040 && TFLAG:44 >= 2
	LOCAL:1 = 0
	FOR LOCAL:0, 0, CHARANUM
		IF IS_PARTICIPATE_TRAIN(LOCAL:0)
			IF RESULT - 4020 == LOCAL:1 && !TCVAR:(LOCAL:0):53
				FLAG:継続コマンド表示絞り込み選択中キャラ = LOCAL:0
				CALL CLEAR_MPLY
				CALL ADD_MPLY(LOCAL:0)
				CALL DEL_MTAR(LOCAL:0)
				CLEARLINE LINECOUNT - LINES_TRAIN
				;LINES_TRAIN = LINECOUNT
				RETURN 1
			ENDIF
			LOCAL:1 ++
		ENDIF
	NEXT
	CLEARLINE LINECOUNT - LINES_TRAIN
	;LINES_TRAIN = LINECOUNT
	RETURN 0

;ターゲット追加・解除
ELSEIF RESULT >= 4040 && RESULT < 4060 && TFLAG:44 >= 2
	LOCAL:1 = 0
	FOR LOCAL:0, 0, CHARANUM
		IF IS_PARTICIPATE_TRAIN(LOCAL:0)
			IF RESULT - 4040 == LOCAL:1 && !TCVAR:(LOCAL:0):53
				FLAG:継続コマンド表示絞り込み選択中キャラ = LOCAL:0
				IF IS_MTAR(LOCAL:0)
					CALL DEL_MTAR(LOCAL:0)
				ELSE
					CALL ADD_MTAR(LOCAL:0)
					CALL DEL_MPLY(LOCAL:0)
				ENDIF
				CLEARLINE LINECOUNT - LINES_TRAIN
				;LINES_TRAIN = LINECOUNT
				RETURN 1
			ENDIF
			LOCAL:1 ++
		ENDIF
	NEXT
	CLEARLINE LINECOUNT - LINES_TRAIN
	;LINES_TRAIN = LINECOUNT
	RETURN 0

;ターゲット切り替え
ELSEIF RESULT >= 4060 && RESULT < 4080 && TFLAG:44 >= 2
	LOCAL:1 = 0
	FOR LOCAL:0, 0, CHARANUM
		IF IS_PARTICIPATE_TRAIN(LOCAL:0)
			IF RESULT - 4060 == LOCAL:1 && !TCVAR:(LOCAL:0):53
				FLAG:継続コマンド表示絞り込み選択中キャラ = LOCAL:0
				CALL CLEAR_MTAR
				CALL ADD_MTAR(LOCAL:0)
				CALL DEL_MPLY(LOCAL:0)
				CLEARLINE LINECOUNT - LINES_TRAIN
				;LINES_TRAIN = LINECOUNT
				RETURN 1
			ENDIF
			LOCAL:1 ++
		ENDIF
	NEXT
	CLEARLINE LINECOUNT - LINES_TRAIN
	;LINES_TRAIN = LINECOUNT
	RETURN 0
;情報
ELSEIF RESULT >= 4080 && RESULT < 4100
	CLEARLINE 1
	LOCAL:2 = RESULT - 4080
	LOCAL:1 = 0
	VARSET SHOP_CHARA_LIST, -1

	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):調教参加フラグ || (FLAG:調教モード != 調教_慰安 && LOCAL == MASTER)
			SHOP_CHARA_LIST:(LOCAL:1) = LOCAL:0
			LOCAL:1 ++
		ENDIF
	NEXT
	LOCAL:1 = 0
	FOR LOCAL:0, 0, CHARANUM
		SIF !CFLAG:(LOCAL:0):調教参加フラグ && LOCAL != MASTER
			CONTINUE
		IF SHOP_CHARA_LIST:(LOCAL:2) == LOCAL
			FLAG:継続コマンド表示絞り込み選択中キャラ = LOCAL:0
			CALL SHOW_INFO_WITH_UI(LOCAL:0, 1, FLAG:ウフフフラグ)
			CLEARLINE LINECOUNT - LINES_TRAIN
			RETURN 1
		ENDIF
		LOCAL:1 ++
	NEXT
	CLEARLINE LINECOUNT - LINES_TRAIN
	;LINES_TRAIN = LINECOUNT
	RETURN 0
;主導権切り替え
ELSEIF RESULT >= 4100 && RESULT < 4120
	LOCAL:1 = 0
	LOCAL:2 = RESULT - 4100
	FOR LOCAL:0, 0, CHARANUM
		IF IS_PARTICIPATE_TRAIN(LOCAL:0)
			IF LOCAL:1 == LOCAL:2 && !TCVAR:(LOCAL:0):51 && !TCVAR:(LOCAL:0):52 && !TCVAR:(LOCAL:0):53
				;捕虜調教のメインターゲットは主導権を持てない
				IF !(FLAG:調教モード == 調教_捕虜調教 && FINDELEMENT(PRISONER_TARGET, LOCAL:0) != -1)
					FLAG:継続コマンド表示絞り込み選択中キャラ = LOCAL:0
					IF FLAG:主導権所有者 == LOCAL:0
						FLAG:主導権所有者 = -1
					ELSE
						FLAG:主導権所有者 = LOCAL:0
					ENDIF
					;実行可能なコマンドの記録
					CALL CREATE_COMABLE_LIST
					CLEARLINE LINECOUNT - LINES_TRAIN
					;LINES_TRAIN = LINECOUNT
					RETURN 1
				ENDIF
			ENDIF
			LOCAL:1 ++
		ENDIF
	NEXT
	CLEARLINE LINECOUNT - LINES_TRAIN
	;LINES_TRAIN = LINECOUNT
	RETURN 0

;行動側反転
ELSEIF RESULT == 5900 && GROUPMATCH(FLAG:調教モード, 調教_会う, 調教_閨, 調教_捕虜調教, 調教_捕虜会話)
	FOR LOCAL:0, 0, MPLY_NUM
		LOCAL:(LOCAL:0 + 20) = MPLY:(LOCAL:0)
	NEXT
	FOR LOCAL:0, 0, MTAR_NUM
		LOCAL:(LOCAL:0 + 40) = MTAR:(LOCAL:0)
	NEXT
	LOCAL:10 = MPLY_NUM
	LOCAL:11 = MTAR_NUM
	CALL CLEAR_MPLY
	CALL CLEAR_MTAR
	FOR LOCAL:0, 0, LOCAL:10
		CALL ADD_MTAR(LOCAL:(LOCAL:0 + 20))
	NEXT
	FOR LOCAL:0, 0, LOCAL:11
		CALL ADD_MPLY(LOCAL:(LOCAL:0 + 40))
	NEXT
	CLEARLINE LINECOUNT - LINES_TRAIN
	;LINES_TRAIN = LINECOUNT
	RETURN 1

;命令させろ
ELSEIF RESULT == 5901 && GROUPMATCH(FLAG:調教モード, 調教_会う, 調教_閨, 調教_捕虜調教, 調教_捕虜会話)
	CALL CLEAR_MPLY
	CALL CLEAR_MTAR
	;調教参加者全員をターゲットに追加
	FOR LOCAL:0, 0, CHARANUM
		SIF IS_PARTICIPATE_TRAIN(LOCAL:0) == 0 || TCVAR:(LOCAL:0):53
			CONTINUE
		;★Skip enraged/otherwise escaped characters
		SIF !GROUPMATCH(TCVAR:(LOCAL:0):53, 0, 1)
			CONTINUE
		;MASTERはプレイヤー
		IF LOCAL:0 == MASTER
			CALL ADD_MPLY(LOCAL:0)
			FLAG:10 = MASTER
		ELSE
			CALL ADD_MTAR(LOCAL:0)
		ENDIF
	NEXT
	CLEARLINE LINECOUNT - LINES_TRAIN
	;LINES_TRAIN = LINECOUNT
	RETURN 1

ELSEIF RESULT == 5902
	FLAG:CANCELLING = !FLAG:CANCELLING
	IF FLAG:CANCELLING
		PRINTFORML 現在継続中のコマンドと干渉し、両立しないコマンドを継続実行した場合、
		PRINTFORML 現在継続中のコマンドを自動的にキャンセルするようにしました。
	ELSE
		PRINTFORML 現在継続中のコマンドと干渉し、両立しない継続コマンドは、
		PRINTFORMW 選択不可になるようにしました。
	ENDIF
ELSEIF RESULT == 5903
	FLAG:調教中表示簡略化 = !FLAG:調教中表示簡略化
	IF FLAG:調教中表示簡略化
		PRINTFORML コマンド実行時の表示のうち、
		PRINTFORMW 体力変動やソース変動の表示をカットします。
	ELSE
		PRINTFORML コマンド実行時の表示のうち、
		PRINTFORMW 体力変動やソース変動の表示も表示します。
	ENDIF
ELSEIF RESULT == 5904
	FLAG:継続コマンド表示絞り込み = !FLAG:継続コマンド表示絞り込み
	IF FLAG:継続コマンド表示絞り込み
		PRINTFORML 継続中のコマンド一覧の表示を、
		PRINTFORMW 最後に選択したキャラが関係するもののみに絞ります。
	ELSE
		PRINTFORML 継続中のコマンド一覧の表示を、
		PRINTFORMW 全て表示します。
	ENDIF
ELSEIF RESULT == 5905
	CALL CONFIG_COM_FILTER
;EQUIP解除
ELSEIF RESULT >= 6000 && RESULT <= 6000 + MAX_MEQUIP
	REDRAW 0
	CLEARLINE 1
	REDRAW 1
	LOCAL = RESULT - 6000
	TRYCALLFORM COM_BEFORE_RELEASE_EQUIP{MEQUIP:LOCAL}(LOCAL)
	CALL RELEASE_EQUIP(LOCAL)
	[IF_DEBUG]
ELSEIF RESULT == 5996
	CALL FORCE_COM
	[ENDIF]

;行動終了
ELSEIF RESULT == 5999
	;逆調教・主人公が行動不能のときは自分の意志で終了できない
	[IF_DEBUG]
	PRINTFORMW (デバッグ_調教を終了します)
	IF FLAG:調教モード == 調教_捕虜調教
		PRINTFORMW （調教を終了します）
		BEGIN AFTERTRAIN
	;ウフフ中
	ELSEIF FLAG:ウフフフラグ
		PRINTFORMW （夜伽を終了します）
		BEGIN AFTERTRAIN
	ELSE
		CALL TRAIN_EXIT()
	ENDIF
	RETURN 1
	[ENDIF]
	IF GROUPMATCH(FLAG:調教モード, 調教_逆調教特殊, 調教_逆調教通常) || !IS_PLAYABLE(MASTER)
		RETURN 0
	;捕虜調教
	ELSEIF FLAG:調教モード == 調教_捕虜調教
		PRINTFORMW （調教を終了します）
		BEGIN AFTERTRAIN
	;ウフフ中
	ELSEIF FLAG:ウフフフラグ
		PRINTFORMW （夜伽を終了します）
		BEGIN AFTERTRAIN
	ELSE
		CALL TRAIN_EXIT()
	ENDIF
	RETURN 1

ELSEIF RESULT == 5997

	CALL CONFIG_HELPMENU

ELSEIF RESULT == 5998

	CALL TRAIN_SHOP

;タブ設定
ELSEIF RESULT >= 2000 && RESULT <= 2008
	IF FLAG:ウフフフラグ || (FLAG:調教モード == 調教_会う && GROUPMATCH(RESULT, 2007, 2008, 2006)) || (FLAG:調教モード == 調教_子育て && GROUPMATCH(RESULT, 2007)) || (FLAG:調教モード == 調教_捕虜会話 && GROUPMATCH(RESULT, 2007, 2008))
		TFLAG:7 = RESULT - 2000
	ENDIF
	CLEARLINE LINECOUNT - LINES_TRAIN
	;LINES_TRAIN = LINECOUNT
;逆調教・行動不能時の「なすがまま」
ELSEIF RESULT == 0 && (GROUPMATCH(FLAG:調教モード, 調教_逆調教特殊, 調教_逆調教通常, 調教_慰安) || !IS_PLAYABLE(MASTER))
	IF TRAIN_FORCED_COM_NUM > 0
		CALL DO_TRAIN_FORCED_COM()
		RETURN 1
	ENDIF
	;おまかせフラグON
	TFLAG:0 = 1
	TFLAG:45 = 1
	CALL COM_AUTOSELECT
;干渉解除
ELSEIF RESULT >= 10000 && RESULT < 11000
	LOCAL = RESULT - 10000
	SIF !COM_CONFLICT_FLAG:LOCAL
		RETURN 0
	RESULT = 0
	TRYCALLFORM COM_IS_EQUIP{LOCAL}
	IF RESULT
		CALL SET_EQUIP(LOCAL)
		CALL RELEASE_EQUIP(RESULT)
	ELSE
		CALL RELEASE_CONFLICT_COM(LOCAL)
	ENDIF
;コマンド及び独自ユーティリティ実行
ELSEIF RESULT >= 0 && RESULT < 2000
	IF TRAIN_FORCED_COM_NUM > 0
		CALL DO_TRAIN_FORCED_COM()
		RETURN 1
	ENDIF

	LOCAL:0 = -1
	;主人公主導、単発
	IF RESULT < 1000
		LOCAL:0 = RESULT
		TFLAG:1 = 1
	;	TFLAG:45 = 0
	;主人公主導、継続
	ELSEIF RESULT < 2000
		LOCAL:0 = RESULT - 1000
		TFLAG:1 = 0
	;	TFLAG:45 = 0
	;相手主導、単発
	;ELSEIF RESULT < 600 && GROUPMATCH(FLAG:調教モード, 調教_会う, 調教_閨, 調教_捕虜会話)
	;	LOCAL:0 = RESULT - 400
	;	TFLAG:1 = 1
	;	TFLAG:45 = 1
	;相手主導、継続
	;ELSEIF RESULT < 800 && GROUPMATCH(FLAG:調教モード, 調教_会う, 調教_閨, 調教_捕虜会話)
	;	LOCAL:0 = RESULT - 600
	;	TFLAG:1 = 0
	;	TFLAG:45 = 1
	ENDIF

	IF LOCAL:0 >= 0 && COM_ABLE_FLAG:LOCAL
		;290「なすがまま」291「おまかせする」
		IF GROUPMATCH(LOCAL, GETNUM(TRAINNAME, "なすがまま"), GETNUM(TRAINNAME, "おまかせする"))
			;おまかせフラグON
			TFLAG:0 = 1
			TFLAG:45 = 1
			CALL COM_AUTOSELECT
		;393「なりゆきまかせ」
		ELSEIF GROUPMATCH(LOCAL, GETNUM(TRAINNAME, "なりゆきまかせ"))
			;おまかせフラグON
			TFLAG:0 = 1
			TFLAG:45 = 1
			CALL COM_AUTOSELECT_N
		ELSE
			;おまかせフラグOFF
			TFLAG:0 = 0
			DOTRAIN LOCAL
		ENDIF
	ENDIF
ENDIF
RETURN 0

@RELEASE_CONFLICT_COM(ARG:0)

FOR LOCAL, 0, MEQUIP_NUM
    ;SIF MEQUIP:LOCAL != -1
    ;    PRINTFORML MEQUIP {MEQUIP:LOCAL} STORED AT TMP POSITION {LOCAL} WITH PLY {MEQUIP_PLAYER:LOCAL:0} TAR {MEQUIP_TARGET:LOCAL:0}
	MEQUIP_TMP:LOCAL = MEQUIP:LOCAL
	MEQUIP_PLAYER_NUM_TMP:LOCAL = MEQUIP_PLAYER_NUM:LOCAL
	MEQUIP_TARGET_NUM_TMP:LOCAL = MEQUIP_TARGET_NUM:LOCAL
	MEQUIP_INITIATIVE_TMP:LOCAL = MEQUIP_INITIATIVE:LOCAL
	MEQUIP_TURN_TMP:LOCAL = MEQUIP_TURN:LOCAL
NEXT
FOR LOCAL, 0, MEQUIP_NUM
	FOR LOCAL:1, 0, MEQUIP_PLAYER_NUM:LOCAL
		MEQUIP_PLAYER_TMP:LOCAL:(LOCAL:1) = MEQUIP_PLAYER:LOCAL:(LOCAL:1)
	NEXT
NEXT
FOR LOCAL, 0, MEQUIP_NUM
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:LOCAL
		MEQUIP_TARGET_TMP:LOCAL:(LOCAL:1) = MEQUIP_TARGET:LOCAL:(LOCAL:1)
	NEXT
NEXT
    ;store MPLY, MTAR, MPLY_NUM, MTAR_NUM
FOR LOCAL, 0, MPLY_NUM
	MPLY_TMP:LOCAL = MPLY:LOCAL
NEXT
FOR LOCAL, 0, MTAR_NUM
	MTAR_TMP:LOCAL = MTAR:LOCAL
NEXT
MPLY_NUM_TMP = MPLY_NUM
MTAR_NUM_TMP = MTAR_NUM

VARSET MEQUIP, -1
VARSET MEQUIP_PLAYER_NUM
VARSET MEQUIP_TARGET_NUM
VARSET MEQUIP_PLAYER, -1
VARSET MEQUIP_TARGET, -1
VARSET MEQUIP_INITIATIVE
VARSET MEQUIP_TURN

LOCAL:1 = 0
FOR LOCAL, 0, MEQUIP_NUM
	MEQUIP:(LOCAL:1) = MEQUIP_TMP:LOCAL
	MEQUIP_PLAYER_NUM:(LOCAL:1) = MEQUIP_PLAYER_NUM_TMP:LOCAL
	MEQUIP_TARGET_NUM:(LOCAL:1) = MEQUIP_TARGET_NUM_TMP:LOCAL
	FOR LOCAL:2, 0, MEQUIP_PLAYER_NUM:(LOCAL:1)
		MEQUIP_PLAYER:(LOCAL:1):(LOCAL:2) = MEQUIP_PLAYER_TMP:LOCAL:(LOCAL:2)
	NEXT
	FOR LOCAL:2, 0, MEQUIP_TARGET_NUM:(LOCAL:1)
		MEQUIP_TARGET:(LOCAL:1):(LOCAL:2) = MEQUIP_TARGET_TMP:LOCAL:(LOCAL:2)
	NEXT
	MEQUIP_INITIATIVE:(LOCAL:1) = MEQUIP_INITIATIVE:LOCAL
	MEQUIP_TURN:(LOCAL:1) = MEQUIP_TURN_TMP:LOCAL

	COM_ENABLE = 1
	FLAG:RECHECKING = 1
	RESULT = 0
	TRYCALLFORM COM_ABLE{ARG:0}
	COM_ENABLE = 0
	FLAG:RECHECKING = 0
	IF RESULT
		LOCAL:1 ++
	ELSE
		MEQUIP:(LOCAL:1) = -1
		MEQUIP_PLAYER_NUM:(LOCAL:1) = 0
		MEQUIP_TARGET_NUM:(LOCAL:1) = 0
		FOR LOCAL:2, 0, MEQUIP_PLAYER_NUM:(LOCAL:1)
			MEQUIP_PLAYER:(LOCAL:1):(LOCAL:2) = -1
		NEXT
		FOR LOCAL:2, 0, MEQUIP_TARGET_NUM:(LOCAL:1)
			MEQUIP_TARGET:(LOCAL:1):(LOCAL:2) = -1
		NEXT
		MEQUIP_INITIATIVE:(LOCAL:1) = 0
		MEQUIP_TURN:(LOCAL:1) = 0
		CALL COLOR_PRINTL(@"※%TRAINNAME:(ARG:0)%と干渉する、%TRAINNAME:(MEQUIP_TMP:LOCAL)%を解除しました※", カラー_選択不可)
	ENDIF
NEXT
WAIT
MEQUIP_NUM = LOCAL:1

;-------------------------------------------------
;DESC  : 調教中ショップ画面
;ARG   ::
;-------------------------------------------------
@TRAIN_SHOP
#DIM SELLING, VARSIZE("ITEM")
#DIM FIRST_LINE
#DIM PRICE
#DIM MAX_PURCHASE_NUM

VARSET SELLING

FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTFORML 何を買いますか？
PRINTFORML 所持金:{MONEY}
CALL SINGLE_DRAWLINE


FOR LOCAL, 0, VARSIZE("SELLING")
	{
		SIF !GROUPMATCH(LOCAL,
			GETNUM(ITEM, "ローション"), 
			GETNUM(ITEM, "利尿剤"),
			GETNUM(ITEM, "コンドーム"),
			GETNUM(ITEM, "醸造酒"),
			GETNUM(ITEM, "果実酒"),
			GETNUM(ITEM, "日本酒"),
			GETNUM(ITEM, "蒸留酒"),
			GETNUM(ITEM, "媚薬"),
			GETNUM(ITEM, "排卵誘発剤"),
			GETNUM(ITEM, "絶倫丸"),
			GETNUM(ITEM, "麻薬"),
			GETNUM(ITEM, "ピアス"),
			GETNUM(ITEM, "桃源香"),
			GETNUM(ITEM, "ビデオテープ"),
			GETNUM(ITEM, "不思議な懐中時計")
			)
	}
		CONTINUE
	SIF ITEMSALES:LOCAL
		SELLING:LOCAL = 1
NEXT

FOR LOCAL, 0, VARSIZE("SELLING")
	SIF !SELLING:LOCAL
		CONTINUE
	CALL GET_PRICE(LOCAL)
	IF MONEY >= RESULT && ITEM:LOCAL < 99
		PRINTFORML [{LOCAL}] %ITEMNAME:LOCAL, 16, LEFT% - {RESULT, 5} 残:{ITEM:LOCAL}
	ELSE
		SETCOLOR カラー_選択不可
		PRINTPLAINFORM [{LOCAL}] %ITEMNAME:LOCAL, 16, LEFT% - {RESULT, 5} 残:{ITEM:LOCAL}
		PRINTL
		RESETCOLOR
	ENDIF
NEXT

PRINTFORML [9999] 戻る

INPUT

IF RESULT == 9999
	RETURN 1
ELSEIF INRANGE(RESULT, 0, VARSIZE("SELLING")) && SELLING:RESULT
	LOCAL:1 = RESULT
	CALL GET_PRICE(LOCAL:1)
	PRICE = RESULT
	MAX_PURCHASE_NUM = MONEY / PRICE
	SIF ITEM:(LOCAL:1) + MAX_PURCHASE_NUM > 99
		MAX_PURCHASE_NUM = MAX_PURCHASE_NUM - ( MAX_PURCHASE_NUM + ITEM:(LOCAL:1) - 99 )
	$INPUT_LOOP_NUM
	CALL PROMPTLY_BUTTONS(0, MAX_PURCHASE_NUM, 0)
	IF ITEM:(LOCAL:1) + RESULT > 99
		CLEARLINE 1
		PRINTW アイテムの数が99個を超えています
		GOTO INPUT_LOOP_NUM
	ELSEIF RESULT * PRICE > MONEY
		CLEARLINE 1
		PRINTW 所持金が不足しています
		GOTO INPUT_LOOP_NUM
	ENDIF
	ITEM:(LOCAL:1) += RESULT
	MONEY -= PRICE * RESULT
	PRINTFORML %ITEMNAME:(LOCAL:1)%を{RESULT}個購入しました
	PRINTFORMW 残金:{MONEY}
ENDIF


CLEARLINE LINECOUNT - FIRST_LINE
RESTART



@FORCE_COM()
#DIM FIRST_LINE
#DIM 有効コマンド数
#DIM ページ数
#DIM 現在ページ
#DIM 始点
#DIM 終点
#DIM ページ内表示数
#DIM 改行カウンタ
#DIM スイッチ

;ページ数の計算
有効コマンド数 = 0
FOR LOCAL:0, 0, 1000
	IF TRAINNAME:(LOCAL:0) != ""
		有効コマンド数 ++
	ENDIF
NEXT
ページ数 = (有効コマンド数 - 1) / 66 + 1
現在ページ = 1

CALL SINGLE_DRAWLINE
PRINTFORML 分かってる人むけの機能です：バグってもサポートしません
SIF FLAG:逆調教コマンド強制 >= 0
	PRINTFORML 現在:%TRAINNAME:(FLAG:逆調教コマンド強制 % MAX(スイッチ * 1000, 1))%
CALL SINGLE_DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP

;コマンドの表示
有効コマンド数 = 0
始点 = (現在ページ - 1) * 88
終点 = 現在ページ * 88
改行カウンタ = 0
FOR LOCAL:0, 0, 1000
	IF TRAINNAME:(LOCAL:0) != ""
		IF 有効コマンド数 >= 始点 && 有効コマンド数 < 終点
			IF 改行カウンタ >= 1 && 改行カウンタ % 4 == 0
				PRINTL 
			ENDIF
			RESULT = 0
			PRINTFORM [{LOCAL:0, 3}] %TRAINNAME:(LOCAL:0), 21, LEFT%
			RESETCOLOR
			改行カウンタ ++
		ENDIF
		有効コマンド数 ++
	ENDIF
NEXT
PRINTL 
CALL SINGLE_DRAWLINE

PRINT [990] 前のページ     
PRINTPLAINFORM page{現在ページ}/{ページ数}    
PRINTL [991] 次のページ


CALL SINGLE_DRAWLINE
CALL PRINTBUTTON_EX("[996] 本人が実行", 996, 1, スイッチ == 0)
PRINT  
CALL PRINTBUTTON_EX("[997] 対象が実行", 997, 1, スイッチ == 1)
PRINT  
CALL PRINTBUTTON_EX("[998] 第三者が実行", 998, 1, スイッチ == 2)
PRINT  
PRINTL [999] 戻る

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 999
	FLAG:逆調教コマンド強制 = -1
	REDRAW 1
	RETURN
ELSEIF RESULT == 996
	スイッチ = 0
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 997
	スイッチ = 1
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 998
	スイッチ = 2
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 990
	現在ページ --
	IF 現在ページ < 1
		現在ページ = ページ数
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 991
	現在ページ ++
	IF 現在ページ > ページ数
		現在ページ = 1
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT >= 0 && RESULT < 1000
	LOCAL:5 = RESULT
	IF TRAINNAME:(LOCAL:5) != ""
		FLAG:逆調教コマンド強制 = LOCAL:5 + スイッチ * 1000
		PRINTFORMW 設定しました
		REDRAW 1
		RETURN
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP
