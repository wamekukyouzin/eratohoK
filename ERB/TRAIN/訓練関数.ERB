@COM_SLG_TRAIN_MAIN_PROCESS(能力, 育児フラグ)
#DIM 能力
#DIM 育児フラグ
#DIM 乱数基準値, 2
#DIM 適正, 2
;コマンドの成否をTFLAG:18にセット
CALL JUDGE_COM_RESULT(MTAR:0, 5, 5)

乱数基準値:0 = MAX(100 - ABL:(MPLY:0):能力 / 3, 30)
乱数基準値:1 = MAX(100 - ABL:(MTAR:0):能力 / 3, 30)
IF ABL:(MPLY:0):能力 > ABL:(MTAR:0):能力 + 10
	乱数基準値:0 -= 10
	乱数基準値:1 += 10
ELSEIF ABL:(MTAR:0):能力 > ABL:(MPLY:0):能力 + 10
	乱数基準値:1 += 10
	乱数基準値:0 -= 10
ENDIF

SELECTCASE TFLAG:18
	CASE -1
		乱数基準値:0 -= 10
		乱数基準値:1 -= 10
	CASE 0
	CASEELSE
		乱数基準値:0 += 10
		乱数基準値:1 += 10
ENDSELECT

適正:0 = GROWTYPE_RATE(MPLY:0, ABLNAME:能力)
適正:1 = GROWTYPE_RATE(MTAR:0, ABLNAME:能力)

乱数基準値:0 += (適正:0 - 100) / 10
乱数基準値:1 += (適正:1 - 100) / 10

IF ABL:(MPLY:0):能力 > ランク閾値:ランク_ＳＬＧ:ランク_B
	TIMES 乱数基準値:0, 0.75
ELSEIF ABL:(MPLY:0):能力 < ランク閾値:ランク_ＳＬＧ:ランク_D
	TIMES 乱数基準値:0, 1.3
ENDIF
IF ABL:(MTAR:0):能力 > ランク閾値:ランク_ＳＬＧ:ランク_B
	TIMES 乱数基準値:1, 0.75
ELSEIF ABL:(MTAR:0):能力 < ランク閾値:ランク_ＳＬＧ:ランク_D
	TIMES 乱数基準値:1, 1.3
ENDIF

SIF TALENT:(MPLY:0):サボり魔
	TIMES 乱数基準値:0, 0.8
SIF TALENT:(MTAR:0):サボり魔
	TIMES 乱数基準値:1, 0.8


IF 育児フラグ == 0
	;適性がなく好感度・従属度が低いと恐怖と反感のソースを得る
	IF 適正:1 < 100
		SOURCE:(MTAR:0):歓楽 = 0
		SOURCE:(MTAR:0):恐怖 = MAX(0, 100 - MAX(CFLAG:(MTAR:0):2, CFLAG:(MTAR:0):4) * 100 / 800)
		SOURCE:(MTAR:0):不安 = MAX(0, 200 - MAX(CFLAG:(MTAR:0):2, CFLAG:(MTAR:0):4) * 200 / 500)
	ENDIF

	SOURCE:(MTAR:0):歓楽 = 20 * 適正 / 100

	;親密に応じた歓楽のソース追加
	CALL ADD_SOURCE_KANRAKU(MTAR:0, 100 + (適正:1 - 100) /2)

	;主導権に応じたソースの追加
	CALL ADD_SOURCE_INITIATIVE_N(MPLY:0, 100, 100)
	CALL ADD_SOURCE_INITIATIVE_N(MTAR:0, 100, 100)

	;ターゲットがサボり魔なら効果減少
	IF TALENT:(MTAR:0):サボり魔
		TIMES SOURCE:(MTAR:0):歓楽, 0.80
		SOURCE:(MTAR:0):反感 = 70 - SOURCE:(MTAR:0):歓楽 / 3
	ENDIF

	;失敗
	IF TFLAG:18 == -1
		TIMES SOURCE:(MTAR:0):歓楽, 0.20
		TIMES SOURCE:(MTAR:0):不安, 1.50
		SOURCE:(MTAR:0):不満 += 300
	;	LOCAL:2 = 1
	;成功
	ELSEIF TFLAG:18 == 0
	;	LOCAL:2 = 1
	;大成功
	ELSE
		TIMES SOURCE:(MTAR:0):歓楽, 2.00
		TIMES SOURCE:(MTAR:0):不安, 0.50
	;	LOCAL:2 = 2
	ENDIF
	
	;運動することにより、締まりが改善する！　歌唱とか下腹部に力はいるし！
	;失敗0　成功0～1　大成功0～2
	IF (能力 == GETNUM(ABL, "武闘")) || (能力 == GETNUM(ABL, "防衛")) || (能力 == GETNUM(ABL, "歌唱"))
		SIF HAS_VAGINA(MPLY:0)
			TALENT:(MPLY:0):Ｖ締まり = LIMIT(TALENT:(MPLY:0):Ｖ締まり + RAND:(TFLAG:18 + 2), 締まり_最低値, 締まり_最高値)
		TALENT:(MPLY:0):Ａ締まり = LIMIT(TALENT:(MPLY:0):Ａ締まり + RAND:(TFLAG:18 + 3), 締まり_最低値, 締まり_最高値)
		SIF HAS_VAGINA(MTAR:0)
			TALENT:(MTAR:0):Ｖ締まり = LIMIT(TALENT:(MTAR:0):Ｖ締まり + RAND:(TFLAG:18 + 2), 締まり_最低値, 締まり_最高値)
		TALENT:(MTAR:0):Ａ締まり = LIMIT(TALENT:(MTAR:0):Ａ締まり + RAND:(TFLAG:18 + 3), 締まり_最低値, 締まり_最高値)
	ENDIF

	;精神力の強化
	SIF MAXBASE:(MTAR:0):精神力 < 3000 || (MAXBASE:(MTAR:0):精神力 < 3500 && RAND:5 == 0)
		MAXBASE:(MTAR:0):精神力 = MIN(MAXBASE:(MTAR:0):精神力 + 2, 3500)


	;体力の強化
	IF MAXBASE:(MTAR:0):体力 < 1600
		MAXBASE:(MTAR:0):体力 += 2
	ELSEIF MAXBASE:(MTAR:0):体力 < 2000
		MAXBASE:(MTAR:0):体力 += 1
	ELSEIF MAXBASE:(MTAR:0):体力 < 3000 && RAND:5 == 0
		MAXBASE:(MTAR:0):体力 += 1
	ENDIF

	;気力の強化
	IF MAXBASE:(MTAR:0):気力 < 1600
		MAXBASE:(MTAR:0):気力 += 1
	ELSEIF MAXBASE:(MTAR:0):気力 < 2000 && RAND:2 == 0
		MAXBASE:(MTAR:0):気力 += 1
	ELSEIF MAXBASE:(MTAR:0):気力 < 2500 && RAND:6 == 0
		MAXBASE:(MTAR:0):気力 += 1
	ENDIF
	;主導度変化基準値
	TFLAG:49 = 2

	;倒錯度変化基準値
	TFLAG:50 = -1
ENDIF

IF !TALENT:(MTAR:0):幼児

	LOCAL = (RAND:100 < 乱数基準値:0)

	IF LOCAL
		SELECTCASE ABL:(MPLY:0):能力
			CASE IS < ランク閾値:ランク_ＳＬＧ:ランク_C
				LOCAL += RAND:3
			CASE IS < ランク閾値:ランク_ＳＬＧ:ランク_B
				LOCAL += RAND:2
		ENDSELECT
	ENDIF

	EXP:(MPLY:0):GET_EXP(能力) += LOCAL

	LOCAL = (RAND:100 < 乱数基準値:1)

	IF LOCAL
		SELECTCASE ABL:(MTAR:0):能力
			CASE IS < ランク閾値:ランク_ＳＬＧ:ランク_C
				LOCAL += RAND:3
			CASE IS < ランク閾値:ランク_ＳＬＧ:ランク_B
				LOCAL += RAND:2
		ENDSELECT
	ENDIF

	EXP:(MTAR:0):GET_EXP(能力) += LOCAL

ELSE
	FOR LOCAL, 0, 10
		EXP:(MTAR:0):GET_EXP(能力) += (RAND:100 < 乱数基準値:1)
	NEXT
ENDIF


