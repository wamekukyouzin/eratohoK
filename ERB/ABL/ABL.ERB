;能力アップ画面の処理

;-------------------------------------------------
;コマンド実行時の自動能力上昇 ARG:0……キャラ番号 ARG:1……1ならメッセージを非表示
;-------------------------------------------------
@TRAIN_AUTO_ABLUP(ARG:0, ARG:1 = 0)
#DIM 初期値
#DIM 能力
#DIM 必要経験値
#DIM 対応経験値
;能力固定フラグが立っているなら処理しない
IF CFLAG:(ARG:0):能力固定フラグ
	RETURN
ENDIF

FOR 能力, 0, 70
	初期値 = ABL:(ARG:0):能力
	IF ABLNAME:能力 == "性知識"
		;性知識は別枠
		CALL TRAIN_AUTO_ABLUP_KNOWLEDGE(ARG:0, ARG:1)
		CONTINUE
	ENDIF

	;能力に対応する経験値の名称を取得
	RESULTS = 
	TRYCALLFORM ABLUP_EXPNAME{能力}
	SIF RESULTS == ""
		CONTINUE

	対応経験値 = GETNUM(EXP, RESULTS)

	WHILE ABL:(ARG:0):能力 < 9999
		;必要な経験値の計算
		RESULT = 0
		TRYCALLFORM CALC_ABLUP_EXP{能力}(ARG:0)
		必要経験値 = RESULT

		SIF 必要経験値 < 0 || EXP:(ARG:0):対応経験値 < 必要経験値
			BREAK
		ABL:(ARG:0):能力 ++
		EXP:(ARG:0):対応経験値 -= 必要経験値
	WEND

	SIF 初期値 == ABL:(ARG:0):能力
		CONTINUE

	IF !ARG:1
		SETCOLOR カラー_オレンジ
		PRINTFORML %ANAME(ARG:0)%の%ABLNAME:(能力)%が上昇しました(Lv{初期値}→{ABL:(ARG:0):能力})
		RESETCOLOR
	ENDIF
NEXT


;-------------------------------------------------
;性知識の特殊なLv上昇処理 ARG:0……キャラ番号 ARG:1……1ならメッセージを非表示
;-------------------------------------------------
@TRAIN_AUTO_ABLUP_KNOWLEDGE(ARG:0, ARG:1)
;能力固定フラグが立っているなら処理しない
IF CFLAG:(ARG:0):能力固定フラグ
	RETURN
ENDIF

WHILE ABL:(ARG:0):性知識 < 5
	;常に経験値100でLv上昇
	IF EXP:(ARG:0):性知識経験値 >= 100
		ABL:(ARG:0):性知識 ++
		EXP:(ARG:0):性知識経験値 = 0
		IF ARG:1 == 0
			SETCOLOR カラー_オレンジ
			PRINTFORML %ANAME(ARG:0)%の性知識が上昇しました(Lv{ABL:(ARG:0):性知識})
			RESETCOLOR
		ENDIF
	ELSE
		BREAK
	ENDIF
WEND


